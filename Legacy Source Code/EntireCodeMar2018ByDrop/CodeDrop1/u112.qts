;#> PROGRAM-ID.     U112.QTS
;? ??????????????????
;
;  THIS PGMS USES CURRENT-EP + OR - 1.. WILL IT WORK FOR 9101 WHERE -1 = 9100 ????????????????

;
;       ((C)) Dyad Technologies
;
;    PURPOSE: Ensure that a CURRENT EP record exists in the
;             PAYCODE/CEILINGS file for all physicians before starting
;	      calcuations for this EP.  If record hasn't already been
;	      manually created by the User then automatically generate
;	      one using the values from the most recent EP record as
;	      defaults (if no previous EP record then zero's will be used
;	      thus defaulting the physician onto PAY CODE 0.
;
;    MODIFICATION HISTORY
;        DATE    SAF #  WHO      DESCRIPTION
;     92/JAN/01  ____   R.A.     - original
;   1999/JAN/15  ----   S.B.     - Checked for Y2K.
;   2003/dec/24         A.A.	 - alpha doctor nbr
;


cancel clear
run u112

set default
set process nolimit

global temp w-current-ep-nbr        numeric
global temp w-current-ep-nbr-minus1 numeric

;-------------------------------------------------------------------------------
; OBTAIN THE CURRENT EP NUMBER AND PASS TO SUBSEQUENT REQUESTS
;
request u112_const_mstr_get_ep_nbr		&
		on edit        errors report	&
                on calculation errors report
access constants-mstr-rec-6
choose const-rec-nbr 6

item w-current-ep-nbr        = current-ep-nbr
item w-current-ep-nbr-minus1 = current-ep-nbr - 1

;-------------------------------------------------------------------------------
; ENSURE THAT A 'PAYCODE/CEILING' RECORD EXISTS FOR THE CURRENT EP FOR
; FOR DOCTOR.  IF NOT FOUND CREATE USING PREVIOUS EP RECORD FOR THAT DOC
; ACCESS THE PREVIOUS EP RECORD FOR EACH DOCTOR AND ATTEMPT LINK TO
; CURRENT EP RECORD. IF NOT FOUND, CREATE ONE USING PREVIOUS EP REC DATA
; !!!! SHOULD THIS ACCESS F020 !!!! TO CATCH NEW DOCTORS ???;
request u112_run_0                 		&
		on edit        errors report	&
                on calculation errors report

access f112-pycdceilings					       &
	link doc-nbr of f112-pycdceilings,                             &
	     w-current-ep-nbr                                          &
	to   doc-nbr,                                                  &
	     ep-nbr of f112-pycdceilings                               &
	alias f112-current-ep optional

select f112-pycdceilings 					       &
 if    ep-nbr of f112-pycdceilings = w-current-ep-nbr-minus1


output f112-pycdceilings add alias f112-output if not record f112-current-ep exists
	item doc-nbr          = doc-nbr      of f112-pycdceilings
	item ep-nbr           = w-current-ep-nbr
	item doc-pay-code     = doc-pay-code of f112-pycdceilings
	item doc-pay-sub-code = doc-pay-sub-code of f112-pycdceilings
	item last-mod-date    = sysdate
	item last-mod-time    = systime/10000
	item last-mod-user-id = "U112      Gen'd"


build $pb_obj/u112
  
