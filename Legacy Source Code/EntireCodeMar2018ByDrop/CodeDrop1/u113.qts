;#> PROGRAM-ID.     U113.QTS
;
;       ((C)) Dyad Technologies
;
;    PURPOSE: SUB-PROCESS WITHIN "EARNINGS GENERATION" PROCESS.
;	      GENERATE COMPENSATION "DEFAULTS" FROM F113 FILE
;
;    MODIFICATION HISTORY
;        DATE    SAF #  WHO      DESCRIPTION
;     92/JAN/01  ____   R.A.     - original
;   1999/JAN/15  ----   S.B.     - Checked for Y2K.
;   1999/June/01 	S.B.     - Added the use file
;                        	   def_compensation_status.def to 
;                        	   prevent hard coding of compensation-status.
;   2003/dec/24         A.A.	 - alpha doctor nbr
;

cancel clear
set default
set process nolimit
cancel clear
run u113

set default
set process nolimit

global temp w-current-ep-nbr            numeric
global temp w-first-ep-nbr-of-fiscal-yr numeric

;-------------------------------------------------------------------------------
; OBTAIN CONSTANTS VALUES FOR PASSING TO SUBSEQUENT REQUESTS
;
request u113_a_constants_values_ep_nbr	     &
 		on edit        errors report &
		on calculation errors report
access constants-mstr-rec-6
choose const-rec-nbr 6
item w-current-ep-nbr            = current-ep-nbr
item w-first-ep-nbr-of-fiscal-yr = first-ep-nbr-of-fiscal-yr

;
; DETERMINE THE 'PROCESS-SEQ' AND 'TRANSACTION TYPE'
; FOR THE TRANSACTIONS BEING CREATED IN THIS RUN
;REQUEST U113_A_GET_YTDDED                    &
 		;ON EDIT        ERRORS REPORT &
		;ON CALCULATION ERRORS REPORT
;ACCESS F190-COMP-CODES
;CHOOSE COMP-CODE "YTDDED"
;ITEM YTDDED-SEQ  = PROCESS-SEQ
;ITEM YTDDED-TYPE = COMP-TYPE

;-------------------------------------------------------------------------------

request u113_run_0        			&
		on edit        errors report	&
                on calculation errors report

access f113-default-comp					      &
	link comp-code of f113-default-comp			      &
	to   comp-code of f190-comp-codes			      &
	link doc-nbr     of f113-default-comp,			      &
	     w-current-ep-nbr,					      &
	     process-seq of f190-comp-codes,			      &
	     comp-code   of f113-default-comp 			      &
	to   doc-nbr,						      &
	     ep-nbr,						      &
	     process-seq,					      &
	     comp-code    of f110-compensation   optional

;S.B.
use $use/def_compensation_status.def

select if    ep-nbr-from of f113-default-comp <= w-current-ep-nbr      &
	 and ep-nbr-to   of f113-default-comp >= w-current-ep-nbr


output f110-compensation add alias f110-default			       &
  if  not  record f110-compensation  exists
	item ep-nbr          = w-current-ep-nbr
	item ep-nbr-entry    = w-current-ep-nbr
	item comp-code       = comp-code       of f113-default-comp
	item comp-type       = comp-type       of f190-comp-codes
	item process-seq     = process-seq     of f190-comp-codes
	item factor          = factor          of f113-default-comp
	item factor-override = factor-override of f113-default-comp
	item comp-units      = comp-units      of f113-default-comp
	item amt-gross       = amt-gross       of f113-default-comp
	item amt-net         = amt-net         of f113-default-comp
;S.B.
;	item compensation-status = " "
	item compensation-status = compensation-status-accepted
	item last-mod-date    = sysdate
	item last-mod-time    = systime / 10000
	item last-mod-user-id = "U113 DEFINED  "


build $pb_obj/u113
  
