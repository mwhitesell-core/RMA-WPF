;#> program-id.     m920.qks
;
;       ((C)) Dyad Systems
;
;    program purpose : Maintain settings of doctor options
;
;    MODIFICATION HISTORY
;      DATE    WHO      DESCRIPTION
; 2001/sep/18  D.B.	- original
; 2003/dec/10  A.A.	- alpha doctor nbr

cancel clear

screen $pb_obj/m920 receiving w-doc-nbr on 6 for 18

;! temp w-doc-nbr
temp w-doc-nbr  char*3

file f920-doctor-options occurs 11
;  access viaindex alternative-doc-key  using w-doc-dept, w-doc-nbr
  access  using w-doc-nbr
  item doc-nbr initial w-doc-nbr

skip 1
title "                 Sub         --Active Dates--       Option " at , 1
skip 
title "     Category    Category    From         To        Value  " at , 1
skip 1

cluster occurs with f920-doctor-options
align (1,,6) (,,18) (,,30) (,,43) (,,55)
;field doc-nbr of f920-doctor-options required nochange
field doc-option-category of f920-doctor-options         required 
field doc-option-subcategory of f920-doctor-options         required 
field doc-option-date-active-from of f920-doctor-options         	&
	lookup noton f920-doctor-options 				&
		via doc-nbr , 						&
	   	    doc-option-category , 				&
	     	    doc-option-subcategory , 				&
		    doc-option-date-active-from 			&
		using doc-nbr of f920-doctor-options , 			&
		      doc-option-category of f920-doctor-options ,  	&
		      doc-option-subcategory of f920-doctor-options , 	&
		      doc-option-date-active-from of f920-doctor-options&
	required

field doc-option-date-active-to of f920-doctor-options	&
	required
field doc-option-value of f920-doctor-options

procedure edit doc-option-date-active-from
begin
  if    doc-option-date-active-to   <> 0				&
    and doc-option-date-active-from >= doc-option-date-active-to
  then error "'From' date must be < 'To' date'"
end

procedure edit doc-option-date-active-to
begin
  if doc-option-date-active-from >= doc-option-date-active-to
  then error "'To' date must be > 'From' date'"
end

build
