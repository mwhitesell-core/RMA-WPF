;#> PROGRAM-ID.     U030B_autoadj_clinic_dtl.QTS
;
; ((C)) Dyad Technologies
;
;    PROGRAM PURPOSE : This program will create auto adjustment at the claim detail after
;                      the regular RA run.
;
;    MODIFICATION HISTORY
;        DATE   WHO          DESCRIPTION
;   2009/Feb/05 M.C.         - automatically adjust for detail has explan cd 'I4' and oma cd 'G313A'
;   2009/Mar/05 M.C.	     - Yasemin requests to include explan cd '36' as well since MOH has changed 
;				their mind for this month run
;   2009/Jun/2  Yas          - Add clinic 79 to the selection
;   2009/Jul/20 M.C.         - Yas requested to add clinic 78 & 79 to the selection
;   2012/Jan/25 MC1          - clone from the original u030b_autoadj_clinic_88.qts which is no longer used
;			     - instead, use this new program, add two more criteria   
;   2012/Apr/02 MC2          - clmhdr-amt-tech-billed is not update properly, think it is the sequence of update
;   2013/Nov/05 MC3  	     - add more selection to be auto adjust, E082A/E083A with reason code 'SX' or
;				K071A/K072A with reason code 'D7', and these apply to all clinics
;			     - regardless of existing scenario, do not check if amt-paid = 0
;   2013/Dec/10 MC4  	     - undo MC3 as Yasemin/Lori requested - do not auto adjust, E082A/E083A with reason code 'SX' or
;				K071A/K072A with reason code 'D7'
;   2016/Jan/28 MC5	     - modify/add more selection to be auto adjust
;   2016/Jul/05 MC6	     - change subfile name

can clear
set default
set process nolimit
set lock record update



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
request calc_batch_nbr on calculation errors report on edit errors report

access *u030_no_adj             					&
   link part-hdr-clinic-nbr, part-hdr-claim-nbr   			&
    to  part-dtl-clinic-nbr, part-dtl-claim-nbr of part-paid-dtl	

; MC5
def x-bal-due zoned*8 signed = part-dtl-amt-bill - part-dtl-amt-paid
; MC5 - end

sorted on part-hdr-claim-id 

sel if (    (    part-dtl-explan-cd = 'I4' 				&
	     or  part-dtl-explan-cd = '36'				&
	    )								&
	and      part-dtl-oma-cd = 'G313A' 				&
; 2013/11/05 - MC3
;	and      part-dtl-amt-paid = 0  				&
; 2013/11/05 - end
; 2009/07/20 - MC
;	and (part-hdr-clinic-nbr = 88 or part-hdr-clinic-nbr = 79)  
	and (    part-hdr-clinic-nbr = 88 				&
	      or part-hdr-clinic-nbr = 78  				&
	      or part-hdr-clinic-nbr = 79  				&
	    )								&
       )								&
; 2009/07/20 - end

; 2012/01/25 - MC1 - add 2 more criteria

   or  (    (    part-dtl-explan-cd = '36' 				&
	     or  part-dtl-explan-cd = '58'				&
; MC5
	     or  part-dtl-explan-cd = 'D6'				&
	     or  part-dtl-explan-cd = 'O9'				&
	     or  part-dtl-explan-cd = 'O2'				&
; MC5 - end
	    )								&
	and (    part-dtl-oma-cd = 'E082A' 				&
    	     or  part-dtl-oma-cd = 'E083A'  				&
    	     or  part-dtl-oma-cd = 'G271A'  				&
    	     or  part-dtl-oma-cd = 'P007A' 				&
; MC5
    	     or  part-dtl-oma-cd = 'P025A' 				&
; MC5 - end
	    )								&
; 2013/11/05 - MC3
;	and      part-dtl-amt-paid = 0  				&
; 2013/11/05 - end
; MC5
;	and (    part-hdr-clinic-nbr = 22 				&
; 	      or part-hdr-clinic-nbr = 36  				&
;	      or part-hdr-clinic-nbr = 46  				&
        and ( x-bal-due <= 5600						&
; MC5 - end
	    )								&
       )								&	
   or  (    (    part-dtl-explan-cd = '80' 				&
	    )								&
; MC5
;	and (    part-hdr-clinic-nbr >= 61				&
;	     and part-hdr-clinic-nbr <= 75 				&
	and (   (    part-hdr-clinic-nbr >= 61				&
	         and part-hdr-clinic-nbr <= 66 				&
	        )							&
	     or (    part-hdr-clinic-nbr >= 71				&
	         and part-hdr-clinic-nbr <= 75 				&
	        )							&
	    )								&
       )								&
; add new criteria
   or  (    (    part-dtl-explan-cd = 'M1' 				&
	    )								&
	and (    part-dtl-oma-cd = 'G388A' 				&
    	     or  part-dtl-oma-cd = 'E423A'  				&
    	     or  part-dtl-oma-cd = 'P005A' 				&
	    )								&
        and ( x-bal-due <= 4600						&
	    )								&
; MC5 - end    
       )							;	&
; 2012/01/25 - end

; 2013/12/10 - MC4 - undo below
; 2013/11/05 - MC3 - add 2 more criteria

;   or  (    (    part-dtl-explan-cd = 'SX' 				&
;	    )								&
;	and (    part-dtl-oma-cd = 'E082A' 				&
;    	     or  part-dtl-oma-cd = 'E083A'  				&
;	    )								&
;       )								&
;   or  (    (    part-dtl-explan-cd = 'D7' 				&
;	    )								&
;	and (    part-dtl-oma-cd = 'K071A' 				&
;    	     or  part-dtl-oma-cd = 'K072A'  				&
;	    )								&
;      )					
; 2013/11/05 - end
; 2013/12/10 - end

temp x-count
  item x-count count

temp x-batch-count
  item x-batch-count = ceiling(x-count / 99)

subfile u030_88_auto_adj keep include                     &
        part-paid-dtl,  x-count, x-batch-count, part-hdr-claim-id


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
request create_b_adjustment on calculation errors report on edit errors report
                        ; Create a batch control record for every
                        ; 99 claim. Create a clmhdr and a clmdtl
                        ; record, and inverted clmdtl key to the
                        ; each adjusting clmdtl record in the
                        ; subfile. At the end of the request,
                        ; update the batch nbr in Constant Mstr
                        ; selection statement.  Create a new subfile
                        ; that store all adjusted batches.

access *u030_88_auto_adj        					&
   link  'B', part-hdr-claim-id[1:8], 	       				&
        nconvert(part-hdr-claim-id[9:2]), '00000', '0' 			&
    to   key-clm-type, key-clm-batch-nbr,             			&
        key-clm-claim-nbr, key-clm-serv-code, key-clm-adj-nbr 		&
         of f002-claims-mstr alias f002-clmhdr          		&
  link   part-hdr-claim-id[3:3]               				&
   to    doc-nbr of f020-doctor-mstr                   			&
   and   nconvert(part-hdr-claim-id[1:2]) to iconst-mstr-rec

sorted on x-batch-count on part-hdr-claim-id

temp x-batch-nbr int*6
  item x-batch-nbr = iconst-clinic-batch-nbr + x-batch-count   &
        	     at x-batch-count                          &
               reset at initial to  iconst-clinic-batch-nbr

define x-clinic-batch-nbr char*8 = part-hdr-claim-id[1:2] + ascii(x-batch-nbr,6)

define x-mod       = mod(x-count,99)

define x-claim-nbr = x-mod if x-mod <> 0 else 99

define x-ohip-bal int*7 signed =                      &
    (part-dtl-amt-bill - part-dtl-amt-paid) * -1

define x-tot-claim-ar-oma int*7 signed =         	&
  round(  (  clmhdr-tot-claim-ar-oma  of f002-clmhdr	&
	   / clmhdr-tot-claim-ar-ohip of f002-clmhdr)	&
	* x-ohip-bal)

; For Clinics 61-75, if reason code = 80 then the full amount
; of writeoff is applied against the tech portion, otherwise
; based upon ratio of original ohip billed to original tech billed

define x-amt-tech-billed  int*7 signed =                &
    x-ohip-bal                                          &
        if    part-dtl-explan-cd = "80"                 &
; MC5
;          and (     part-hdr-claim-id[1:2] >= "60"      &
;                and part-hdr-claim-id[1:2] <= "75"      &
          and (    (     part-hdr-claim-id[1:2] >= "61" &
                     and part-hdr-claim-id[1:2] <= "66" &
		   )					&
               or  (     part-hdr-claim-id[1:2] >= "71" &
                     and part-hdr-claim-id[1:2] <= "75" &
		   )					&
; MC5 - end
              )                                         &
else                                                    &
   round(  (  clmhdr-amt-tech-billed   of f002-clmhdr   &
            / clmhdr-tot-claim-ar-ohip of f002-clmhdr)  &
          * x-ohip-bal)


use $use/def_batctrl_batch_status.def
use $use/def_clmhdr_status_ohip.def

; MC6
;subfile u030mc_88_adj   keep include	&
subfile u030mc_debug_adj   keep include	&
; MC6 - end
u030_88_auto_adj,		&
x-clinic-batch-nbr ,		&
x-claim-nbr     ,		&
clmhdr-tot-claim-ar-oma,	&
x-tot-claim-ar-oma,		&
clmhdr-tot-claim-ar-ohip,	&
x-ohip-bal,			&
clmhdr-amt-tech-billed,		&
x-amt-tech-billed
 
output f001-batch-control-file add at x-batch-count on errors report
   item batctrl-batch-nbr      		 initial x-clinic-batch-nbr
   item batctrl-batch-type            	 initial 'A'
   item batctrl-clinic-nbr         	 initial iconst-clinic-nbr
   item batctrl-adj-cd                   initial 'B'
   item batctrl-date-batch-entered initial ascii(sysdate,8)
   item batctrl-date-period-end    initial ascii(iconst-date-period-end,8)

   item batctrl-cycle-nbr          initial iconst-clinic-cycle-nbr
   item batctrl-ar-yy-mm           initial '000000'

   item batctrl-adj-cd-sub-type    initial 'A'
   item batctrl-nbr-claims-in-batch final x-claim-nbr
   item batctrl-last-claim-nbr      final x-claim-nbr
   item batctrl-batch-status        final batctrl-batch-status-balanced
   item batctrl-amt-act             subtotal x-ohip-bal
   item batctrl-amt-est             subtotal x-ohip-bal
   item batctrl-calc-ar-due         subtotal x-ohip-bal
   item batctrl-calc-tot-rev        subtotal x-ohip-bal
   item batctrl-agent-cd            final clmhdr-agent-cd of f002-clmhdr


output f002-claims-mstr alias f002-adj-hdr add noitems on errors report
;Preset claim header data from batctrl record
   item key-clm-type                 initial 'B'
   item key-clm-batch-nbr            initial x-clinic-batch-nbr
   item key-clm-claim-nbr            initial x-claim-nbr
   item key-clm-serv-code            initial '00000'
   item key-clm-adj-nbr              initial '0'
   item clmhdr-orig-batch-nbr        initial batctrl-batch-nbr
   item clmhdr-orig-claim-nbr        initial x-claim-nbr
   item clmhdr-batch-type            initial batctrl-batch-type
   item clmhdr-adj-cd-sub-type       initial batctrl-adj-cd-sub-type
   item clmhdr-adj-cd              initial batctrl-adj-cd
   item clmhdr-date-period-end     initial nconvert(batctrl-date-period-end)
   item clmhdr-cycle-nbr           initial batctrl-cycle-nbr
; Preset claim header data with values from claim being adjusted
   item clmhdr-claim-id            initial clmhdr-claim-id of f002-clmhdr
   item clmhdr-diag-cd             initial clmhdr-diag-cd of f002-clmhdr
   item clmhdr-hosp                initial clmhdr-hosp of f002-clmhdr
   item clmhdr-i-o-pat-ind         initial clmhdr-i-o-pat-ind of f002-clmhdr
   item clmhdr-agent-cd            initial clmhdr-agent-cd of f002-clmhdr
   item clmhdr-loc                 initial clmhdr-loc of f002-clmhdr
   item clmhdr-pat-ohip-id-or-chart initial clmhdr-pat-ohip-id-or-chart of f002-clmhdr
   item clmhdr-pat-acronym         initial clmhdr-pat-acronym of f002-clmhdr
   item clmhdr-doc-dept            initial doc-dept of f020-doctor-mstr
; Assign values to the remaining fields
   item clmhdr-reference    	   initial part-dtl-explan-cd of u030_88_auto_adj
   item clmhdr-date-admit          initial '00000000'
   item clmhdr-date-cash-tape-payment initial ascii(sysdate,8)
   item clmhdr-date-sys            initial ascii(sysdate,8)
   item clmhdr-status-ohip         initial clmhdr-status-ohip-accepted
   item clmhdr-tape-submit-ind     initial 'N'
   item clmhdr-doc-nbr-ohip        initial 0
   item clmhdr-doc-spec-cd         initial 0
   item clmhdr-refer-doc-nbr       initial 0
   item clmhdr-curr-payment        initial 0
   item clmhdr-amt-tech-paid       initial 0
   item clmhdr-manual-and-tape-payments initial 0
;  G313 is a professional code only
;   item clmhdr-amt-tech-billed     initial 0                   
   item clmhdr-amt-tech-billed     initial x-amt-tech-billed   
   item clmhdr-tot-claim-ar-oma    initial x-tot-claim-ar-oma
   item clmhdr-tot-claim-ar-ohip   initial x-ohip-bal
   item key-p-clm-type 		   initial 'Z'
   item key-p-clm-data   	   initial clmhdr-pat-ohip-id-or-chart of f002-clmhdr[2:14]

output f002-claims-mstr alias f002-adj-dtl add noitems on errors report
   item key-clm-type               initial 'B'
   item key-clm-batch-nbr          initial x-clinic-batch-nbr
   item key-clm-claim-nbr          initial x-claim-nbr
   item key-clm-serv-code          initial part-dtl-oma-cd of u030_88_auto_adj  
   item key-clm-adj-nbr            initial '0'
   item clmdtl-batch-nbr           initial clmhdr-batch-nbr of f002-adj-hdr
   item clmdtl-claim-nbr           initial clmhdr-claim-nbr of f002-adj-hdr
   item clmdtl-oma-cd              initial part-dtl-oma-cd  of u030_88_auto_adj[1:4]
   item clmdtl-oma-suff            initial part-dtl-oma-cd  of u030_88_auto_adj[5:1]
   item clmdtl-adj-nbr             initial 1
   item clmdtl-agent-cd            initial clmhdr-agent-cd of f002-adj-hdr
   item clmdtl-adj-cd              initial clmhdr-adj-cd of f002-adj-hdr
   item clmdtl-sv-date             initial ascii(part-dtl-serv-date of u030_88_auto_adj,8)
   item clmdtl-nbr-serv            initial 0
   item clmdtl-consec-dates        initial 0

   item clmdtl-date-period-end     initial ascii(clmhdr-date-period-end of f002-adj-hdr,8)
   item clmdtl-cycle-nbr           initial clmhdr-cycle-nbr of f002-adj-hdr
   item clmdtl-orig-batch-id       initial clmhdr-orig-batch-id of f002-adj-hdr
   item clmdtl-fee-oma             initial clmhdr-tot-claim-ar-oma of f002-adj-hdr
   item clmdtl-fee-ohip            initial clmhdr-tot-claim-ar-ohip of f002-adj-hdr
   item clmdtl-amt-tech-billed     initial clmhdr-amt-tech-billed of f002-adj-hdr
   item clmdtl-diag-cd             initial clmhdr-diag-cd of f002-adj-hdr 
   item clmdtl-rev-group-cd        initial ' '
   item clmdtl-line-no             initial 1  
   item key-p-clm-type 		   initial 'Z'
   item key-p-clm-data   	   initial x-clinic-batch-nbr   +           &   
					   ascii(x-claim-nbr,2) +	    &
					   part-dtl-oma-cd of u030_88_auto_adj &
					   + '0'

; update the adjusted balance into the original claim header
output f002-clmhdr       update at part-hdr-claim-id  on errors report
; 2012/04/02 - MC2 - transfer from below
   item clmhdr-amt-tech-billed     subtotal x-amt-tech-billed
   item clmhdr-tot-claim-ar-oma    subtotal x-tot-claim-ar-oma
   item clmhdr-tot-claim-ar-ohip   subtotal x-ohip-bal
;  item clmhdr-amt-tech-billed     subtotal x-amt-tech-billed
; 2012/04/02 - end

output iconst-mstr-rec update at final on errors report
   item iconst-clinic-batch-nbr     final x-batch-nbr

;Create Clmdtl Key in Subfile, so Cobol pgm can write inverted to the adjusting Clmdtl
subfile u030_88_dtl_key keep include                   &
   key-clm-type      of f002-adj-dtl,                  &
   key-clm-batch-nbr of f002-adj-dtl,                  &
   key-clm-claim-nbr of f002-adj-dtl,                  &
   key-clm-serv-code of f002-adj-dtl,                  &
   key-clm-adj-nbr   of f002-adj-dtl

subfile u030_88_adj_batches keep at x-batch-count     &
       include batctrl-batch-nbr

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request part_adj_batch on calculation errors report on edit errors report
                           ;  Store the adjusted batches into the file
                           ;  PART_ADJ_BATCH

access *u030_88_adj_batches link 'B', batctrl-batch-nbr    &
   to key-clm-type, key-clm-batch-nbr of f002-claims-mstr

sel if clmhdr-oma-suff-adj = '000000'

output part-adj-batch add on errors report
   item  part-adj-claim-id final clmhdr-claim-id[1:10]
   item  part-adj-bal final clmhdr-tot-claim-ar-ohip


build $pb_obj/u030b_autoadj_clinic_dtl
