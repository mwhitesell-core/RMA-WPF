; 2013/Sep/11	MC	- u031.qts 
;			- delete the undefined payment batch claim from premium payments via AGEP/MOHR/MOHD1/2
;			  based on the subfile r030r_undefined_doc which is generated from r030r2.qzc of r030r.qzs

cancel clear
set process nolimit
set lock record update

request delete_payment_claim

access *r030r_undefined_doc		&
	link 'B', batctrl-batch-nbr, key-clm-claim-nbr	&
	 to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr of f002-claims-mstr

subfile savef002_pay_clmhdr keep if clmdtl-oma-cd of f002-claims-mstr  = '0000' include f002-claims-mstr
subfile savef002_pay_clmdtl keep if clmdtl-oma-cd of f002-claims-mstr <> '0000' include f002-claims-mstr


output  f002-claims-mstr delete on errors report

request reduce_batch_amount

access *r030r_undefined_doc		&
	link  to f001-batch-control-file  

sorted on batctrl-batch-nbr

temp batch-amount zoned*7 signed
item batch-amount = batch-amount + clmhdr-manual-and-tape-payments reset at batctrl-batch-nbr

temp nbr-of-claim zoned*2 unsigned
item nbr-of-claim = nbr-of-claim + 1 reset at batctrl-batch-nbr

subfile savef001_pay_batch keep at batctrl-batch-nbr include f001-batch-control-file

output f001-batch-control-file update at batctrl-batch-nbr on errors report
   item batctrl-amt-act 	final batctrl-amt-act - batch-amount                
   item batctrl-amt-est 	final batctrl-amt-est - batch-amount 
   item batctrl-calc-tot-rev  	final batctrl-calc-tot-rev - batch-amount 
   item batctrl-manual-pay-tot 	final batctrl-manual-pay-tot - batch-amount 
   item batctrl-nbr-claims-in-batch final batctrl-nbr-claims-in-batch -  nbr-of-claim 

build $pb_obj/u031
