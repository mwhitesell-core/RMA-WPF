;#PROGRAM-ID.     R717.QZS
;
;
;    PROGRAM PURPOSE : TO REPORT WHEN SUSPEND HDR AND SUSPEND DTL DO NOT MATCH ON AMOUNTS
;                      STATUS "
;
;    MODIFICATION HISTORY
;     DATE            WHO     DESCRIPTION
;   2016/Apr/21         MC      original
;   2016/Apr/25		MC1	exclude clmdtl-status = 'D'elete


cancel clear
set rep nolimit
set report device disc name r717
set page width 132 length 60
set formfeed

access f002-suspend-dtl

;MC1
sel if clmdtl-status <> 'D'

sorted on clmdtl-doc-ohip-nbr on clmdtl-accounting-nbr
set subfile name susp_dtl keep at clmdtl-accounting-nbr
rep summ clmdtl-doc-ohip-nbr clmdtl-accounting-nbr clmdtl-fee-ohip subt

go

access *susp_dtl              &
      link clmdtl-doc-ohip-nbr, clmdtl-accounting-nbr &
       to clmhdr-doc-ohip-nbr, clmhdr-accounting-nbr of f002-suspend-hdr

sel if clmdtl-fee-ohip <> clmhdr-tot-claim-ar-ohip            

page heading  &
  tab   1 "RUN DATE:"         &
  tab  12 sysdate             &
  tab  55 "REGIONAL MEDICAL ASSOCIATES"       &
  tab 120 "PAGE"              &
  tab 126 syspage             &
skip 2        &
  tab  50 "LIST OF CLAIMS WHERE AMOUNTS ARE DIFFERENCE" &
skip  &
  tab  50 " BETWEEN SUSPEND HEADER AND DETAIL RECORDS " &
skip 2                                        &
  tab   1 "DOCTOR OHIP NBR"           &
  tab  25 "ACCOUNTING NBR"            &
  tab  45 "DETAIL AMOUNT"             &
  tab  65 "HEADER AMOUNT"             &
skip 2

report                                        &
  tab   5 clmdtl-doc-ohip-nbr pic "^^^^^^" significance 6     &
  tab  28 clmdtl-accounting-nbr               &
  tab  45 clmdtl-fee-ohip             &
  tab  65 clmhdr-tot-claim-ar-ohip

go
exit

