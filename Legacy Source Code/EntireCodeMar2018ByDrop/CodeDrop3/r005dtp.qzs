; DOC: R005DTP.QZS
; DOC: MONTHLY CASH APPLIED RECONCILIATION
; DOC: SORT BY CLINIC
; DOC: RUN FOR: MUMC DIAGNOSTICS
;
;PROGRAM PURPOSE : CASH ANALYSIS BY LOCATION (SUMMARY BY CLINIC REPORT)
;                  R005TP.CB CONVERSTION TO POWERHOUSE R005DTP.QZS
;                  APPEND R005DTP.TXT TO END OF R005CTP.TXT
;
;DATE       WHO       DESCRIPTION
;92/05/04   Y.B.      ORIGINAL
;03/dec/17  A.A.      alpha doctor nbr
;2010/02/04 yas       - add new clinic 66
;
can clear
set rep nolimit
set page length 62 width 132
set rep device disc name r005dtp
set formfee


access f051tp-doc-cash-mstr                                &
  link docashtp-doc-nbr                                    &
    to doc-nbr of f020-doctor-mstr    opt                  &
  link docashtp-clinic-nbr                                 &
    to iconst-clinic-nbr-1-2 of iconst-mstr-rec  opt       &
  link docashtp-loc-cd to loc-nbr of f030-locations-mstr   opt

 choose docashtp-key "60@" to "66@"

sorted on docashtp-clinic-nbr

def x-tech-mtd zoned*8 signed = (docashtp-tech-in-mtd + docashtp-tech-out-mtd)
def x-prof-mtd zoned*8 signed = (docashtp-prof-in-mtd + docashtp-prof-out-mtd)
def x-total-mtd zoned*8 signed = (x-tech-mtd + x-prof-mtd)

def x-tech-ytd zoned*8 signed = (docashtp-tech-in-ytd + docashtp-tech-out-ytd)
def x-prof-ytd zoned*8 signed = (docashtp-prof-in-ytd + docashtp-prof-out-ytd)
def x-total-ytd zoned*8 signed = (x-tech-ytd + x-prof-ytd)

;y2k
def x-year cha*4 = ascii(sysdate,8)[1:4]

;y2k
def x-mth cha*2 = ascii(sysdate,8)[5:2]

;y2k
def x-day cha*2 = ascii(iconst-date-period-end,8)[7:2]

def x-month cha*9 = "JANUARY"   if x-mth  = "01"      &
               else "FEBRUARY"  if x-mth  = "02"      &
               else "MARCH"     if x-mth  = "03"      &
               else "APRIL"     if x-mth  = "04"      &
               else "MAY"       if x-mth  = "05"      &
               else "JUNE"      if x-mth  = "06" and x-day < "30"  &
               else "YEAREND"   if x-mth  = "06" and x-day = "30"  &
               else "JULY"      if x-mth  = "07"      &
               else "AUGUST"    if x-mth  = "08"      &
               else "SEPTEMBER" if x-mth  = "09"      &
               else "OCTOBER"   if x-mth  = "10"      &
               else "NOVEMBER"  if x-mth  = "11"      &
               else "DECEMBER"  if x-mth  = "12"

;y2k
def x-date cha*15 = pack(x-month +"/ "+ x-year)

page heading                                              	&
	tab 1   "R005TP"   			          	&
;	TAB 8   "/"        			          	&
;	TAB 9   DOCASHTP-CLINIC-NBR                             &
	tab 13  "P.E.D"				          	&
;y2k
	tab 20  iconst-date-period-end pic "^^^^/^^/^^"     	&
;	TAB 54  "M. U. M. C. DIAGNOSTICS"                 	&
	tab 103 "RUN DATE:"                               	&
;y2k
	tab 112  sysdate                                  	&
        tab 124 "PAGE:" tab 130 syspage pic "^^^"      	 	&
skip 1  tab 47  "MONTHLY CASH APPLIED RECONCILLIATIONS"   	&
;       TAB 105 "CLINIC:"                                       &
;       TAB 113  DOCASHTP-CLINIC-NBR                            &
;       TAB 116  "-"                                            &
;       TAB 118  ICONST-CLINIC-NBR                              &
skip 1  tab 56  "BY LOCATION SUMMARY"                           &
;y2k
skip 1  tab 51  "FOR THE MONTH OF" tab 68 x-date                &
skip 2  tab 53  "----------- MONTH TO DATE -----------"		&
        tab 96  "----------- YEAR TO DATE -----------" 		&
skip 1  tab 60  "TECH"                                          &
        tab 73  "PROF"                                          &
        tab 85  "TOTAL"                                         &
        tab 102 "TECH"                                          &
        tab 115 "PROF"                                          &
        tab 127 "TOTAL"                                         &
 skip 2 tab 4 "SUMMARY BY CLINIC ------------------->"


report

footing at docashtp-clinic-nbr                                &
  tab 41 docashtp-clinic-nbr                   &
  tab 54  x-tech-mtd  subtotal pic "^^^,^^^.^^"  &
  tab 67  x-prof-mtd  subtotal pic "^^^,^^^.^^"  &
  tab 80  x-total-mtd subtotal pic "^^^,^^^.^^"  &
  tab 95  x-tech-ytd  subtotal pic "^^^^,^^^.^^"  &
  tab 108 x-prof-ytd  subtotal pic "^^^^,^^^.^^"  &
  tab 121 x-total-ytd subtotal pic "^^^^,^^^.^^" &
skip 2

final footing      &
  tab 4 "GRAND TOTAL FOR ALL CLINICS --------->" &
  tab 54  x-tech-mtd  subtotal pic "^^^,^^^.^^"  &
  tab 67  x-prof-mtd  subtotal pic "^^^,^^^.^^"  &
  tab 80  x-total-mtd subtotal pic "^^^,^^^.^^"  &
  tab 95  x-tech-ytd  subtotal pic "^^^^,^^^.^^"  &
  tab 108 x-prof-ytd  subtotal pic "^^^^,^^^.^^"  &
  tab 121 x-total-ytd subtotal pic "^^^^,^^^.^^"


build $pb_obj/r005dtp


  
