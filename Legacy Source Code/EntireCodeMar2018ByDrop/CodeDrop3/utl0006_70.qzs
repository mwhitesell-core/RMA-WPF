; DOC: utl0006_70.qzs
; DOC: REP CASH APPLIED BY DOC/DEPT/LOCATION/TECH/PROF/TOTAL  TOTALS AT
; DOC: DOCTOR NUMBER FOR ALL AGENTS (CLINIC 60 ONLY)
; DOC: RUN FOR: ACCOUNTING DEPT MARY BROWNRIDGE
;
;DATE       WHO       DESCRIPTION
;2007/mar   Y.B.      utl0006 
;

can clear
set rep nolimit
set page length 62 width 132
set rep device disc name utl0006_70
set formfee


access f051tp-doc-cash-mstr                          &
   link docashtp-doc-nbr                             &
to doc-nbr of f020-doctor-mstr opt                   &
   link docashtp-clinic-nbr                          &
to iconst-clinic-nbr-1-2 of iconst-mstr-rec opt      &
  link doc-dept to dept-nbr of f070-dept-mstr  opt   

choose docashtp-key "71@" to "75@"

sort on docashtp-doc-nbr on docashtp-clinic-nbr

def x-tech zoned*8 signed = (docashtp-tech-in-mtd + docashtp-tech-out-mtd)
def x-prof zoned*8 signed = (docashtp-prof-in-mtd + docashtp-prof-out-mtd)
def x-total zoned*8 signed = (x-tech + x-prof)

def x-clinic cha*4 = "H520"  if docashtp-clinic-nbr =  71    &
                else "H521"  if docashtp-clinic-nbr =  72    &
                else "H522"  if docashtp-clinic-nbr =  73    &
                else "H523"  if docashtp-clinic-nbr =  74    &
                else "H524"  if docashtp-clinic-nbr =  75    

select if       dept-company = 1   &
           and (x-prof ne 0 or x-tech ne 0)

page heading                                          &
	tab 1  "P.E.D"				      &
	tab 7  iconst-date-period-end                 &
	tab 35 sysname				      &
	tab 85 "RUN DATE:"                            &
	tab 96 sysdate                                &
        tab 108 "PAGE:" tab 113 syspage pic "^^^"     &
skip 2  tab 47 "CASH APPLIED" skip 2                  &
        tab 1   "DOC #"                               &
        tab 10  "DEPT"                                &
        tab 16  "DOC NAME"                            &
        tab 40  "INITS"                               &
        tab 54  "CLINIC"                              &
        tab 69  "TECHNICAL"                           &
        tab 82  "PROFESSIONAL"                        &
        tab 106 "TOTALS"                              &
skip 2

report

footing at docashtp-clinic-nbr                        &
  tab 1   docashtp-doc-nbr print at docashtp-doc-nbr  &
  tab 10  doc-dept         print at docashtp-doc-nbr  &
  tab 16  doc-name         print at docashtp-doc-nbr  &
  tab 41  doc-inits        print at docashtp-doc-nbr  &
  tab 54  x-clinic                                    &
  tab 67  x-tech  subtotal pic "^^^^,^^^.^^"          &
  tab 84  x-prof  subtotal pic "^^^^,^^^.^^"          &
  tab 102 x-total subtotal pic "^^^,^^^.^^"

footing at docashtp-doc-nbr skip 2                    &
  tab 59  "TOTALS:"                                   &
  tab 67  x-tech  subtotal pic "^^^^,^^^.^^"          &
  tab 84  x-prof  subtotal pic "^^^^,^^^.^^"          &
  tab 102 x-total subtotal pic "^^^,^^^.^^"           &
skip 2

final footing                                         &
  tab 47  "GRAND TOTALS:"                             &
  tab 67  x-tech  subtotal pic "^^^^,^^^.^^"          &
  tab 84  x-prof  subtotal pic "^^^^,^^^.^^"          &
  tab 102 x-total subtotal pic "^^^,^^^,^^^.^^"       &
skip 2

build $pb_obj/utl0006_70


  
