;  NAME   : R801C.QZS
;  PURPOSE: LIST OF ALL AUTOMATIC ADJUSTED CLAIMS WITH AGENT '0', '2',
;           AND '4', '6' AND '9' SORT BY CLINIC TOTALS.
;
;  MODIFICATION HISTORY:
;
;  YY/MMM/DD     BY WHOM        WHY
;  93/SEP/07     YASEMIN        ORIGINAL (SMS 143)
;  1999/May/19   S.B.		Y2K checked.
;  2004/jan/05   b.e.		- alpha doctor nbr
;
can clear
set default
set report nolimit
set report device disc name r801c
set page length 60 width 132
set formfeed

access adj-claim-file

;!def clm-id char*11 = ascii(adj-batch-nbr,9) + ascii(adj-claim-nbr,2)
def clm-id char*10 =  adj-batch-nbr			&
		    + ascii(adj-claim-nbr,2)

;!def clinic-nbr char*2 = (ascii(adj-batch-nbr,9))[1:2]
def clinic-nbr char*2 = adj-batch-nbr[1:2]

sel if adj-agent-cd = 0 or adj-agent-cd = 2 or adj-agent-cd = 4  &
                        or adj-agent-cd = 6 or adj-agent-cd = 9  &
                        or adj-agent-cd = 8

sort on clinic-nbr on adj-agent-cd on clm-id

page heading 						&
	tab   1  'R801C'				&
	tab  10  'RUN DATE: '				&
	tab  23  sysdate				&
	tab  45  'LIST OF AUTOMATIC ADJUSTED CLAIMS'	&
	tab 120  'PAGE: '				&
	tab 128  syspage pic '^^^^'			&
skip 3

footing at clinic-nbr                                   &
skip 3	tab   1 'CLINIC NBR:'           		&
      	tab  13 clinic-nbr              		&
      	tab  18 'TOTAL AMOUNT:'         		&
	tab  35 adj-amt-bal subtotal at clm-id pic '^^^,^^^.^^ ' trail '-' &
	tab  51 'TOTAL # OF CLAIMS: '			&
	tab  70 count at clm-id pic '^^^^'

final footing						&
skip 5	tab  18 'GRAND TOTALS:'         		&
	tab  35 adj-amt-bal subtotal at clm-id pic '^^^,^^^.^^ ' trail '-' &
	tab  51 'TOTAL # OF CLAIMS: '			&
	tab  70 count at clm-id pic '^^^^'

build $pb_obj/r801c

  
