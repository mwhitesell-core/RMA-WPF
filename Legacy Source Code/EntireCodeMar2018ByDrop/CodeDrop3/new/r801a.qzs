;  NAME   : R801A.QZS
;  PURPOSE: LIST OF ALL AUTOMATIC ADJUSTED CLAIMS WITH AGENT '0', '2',
;           AND '4'.
;
;  MODIFICATION HISTORY:
;
;  YY/MMM/DD     BY WHOM        WHY
;  93/AUG/25     M. CHAN        ORIGINAL (SMS 143)
;  94/JAN/11     YASEMIN        CLINIC TOTALS
;  1999/May/19   S.B.		Y2K checked.
;  2004/jan/05   b.e.		- alpha doctor nbr
;
can clear
set default
set report nolimit
set report device disc name r801a
set page length 60 width 132
set formfeed

access adj-claim-file

;!def x-clinic char*2 = (ascii(adj-batch-nbr,9))[1:2]
def x-clinic char*2 = adj-batch-nbr[1:2]

;!def clm-id char*11 = ascii(adj-batch-nbr,9) + ascii(adj-claim-nbr,2)
def clm-id char*10 =  adj-batch-nbr			&
		    + ascii(adj-claim-nbr,2)

sel if adj-agent-cd = 0 or adj-agent-cd = 2 or adj-agent-cd = 4

sort on x-clinic on clm-id

page heading 						&
	tab   1  'R801A'				&
	tab  10  'RUN DATE: '				&
	tab  23  sysdate				&
	tab  45  'LIST OF AUTOMATIC ADJUSTED CLAIMS'	&
	tab 120  'PAGE: '				&
	tab 128  syspage pic '^^^^'			&
skip 3  tab   1 'CLAIM NBR'				&
	tab  16 'SERV CD'				&
	tab  25 'ADJUSTED AMT'				&
	tab  40 'PAT ACRONYM'				&
	tab  55 'AGENT'					&
skip 2

report

heading at x-clinic skip page

footing at clm-id 					&
        tab   1 clm-id                                  &
	tab  17 adj-oma-cd-suff                         &
        tab  26 adj-amt-bal pic '^^,^^^.^^ ' trail '-'  &
        tab  41 adj-pat-acronym                         &
	tab  57 adj-agent-cd

footing at x-clinic   				        &
skip 3	tab   1 'TOTAL AMOUNT:'         		&
	tab  25 adj-amt-bal subtotal at clm-id pic '^^^,^^^.^^ ' trail '-' &
	tab  41 'TOTAL # OF CLAIMS: '			&
	tab  60 count at clm-id pic '^^^^'

final footing						&
skip 3	tab   1 'TOTAL AMOUNT:'         		&
	tab  25 adj-amt-bal subtotal at clm-id pic '^^^,^^^.^^ ' trail '-' &
	tab  41 'TOTAL # OF CLAIMS: '			&
	tab  60 count at clm-id pic '^^^^'

build $pb_obj/r801a

  
