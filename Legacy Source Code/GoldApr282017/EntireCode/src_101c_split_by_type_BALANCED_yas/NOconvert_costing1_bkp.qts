; Program: costing1.qts (creates costing.qtc)
; Purpose: costing RMA for rma - extracts revenue from f050 (current and history)
;                                and creates download files

;99/06/02 M. Chan	- Yasemin to increase the size of nbr-svc,
;			  nbr-dtl, nbr-clm  from 4 to 6
;99/06/08 M. Chan	- Yasemin to apply tech-ind <> 'Y' for clinic
;			  60 to 65 claims only  , add a new column man reject
;			- add request seven to extract man reject from manual
;			  rejected claims history file
;99/07/20  B.E. 	- added qualification of clinic nbr in request 4

cancel clear
set lock file update
set process nolimit


; DOC: costing1.qzs   
; DOC: 
; DOC:  
; DOC: RUN FOR: Created for Chris Costanza     
;
;PROGRAM PURPOSE : report number of claims and nbr of services
;                  sort by clinic/dept/doc#


;
;DATE       BY WHOM      DESCRIPTION
;99/01/07   YASEMIN      ORIGINAL
;99/05/28   M. Chan      exclude technical oma fee code
;			 include all active doctors

request one     &
	on calculation errors report	&
	on edit errors report

access f002-claims-mstr         &
        link (nconvert(clmhdr-claim-id[4:3]))   &
        to doc-nbr of f020-doctor-mstr 	        &
        link clmdtl-oma-cd to fee-oma-cd of f040-oma-fee-mstr opt

choose  key-clm-type 'B' ,  key-clm-batch-nbr, key-clm-claim-nbr,	&
	key-clm-serv-code, key-clm-adj-nbr '0'



select f002-claims-mstr if                      &
	        clmdtl-date-period-end >= '980701'	&
           and  clmdtl-date-period-end <= '990630'	&
           and  clmdtl-oma-cd      <> "0000"    & ; details only
           and  clmdtl-oma-cd      <> "ZZZZ"    & ; details only
           and  clmdtl-oma-cd      <> "PAID"    & ; details only
           and  clmdtl-oma-cd      <> "MICM"    & ; details only
           and  clmdtl-oma-cd      <> "MISJ"    & ; details only
           and  clmdtl-oma-cd      <> "MISC"    & ; details only
           and  clmdtl-oma-cd      <> "MICV"    & ; details only
           and  clmdtl-oma-cd      <> "MISP"    & ; details only
	   and  clmdtl-adj-nbr	   = 0		& ; regular details only
	   and  clmdtl-id[1:1]     <> "6"  	; ignore clinic 60

select   f020-doctor-mstr if    doc-date-fac-term = 000000         &
		             or doc-date-fac-term > 990630

;select if (fee-tech-ind <> 'Y' and clmdtl-id[1:2] >= '60' and 	&
;				   clmdtl-id[1:2] <= '65') 	&
;      or clmdtl-id[1:2] < '60' or clmdtl-id[1:2] >  '65'

;def x-doc-nbr cha*3 = clmdtl-id[4:3]

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

define x-sv-nbr2  zoned*2 unsigned = clmdtl-sv-nbr(1)                    &
                     if    clmdtl-consecutive-sv-days(1)[2:2] ne 'OP'    &
                       and clmdtl-consecutive-sv-days(1)[2:2] ne 'MR'    &
                       and clmdtl-consecutive-sv-days(1)[2:2] ne 'BI'    &
                       and clmdtl-consecutive-sv-days(1)[2:2] ne '  '    &
                   else 0 

define x-sv-nbr3  zoned*2 unsigned = clmdtl-sv-nbr(2)                    &
                     if    clmdtl-consecutive-sv-days(2)[2:2] ne 'OP'    &
                       and clmdtl-consecutive-sv-days(2)[2:2] ne 'MR'    &
                       and clmdtl-consecutive-sv-days(2)[2:2] ne 'BI'    &
                       and clmdtl-consecutive-sv-days(2)[2:2] ne '  '    &
                   else 0 

define x-sv-nbr4  zoned*2 unsigned = clmdtl-sv-nbr(3)                    &
                     if    clmdtl-consecutive-sv-days(3)[2:2] ne 'OP'    &
                       and clmdtl-consecutive-sv-days(3)[2:2] ne 'MR'    &
                       and clmdtl-consecutive-sv-days(3)[2:2] ne 'BI'    &
                       and clmdtl-consecutive-sv-days(3)[2:2] ne '  '    &
                   else 0 

def x-claims char*16 = clmdtl-id[1:16]

sorted on x-claims

temp x-nbr-svcs zoned*4 unsigned
item x-nbr-svcs = x-nbr-svcs       + x-sv-nbr1 &
                                   + x-sv-nbr2 & 
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4 &
	reset at x-claims

subfile costing1 keep at x-claims 	&
include         &
doc-nbr        ,&
doc-name	,&
doc-inits	,&
doc-dept	,&
doc-clinic-nbr	,&
x-claims        ,&
clmdtl-id       ,&
x-nbr-svcs 
;
;PROGRAM PURPOSE : report number of claims and nbr of services
;                  sort by clinic/dept/doc#


;
;DATE       BY WHOM      DESCRIPTION
;99/01/07   YASEMIN      ORIGINAL
;99/03/28   YASEMIN      add count (each detail of claim) 
;
;PROGRAM PURPOSE : count nbr of detail recs for each claim and nbr of details
;                  for amounts less than $10.00
;
;
;DATE       BY WHOM      DESCRIPTION
;99/01/07   YASEMIN      ORIGINAL
;99/03/28   YASEMIN      add count (each detail of claim)
;99/06/07   B.E.         - added keep at claim nbr in stead of doc nbr
;
request two-a   &
        on calculation errors report    &
        on edit errors report

set process nolimit

access *costing1

def x-claim-nbr-only char*11 = x-claims[1:11]

sort on doc-nbr   &
     on x-claim-nbr-only

temp c-nbr-svc zoned*6 unsigned
;item nbr-svc = nbr-svc + x-nbr-svcs  reset at doc-nbr
item c-nbr-svc = c-nbr-svc + x-nbr-svcs  reset at x-claim-nbr-only

temp c-nbr-clm zoned*6 unsigned
;item nbr-clm = nbr-clm + 1 at x-claim-nbr-only reset at doc-nbr
item c-nbr-clm = c-nbr-clm + 1 at x-claim-nbr-only reset at x-claim-nbr-only

temp c-nbr-dtl zoned*6 unsigned
;item nbr-dtl = nbr-dtl + 1                     reset at doc-nbr
item c-nbr-dtl = c-nbr-dtl + 1                  reset at x-claim-nbr-only

temp dtl-amt zoned*6 unsigned
item dtl-amt = 1000

temp c-nbr-dtl-less-than-min-amt zoned*6 unsigned
item c-nbr-dtl-less-than-min-amt = c-nbr-dtl-less-than-min-amt + 1      &
                if dtl-amt < 300                reset at x-claim-nbr-only

def nbr-reject zoned*4 unsigned = 0

def amt-ytd      zoned*8 signed = 0
def misc-amt-ytd zoned*8 signed = 0
def mohr-amt-ytd zoned*8 signed = 0
def total-amt-ytd zoned*8 signed = 0
def man-reject   zoned*6 unsigned = 0

;subfile costing2  keep append at doc-nbr                 &
 subfile costing1b keep        at x-claim-nbr-only        &
include                                     &
         x-claim-nbr-only,                  &
         doc-nbr,                           &
         doc-name,                          &
         doc-inits,                         &
         doc-dept,                          &
         doc-clinic-nbr,                    &
         c-nbr-svc,                         &
         c-nbr-clm,                         &
         c-nbr-dtl,                         &
         c-nbr-dtl-less-than-min-amt,     &
         nbr-reject,                      &
         amt-ytd,                         &
         misc-amt-ytd,                    &
         mohr-amt-ytd,                    &
         total-amt-ytd,                   &
         man-reject

;PROGRAM PURPOSE : This pgm used for 'what if' type analysis of costing
;                  by allowing the programmer to change the calculation
;                  of 'nbr detail recs'.
;                  - code tests if 'clinic 6x' and nbr details > 3 then
;                    only count 3 as max details.
;                  - this logic can be changed to test impact on costing model

;
;DATE       BY WHOM      DESCRIPTION
;99/01/07   YASEMIN      ORIGINAL
;99/03/28   YASEMIN      add count (each detail of claim)
;
request two-b   &
        on calculation errors report    &
        on edit errors report

access *costing1b

sort on doc-nbr   &
     on x-claim-nbr-only

temp nbr-svc zoned*6 unsigned
item nbr-svc = nbr-svc + c-nbr-svc  reset at doc-nbr

temp nbr-clm zoned*6 unsigned
item nbr-clm = nbr-clm + 1 at x-claim-nbr-only reset at doc-nbr

; (don't count more than 3 details for any claim if claim is for clinic 60-65)
def x-clinic char*2 = x-claim-nbr-only[1:2]
temp nbr-dtl zoned*6 unsigned
item nbr-dtl                                            &
; ********************** changeable logic ********************
;     = nbr-dtl                    + 2                           &
;                if     (c-nbr-dtl > 2)                          &
;                   and (x-clinic >='60' and x-clinic <='65')    &
      =  nbr-dtl + c-nbr-dtl if 1=1				 &
   else  nbr-dtl + c-nbr-dtl                                     &
; ************************************************************
                                reset at doc-nbr

def nbr-reject zoned*4 unsigned = 0

def amt-ytd      zoned*8 signed = 0
def misc-amt-ytd zoned*8 signed = 0
def mohr-amt-ytd zoned*8 signed = 0
def total-amt-ytd zoned*8 signed = 0
def man-reject   zoned*6 unsigned = 0

;subfile costing2 keep append at doc-nbr  &
 subfile costing2 keep        at doc-nbr  &
include                                     &
         doc-nbr,                           &
         doc-name,                          &
         doc-inits,                         &
         doc-dept,                          &
         doc-clinic-nbr,                    &
         nbr-svc,                         &
         nbr-clm,                         &
         nbr-dtl,                         &
         nbr-reject,                      &
         amt-ytd,                         &
         misc-amt-ytd,                    &
         mohr-amt-ytd,                    &
         total-amt-ytd,                   &
         man-reject

  
;
;PROGRAM PURPOSE : report if reason code not blank by doc number


;
;DATE       BY WHOM      DESCRIPTION
;99/03/28   YASEMIN      ORIGINAL
;

request three		&
	on calculation errors report	&
	on edit errors report

access f002-claims-mstr		&
	link (nconvert(clmhdr-claim-id[4:3]))	&
	to doc-nbr of f020-doctor-mstr          &
	link clmhdr-status-ohip to rat-code of f096-ohip-pay-code opt

;select headers only
choose  key-clm-type 'B', key-clm-batch-nbr, key-clm-claim-nbr, &
	key-clm-serv-code '00000', key-clm-adj-nbr '0'   

select f002-claims-mstr if 			&
	     clmhdr-date-period-end >= 980701	& ; within range
        and  clmhdr-date-period-end <= 990630   &
	and  clmhdr-status-ohip <> "  "		& ;rejection
	and  clmhdr-status-ohip <> "00"          &
        and  clmhdr-status-ohip <> "EV"          &
        and  clmhdr-status-ohip <> "40"          &
        and  clmhdr-status-ohip <> "48"          &
        and  clmhdr-status-ohip <> "51"          &
        and  clmhdr-status-ohip <> "I5"          &
        and  clmhdr-status-ohip <> "80"          &
        and (   clmhdr-status-ohip <> "35"                              &
             or (    clmhdr-doc-dept = 5                                &
                  or clmhdr-doc-dept = 2)                               &
                )
select if   not record f096-ohip-pay-code exists                    	&
         or (    clmhdr-status-ohip = "35" 				&
  	     and (    clmhdr-doc-dept = 5   				&
                   or clmhdr-doc-dept = 2)				&
		)

select f020-doctor-mstr  if 			&
	doc-date-fac-term = 000000 or doc-date-fac-term > 990630

                              
; def x-doc-nbr  char*3  = clmhdr-claim-id[4:3]	
  def x-claim-id char*11 = clmhdr-claim-id[1:11] 
; def x-claim-id char*11 = clmhdr-claim-id[4:11] 

def nbr-svc zoned*6 unsigned = 0
def nbr-clm zoned*6 unsigned = 0
def nbr-dtl zoned*6 unsigned = 0

sort   on doc-nbr	&
       on x-claim-id

temp nbr-reject zoned*4 unsigned
item nbr-reject = nbr-reject + 1  reset at doc-nbr

def amt-ytd      zoned*8 signed = 0 
def misc-amt-ytd zoned*8 signed = 0 
def mohr-amt-ytd zoned*8 signed = 0 
def total-amt-ytd zoned*8 signed = 0

def man-reject   zoned*6 unsigned = 0

subfile costing2 keep append at doc-nbr  &
include                                     &
         doc-nbr,                           &
         doc-name,                          &
         doc-inits,                         &
         doc-dept,                          &
	 doc-clinic-nbr,		    &
         nbr-svc,                         & 
         nbr-clm,                         &
         nbr-dtl,                         &   
         nbr-reject,			  &
         amt-ytd,			  &
	 misc-amt-ytd,			  & 
	 mohr-amt-ytd,			  & 
	 total-amt-ytd,			  &
	 man-reject

  
request four

access f050-doc-revenue-mstr-history
select if ep-yr = 99 and docrev-key[1:1] <> "6"

sort on docrev-doc-nbr on docrev-clinic-1-2 d
subfile docclinic keep at docrev-doc-nbr	&
include docrev-doc-nbr, docrev-clinic-1-2, docrev-key

request five

access *docclinic

; b.e. july 20/99 - added qualification of clinic nbr
subfile f050_22     keep if docrev-clinic-1-2 of docclinic =  '22'	&
	include docclinic
subfile f050_not_22 keep if docrev-clinic-1-2 of docclinic <> '22'	&
	include docclinic


; program:
; purpose: put all doc's with values in clinic 22 into temp file

request five_b
set process nolimit

access *f050_22

sort on docrev-doc-nbr

output doc-totals-tmp add at docrev-doc-nbr
  item doc-nbr final docrev-doc-nbr


; program:
; purpose: put doc into subfile only if no '22' recs - ie no rec in temp file

request five_c

access *f050_not_22	&
	link docrev-doc-nbr to doc-nbr of doc-totals-tmp opt

select if not record doc-totals-tmp exists

subfile f050_non_22_claims_only include f050_not_22

; DOC: costrev.qzs
; DOC: 
; DOC: 
; DOC: RUN FOR: run for Chris Costanza
;
;PROGRAM PURPOSE : all clinics doc ytd revenue for 1998
;                 
;
;DATE       BY WHOM   DESCRIPTION
;99/03/26   YASEMIN   ORIGINAL
;
;
request six   				&
	on calculation errors report	&
	on edit errors report


access f050-doc-revenue-mstr-history                             &
  link docrev-doc-nbr 					 &
    to doc-nbr of f020-doctor-mstr   

choose docrev-key '22@'
select if ep-yr = 99

select   f020-doctor-mstr if    doc-date-fac-term = 000000         &
                             or doc-date-fac-term > 990630

sort on docrev-doc-nbr

def nbr-svc zoned*6 unsigned = 0
def nbr-clm zoned*6 unsigned = 0
def nbr-dtl zoned*6 unsigned = 0
def nbr-reject zoned*4 unsigned = 0

 
temp amt-ytd     zoned*8 signed 
item amt-ytd = amt-ytd +  docrev-ytd-in-rec + docrev-ytd-out-rec &
                if docrev-location ne "MISC"	&
               		reset at docrev-doc-nbr

temp misc-amt-ytd zoned*8 signed 
item misc-amt-ytd = misc-amt-ytd + docrev-ytd-out-rec  &
             if docrev-location = "MISC" and docrev-oma-code ne "MOHR"  &
               		reset at docrev-doc-nbr

temp mohr-amt-ytd zoned*8 signed 
item mohr-amt-ytd = mohr-amt-ytd + docrev-ytd-out-rec  &
               if docrev-location = "MISC" and docrev-oma-code = "MOHR"  &
               		reset at docrev-doc-nbr

def total-amt-ytd zoned*8 signed= amt-ytd + misc-amt-ytd + mohr-amt-ytd

def man-reject   zoned*6 unsigned = 0



subfile costing2 keep append at docrev-doc-nbr  &
include                                     &
         doc-nbr,                           &
         doc-name,                          &
         doc-inits,                         &
         doc-dept,                          &
	 doc-clinic-nbr,		    &
         nbr-svc,                         & 
         nbr-clm,                         &
         nbr-dtl,                         &   
         nbr-reject,			  &
         amt-ytd,			  &
	 misc-amt-ytd,			  & 
	 mohr-amt-ytd,			  & 
	 total-amt-ytd,			  &
	 man-reject

  
;PROGRAM PURPOSE : all manual rejected claims history
;                 
;
;DATE       BY WHOM   DESCRIPTION
;99/06/08   M. Chan   ORIGINAL
;
;
request seven 				&
	on calculation errors report	&
	on edit errors report


access manual-rejected-claims-hist                       &
  link clmhdr-doc-nbr 					 &
    to doc-nbr of f020-doctor-mstr   

choose clmhdr-doc-nbr  

select   f020-doctor-mstr if    doc-date-fac-term = 000000         &
                             or doc-date-fac-term > 990630

sorted on clmhdr-doc-nbr

def nbr-svc zoned*6 unsigned = 0
def nbr-clm zoned*6 unsigned = 0
def nbr-dtl zoned*6 unsigned = 0
def nbr-reject zoned*4 unsigned = 0

def amt-ytd     zoned*8 signed = 0 
def misc-amt-ytd zoned*8 signed = 0 
def mohr-amt-ytd zoned*8 signed = 0 
def total-amt-ytd zoned*8 signed = 0

temp man-reject  zoned*6 unsigned 
item man-reject  = man-reject + 1 reset at clmhdr-doc-nbr

subfile costing2 keep append at clmhdr-doc-nbr  &
include                                     &
         doc-nbr,                           &
         doc-name,                          &
         doc-inits,                         &
         doc-dept,                          &
	 doc-clinic-nbr,		    &
         nbr-svc,                         & 
         nbr-clm,                         &
         nbr-dtl,                         &   
         nbr-reject,			  &
         amt-ytd,			  &
	 misc-amt-ytd,			  & 
	 mohr-amt-ytd,			  & 
	 total-amt-ytd,			  &
	 man-reject

  


build costing
