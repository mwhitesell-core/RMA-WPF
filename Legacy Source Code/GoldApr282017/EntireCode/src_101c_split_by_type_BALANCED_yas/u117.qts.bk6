;#> PROGRAM         U117.QTS
;
;       ((C)) Dyad Technologies
;
;    PURPOSE: SUB-PROCESS WITHIN "EARNINGS GENERATION" PROCESS.
;	      CALCULATE 'TAX' TRANSACTIONS FOR ALL PAY CODES
;
;    MODIFICATION HISTORY
;        DATE    SAF #  WHO      DESCRIPTION
;     93/JAN/01  ____   B.E.     - original
;     93/MAY/20  ____   B.E.     - optimize, removed SELECT, round calc
;     93/JUN/08  ____   B.E.     - round using ROUND function
;   1999/Feb/18         S.B.     - Checked for Y2K.
;   1999/June/01 	S.B.     - Added the use file
;                        	   def_compensation_status.def to 
;                             	   prevent hard coding of compensation-status.
; 2001/may/22 B.E. subfile f119 made permanent for icu payroll
; 2001/may/23 B.E. gross amount of TAX was original the total dollars taxed
;		   which was a figure that was not used. Using this amount
;		   also increased the gross amount of the TOTDED transaction
;		   and this made the gross amt of the PAYEFT transaction
;		   be lower than the net amt since the gross amount was 
;		   calculated as TOTBIL - TOTDED. 
;		   This gross amount now set equal to net amount.
;

cancel clear
run u117

set default
set process nolimit

global temp w-current-ep-nbr        numeric
global temp w-current-ep-nbr-minus1 numeric
global temp tax-seq       zoned*2 unsigned
global temp tax-seq-rpt   zoned*2 unsigned
global temp tax-type      char*1
global temp tax-group     char*1
global temp tax-factor    numeric

global temp paypot-seq    zoned*2 unsigned
global temp paypot-type   char*1
global temp paypot-group  char*1
;-------------------------------------------------------------------------------
; OBTAIN THE CURRENT EP NUMBER AND PASS TO SUBSEQUENT REQUESTS
;
;REQUEST U117_CONST_MSTR_GET_EP_NBR		&
;		ON EDIT        ERRORS REPORT	&
;                ON CALCULATION ERRORS REPORT
;ACCESS CONSTANTS-MSTR-REC-6
;CHOOSE CONST-REC-NBR 6

;ITEM W-CURRENT-EP-NBR        FINAL  CURRENT-EP-NBR
;ITEM W-CURRENT-EP-NBR-MINUS1 FINAL  CURRENT-EP-NBR - 1
;
; DETERMINE THE 'PROCESS-SEQ' AND 'TRANSACTION TYPE'
; FOR THE TRANSACTIONS BEING CREATED IN THIS RUN
request u117_a_get_tax                       &
  		on edit        errors report &
 		on calculation errors report
access f190-comp-codes
choose comp-code "TAX"
item    tax-seq     =  process-seq
item    tax-seq-rpt =  reporting-seq
item    tax-type    =  comp-type
item    tax-group   =  comp-code-group
item    tax-factor  =  factor
request u117_a_get_paypot                    &
 		on edit        errors report &
		on calculation errors report
access f190-comp-codes
choose comp-code "PAYPOT"
item paypot-seq  =  process-seq
item paypot-type = comp-type

;-------------------------------------------------------------------------------
; PAY CODE 0/1/2/3/4
;
;
request u117_calc_potpay_0_1_2_3_4		&
		on edit        errors report	&
                on calculation errors report

access constants-mstr-rec-6	                	&
        link current-ep-nbr	                	&
	viaindex f112-ep-doc-nbr			&
        to   ep-nbr of f112-pycdceilings		&
	link doc-nbr of f112-pycdceilings,			      &
	     ep-nbr  of f112-pycdceilings,			      &
	     tax-seq,						      &
	     "TAX" 						      &
	to   doc-nbr,						      &
	     ep-nbr,						      &
	     process-seq,					      &
	     comp-code    of f110-compensation          	      &
	               alias f110-tax          optional		      &
	link doc-nbr of f112-pycdceilings,			      &
	     ep-nbr  of f112-pycdceilings,			      &
	     paypot-seq,					      &
	     "PAYPOT" 						      &
	to   doc-nbr,						      &
	     ep-nbr,						      &
	     process-seq,					      &
	     comp-code    of f110-compensation          	      &
	               alias f110-paypot       optional		

choose const-rec-nbr 6

; round tax calc
temp amt-net-tax integer*8 signed size 4	 		
item amt-net-tax 	 		&
     =round(( amt-net of f110-paypot				       &
             * tax-factor)/10000,0,near) if not record f110-tax exists&
 else round((  amt-net of f110-paypot				       &
	     * factor of f110-tax)/10000,0,near) if     record f110-tax exists
item amt-net-tax = 0 if amt-net-tax lt 0 else amt-net-tax

;S.B.
use $use/def_compensation_status.def

subfile u117_test keep                                     include &
         doc-nbr of f112-pycdceilings	, &
         ep-nbr of f112-pycdceilings, &
         paypot-seq, &
         amt-net of f110-paypot         , &
         factor of f110-tax     	, &
         tax-factor, &
    amt-net-tax

; --------- TAX ---------
output f110-compensation add alias f110-output-tax-not-exists	&
    if not record f110-tax exists
	item ep-nbr          =  current-ep-nbr
	item ep-nbr-entry    =  current-ep-nbr
	item comp-code       =  "TAX"
	item comp-type	     =  tax-type
	item process-seq     =  tax-seq
	item factor          =  tax-factor
	item factor-override =  " "
	item comp-units      =  comp-units of f110-paypot

;  	   item amt-gross       = amt-net of f110-paypot
	item amt-gross       = amt-net-tax

	item amt-net         = amt-net-tax
	item compensation-status =  compensation-status-accepted
	item last-mod-date    =  sysdate
	item last-mod-time    =  systime / 10000
	item last-mod-user-id =  "U117 gen'd"

output f110-tax          update                               &
    if     record f110-tax exists
	item ep-nbr          = current-ep-nbr
	item factor-override = "*"	; INDICATE OVERRIDE FACTOR USED
	item comp-units      = comp-units      of f110-paypot
	item amt-gross       = amt-net         of f110-paypot
	item amt-net         = amt-net-tax
	item compensation-status = compensation-status-accepted
	item last-mod-date    = sysdate
	item last-mod-time    = systime / 10000
	item last-mod-user-id = "U117 Upd'd"

define x-not-needed integer*8 signed size 4 = 0
define x-rec-type  char*1 = "A"
define x-comp-code char*6 = "TAX"
subfile f119 keep append                                   include &
         doc-nbr of f112-pycdceilings	, &
         x-comp-code			, &
         tax-seq-rpt                	, &
         tax-group   			, &
         x-rec-type			, &
         x-not-needed    		, &
         amt-net-tax


build $pb_obj/u117
