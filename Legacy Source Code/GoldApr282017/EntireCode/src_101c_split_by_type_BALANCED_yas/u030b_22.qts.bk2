;#> PROGRAM-ID.     U030B_22.QTS
;
;	((C)) Dyad Technologies
;
;    PROGRAM PURPOSE : RERUN CLINIC 22 FOR THE UNMATCH FILE.
;		       RENAME U030_UNMATCH TO U030_GOOD_145_REC
;		
;
set default
set process nolimit


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request sort_good_records on calculation errors report on edit errors report
				; sort the record by account nbr from
				; good subfile

access *u030_good_145_rec

sort on rat-145-account-nbr on rat-145-explan-cd

subfile u030_sort_145_file keep include u030_good_145_rec

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request extract_claims on calculation errors report on edit errors report
                      ; Extract all claims into match and unmatch
		      ; subfiles.  For the match claims, update the
		      ; amount paid to CLAIMS MSTR. Summarize total
		      ; amount paid. Create partially paid claims
		      ; into PART_PAID_HDR file.
		

access *u030_sort_145_file  					&
  link 'B', nconvert('220' + rat-145-account-nbr[1:6]), 	&
 	nconvert(rat-145-account-nbr[7:2]) , '00000', '0'  	&
   to key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr, 	&
      key-clm-serv-code, key-clm-adj-nbr of f002-claims-mstr optional

sorted on rat-145-account-nbr

define x-type char*1 = 'H'
define x-oma-found char*1 = 'Y'
define x-tech-amt int*7 signed = 0
define x-prof-amt int*7 signed = 0
def clmhdr-claim-id zoned*11 unsigned = 0


def x-out-bal int*7 signed =   clmhdr-manual-and-tape-payments &
	 + clmhdr-tot-claim-ar-ohip

def x-bal-due char*1 = 'Y' if x-out-bal <> 0

def x-rat-145-amt-paid int*7 signed = rat-145-amt-paid * -1

temp x-tot-amt-paid int*7 signed
   item x-tot-amt-paid subtotal rat-145-amt-paid 		&
	if (record f002-claims-mstr exists and			&
	  (clmhdr-agent-cd = 0 or clmhdr-agent-cd = 2  		&
		or clmhdr-agent-cd = 4))			&
	reset at rat-145-account-nbr

temp x-tot-amt-bill int*7 signed
   item x-tot-amt-bill subtotal rat-145-amount-sub		&
	if (record f002-claims-mstr exists and			&
	  (clmhdr-agent-cd = 0 or clmhdr-agent-cd = 2  		&
		or clmhdr-agent-cd = 4))			&
	reset at rat-145-account-nbr

subfile u030_dtl keep						&
   if (record f002-claims-mstr exists and                      	&
      (clmhdr-agent-cd = 0 or clmhdr-agent-cd = 2  	 	&
		or clmhdr-agent-cd = 4))			&
;  INCLUDE U030_SORT_145_FILE - 97/09/08
   include u030_sort_145_file, clmhdr-claim-id

subfile u030_unmatch keep  					&
   if   (not record f002-claims-mstr exists 			&
     or  (clmhdr-agent-cd <> 0	and clmhdr-agent-cd <> 2  	&
		and clmhdr-agent-cd <> 4))			&
   include u030_sort_145_file, x-type, x-oma-found, 		&
;	   X-TECH-AMT, X-PROF-AMT - 97/09/08
	   x-tech-amt, x-prof-amt, clmhdr-claim-id

output part-paid-hdr add on errors report                       &
        at rat-145-account-nbr if (x-bal-due = 'Y'		&
	and  record f002-claims-mstr exists and			&
	(clmhdr-agent-cd = 0 or clmhdr-agent-cd = 2		&
		or clmhdr-agent-cd = 4))		
   item  part-hdr-clinic-nbr initial 22	;	&
;	NCONVERT(RAT-145-GROUP-NBR OF U030_SORT_145_FILE[1:2])
;	CLAIM-ID OF F099-GROUP-CLAIM-MSTR / 1000000000
   item  part-hdr-claim-nbr initial 				&
		nconvert(rat-145-account-nbr of u030_sort_145_file)
   item  part-hdr-amt-bill  initial clmhdr-tot-claim-ar-ohip
   item  part-hdr-amt-paid  final   x-tot-amt-paid
   item  part-hdr-ohip-bill final   x-tot-amt-bill
   item  part-hdr-last-name initial rat-145-last-name of u030_sort_145_file &
	if rat-145-last-name of u030_sort_145_file <> ' '	&
      else clmhdr-pat-acronym[1:6]
   item  part-hdr-first-name initial rat-145-first-name of u030_sort_145_file &
	if rat-145-first-name of u030_sort_145_file <> ' '	&
      else clmhdr-pat-acronym[7:3]
   item  part-hdr-ohip-clm-nbr initial rat-145-claim-nbr of u030_sort_145_file
   item  part-hdr-version-cd initial rat-145-version-cd of u030_sort_145_file
   item  part-hdr-pay-pgm initial rat-145-pay-prog of u030_sort_145_file
   item  part-hdr-register-nbr initial rat-145-health-ohip-nbr of u030_sort_145_file
   item  part-hdr-explan-cd final rat-145-explan-cd of u030_sort_145_file

output f002-claims-mstr update on errors report 		&
  	at rat-145-account-nbr					&
	if (record f002-claims-mstr exists and			&
	(clmhdr-agent-cd = 0 or clmhdr-agent-cd = 2		&
		or clmhdr-agent-cd = 4))		
   item clmhdr-manual-and-tape-payments 			&	
	subtotal x-rat-145-amt-paid
   item clmhdr-date-cash-tape-payment final ascii(sysdate,6)
   item clmhdr-status-ohip	   final part-hdr-explan-cd


subfile u030_paid_amt keep at rat-145-account-nbr		&
	if (record f002-claims-mstr exists and 			&
	   (clmhdr-agent-cd = 0 or clmhdr-agent-cd = 2  	&
		or clmhdr-agent-cd = 4))			&
include rat-145-group-nbr of u030_sort_145_file, 		&
	rat-145-account-nbr of u030_sort_145_file,		&
	clmhdr-doc-dept, clmhdr-loc,				&
 	x-tot-amt-paid, x-tot-amt-bill, clmhdr-agent-cd,	&
	clmhdr-claim-id

;97/09/08 - WRITE A RECORD TO SUBFILE IF TRANSLATION RECORD NOT EXIST


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request split_claims on calculation errors report on edit errors report
                      ; Extract all claims into fully and partially
		      ; paid subfiles.

;ACCESS *U030_PAID_AMT LINK (RAT-145-GROUP-NBR[1:2] + '0' +   &
;RAT-145-ACCOUNT-NBR) TO PART-HDR-CLAIM-ID OF PART-PAID-HDR OPT

access *u030_paid_amt link                                   &
	'220' + rat-145-account-nbr to part-hdr-claim-id of  part-paid-hdr opt

subfile u030_fully_paid keep if not record part-paid-hdr exists &
	include u030_paid_amt

subfile u030_partially_paid keep if record part-paid-hdr exists &
	include u030_paid_amt

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request update_cash_mstr on calculation errors report on edit errors report
		      ; Update Doctor Cash File


access *u030_paid_amt      					


output f051-doc-cash-mstr add update on errors report           &
        viaindex docash-key using                		&	
;  RAT-145-GROUP-NBR[1:2] + ASCII(CLMHDR-DOC-DEPT,2) +		&
   '22'                   +  ascii(clmhdr-doc-dept,2) +		&
   rat-145-account-nbr[1:3] + clmhdr-loc + ascii(clmhdr-agent-cd,1)
   item  docash-mtd-in-rec subtotal x-tot-amt-paid
   item  docash-ytd-in-rec subtotal x-tot-amt-paid
   item  docash-mtd-in-svc initial 0
   item  docash-ytd-in-svc initial 0


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request create_isam_dtl on calculation errors report on edit errors report
                        ; Create partially paid claim details
			; Add the sort stmt on 94/01/07
			; Create one record per ohip code
			; 94/08/03 - update PART-HDR-SERV-DATE


access *u030_dtl          					&
;  LINK (RAT-145-GROUP-NBR[1:2] + '0' +	RAT-145-ACCOUNT-NBR)    &
   link ('220' + rat-145-account-nbr)                           &
   viaindex part-hdr-claim-id to part-paid-hdr

sort on part-hdr-claim-id on rat-145-service-cd on rat-145-explan-cd

;OUTPUT PART-PAID-DTL ADD ON ERRORS REPORT
output part-paid-dtl add at rat-145-service-cd on errors report
;   ITEM PART-DTL-CLINIC-NBR INITIAL NCONVERT(RAT-145-GROUP-NBR[1:2])
   item part-dtl-clinic-nbr initial 22
   item part-dtl-claim-nbr initial nconvert(rat-145-account-nbr)
   item part-dtl-oma-cd    initial rat-145-service-cd
;  ITEM PART-DTL-AMT-BILL  INITIAL RAT-145-AMOUNT-SUB
;  ITEM PART-DTL-AMT-PAID  INITIAL RAT-145-AMT-PAID
   item part-dtl-amt-bill  subtotal rat-145-amount-sub
   item part-dtl-amt-paid  subtotal rat-145-amt-paid
;  ITEM PART-DTL-EXPLAN-CD INITIAL RAT-145-EXPLAN-CD
   item part-dtl-explan-cd final   rat-145-explan-cd
   item part-dtl-serv-date initial rat-145-service-date
   item part-dtl-equiv-flag initial ' '
;  ITEM PART-DTL-NBR-SERV INITIAL RAT-145-NBR-OF-SERV
   item part-dtl-nbr-serv subtotal rat-145-nbr-of-serv

output part-paid-hdr update  at part-hdr-claim-id on errors report
   item part-hdr-serv-date final rat-145-service-date


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request match_clmdtl on calculation errors report on edit errors report
                   ; Check if every rat dtl matches to claims mstr file.
		   ; If no match, put into the subfile, so they can
		   ; try to match with equivalence table in next request

access part-paid-dtl 					&
   link 'B', nconvert(part-dtl-claim-id[1:9]),	 	&
        nconvert(part-dtl-claim-id[10:2]),		&
        part-dtl-oma-cd, '0'				&
    to   key-clm-type, key-clm-batch-nbr, 		&
	 key-clm-claim-nbr, key-clm-serv-code,		&
	 key-clm-adj-nbr   of f002-claims-mstr	optional

sel if not record f002-claims-mstr exists
	
subfile u030_nomatch_dtl keep include part-paid-dtl


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request match_equiv_table on calculation errors report on edit errors report
                   ; Match rat detail with equiv table. If match, update
		   ; PART-PAID-DTL record with the equivalence oma code
		   ; and set equiv flag.  If no match, put into subfile.

access *u030_nomatch_dtl 				&
	link part-dtl-oma-cd to orig-oma-code of f098-equiv-oma-code-mstr optional

output part-paid-dtl update if record f098-equiv-oma-code-mstr exists &
	via part-dtl-clinic-nbr, part-dtl-claim-nbr,		&
    	    part-dtl-oma-cd                      	  	&
      using part-dtl-clinic-nbr of u030_nomatch_dtl,		&
            part-dtl-claim-nbr  of u030_nomatch_dtl,		&
            part-dtl-oma-cd     of u030_nomatch_dtl 		&
      on errors report
   item part-dtl-oma-cd of part-paid-dtl final equiv-oma-code
   item part-dtl-equiv-flag of part-paid-dtl final '?'

subfile u030_no_equiv keep 					&
	if not record f098-equiv-oma-code-mstr exists	  	&
	include u030_nomatch_dtl


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request determine_adjustment on calculation errors report on edit errors report
                             ; Determine if automatic 'B' Adjustment
			     ; is to be generated
			     ; Automatic adjustment if balance is
			     ; less than $1.00 for overpayment and
			     ; underpayment
			     ; 94/01/08 - Consider the Social contract
			     ; adjustment, create a new holdback subfile
			     ; and overpay subfile
			     ; 94/08/03 - add linkage to SOCIAL-CONTRACT
			     ; -FACTOR file, select the correct factor
			     ; based on the service date

access part-paid-hdr 	 					     &
	link nconvert(part-hdr-claim-id[1:2]) to iconst-mstr-rec     &
	link part-hdr-explan-cd to rat-code of f096-ohip-pay-code opt &
	link nconvert(part-hdr-claim-id[1:2]) to social-contract-factor

sel if  part-hdr-serv-date ge const-serv-date-from &
    and part-hdr-serv-date le const-serv-date-to


def x-part-bal int*7 signed = part-hdr-amt-bill - part-hdr-amt-paid

def x-over char*1 = 'Y' if x-part-bal < 0 else ' '

def x-under char*1 = 'Y' if x-part-bal > 0 else ' '

def x-part-bal-diff int*7 signed = x-part-bal * -1 if x-over = 'Y' &
	else x-part-bal

def x-bal-flag char*1 = 'Y'				&
     if  part-hdr-ohip-bill = part-hdr-amt-bill		&
     else 'N'

def x-auto-adj char*1 = 'Y'				&
   if   (x-part-bal-diff le 99)				&
   or  ((x-over = 'Y' and part-hdr-explan-cd = ' ' 	&
     and x-part-bal-diff le iconst-clinic-over-lim1) 	&
   or   (x-over = 'Y' and part-hdr-explan-cd <> ' ' 	&
     and x-part-bal-diff le iconst-clinic-over-lim4) 	&
   or   (x-under = 'Y' and part-hdr-amt-paid <> 0  	&
     and x-part-bal-diff le iconst-clinic-under-lim2) 	&
   or   (x-under = 'Y' and record f096-ohip-pay-code exists &
     and x-part-bal-diff le iconst-clinic-under-lim3)   &
   and  (x-bal-flag = 'Y'))

;DEF X-RED-AMOUNT INT*4 = PART-HDR-AMT-BILL * ICONST-REDUCTION-FACTOR &
def x-red-amount int*4 = part-hdr-amt-bill * const-reduction-factor &
			 / 10000

def x-from-red-range int*6 = x-red-amount - 5 if x-red-amount <> 0
def x-to-red-range int*6 = x-red-amount + 5 if x-red-amount <> 0

;DEF X-OVER-AMOUNT INT*6  = PART-HDR-AMT-BILL * ICONST-OVERPAY-FACTOR &
def x-over-amount int*6  = part-hdr-amt-bill * const-overpay-factor &
			 / 10000

def x-from-over-range int*6 = x-over-amount - 5 if x-over-amount <> 0
def x-to-over-range int*6 = x-over-amount + 5 if x-over-amount <> 0

def x-hold-back char*1 = 'Y'				&
	if x-part-bal ge x-from-red-range  and 		&
	   x-part-bal le x-to-red-range    and		&
	   part-hdr-pay-pgm = 'HCP'        and		&
	   x-under = 'Y'				&
     else  'N'

def x-over-pay  char*1 = 'Y'				&
	if x-part-bal-diff ge x-from-over-range and 	&
	   x-part-bal-diff le x-to-over-range and	&
	   part-hdr-pay-pgm = 'HCP'        and		&
	   x-over = 'Y'					&
     else  'N'

def x-adj-serv-code char*5 = 'Y900A' if x-hold-back = 'Y' &
			else 'Y901A' if x-over-pay  = 'Y'

subfile u030_auto_adj keep if x-auto-adj = 'Y'          &
			and   x-hold-back = 'N'		&
			and   x-over-pay  = 'N'		&
			and part-hdr-claim-id[1:2] ne "81"	&
			and part-hdr-claim-id[1:2] ne "82"	&
			and part-hdr-claim-id[1:2] ne "83"	&
;  INCLUDE U030_HIGH_OMA_CD
   include part-paid-hdr, x-bal-flag, x-part-bal

subfile u030_no_adj keep if (x-auto-adj <> 'Y'	        &
			and   x-hold-back = 'N'		&
			and   x-over-pay  = 'N')	&
			or part-hdr-claim-id[1:2] = "81"	&
			or part-hdr-claim-id[1:2] = "82"	&
			or part-hdr-claim-id[1:2] = "83"	&
;  INCLUDE U030_HIGH_OMA_CD, X-BAL-FLAG
   include part-paid-hdr, x-bal-flag

subfile u030_holdback keep if x-hold-back = 'Y'		&
   include part-paid-hdr, x-bal-flag

subfile u030_overpay  keep if x-over-pay  = 'Y'		&
   include part-paid-hdr, x-bal-flag

subfile u030_tot_claims keep        			&
;  INCLUDE U030_HIGH_OMA_CD, X-AUTO-ADJ, X-BAL-FLAG
   include part-paid-hdr, x-auto-adj, x-bal-flag,       &
           x-hold-back, x-over-pay, x-adj-serv-code, x-part-bal
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request summ_claim_serv_code on calculation errors report on edit errors report
			; Summarize duplicate ohip code details
			; into one record per claim

access *u030_auto_adj   				&
   link 'B', nconvert(part-hdr-claim-id[1:9]),		&
	 nconvert(part-hdr-claim-id[10:2])		&
    to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr &
        of f002-claims-mstr

sel if clmdtl-oma-cd <> '0000' and clmdtl-oma-cd <> 'ZZZZ' &
  and  clmdtl-adj-nbr = 0

sorted on part-hdr-claim-id on key-clm-serv-code

temp x-amt-billed int*7
   item x-amt-billed = x-amt-billed + clmdtl-fee-ohip 	&
		reset at key-clm-serv-code

subfile u030_summ_serv_code keep at key-clm-serv-code   &
   include x-amt-billed, key-clm-serv-code, 		&
 	   part-hdr-claim-id, part-hdr-claim-nbr,       &
	   part-hdr-clinic-nbr, clmdtl-diag-cd,         &
	   clmdtl-sv-date, clmdtl-line-no, x-part-bal

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request match_dtl on calculation errors report on edit errors report
                   ; Match paid detail and claim detail, calculate
		   ; the difference in payment for each match detail
		   ; 91/12/16 - Select claim detail which has the
		   ; balance not equal to 0


;ACCESS PART-PAID-HDR 					&
;  LINK 'B', NCONVERT(PART-HDR-CLAIM-ID[1:9]), 		&
;     		NCONVERT(PART-HDR-CLAIM-ID[10:2])   	&
;   TO   KEY-CLM-TYPE, KEY-CLM-BATCH-NBR, 		&
;	 KEY-CLM-CLAIM-NBR OF F002-CLAIMS-MSTR		&
access *u030_summ_serv_code				&
   link  part-hdr-clinic-nbr, part-hdr-claim-nbr, key-clm-serv-code &
    to   part-dtl-clinic-nbr, part-dtl-claim-nbr, part-dtl-oma-cd   &
	 of part-paid-dtl optional

sorted on part-hdr-claim-id

;DEF X-BAL-DIFF INT*7 SIGNED = CLMDTL-FEE-OHIP - PART-DTL-AMT-PAID
def x-bal-diff int*7 signed = x-amt-billed  - part-dtl-amt-paid

temp x-sel-dtl
item x-sel-dtl = x-sel-dtl + 1 if x-bal-diff <> 0 and   &
	record part-paid-dtl exist reset at part-hdr-claim-id

temp x-tot-bal
item x-tot-bal = x-tot-bal + x-bal-diff if x-bal-diff <> 0 and   &
	record part-paid-dtl exist reset at part-hdr-claim-id

def x-adj-serv-code char*5 = key-clm-serv-code

;Select all Claim detail records except adjustment one
;SEL F002-CLAIMS-MSTR IF CLMDTL-OMA-CD <> '0000'		&
;                   AND CLMDTL-OMA-CD <> 'ZZZZ'		&
;	    AND CLMDTL-ADJ-NBR = 0 		

; 91/12/16 - add the following selection
;SEL IF X-BAL-DIFF <> 0  - TAKE THIS SELECT OUT ON 92/08/17 BY MC.

subfile u030_auto_adjdtl keep if x-bal-diff <> 0 and      &
    record part-paid-dtl exist	        		&
;   PART-PAID-HDR, PART-PAID-DTL, KEY-CLM-SERV-CODE, X-BAL-DIFF
   include x-adj-serv-code,   				&
 	   part-hdr-claim-id,                           &
	   clmdtl-diag-cd,         			&
	   clmdtl-sv-date, clmdtl-line-no, x-bal-diff,  &
	   part-dtl-explan-cd

subfile u030_no_adjclm_created keep at part-hdr-claim-id &
   include x-adj-serv-code,   				&
 	   part-hdr-claim-id of u030_summ_serv_code,     &
           clmdtl-diag-cd of u030_summ_serv_code,        &
  	   clmdtl-sv-date of u030_summ_serv_code,   	 &
 	   clmdtl-line-no of u030_summ_serv_code,       &
 	   part-dtl-explan-cd of part-paid-dtl,       	&
 	   x-part-bal of u030_summ_serv_code,		&
           x-tot-bal, x-sel-dtl

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;REQUEST CREATE_DUMMY_SERV_CODE
				; FOR THE CLAIMS THAT DO NOT HAVE ANY
				; MATCHING DETAIL, OR THE CLAIMS THAT
				; STILL HAVE THE REMAINING BALANCE,
				; CREATE THE ADJUSTMENT CLMDTL WITH
				; THE DUMMY OHIP CODE 'Y999A'.

;ACCESS *U030_NO_ADJCLM_CREATED

;DEF X-BAL-DIFF INT*7 SIGNED = X-PART-BAL  - X-TOT-BAL
;DEF X-SERV-CODE CHAR*5 = 'Y999A'

;SEL IF X-SEL-DTL = 0  OR (X-BAL-DIFF <> 0 AND X-SEL-DTL > 0)

;SUBFILE U030_AUTO_ADJDTL APPEND         		&
;  INCLUDE X-SERV-CODE,        				&
;	   PART-HDR-CLAIM-ID,                           &
;	   CLMDTL-DIAG-CD,         			&
;	   CLMDTL-SV-DATE, CLMDTL-LINE-NO,              &
;	   X-BAL-DIFF, PART-DTL-EXPLAN-CD


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request gen_holdback_overpay_dtl on calculation errors report on edit errors report

access *u030_tot_claims      				&
   link 'B', nconvert(part-hdr-claim-id[1:9]), 		&
      		nconvert(part-hdr-claim-id[10:2])   	&
    to   key-clm-type, key-clm-batch-nbr, 		&
 	 key-clm-claim-nbr of f002-claims-mstr		&
   link  part-hdr-clinic-nbr, part-hdr-claim-nbr,	&
	 key-clm-serv-code				&
    to   part-dtl-clinic-nbr, part-dtl-claim-nbr,       &
	 part-dtl-oma-cd of part-paid-dtl optional

;Select all Claim detail records except adjustment one
sel f002-claims-mstr if clmdtl-oma-cd <> '0000'		&
                    and clmdtl-oma-cd <> 'ZZZZ'		&
 		    and clmdtl-adj-nbr = 0 		&
		    and (x-hold-back = 'Y' or x-over-pay = 'Y')

define x-dtl-bal-diff int*7 signed = clmdtl-fee-ohip - part-dtl-amt-paid
define x-bal-diff int*7 signed = x-part-bal

subfile u030_paid_diff keep        			&
   include x-adj-serv-code,    				&
 	   part-hdr-claim-id,                           &
	   clmdtl-diag-cd,         			&
	   clmdtl-sv-date, clmdtl-line-no,		&
	   x-bal-diff, x-dtl-bal-diff, part-dtl-explan-cd

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request sort_by_diff_bal on calculation errors report on edit errors report
                         ; Extract the Oma Code with the highest
			 ; outstanding balance

access *u030_paid_diff

sort on part-hdr-claim-id on x-dtl-bal-diff

subfile u030_auto_adjdtl append  at part-hdr-claim-id 	&
   include x-adj-serv-code,    				&
 	   part-hdr-claim-id,                           &
	   clmdtl-diag-cd,         			&
	   clmdtl-sv-date, clmdtl-line-no,            	&
	   x-bal-diff, part-dtl-explan-cd


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


;REQUEST SORT_BEFORE_ADJ ON CALCULATION ERRORS REPORT ON EDIT ERRORS REPORT
			;91/12/16 - Comment out the linkage to F002-
			; CLMHDR and define and selection statements
			; Add the sorted Statement, create only one
			; record per claim, this will prevent multiple
			; records for the same claim.
			; Store CLMDTL-DIAG-CD and CLMDTL-SV-DATE in
			; the subfile


;ACCESS *U030_AUTO_ADJ					&
;  LINK 'B', NCONVERT(PART-HDR-CLAIM-ID[1:9]), 		&
;	 NCONVERT(PART-HDR-CLAIM-ID[10:2]),		&
;	 KEY-CLM-SERV-CODE OF U030_AUTO_ADJ, '0'	&
;   TO   KEY-CLM-TYPE, KEY-CLM-BATCH-NBR, 		&
;	 KEY-CLM-CLAIM-NBR, KEY-CLM-SERV-CODE, KEY-CLM-ADJ-NBR &
;        OF F002-CLAIMS-MSTR ALIAS F002-CLMDTL		


;SORTED ON PART-HDR-CLAIM-ID

;SUBFILE U030_SRT_DTL KEEP AT PART-HDR-CLAIM-ID INCLUDE  &
;	U030_AUTO_ADJ, CLMDTL-DIAG-CD, CLMDTL-SV-DATE, CLMDTL-LINE-NO

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request calc_batch_nbr on calculation errors report on edit errors report

access *u030_auto_adjdtl

sort on part-hdr-claim-id on x-adj-serv-code

temp x-count
  item x-count count

temp x-batch-count
  item x-batch-count = ceiling(x-count / 99)

temp x-claim-bal int*7 signed
  item x-claim-bal = x-claim-bal + x-bal-diff &
		reset at part-hdr-claim-id

subfile u030_srt_adj keep include                         &
        u030_auto_adjdtl,  x-count, x-batch-count

subfile u030_update_clmhdr keep at part-hdr-claim-id include &
	part-hdr-claim-id of u030_auto_adjdtl, x-claim-bal

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request create_b_adjustment on calculation errors report on edit errors report
                            ; Create a batch control record for every
			    ; 99 claim. Create a clmhdr and a clmdtl
			    ; record, and inverted clmdtl key to the
			    ; each adjusting clmdtl record in the
			    ; subfile. At the end of the request,
			    ; update the batch nbr in Constant Mstr
			    ; Note:  Create adjustment only if the
			    ;        Rma Billed = Ohip Billed
			    ; 91/12/16 - Comment out the linkage to
			    ; F002-CLMDTL, some define statements and
			    ; selection statement.  Create a new subfile
 			    ; that store all adjusted batches.
			    ; 93/02/19 - link to doctor master to use
			    ; the department instead of from claims


access *u030_srt_adj					&
   link  'B', nconvert(part-hdr-claim-id[1:9]),      	&
	 nconvert(part-hdr-claim-id[10:2]), '00000', '0' &
    to   key-clm-type, key-clm-batch-nbr, 		&
	 key-clm-claim-nbr, key-clm-serv-code, key-clm-adj-nbr &
         of f002-claims-mstr alias f002-clmhdr		&
  link   nconvert(part-hdr-claim-id[4:3]) 		&
   to    doc-nbr of f020-doctor-mstr			&
   and   nconvert(part-hdr-claim-id[1:2]) to iconst-mstr-rec

sorted on x-batch-count on part-hdr-claim-id


temp x-batch-nbr int*6
  item x-batch-nbr = iconst-clinic-batch-nbr + x-batch-count   &
	       at x-batch-count				 &
               reset at initial to  iconst-clinic-batch-nbr

define x-clinic-batch-nbr int*9 =			&
	nconvert(part-hdr-claim-id[1:2] +  		&
	         ascii(x-batch-nbr,7))

define x-mod       = mod(x-count,99)

define x-claim-nbr = x-mod if x-mod <> 0 else 99

define x-ohip-bal int*7 signed = 			&
;	(PART-HDR-AMT-BILL - PART-HDR-AMT-PAID) * -1
	x-bal-diff * -1

define x-tot-claim-ar-oma int*7 signed =		&
  round((clmhdr-tot-claim-ar-oma of f002-clmhdr /	&
	 clmhdr-tot-claim-ar-ohip of f002-clmhdr) * x-ohip-bal)

define x-amt-tech-billed  int*7 signed =		&
  round((clmhdr-amt-tech-billed  of f002-clmhdr /	&
	 clmhdr-tot-claim-ar-ohip of f002-clmhdr) * x-ohip-bal)

output f001-batch-control-file add at x-batch-count on errors report
   item batctrl-batch-nbr 	   initial x-clinic-batch-nbr
   item batctrl-batch-type 	   initial 'A'
   item batctrl-clinic-nbr         initial iconst-clinic-nbr
   item batctrl-adj-cd 		   initial 'B'
   item batctrl-date-batch-entered initial ascii(sysdate,6)
   item batctrl-date-period-end    initial ascii(iconst-date-period-end,6)
   item batctrl-cycle-nbr 	   initial iconst-clinic-cycle-nbr
   item batctrl-ar-yy-mm 	   initial '0000'
   item batctrl-adj-cd-sub-type    initial 'A'
   item batctrl-nbr-claims-in-batch final x-claim-nbr
   item batctrl-last-claim-nbr      final x-claim-nbr
   item batctrl-batch-status	    final "1"
   item batctrl-amt-act             subtotal x-ohip-bal
   item batctrl-amt-est             subtotal x-ohip-bal
   item batctrl-calc-ar-due         subtotal x-ohip-bal
   item batctrl-calc-tot-rev        subtotal x-ohip-bal
   item batctrl-agent-cd	    final clmhdr-agent-cd of f002-clmhdr


output f002-claims-mstr alias f002-adj-hdr add noitems on errors report
;Preset claim header data from batctrl record
   item key-clm-type	           initial 'B'
   item key-clm-batch-nbr 	   initial x-clinic-batch-nbr
   item key-clm-claim-nbr	   initial x-claim-nbr
   item key-clm-serv-code	   initial '00000'
   item key-clm-adj-nbr		   initial '0'
   item clmhdr-orig-batch-nbr	   initial batctrl-batch-nbr
   item clmhdr-orig-claim-nbr	   initial x-claim-nbr
   item clmhdr-batch-type          initial batctrl-batch-type
   item clmhdr-adj-cd-sub-type     initial batctrl-adj-cd-sub-type
;  ITEM CLMHDR-AGENT-CD            INITIAL BATCTRL-AGENT-CD
   item clmhdr-adj-cd              initial batctrl-adj-cd
   item clmhdr-date-period-end	   initial nconvert(batctrl-date-period-end)
   item clmhdr-cycle-nbr	   initial batctrl-cycle-nbr
; Preset claim header data with values from claim being adjusted
   item clmhdr-claim-id  	   initial clmhdr-claim-id of f002-clmhdr
   item clmhdr-diag-cd  	   initial clmhdr-diag-cd of f002-clmhdr
   item clmhdr-hosp         	   initial clmhdr-hosp of f002-clmhdr
   item clmhdr-i-o-pat-ind         initial clmhdr-i-o-pat-ind of f002-clmhdr
   item clmhdr-agent-cd            initial clmhdr-agent-cd of f002-clmhdr
   item clmhdr-loc                 initial clmhdr-loc of f002-clmhdr
   item clmhdr-pat-ohip-id-or-chart initial clmhdr-pat-ohip-id-or-chart of f002-clmhdr
   item clmhdr-pat-acronym         initial clmhdr-pat-acronym of f002-clmhdr
;  ITEM CLMHDR-DOC-DEPT            INITIAL CLMHDR-DOC-DEPT OF F002-CLMHDR
   item clmhdr-doc-dept            initial doc-dept of f020-doctor-mstr
; Assign values to the remaining fields
   item clmhdr-reference	   initial part-dtl-explan-cd
   item clmhdr-date-admit          initial '000000'
   item clmhdr-date-cash-tape-payment initial ascii(sysdate,6)
   item clmhdr-date-sys            initial ascii(sysdate,6)
   item clmhdr-status-ohip         initial '00'
   item clmhdr-tape-submit-ind     initial 'N'
   item clmhdr-doc-nbr-ohip        initial 0
   item clmhdr-doc-spec-cd         initial 0
   item clmhdr-refer-doc-nbr       initial 0
   item clmhdr-curr-payment        initial 0
   item clmhdr-amt-tech-paid       initial 0
   item clmhdr-manual-and-tape-payments initial 0
;
   item clmhdr-amt-tech-billed 	   initial x-amt-tech-billed
   item clmhdr-tot-claim-ar-oma    initial x-tot-claim-ar-oma
   item clmhdr-tot-claim-ar-ohip   initial x-ohip-bal


output f002-claims-mstr alias f002-adj-dtl add noitems on errors report
   item key-clm-type               initial 'B'
   item key-clm-batch-nbr          initial x-clinic-batch-nbr
   item key-clm-claim-nbr          initial x-claim-nbr
   item key-clm-serv-code          initial x-adj-serv-code of u030_srt_adj
   item key-clm-adj-nbr            initial '0'
   item clmdtl-batch-nbr           initial clmhdr-batch-nbr of f002-adj-hdr
   item clmdtl-claim-nbr           initial clmhdr-claim-nbr of f002-adj-hdr
   item clmdtl-oma-cd              initial x-adj-serv-code  of u030_srt_adj[1:4]
   item clmdtl-oma-suff            initial x-adj-serv-code of u030_srt_adj[5:1]
   item clmdtl-adj-nbr             initial 1
   item clmdtl-agent-cd            initial clmhdr-agent-cd of f002-adj-hdr
   item clmdtl-adj-cd              initial clmhdr-adj-cd of f002-adj-hdr
;  ITEM CLMDTL-SV-DATE             INITIAL CLMDTL-SV-DATE OF F002-CLMDTL
   item clmdtl-sv-date             initial clmdtl-sv-date of u030_srt_adj
   item clmdtl-nbr-serv            initial 0
   item clmdtl-consec-dates        initial 0
   item clmdtl-date-period-end     initial ascii(clmhdr-date-period-end of f002-adj-hdr,6)
   item clmdtl-cycle-nbr           initial clmhdr-cycle-nbr of f002-adj-hdr
   item clmdtl-orig-batch-id       initial clmhdr-orig-batch-id of f002-adj-hdr
   item clmdtl-fee-oma             initial clmhdr-tot-claim-ar-oma of f002-adj-hdr
   item clmdtl-fee-ohip            initial clmhdr-tot-claim-ar-ohip of f002-adj-hdr
   item clmdtl-amt-tech-billed     initial clmhdr-amt-tech-billed of f002-adj-hdr
;  ITEM CLMDTL-DIAG-CD 	   	   INITIAL CLMDTL-DIAG-CD OF F002-CLMDTL
   item clmdtl-diag-cd 	   	   initial clmdtl-diag-cd of u030_srt_adj
   item clmdtl-rev-group-cd        initial ' '
   item clmdtl-line-no		   initial clmdtl-line-no of u030_srt_adj


;OUTPUT F002-CLMHDR UPDATE ON ERRORS REPORT
;OUTPUT F002-CLMHDR UPDATE AT PART-HDR-CLAIM-ID ON ERRORS REPORT
;  ITEM CLMHDR-TOT-CLAIM-AR-OMA    SUBTOTAL X-TOT-CLAIM-AR-OMA
;  ITEM CLMHDR-TOT-CLAIM-AR-OHIP   SUBTOTAL X-OHIP-BAL
;  ITEM CLMHDR-AMT-TECH-BILLED     SUBTOTAL X-AMT-TECH-BILLED
;  ITEM CLMHDR-STATUS-OHIP	   FINAL PART-DTL-EXPLAN-CD


output iconst-mstr-rec update at final on errors report
;  ITEM ICONST-CLINIC-BATCH-NBR     FINAL X-BATCH-NBR + 1
;  ITEM ICONST-CLINIC-BATCH-NBR     FINAL X-BATCH-NBR - 1
   item iconst-clinic-batch-nbr     final x-batch-nbr


;Create Clmdtl Key in Subfile, so Cobol pgm can write inverted to
;the adjusting Clmdtl
subfile u030_dtl_key keep include 			&
   key-clm-type      of f002-adj-dtl,			&
   key-clm-batch-nbr of f002-adj-dtl,			&
   key-clm-claim-nbr of f002-adj-dtl,			&
   key-clm-serv-code of f002-adj-dtl,			&
   key-clm-adj-nbr   of f002-adj-dtl



subfile u030_adj_batches keep at x-batch-count	&
	include batctrl-batch-nbr

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request hold_claim_status on calculation errors report on edit errors report
                           ;  If partial payment, set the claim to 'H'old
			   ;  and update OHIP claim nbr in F020-CLAIMS-EXTRA

access *u030_no_adj 					&
   link  'B', nconvert(part-hdr-claim-id[1:9]),      	&
	 nconvert(part-hdr-claim-id[10:2]), '00000', '0' &
    to   key-clm-type, key-clm-batch-nbr, 		&
	 key-clm-claim-nbr, key-clm-serv-code, key-clm-adj-nbr &
         of f002-claims-mstr				&
   link  nconvert(part-hdr-claim-id)			&
    to   clmhdr-rma-clm-nbr of f002-claims-extra opt

output f002-claims-mstr update on errors report
   item clmhdr-tape-submit-ind final 'H'
;  ITEM CLMHDR-STATUS-OHIP FINAL PART-DTL-EXPLAN-CD
   item clmhdr-status-ohip final part-hdr-explan-cd

output f002-claims-extra add update on errors report
   item clmhdr-ohip-clm-nbr final part-hdr-ohip-clm-nbr
   item clmhdr-rma-clm-nbr initial nconvert(part-hdr-claim-id)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request part_adj_batch on calculation errors report on edit errors report
                           ;  Store the adjusted batches into the file
			   ;  PART_ADJ_BATCH

access *u030_adj_batches link 'B', batctrl-batch-nbr    &
	to key-clm-type, key-clm-batch-nbr of f002-claims-mstr

sel if clmhdr-oma-suff-adj = '000000'

output part-adj-batch add
   item  part-adj-claim-id final clmhdr-claim-id[1:11]
   item  part-adj-bal final clmhdr-tot-claim-ar-ohip


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request update_claim_bal on calculation errors report on edit errors report
                           ;  Update the adjusted balance to the
			   ;  original claim header




access *u030_update_clmhdr				&
   link  'B', nconvert(part-hdr-claim-id[1:9]),      	&
	 nconvert(part-hdr-claim-id[10:2]), '00000', '0' &
    to   key-clm-type, key-clm-batch-nbr, 		&
	 key-clm-claim-nbr, key-clm-serv-code, key-clm-adj-nbr &
         of f002-claims-mstr

define x-ohip-bal int*7 signed = 			&
	x-claim-bal * -1

define x-tot-claim-ar-oma int*7 signed =		&
  round((clmhdr-tot-claim-ar-oma * x-ohip-bal) / 	&
	 clmhdr-tot-claim-ar-ohip  )

define x-amt-tech-billed  int*7 signed =		&
  round((clmhdr-amt-tech-billed  * x-ohip-bal) / 	&
	 clmhdr-tot-claim-ar-ohip  )


output f002-claims-mstr  update on errors report
   item clmhdr-tot-claim-ar-oma    subtotal x-tot-claim-ar-oma
   item clmhdr-tot-claim-ar-ohip   subtotal x-ohip-bal
   item clmhdr-amt-tech-billed     subtotal x-amt-tech-billed

build $pb_obj/u030b_22

  
