; Program: u140_d.qts
; Purpose:	Calculate individual doctor AFP conversion payment /submission amounts 
;		factoring values sent from MOH by the RA payments
;		For doctors whose group is "R"eport only, there is no existing
;		doctor record in f075 so add with zero RMA doctor number and
;		payment to 100% for that transaction
; 
; modification history
; 04/jul/01 b.e.	-original
; 04/aug/12 b.e.	-add record to f075 if payment comes in for doctor
;			 not on file .. 
; 04/oct/16 b.e.	- store afp-submission-amt in tmp-counter-2
; 05/mar/08 M.C.        - substitute afp-payment-percentage with afp-multi-doc-ra-percentage
; 2005/apr/08 b.e. - set lock update statement
; 2005/jun/09 M.C. - change def factored with zoned*11 numeric
; 2007/feb/26 b.e. - add logic to process doctors not already in f075 whose
;		     afp group's process flag is not "R"eport only

cancel clear
set process nolimit
run u140_d
;set lock file update 
set lock record update 

;-------------------------------------------------------
request u140_d1_get_trans_in_a2s          		&
                on edit        errors report    	&
                on calculation errors report

access afp-a2s-file						&
	link (nconvert(doc-afp-paym-solo))			&
	to   doc-ohip-nbr	of f020-doctor-mstr opt 	&
        link doc-nbr of f020-doctor-mstr			&
	to   doc-nbr of f075-afp-doc-mstr opt 			&
	link doc-afp-paym-group of afp-a2s-file   		&
	to   doc-afp-paym-group of f074-afp-group-mstr opt 	&
        link doc-dept                                           &
        to   dept-nbr            of f070-dept-mstr      opt

; select "E"arning type afp groups
select if    (   doc-afp-paym-group of afp-a2s-file                     &
               = doc-afp-paym-group of f020-doctor-mstr                 &
             )                                                          &
        and (afp-group-process-flag of f074-afp-group-mstr = "E")


def x-doc-name char*35                          &
        =   pack(  doc-name                     &
                 + ", "                         &
                 + doc-inits                    &
                 + " ("                         &
                 + doc-nbr of f020-doctor-mstr 	&
                 + ")"                          &
                )

def x-conversion-amt-unfactored zoned*11 numeric&
        = afp-conversion-amt                    &
                if afp-conversion-sign = " "    &
    else  0 - afp-conversion-amt

def x-conversion-amt zoned*11 numeric           			&
   =  round(  x-conversion-amt-unfactored 				&
	    * (  afp-multi-doc-ra-percentage of f075-afp-doc-mstr	&
	       / 100000							&
	      )								&
	   ) 

def x-submission-amt    zoned*11 numeric        &
        = afp-submission-amt                    &
                if afp-submission-sign = " "    &
    else  0 - afp-submission-amt


sort    on doc-afp-paym-solo            &
        on doc-afp-paym-group		&
	on doc-nbr of f020-doctor-mstr

;subfile u140_d1   append   keep at doc-afp-paym-solo   include	&
subfile u140_d1   append   keep at doc-nbr of f020-doctor-mstr  include	&
; 2007/apr/5
;  afp-a2s-file,                                                        &
  afp-transaction-id,                                   &
  afp-record-id ,                                       &
  doc-afp-paym-group of afp-a2s-file  ,                 &
  afp-group-name,					&	
  doc-afp-paym-solo ,                                   &
  doc-nbr of f020-doctor-mstr,				&
  afp-solo-name  ,                                      &
  x-doc-name,						&
  dept-nbr,						&
  dept-name,						&
  afp-payment-percentage ,                              &
  afp-multi-doc-ra-percentage of f075-afp-doc-mstr,	&
  afp-payment-amt of f075-afp-doc-mstr,			&
  afp-submission-amt of f075-afp-doc-mstr,		&	
  x-conversion-amt,					&
  x-submission-amt,					&
  afp-group-process-flag,				&
  afp-reporting-mth of f075-afp-doc-mstr
  

output tmp-doctor-alpha alias tmp-doc-group-earnings add  on errors report 
	item doc-ohip-nbr      final nconvert(doc-afp-paym-solo of afp-a2s-file)
	item doc-nbr	       final doc-nbr of f020-doctor-mstr
	item tmp-alpha-field-1 final doc-afp-paym-group of afp-a2s-file
	item tmp-counter-1     final x-conversion-amt 
	item tmp-counter-2     final x-submission-amt 

output f075-afp-doc-mstr alias f075-update update	&
     if record f075-afp-doc-mstr exists
  item afp-reporting-mth  	final afp-reporting-mth of f074-afp-group-mstr 
  item afp-payment-amt-total	final x-conversion-amt-unfactored
  item afp-payment-amt		final x-conversion-amt
  item afp-submission-amt	final x-submission-amt

output f075-afp-doc-mstr alias f075-add    add		&
     if not record f075-afp-doc-mstr exists
  item doc-nbr 		       final doc-nbr 
  item doc-ohip-nbr 	       final nconvert(doc-afp-paym-solo of afp-a2s-file)
  item doc-afp-paym-group      final doc-afp-paym-group of u140_d1
  item afp-reporting-mth       final afp-reporting-mth of f074-afp-group-mstr 
  item afp-payment-amt-total   final x-conversion-amt-unfactored
  item afp-payment-amt	       final x-conversion-amt
  item afp-submission-amt      final x-submission-amt


;-------------------------------------------------------
; additional pass to ensure each doctor in f020, if even terminated but has
; RA amounts is stamped with the reporting month so that it is reported on
request u140_d5_update_f075_include_termed_doc_with_ra_amt	&
                on edit        errors report    		&
                on calculation errors report

access f020-doctor-mstr					&
	link doc-nbr					&
	to   doc-nbr of f075-afp-doc-mstr		&
	link doc-afp-paym-group of f075-afp-doc-mstr	&
	to   doc-afp-paym-group of f074-afp-group-mstr opt 


;select if     doc-date-fac-term <> 0			&
;	  and (    ra-payment-amt       <> 0		&
;	      )
; select if not terminated OR RA payments are found
select if      doc-date-fac-term = 0	&
	   or  ra-payment-amt <> 0

output f075-afp-doc-mstr  update on errors report
  item afp-reporting-mth  	final afp-reporting-mth of f074-afp-group-mstr 

build $obj/u140_d
