; NAME   : PM17.QZS
; PURPOSE: Create a report of the PWO invoiced hours and $ amounts
;          broken down by Activity Code
; MODIFICATION HISTORY
; YY/MMM/DD 	By Whom     	Why
; ??/???/?? 	?. ????????  	- original
; 87/Jun/16 	B. Elliott   	- added billable (ie. invoice) dollar
;				 amounts to report
; 87/Aug/05 	S. BLAIR     	- change order of paramater input.
; 88/Feb/09 	M. SO	 	- Provide flexibility to optionally
;			   	  print marketing activity codes
;				  (ie MK01 and MK02)
; 88/Mar/22 	B. Elliott   	- fixed bug discovered with upgrade to
;				  500D.
;			   	  Variable EMP-NUM in SORT and FOOTING
;			   	  replaced with TSHT-EMP.
; 88/SEP/16 	B. Elliott   	- MODIFY SORT SEQUENCE TO SORT ON PWO #
; 89/MAY/31 	K. Zatzman	- Display non-billable "wriiten-off" hours
; 89/sep/20 	M. CHAN	 	- Change the program to choose single
;			   	  PWO run instead of select
; 90/MAY/08 	B. Elliott   	- ALLOW OPTION TO PRINT ALL PWO'S OR
;				  ONLY THOSE REQUIRING A BREAKDOWN
; 91/APR/17 	M. CHAN      	- CHANGE THE ACCESS STATEMENT FROM
;			   	  ASCII(PWO-NUM) TO ASCII(PWO-NUM,5)
; 92/FEB/28	M. CHAN		- SAF 1005
;				- ADD CAN CLEAR
;				- TAKE OUT THE WRITTEN OFF AMT

cancel clear

set default
set report device printer
set page length 60 width 80
set report nolimit

access ftimesht 	  &
       link to fclients   &
       and  to fpwos      &
       link(ascii(pwo-num,5)+activity) to pwoact-key of fpwoacts &
       and  tsht-emp to emp-num of femployees &
       and  (characters(tsht-emp) + characters(pwo-num)) to emppwo-key of femppwos opt
choose pwo-num parm                  &
       prompt "Choose PWO # without period (Enter new line to select all): "
define x-mark char*1 = parm upshift prompt &
	"Do you want to print MARKETING activity code (Y)es or (N)o? "

define x-breakdown char*1 = parm upshift prompt &
	"Do you want to print ONLY THOSE PWO's REQUIRING THIS BREAKDOWN (Y)es or (N)o? "

define x-yymm zoned*4 unsigned = parm &
	prompt "ENTER YYMM FOR INVOICING: "
define year character*2 = characters(x-yymm)[1:2]
define month character*9 = &
 	"  JANUARY" if characters(x-yymm)[3:2] = "01" else &
        " FEBRUARY" if characters(x-yymm)[3:2] = "02" else &
        "    MARCH" if characters(x-yymm)[3:2] = "03" else &
        "    APRIL" if characters(x-yymm)[3:2] = "04" else &
        "      MAY" if characters(x-yymm)[3:2] = "05" else &
        "     JUNE" if characters(x-yymm)[3:2] = "06" else &
        "     JULY" if characters(x-yymm)[3:2] = "07" else &
        "   AUGUST" if characters(x-yymm)[3:2] = "08" else &
        "SEPTEMBER" if characters(x-yymm)[3:2] = "09" else &
        "  OCTOBER" if characters(x-yymm)[3:2] = "10" else &
        " NOVEMBER" if characters(x-yymm)[3:2] = "11" else &
        " DECEMBER" if characters(x-yymm)[3:2] = "12"
;DEFINE X-INVOICE CHAR*1 = PARM                             &
;      PROMPT "Do you wish INVOICED AMOUNT printed (Y)es or (N)o: "
define x-bill-hrs pic '^^^^^^.^^' &
       = tot if billable = ' '  &
       else 0
;DEFINE X-BILL-DOLLAR-AMT NUMERIC*8 = X-BILL-HRS * BILL-RATE/100 &
;                                    IF INVOICED-AMOUNT = "Y" ELSE 0
define x-nobill-hrs pic '^^^^^^.^^' &
       = tot if billable = '*'  &
       else 0
define x-nobill-dollar-amt numeric*8 = x-nobill-hrs * bill-rate/100 &
                                     if billable = "*" else 0
;DEFINE X-INVOICED CHAR*8 = "INVOICED" IF INVOICED-AMOUNT = "Y" ELSE " "
;DEFINE X-AMOUNT   CHAR*8 = " AMOUNT " IF INVOICED-AMOUNT = "Y" ELSE " "
;DEFINE X-WRITEOFF CHAR*11  = "WRITTEN OFF"

;DEFINE X-LINE1    CHAR*8 = "--------" IF INVOICED-AMOUNT = "Y" ELSE " "
;DEFINE X-LINE2    CHAR*8 = "========" IF INVOICED-AMOUNT = "Y" ELSE " "


select ftimesht if (   (tsht-yymm = x-yymm and x-mark = "Y")	 &
                    or (     tsht-yymm = x-yymm and x-mark = "N"     &
                         and activity of ftimesht <> "MK01"  	    &
 	                 and activity of ftimesht <> "MK02"))

select fpwos if (   x-breakdown = "N" &
		 or (pwo-breakdown = "Y" and x-breakdown = "Y"))

sort on pwo-num reset page on activity on tsht-emp

page heading                   sysdate format yymmmdd 	&
	tab 29 "DYAD TECHNOLOGIES INC." tab 70 "PAGE: " &
	tab 76 syspage	skip 2 tab 29 "PWO/ACTIVITY BREAKDOWN" &
        skip 1 &
        tab  33 month tab 42 "/" tab 43 year &
	skip 3 "Client:" tab 15 cl-name &
        skip 2 "PWO # :" tab 15 pwo-num bwz tab 25 pwo-desc &
        skip 3 "ACTIVITY" tab 41 "EMPLOYEE" tab 61 "-------- HOURS ----" &
;                          TAB 84 X-INVOICED         &
;			   TAB 97 X-WRITEOFF	     &
;       SKIP 1 TAB 61 "   N/C     BILLABLE" TAB 84 X-AMOUNT SKIP 2
        skip 1 tab 61 "   N/C     BILLABLE" skip 2

footing at tsht-emp tab 05 activity print at activity &
                    tab 10 act-desc of fpwoacts print at activity &
        tab 41 name                       &
        tab 58 x-nobill-hrs   subtotal pic "^^^^^^.^^" bwz &
        tab 71 x-bill-hrs   subtotal pic "^^^^^^.^^" bwz
;       TAB 84 X-BILL-DOLLAR-AMT SUBTOTAL PIC "^^^^^^.^^" BWZ &
;	TAB 97 X-NOBILL-DOLLAR-AMT SUBTOTAL PIC "^^^^^^.^^" BWZ

footing at activity skip 1 tab 58 "---------" tab 71 "---------" &
        skip 1 tab 41 "TOTAL ACTIVITY:" &
        tab 58 x-nobill-hrs subtotal pic "^^^^^^.^^" bwz &
        tab 71 x-bill-hrs subtotal pic "^^^^^^.^^" bwz skip 2
;       TAB 84 X-BILL-DOLLAR-AMT SUBTOTAL PIC "^^^^^^.^^" BWZ SKIP 2 &
;	TAB 97 X-NOBILL-DOLLAR-AMT SUBTOTAL PIC "^^^^^^.^^" BWZ

footing at pwo-num  skip 2 tab 58 "=========" tab 71 "=========" &
;       TAB 84 X-LINE2 SKIP 1 TAB 46 "TOTAL PWO:" &
                       skip 1 tab 46 "TOTAL PWO:" &
        tab 58 x-nobill-hrs subtotal pic "^^^^^^.^^" bwz &
        tab 71 x-bill-hrs subtotal pic "^^^^^^.^^" bwz skip page
;       TAB 84 X-BILL-DOLLAR-AMT SUBTOTAL PIC "^^^^^^.^^" BWZ SKIP PAGE


build $pb_obj/pm17
  
