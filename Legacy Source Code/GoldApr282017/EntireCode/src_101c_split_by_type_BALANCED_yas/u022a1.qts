;#> PROGRAM-ID.     u022a1.qts
;
;       ((C)) Dyad Technologies
;
;    PROGRAM PURPOSE : TO WORK WITH R022A.QZS - BY CREATING A AUDIT SUBFILE
;		       THIS USED TO BE THE FIRST RUN OF THAT PROGRAM
;
;  MODIFICATION HISTORY
;    DATE   WHO   DESCRIPTION
; 00/MAY/08 B.A.  - ORIGINAL (transformed from r022a.qzs to 
;		    u022a.qts so that multiple subfiles (audit)
;		    could be generated in 1 pass
; 00/may/08 B.E.  - added selection of "X" status claims to force
;		    resubmit of claims regardless of other conditions
; 00/may/29 B.E.  - don't select for resubmission claims that haven't 
;		    been submitted to ohip yet (ie. have blank
;		    submission date)
; 00/may/31 B.E.  - clmhdr-date-cash-tape-payment is character field
;		    and selection added to select if date is "  00" 
; 00/jun/01 B.E.  - changed test to use submit-date rather than entry-date
; 00/jun/01 B.E.  - some agent 6 claims were incorrecltcy re-submitted to ohip
;		    when and "X" status was placed into claim. Select logic
;		    changed to first check moh-flag indicating claim should go 
;		    to ohip, then apply other checks.
; 02/sep/23 M.C.  - include clinic 95 to the same as other clinic 80's			     
; 02/nov/13 M.C.  - optimize the select statement by removing redundant statements
; 03/may/30 M.C.  - include contract-code in the subfile, so that in u022b.qts, sort
;		    on contract-code before any other sort fields; contract D contains
;		    all 60's clinics
; 03/jun/11 yas   - include clinics 91,92,93,94,96 to do the same as other clinic 80's			     
; 03/dec/09 MC/BE - modify selection criteria to include stale date claims that are 
;		    150 days or greater   and there is balance and no submission date
;
; 03/dec/11 A.A.  - alpha doctor nbr
; 04/feb/26 M.C.  - change/extend the definition of w-clmhdr-hosp and add constant mstr
;                   in the access statement
; 04/mar/01 M.C.  - use afp-flag instead of checking the clinic
; 04/mar/17 M.C.  - Do not resubmit claims if afp clinic with reason 'I2'(fully paid)
;                   even the claim status has set to 'X'
; 04/apr/21 M.C.  - modify the criteria for 2 rat processed and 'R'esubmit
;
; 04/may/19 M.C.  - modify the value check on afp-flag(iconst-clinic-card-colour)
;		  - value 'O' represents old afp	 
; 05/jun/14 M.C.  - check clmhdr-status <> ' ' when selecting stale date claims
; 06/apr/10 M.C.  - effective Apr 1, 2006, do not require to submit loc-ministry-loc-code
;		  - use service location indicator instead
; 12/Dec/19 MC1   - allow to resubmit claims with 'X' and health nbr = 1111111116 for CME claims 
;		  - include f010-pat-mstr in the acces statement in order to check pat-health-nbr
; 14/Apr/10 MC2   - change access to use f002-outstanding as the driver to link to claims mstr
;		  - transfer the selection for resubmit into the copybook $use/select_for_resubmit.use
;		  - comment out the choose statement as it is not needed
 
can clear
set default
set input nolimit
set process nolimit
set lock record update

request extract on calculation errors report				&
		on edit errors report

; (99/aug/25 B.E. added link to pickup hospital code)
; MC2 
;access f002-claims-mstr   						&
access f002-outstanding							&
  link key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr, '00000', '0' &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr, 		&
       key-clm-serv-code, key-clm-adj-nbr of f002-claims-mstr		&
; MC2 - end
  link nco(key-clm-batch-nbr[1:2]), clmhdr-agent-cd    			&
    to clinic-nbr, agent-cd of contract-dtl                             &
  link key-clm-batch-nbr of f002-claims-mstr[3:3]		  	&
    to doc-nbr of f020-doctor-mstr       				&
; 2012/12/19 - MC1 - include f010 file
  link clmhdr-pat-ohip-id-or-chart       	 			&
   to  key-pat-mstr of f010-pat-mstr opt				&
; 2012/12/19 -end
  link nconvert(key-clm-batch-nbr[1:2])  				&
    to iconst-clinic-nbr-1-2 of iconst-mstr-rec                         &
  link clmhdr-loc                                                 	&
    to loc-nbr    of f030-locations-mstr opt

use $use/def_group_nbr.use             nolist nodetail

def w-date-from date = parm prompt "ENTER DATE FROM (YYYYMMDD) "
def w-date-to   date = parm prompt "ENTER DATE TO   (YYYYMMDD) "

; MC2
;choose key-clm-type "B", key-clm-batch-nbr, key-clm-claim-nbr, &
;	key-clm-serv-code '00000', key-clm-adj-nbr '0'
; MC2 - end

def x-clinic-nbr-1-2 char*2 = clmhdr-claim-id[1:2]

def balance-due num                            &
     =  clmhdr-tot-claim-ar-ohip               &
; 2003/12/10 - MC
;      - clmhdr-manual-and-tape-payments        &
      + clmhdr-manual-and-tape-payments        &
; 2003/12/10 - end
       if clmhdr-adj-oma-cd = "0000"           & ; perform calc only for
  else 0                                         ; claim header recs

; (if two rats have been processed since claim entered, then resubmit -
;  if the claim was entered before the 18th cut off, then the 2nd rat
;  that could contain payment is found within 75 day, however claims entered
;  on or after the 18th have to wait for the 3rd tape to be processed
;  ie. up to 90 days after entry)
;
; --entry mth-|--1st mth----|---2nd mth---|--3rd mth----|
; 1   18   30 | 1   18   30 | 1   18   30 | 1   18   30 |
; --x-           1st          2nd                      (claim entry vs rat)
;     ---x---                 1st           2nd                (claim entry vs 
;rat)
;

; (00/jun/01 B.E. - changed test to use submit-date rather than entry-date)
def x-test-date char*8  		&
     = ascii(clmhdr-submit-date,8)	&
	   if clmhdr-submit-date <> 0	&
 else  clmhdr-date-sys

def x-test-day char*2			&
     = x-test-date[7:2]

def w-days-since-entry num                     &
     =  days(sysdate)                          &
      - days(nconvert(x-test-date))	       &
       if clmhdr-adj-oma-cd = "0000"           & ; perform calc only for
  else 0                                         ; claim header recs

def two-rats-processed char*1                &
     = "Y" if   x-test-day         <= "18"   &
            and w-days-since-entry >= 75     &
  else "Y" if   x-test-day         >  "18"   &
            and w-days-since-entry >= 90     &
  else "N"

def x-days-since-ped 			&
     = days(sysdate) - days(clmhdr-date-period-end)	&
       if clmhdr-adj-oma-cd = "0000"           

; 2003/12/09 - MC
def x-days-since-service = days(sysdate) - days(clmhdr-serv-date)  &
       if clmhdr-adj-oma-cd = "0000"           	&
      and clmhdr-serv-date <> 0
; 2003/12/09/ end

; MC2 - select for resubmit has transferred to copybook
use $use/select_for_resubmit.use  nolist nodetail
; MC2 - end

; 99/jul/22 B.E.
def w-moh-location-code char*4                          &
    = "    "                                            &
           if not record f030-locations-mstr exists     &
; 2006/04/10  - MC
;else ascii(loc-ministry-loc-code,4)
 else loc-service-location-indicator
; 2006/04/10 - end 


; Location now used to determine if hospital nbr is needed
; regardless of patient's in/out indicator!!!
; (hospital blank unless 'I'n patient and location code found
;  that can be used to obtain hospital nbr)
def w-clmhdr-hosp char*4                                        &
    = "    "                                                    &
;       if   clmhdr-i-o-pat-ind of f002-claims-mstr <> "I"      &
;         or not record f030-locations-mstr exists              &
        if   not record f030-locations-mstr exists              &
; 2004/02/26 - MC
 else loc-hospital-code                                         &
         if iconst-clinic-card-colour = 'Y'                     &
; 2004/05/19 - MC - cannot recall what is 'S' stand for - ignore this value  
;       or iconst-clinic-card-colour = 'S'                     &
; 2004/05/19 - end

; 2004/02/26 - end
 else ascii(loc-hospital-nbr,4)

def d-sysdate date = sysdate

temp x-rec-count
item x-rec-count = x-rec-count + 1

subfile u022a1  keep include    &
; MC2
;  key-clm-type,                 &
;  key-clm-batch-nbr,             &
;  key-clm-claim-nbr,            &
   key-clm-type of f002-claims-mstr,                &
   key-clm-batch-nbr of f002-claims-mstr,           &
   key-clm-claim-nbr of f002-claims-mstr,           &
; MC2 - end
  clmhdr-batch-type,            &
  clmhdr-doc-nbr-ohip,          &
;y2k
  clmhdr-date-sys,              &
  clmhdr-claim-id,              &
  clmhdr-claim-nbr,             &
  w-clmhdr-hosp,                &
  clmhdr-date-admit,            &
  clmhdr-pat-ohip-id-or-chart,  &
  clmhdr-refer-doc-nbr,         &
  clmhdr-loc,                   &
  clmhdr-i-o-pat-ind,           &
  clmhdr-agent-cd,              &
  clmhdr-tot-claim-ar-oma,      &
  clmhdr-tot-claim-ar-ohip,     &
  clmhdr-status-ohip,           &
  clmhdr-doc-dept,              &
  clmhdr-doc-spec-cd,           &
  clmhdr-sub-nbr,               &
  clmhdr-manual-review,         &
  dollar-flag,                  &
  moh-flag,                     &
;1997/JUN/12 - KEVIN MILES - ADDED THIS FIELD TO SUBFILE
  translated-group-nbr,         &
; 99/11/17 - MC - include w-moh-location-code
  w-moh-location-code,		&
; 03/05/30 - MC - include contract-code
  contract-code
; 03/05/30 - end

;2000/05/02 - B.A. added audit subfile to capture criteria for resubmitting
;		   a claim. In case of dispute as to why a claim was resubmitted
;		   this file can be dumped out

subfile u022a1_audit keep include                               &
               clmhdr-tape-submit-ind of f002-claims-mstr,	&
               key-clm-type of f002-claims-mstr,                &
               key-clm-batch-nbr of f002-claims-mstr,           &
               key-clm-claim-nbr of f002-claims-mstr,           &
               clmhdr-batch-type of f002-claims-mstr,           &
               clmhdr-adj-oma-cd of f002-claims-mstr,           &
               moh-flag of contract-dtl,                        &
               balance-due,                                     &
               clmhdr-tot-claim-ar-ohip of f002-claims-mstr,    &
               clmhdr-manual-and-tape-payments of f002-claims-mstr, &
               clmhdr-status-ohip of f002-claims-mstr,          &
	       x-test-date,					&
	       x-test-day,					&
               two-rats-processed,                              &
               clmhdr-date-cash-tape-payment of f002-claims-mstr,   &
               clmhdr-date-sys  of f002-claims-mstr,            &
               x-days-since-service,                            &
               d-sysdate,                                       &
               clmhdr-submit-date of f002-claims-mstr,		&
	       x-rec-count,					&
	       clmhdr-serv-date

build $obj/u022a1
