; DOC: R005_CSV.QZS
; DOC: MONTHLY CASH APPLIED RECONCILIATION
; DOC: SORT BY CLINIC
; DOC: RUN FOR: Ross  
;
;PROGRAM PURPOSE : CASH ANALYSIS BY CLINIC   (DETAIL & SUMMARY REPORTS)
;
;DATE           WHO       DESCRIPTION
;2015/Jun/16	MC	  ORIGINAL
;2015/jun/24	be1	  added sort
;2015/jun/25	MC1	  added sort in the summary section as well
;
can clear
set rep nolimit
set page length 62 width 132
set rep device disc name r005dtl_csv
set nohead
set noformfeed


access *r005_dtl  
;----------------------------------------
def x-delimiter char*1 = "~"

def x-period char*1 = "."

def x-amt-mtd-sign char*1 = "-"                  	if agent-mtd-amt < 0       &
                else " "
def x-amt-mtd-dollars = abs(agent-mtd-amt) / 100 	if agent-mtd-amt <> 0      &
                else  0
def x-amt-mtd-cents   = mod(abs(agent-mtd-amt),100)     if agent-mtd-amt <> 0      &
                else  0

def x-amt-ytd-sign char*1 = "-"                 	if agent-ytd-amt < 0       &
                else " "
def x-amt-ytd-dollars = abs(agent-ytd-amt) / 100 	if agent-ytd-amt <> 0      &
                else  0
def x-amt-ytd-cents   = mod(abs(agent-ytd-amt),100)     if agent-ytd-amt <> 0      &
                else  0

def x-clinic       char*4  = '"' + docash-clinic-1-2  + '"'

def x-dept         char*4  = '"' + ascii(docash-dept,2) + '"'

def x-company	   char*1  = ascii(dept-company) 

def x-doc-nbr      char*5  = '"' + docash-doc-nbr     + '"'

def x-agent	   char*1  = docash-agency-type

def x-amt-mtd-alpha char*10 =  pack(trunc(lj(x-amt-mtd-sign +       		&
                                              ascii(x-amt-mtd-dollars) +     	&
                                              x-period +                        &
                                              ascii(x-amt-mtd-cents,2))))

def x-amt-ytd-alpha char*11 =  pack(trunc(lj(x-amt-ytd-sign +       		&
                                              ascii(x-amt-ytd-dollars) +        &
                                              x-period +                        &
                                              ascii(x-amt-ytd-cents,2))))

def x-line char*132 =                                   &
        lj(pack(                                        &
                x-clinic        +                       &
                x-delimiter  +                                   &
                x-dept          +                       &
                x-delimiter  +                                   &
                x-company       +                       &
                x-delimiter  +                                   &
                x-doc-nbr       +                       &
                x-delimiter  +                                   &
                x-agent         +                       &
                x-delimiter  +                                   &
                x-amt-mtd-alpha    +                         &
                x-delimiter  +                                   &
                x-amt-ytd-alpha                            &
                ) )


initial heading							&
        tab  1  'CLINIC'                             		&
	tab  7  x-delimiter					&
        tab  8  'DEPT'                               		&
	tab 12  x-delimiter					&
        tab 13  'COMPANY'                                       &
	tab 20  x-delimiter					&
        tab 21  'DOC#'                               		&
	tab 25  x-delimiter					&
        tab 26  'AGENT'                              		&
	tab 31  x-delimiter					&
        tab 32  'MTD AMT'                                       &
	tab 39  x-delimiter					&
        tab 40  'YTD AMT'                     

;be1 added sort
sort on  docash-clinic-1-2  	&
     on  docash-dept        	&
     on	 dept-company		&
     on  docash-doc-nbr

report tab 01 x-line
 
build $pb_obj/r005a_csv

;------------------------------
  
set rep device disc name r005sum_csv
set nohead
set noformfeed


access *r005_summ  

;MC1 added sort
sort on  docash-clinic-1-2
     
;----------------------------------------
def x-delimiter char*1 = "~"

def x-period char*1 = "."

def x-amt-mtd-sign char*1 = "-"                  	if dept-mtd-amt < 0       &
                else " "
def x-amt-mtd-dollars = abs(dept-mtd-amt) / 100 	if dept-mtd-amt <> 0      &
                else  0
def x-amt-mtd-cents   = mod(abs(dept-mtd-amt),100)      if dept-mtd-amt <> 0      &
                else  0

def x-amt-ytd-sign char*1 = "-"                 	if dept-ytd-amt < 0       &
                else " "
def x-amt-ytd-dollars = abs(dept-ytd-amt) / 100 	if dept-ytd-amt <> 0      &
                else  0
def x-amt-ytd-cents   = mod(abs(dept-ytd-amt),100)      if dept-ytd-amt <> 0      &
                else  0

def x-clinic       char*4  = '"' + docash-clinic-1-2 + '"'

def x-dept         char*4  = '"' + ascii(docash-dept,2) + '"'

def x-company	   char*1  = ascii(dept-company) 

def x-amt-mtd-alpha char*10 =  pack(trunc(lj(x-amt-mtd-sign +       		&
                                              ascii(x-amt-mtd-dollars) +     	&
                                              x-period +                        &
                                              ascii(x-amt-mtd-cents,2))))

def x-amt-ytd-alpha char*11 =  pack(trunc(lj(x-amt-ytd-sign +       		&
                                              ascii(x-amt-ytd-dollars) +        &
                                              x-period +                        &
                                              ascii(x-amt-ytd-cents,2))))

def x-line char*132 =                                   &
        lj(pack(                                        &
                x-clinic        +                       &
                x-delimiter  +                                   &
                x-dept          +                       &
                x-delimiter  +                                   &
                x-company       +                       &
                x-delimiter  +                                   &
                x-amt-mtd-alpha    +                         &
                x-delimiter  +                                   &
                x-amt-ytd-alpha                                &
                ) )


initial heading							&
        tab  1  'CLINIC'                             		&
	tab  7  x-delimiter					&
        tab  8  'DEPT'                               		&
	tab 12  x-delimiter					&
        tab 13  'COMPANY'                                       &
	tab 20  x-delimiter					&
        tab 21  'MTD AMT'                                       &
	tab 28  x-delimiter					&
        tab 29  'YTD AMT'                  

report tab 01 x-line
 
build $obj/r005b_csv
