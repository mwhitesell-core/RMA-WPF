;#> PROGRAM-ID.     U111C.QTS
;
;       ((C)) Dyad Technologies
;
;    PURPOSE: SUB-PROCESS WITHIN "EARNINGS GENERATION" PROCESS.
;             Create EARNINGS transactions in F110-COMPENSATION for
;             the current EP-NBR using MTD values taken from F050-REVENUE-MSTR
;
;	      PHASE 3 - update F110-COMPENSATION file

;    MODIFICATION HISTORY
;        DATE    SAF #  WHO      DESCRIPTION
;     93/JUL/25  ____   B.E.     - added sort in U111A.qzs pre-pass
;			           removed SORTED phrase
;     93/AUG/03  ____   B.E.     - NET now 100% of GROSS regardless of FACTOR
;     93/AUG/09  ____   B.E.     - Reverse above logic - NET = GROSS * FACTOR again
;     93/AUG/10  ____   B.E.	 - removed calc. of GST - more complicated
;				   than originally pgmmed. Moved into U115
;     93/OCT/05  ____   B.E.     - modify ROUNDING of NET calc to ensure
;				   consistent with D110
;                                - if no matching COMP code rec is found in F110
;				   and no default rec in F190 (which should
;				   never happen) then amount is set to zero!
;     93/OCT/09  ____   B.E.     - write out F110 record regardless of amount of MTD-BILLING
;   1999/JAN/15  ----   S.B.     - Checked for y2k.
;   1999/June/01 	S.B.     - Added the use file
;                       	   def_compensation_status.def to 
;                       	   prevent hard coding of compensation-status.
;

cancel clear
set stacksize 500
run u111c

set default
set process nolimit

global temp w-current-ep-nbr        numeric
global temp w-current-ep-nbr-minus1 numeric

;-------------------------------------------------------------------------------
; OBTAIN THE CURRENT EP NUMBER AND PASS TO SUBSEQUENT REQUESTS
;
request u111_const_mstr_get_ep_nbr              &
                on edit        errors report    &
                on calculation errors report
access constants-mstr-rec-6
choose const-rec-nbr 6

item w-current-ep-nbr        final  current-ep-nbr
item w-current-ep-nbr-minus1 final  current-ep-nbr - 1
;


;-------------------------------------------------------------------------------
;
request u111_process                           &
                on edit        errors report    &
                on calculation errors report

access *u111_sorted                                                   &
        link docrev-doc-nbr   of u111_sorted,                         &
             w-current-ep-nbr,                                        &
             process-seq  of u111_sorted,                             &
             comp-code of u111_sorted                                 &
        to   doc-nbr,                                                 &
             ep-nbr,                                                  &
             process-seq,                                             &
             comp-code    of f110-compensation    opt		      &
        link comp-code    of u111_sorted 			      &
	to   comp-code	  of f190-comp-codes      opt

;SORTED ON DOC-NBR ON COMP-CODE


define  x-amt-net  						      &
    = round((mtd-billing * factor of f110-compensation)/10000,0,near) &
	if record f110-compensation exists			      &
 else round((mtd-billing * factor of f190-comp-codes  )/10000,0,near) &
	if record f190-comp-codes   exists			      &
else  0

;S.B.
use $use/def_compensation_status.def

;----------- BILL ------------
output f110-compensation add alias f110-add                          &
    if     not record f110-compensation  exists                ;     &
;      AND MTD-BILLING > 0
        item doc-nbr	     final  docrev-doc-nbr of u111_sorted
        item ep-nbr          final  w-current-ep-nbr
        item ep-nbr-entry    final  w-current-ep-nbr
        item comp-code       final  comp-code   of u111_sorted
        item comp-type       final  comp-type   of u111_sorted
        item process-seq     final  process-seq of u111_sorted
        item factor          final  factor      of u111_sorted
        item factor-override final  " "
        item comp-units      final  0    ; BUT COULD STORE NBR SVCS ?????!!!!
        item amt-gross       final  mtd-billing
        item amt-net         final  x-amt-net
;S.B.
;        item compensation-status final  " "
        item compensation-status final  compensation-status-accepted
        item last-mod-date    final  sysdate
        item last-mod-time    final  systime / 10000
        item last-mod-user-id final  "U111C    Gen'd"

output f110-compensation update alias f110-update                   &
    if     record f110-compensation exists
        item factor-override final  "*"
        item comp-units      final  0    ; BUT COULD STORE NBR SVCS ?????!!!!
        item amt-gross       final  mtd-billing
        item amt-net         final x-amt-net
;S.B.
;        item compensation-status final  " "
        item compensation-status final  compensation-status-accepted
        item last-mod-date    final  sysdate
        item last-mod-time    final  systime / 10000
        item last-mod-user-id final  "U111C    Upd'd"


build $pb_obj/u111c
  
