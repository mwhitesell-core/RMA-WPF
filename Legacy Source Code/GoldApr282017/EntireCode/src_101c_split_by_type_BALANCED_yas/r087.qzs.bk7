; program: r087
; purpose: rma history report for letters send to patients
;
;	93/04/19	AGK       SMS 141 (ORIGINAL)
;       93/04/22        Y.B.      MODIFY REPORT FORMAT
;	93/05/21	M. CHAN	  - ADD THE MESS CODE CHECK IN THE
;				    SELECTION CRITERIA (SAME AS R085.QZS)
;	94/02/22	M. CHAN	  - PDR 594
;				  - CHECK LAST MAILING > 21 DAYS OLD
;				    INSTEAD OF 10 DAYS
;	98/12/10        B.E.	  - changed to access *r085 subfile rather than
;				    selecting records directly from the 
;				    rejected-claims file.
; 99/jan/31 B.E. - y2k
; 00/jan/21 yas	 - print patient once 
; 00/jun/29 B.E. - changed to access *u085b rather than 
; 00/jun/29 B.E. - changed to access *u085b rather than *r085. This ensures
;                  the claims printed are the same ones printed in the
;                  letters and have been filtered so that no 'confidential'
;                  claims are printed


cancel clear

set def
set rep dev disc name r087
set report nolimit
set nohead
set rep page length 60
set formfeed
set page width 132

access *u085b							&
  link ("B",  							&
	nconvert(ascii(claim-nbr,10)[1:2] + "0" + ascii(claim-nbr,10)[3:6]), &
	nconvert(ascii(claim-nbr,10)[9:2]),  			&
	"00000", "0")						&
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr,      &
       key-clm-serv-code, key-clm-adj-nbr of f002-claims-mstr   &
  link clmhdr-pat-ohip-id-or-chart   		           	&
   viaindex key-pat-mstr					&
   to f010-pat-mstr

; y2k
;define d-test-date numeric*6 = 				&
define d-test-date numeric*8 = 				&
 	days(sysdate) - days(pat-date-last-elig-mailing) &
	if pat-date-last-elig-mailing > 0

define x-pat-name cha*30 = pack(pat-surname + ", " + pat-given-name)

;sort on pat-surname of f010-pat-mstr on key-pat-mstr
sort on pat-surname    of f010-pat-mstr & 
     on pat-given-name of f010-pat-mstr &
     on clmhdr-pat-ohip-id-or-chart
page heading 				                    &
	tab 01	'r087'					    &	
	tab 11   sysdate		                    &
	tab 50  'Eligibility Letters Printed - Audit Report'&
	tab 120 'Page:'					    &
	tab 126  syspage				    &
skip 3  tab 1   'PATIENT NAME'                              &
        tab 31  'REASON'                                    &
        tab 38  'HEALTH #'                                  &
        tab 49  'VERSION'                                   &
        tab 57  'BIRTH DATE'                                &
        tab 68  'CLAIM #'                                   &
        tab 80  'AG'                                        &
        tab 83  'LOCATION'                                  &
        tab 92  'PREVIOUS'                                  &
skip 1  tab 32  'CODE'                                      &
        tab 50  'CODE'                                      &
        tab 58  'YY/MM/DD'                                  &
        tab 85  'CODE'                                      &
        tab 92  'LETTER SEND'                               &
skip 1 	tab 1   '------------'                              &
        tab 31  '------'                                    &
        tab 38  '----------'                                &
        tab 49  '-------'                                   &
        tab 57  '----------'                                &
        tab 70  '----------'                                &
        tab 82  '--'                                        &
        tab 85  '--------'                                  &
        tab 94  '-----------'                               &
skip 2

;report 					        &
footing at clmhdr-pat-ohip-id-or-chart          &
    	tab 1   x-pat-name 			&
	tab 31  mess-code 			&
	tab 38  pat-health-nbr			&
	tab 52  pat-version-cd			&
; y2k
	tab 58  pat-birth-date pic "^^^^/^^/^^"   &
	tab 70  claim-nbr			&
        tab 82  clmhdr-agent-cd			&
	tab 87  clmhdr-loc     			&
	tab 96  pat-date-last-elig-mailing      &
skip 2

build $pb_obj/r087
  
