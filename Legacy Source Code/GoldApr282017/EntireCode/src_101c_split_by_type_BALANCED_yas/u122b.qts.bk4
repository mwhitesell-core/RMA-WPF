;#> PROGRAM         U122B.QTS
;
;       ((C)) Dyad Infosys LTD 
;
;    PURPOSE: SUB-PROCESS WITHIN "EARNINGS GENERATION" PROCESS.
;	Purpose:
;		This pgm calculates the TITHE1/2/3 amounts for EP and
;		updates stuff.
;	 Logic
;	      Get "TOTITE/D" record in f119/f110 just generated by u130/ and originally u115b but now u015a_0. This
;	      record has total of this EP's titheable income. 
;	      GET "TOTITE/D" potentially existing in f119 ("D") of previous
;	      YTD total titheable income.  Adding these 2 amounts together
;	      to get potential TITHE1/2/3 amounts.
;	      Access f119 for potentially existing TITHE1/2/3 ("A") records to
;	      see what has previously been changed and determine for
;	      each TITHE1/2/3 what should be charge this EP.
;	      Create TITHE1/2/3 records in f110 with these amounts and 
;	      update TITHE1/2/3 records in f119 with YTD amounts
;
;
;    MODIFICATION HISTORY
;        DATE    SAF #  WHO      DESCRIPTION
; 2008/jun/10 M.C.      - original  
; 2008/aug/19 M.C.	- generate  for TITHD1/2/3 or TITHE1/2/3
;			  depending on payroll-flag            
; 2008/oct/21 M.C.	- create a new request to either create/update f119-doctor-ytd with rec-type 'D'
;			  add amt-net to amt-mtd and amt-ytd in f119-doctor-ytd
; 2008/oct/21 brad1     - consider that incoming transactions can have ytd values so use those amounts
; 2008/oct/22 brad2     - use net amounts not gross since gross figures reduced by factor to get net and that's values neeeded
; 2008/oct/24 brad3     - don't output prim doctor is prim doct nbr is blank (happends if all docs are terminate)

cancel clear
run u122b

set default
set process nolimit
Set lock record update

global temp totexp-code   char*6
global temp totexp-seq    zoned*2 unsigned
global temp totexp-type   char*1
global temp totexp-group  char*1

global temp incexp-code   char*6
global temp incexp-seq    zoned*2 unsigned
global temp incexp-type   char*1
global temp incexp-group  char*1

global temp tithe1-code   char*6
global temp tithe1-seq    zoned*2 unsigned
global temp tithe1-type   char*1
global temp tithe1-group  char*1
global temp tithe1-factor numeric

global temp tithe2-code   char*6
global temp tithe2-seq    zoned*2 unsigned
global temp tithe2-type   char*1
global temp tithe2-group  char*1
global temp tithe2-factor numeric

global temp tithe3-code   char*6
global temp tithe3-seq    zoned*2 unsigned
global temp tithe3-type   char*1
global temp tithe3-group  char*1
global temp tithe3-factor numeric

global temp tithd1-code   char*6
global temp tithd1-seq    zoned*2 unsigned
global temp tithd1-type   char*1
global temp tithd1-group  char*1
global temp tithd1-factor numeric

global temp tithd2-code   char*6
global temp tithd2-seq    zoned*2 unsigned
global temp tithd2-type   char*1
global temp tithd2-group  char*1
global temp tithd2-factor numeric

global temp tithd3-code   char*6
global temp tithd3-seq    zoned*2 unsigned
global temp tithd3-type   char*1
global temp tithd3-group  char*1
global temp tithd3-factor numeric

global temp totite-code   char*6
global temp totite-seq    zoned*2 unsigned
global temp totite-type   char*1
global temp totite-group  char*1
global temp totite-factor numeric
global temp totitd-code   char*6
global temp totitd-seq    zoned*2 unsigned
global temp totitd-type   char*1
global temp totitd-group  char*1
global temp totitd-factor numeric

global temp payroll-flag  char*1  parm prompt 'Enter payroll (A/C) : '

global temp x-comp-code1  char*6
global temp x-comp-code2  char*6
global temp x-comp-code3  char*6
;-------------------------------------------------------------------------------

request u122b_get_totexp                     &
 		on edit        errors report &
		on calculation errors report
access f190-comp-codes
choose comp-code "TOTEXP"
item totexp-code = 'TOTEXP'
item totexp-seq  =  reporting-seq
item totexp-group = comp-code-group  
;-------------------------------------------------------------------------------

request u122b_get_incexp                     &
 		on edit        errors report &
		on calculation errors report
access f190-comp-codes
choose comp-code "INCEXP"
item incexp-code = 'INCEXP'
item incexp-seq  =  reporting-seq
item incexp-group = comp-code-group  
;-------------------------------------------------------------------------------

request u122b_get_tithe1                     &
 		on edit        errors report &
		on calculation errors report
access f190-comp-codes
choose comp-code "TITHE1"
item tithe1-code = 'TITHE1'
item tithe1-seq  =  reporting-seq
item tithe1-type =  comp-type       
item tithe1-group = comp-code-group  
item tithe1-factor = factor
;-----------------------------------------------
; solo deduction
request u122b_get_tithd1                     &
                on edit        errors report &
                on calculation errors report
access f190-comp-codes
choose comp-code   "TITHD1"
item tithd1-code = "TITHD1"
item tithd1-seq  =  reporting-seq
item tithd1-type =  comp-type
item tithe1-group = comp-code-group
item tithe1-factor = factor
;-------------------------------------------------------------------------------

request u122b_get_tithe2                     &
 		on edit        errors report &
		on calculation errors report
access f190-comp-codes
choose comp-code "TITHE2"
item tithe2-code = 'TITHE2'
item tithe2-seq  =  reporting-seq
item tithe2-type =  comp-type       
item tithe2-group = comp-code-group  
item tithe2-factor = factor
;----------------------------------------------
; solo deduction
request u122b_get_tithd2                     &
                on edit        errors report &
                on calculation errors report
access f190-comp-codes
choose comp-code   "TITHD2"
item tithd2-code = "TITHD2"
item tithd2-seq  =  reporting-seq
item tithd2-type =  comp-type
item tithe2-group = comp-code-group
item tithe2-factor = factor

;-------------------------------------------------------------------------------
request u122b_get_tithe3                     &
 		on edit        errors report &
		on calculation errors report
access f190-comp-codes
choose comp-code "TITHE3"
item tithe3-code = 'TITHE3'
item tithe3-seq  =  reporting-seq
item tithe3-type =  comp-type       
item tithe3-group = comp-code-group  
item tithe3-factor = factor
;---------------------------------------------
; solo deduction
request u122b_get_tithd3                     &
                on edit        errors report &
                on calculation errors report
access f190-comp-codes
choose comp-code   "TITHD3"
item tithd3-code = "TITHD3"
item tithd3-seq  =  reporting-seq
item tithd3-type =  comp-type
item tithd3-group = comp-code-group
item tithe3-factor = factor

;-------------------------------------------------------------------------------

request u122b_get_totite                     &
 		on edit        errors report &
		on calculation errors report
access f190-comp-codes
choose comp-code "TOTITE"
item totite-code = 'TOTITE'
item totite-seq  =  reporting-seq
item totite-type =  comp-type       
item totite-group = comp-code-group  
item totite-factor = factor
;-------------------------------------------------------------------------------

request u122b_get_totitd                     &
 		on edit        errors report &
		on calculation errors report
access f190-comp-codes
choose comp-code "TOTITD"
item totitd-code = 'TOTITD'
item totitd-seq  =  reporting-seq
item totitd-type =  comp-type       
item totitd-group = comp-code-group  
item totitd-factor = factor

item x-comp-code1 = tithe1-code if payroll-flag = 'A'	&
	    else    tithd1-code if payroll-flag = 'C'

item x-comp-code2 = tithe2-code if payroll-flag = 'A'	&
	    else    tithd2-code if payroll-flag = 'C'

item x-comp-code3 = tithe3-code if payroll-flag = 'A'	&
	    else    tithd3-code if payroll-flag = 'C'

;-------------------------------------------------------------------------------
request u122b_create_update_f119_rec_type_d	&
		on edit        errors report	&
                on calculation errors report
; main access
access *f119_tithe_one_comp_code_per_doc_nbr		&
	link doc-ohip-nbr,				&
	     doc-nbr,					&
	     comp-code, rec-type-d			&
	  to doc-ohip-nbr,				&
	     doc-nbr,         				& 
	     comp-code, rec-type of f119-doctor-ytd opt    

; do not select summarized doctor records
select if doc-nbr of f119_tithe_one_comp_code_per_doc_nbr <> '000'

output f119-doctor-ytd update if record f119-doctor-ytd exists on errors report
   item amt-mtd 		final 		x-amt-net

;   item amt-ytd 		final amt-ytd + x-amt-gross
   item amt-ytd 		final           x-amt-net; brad2 - gross is reduced by factor so net is amount needed in both 
							;	mtd and ytd fields
   item last-mod-date 		final sysdate
   item last-mod-time 		final systime / 10000
   item last-mod-user-id	final 'u122b'

output f119-doctor-ytd alias f119-add add if not record f119-doctor-ytd exists on errors report
   item doc-ohip-nbr 		final doc-ohip-nbr 	of f119_tithe_one_comp_code_per_doc_nbr      
   item doc-nbr 		final doc-nbr 		of f119_tithe_one_comp_code_per_doc_nbr  
   item comp-code 		final comp-code 	of f119_tithe_one_comp_code_per_doc_nbr
   item comp-code-group 	final comp-code-group 	of f119_tithe_one_comp_code_per_doc_nbr
   item process-seq 		final reporting-seq 	of f119_tithe_one_comp_code_per_doc_nbr
   item rec-type 		final rec-type-d
   item amt-mtd 		final x-amt-net
   item amt-ytd 		final x-amt-net

   item last-mod-date final sysdate
   item last-mod-time final systime / 10000
   item last-mod-user-id final 'u122b'

;-------------------------------------------------------------------------------
request u122b_search_primary_doctor    		&
		on edit        errors report	&
                on calculation errors report

access *f119_tithe_one_comp_code_per_doc_nbr            &
	link doc-ohip-nbr				&
          to doc-ohip-nbr of f020-doctor-mstr  opt      &
        link doc-nbr of f020-doctor-mstr                &
          to doc-nbr of f020-doctor-extra opt

;brad3
;select if doc-date-fac-term = 0


select f119_tithe_one_comp_code_per_doc_nbr if                  &
        (comp-code = totite-code and payroll-flag = 'A')        &
   or   (comp-code = totitd-code and payroll-flag = 'C')

;select if     doc-flag-primary = 'Y'

;sorted on doc-ohip-nbr
sort on doc-ohip-nbr on doc-nbr

temp x-amt-net-summary-doc zoned*9 numeric
item x-amt-net-summary-doc = x-amt-net 				   &
	if doc-nbr of f119_tithe_one_comp_code_per_doc_nbr = "000" &
		 	reset at doc-ohip-nbr

temp x-primary-doc-nbr char*3
item x-primary-doc-nbr = doc-nbr of f020-doctor-mstr		   &
	if doc-flag-primary = "Y" 	reset at doc-ohip-nbr


;brad3
subfile u122b_prim_doc_temp keep at doc-ohip-nbr include     	&
        x-primary-doc-nbr,					&
	x-amt-net-summary-doc,					&
        f119_tithe_one_comp_code_per_doc_nbr

;----------------------------------------------------------
request u122b_swap_doc_nbr_with_primary		&
		on edit        errors report	&
                on calculation errors report

access *u122b_prim_doc_temp               &
        link x-primary-doc-nbr 		&
	 to  doc-nbr of f020-doctor-mstr

subfile u122b_prim_doc keep  if doc-date-fac-term = 0	&
	include u122b_prim_doc_temp 	
item doc-nbr final x-primary-doc-nbr            

;----------------------------------------------------------
request u122b_calc_tithe_1_2_3         		&
		on edit        errors report	&
                on calculation errors report

access *u122b_prim_doc 						&
	link 6 to const-rec-nbr of constants-mstr-rec-6 	&		
        link doc-ohip-nbr, 					&
	     '000', 						&
; comp-code is either 'TOTITE' or 'TOTITD'
	     comp-code,  					&
	     'D'                            			&
          to doc-ohip-nbr, 					&
	     doc-nbr, 						&
	     comp-code, 					&
	     rec-type of f119-doctor-ytd   alias f119-totite opt  &
        link doc-ohip-nbr, 					&
	     '000', 						&
; comp-code is either 'TITHE1' or 'TITHD1'
	     x-comp-code1,					&
	     'D'                            			&
          to doc-ohip-nbr, 					&
	     doc-nbr, 						&
	     comp-code, 					&
	     rec-type of f119-doctor-ytd   alias f119-tithe1 opt  &
        link doc-ohip-nbr, 					&
	     '000', 						&
; comp-code is either 'TITHE2' or 'TITHD2'
	     x-comp-code2,					&
	     'D'                            			&
        to   doc-ohip-nbr, 					&
	     doc-nbr, 						&
	     comp-code, 					&
	     rec-type of f119-doctor-ytd   alias f119-tithe2 opt  &
        link doc-ohip-nbr, 					&
	     '000', 						&
; comp-code is either 'TITHE3' or 'TITHD3'
	     x-comp-code3,					&
	     'D'                            			&
          to doc-ohip-nbr, 					&
	     doc-nbr, 						&
	     comp-code, 					&
	     rec-type of f119-doctor-ytd   alias f119-tithe3 opt  ;&


def tithe1-mtd zoned*10 numeric = amt-mtd   of f119-tithe1
def tithe1-ytd zoned*10 numeric = amt-ytd   of f119-tithe1

def tithe2-mtd zoned*10 numeric = amt-mtd   of f119-tithe2
def tithe2-ytd zoned*10 numeric = amt-ytd   of f119-tithe2

def tithe3-mtd zoned*10 numeric = amt-mtd   of f119-tithe3
def tithe3-ytd zoned*10 numeric = amt-ytd   of f119-tithe3

; mtd/ytd total titheable revenue is existing YTD + this mth's calculation

;def new-totite-mtd zoned*10 numeric = x-amt-net of u122b_prim_doc 
def new-totite-mtd zoned*10 numeric = x-amt-net-summary-doc of u122b_prim_doc 

def new-totite-ytd zoned*10 numeric = amt-ytd   of f119-totite	&
;				    + x-amt-net of u122b_prim_doc
				    + x-amt-net-summary-doc of u122b_prim_doc

def x-current-ep-nbr = current-ep-nbr

; get new tithe total income and compare against last ytd tithe income.
; if new       YTD titheable income is<= 75000 then calcluate using just this month's new MTD titheable income
; otherwise if YTD titheable income is > 75000 then calculate on what was left between last month's YTD titheabe income and 750000

def old-totite-ytd  zoned*10  = amt-ytd of f119-totite
def tot1 zoned*10 numeric = new-totite-ytd - amt-ytd of f119-totite
def tot2 zoned*10 numeric =  7500000       - amt-ytd of f119-totite
def tot3 zoned*10 numeric = 15000000       - amt-ytd of f119-totite

def amt-tithe1-actual-ytd  zoned*10 numeric = 	       			      		&
; (titheable income for first 75K has 2 options:
; 1 - if new ytd tithe is <= 75K then tithe1 income is diff between this mths and last mths ytd titheable income
; 2 - if new ytd tithe is >  75K and prev mths ytd was < 75K then tithe1 income is remaining inc up to 75K limit)
     round(((new-totite-ytd        - old-totite-ytd) * tithe1-factor)/10000,0,near) 	&
							if new-totite-ytd <=  7500000	&
else round(((7500000               - old-totite-ytd) * tithe1-factor)/10000,0,near) 	&	
			          if     old-totite-ytd                   <=  7500000	&
			   	     and new-totite-ytd		  	  >   7500000	
; (titheable income for 2nd 75K (75K to 150K range) has 3 options:
;1- if prev ytd tithe is in the same 75K - 150K range as new ytd inc then tithe2 income is this mths new income (new-old)
;2- if old ytd tithe was < 75K and new ytd is in 75K to -150K range, then tithe2 inc is amount of income within that 75K-150K range
;3- if old ytd tithe inc < 75K and new ytd is over 150K range, then total 75K of range is titheable income
def amt-tithe2-actual-ytd  zoned*10 numeric = 	       			      		&
     round(((new-totite-ytd        - old-totite-ytd) * tithe2-factor)/10000,0,near) 	&
			          if     old-totite-ytd                   >   7500000	&
			             and old-totite-ytd                   <= 15000000	&
			   	     and new-totite-ytd		  	  >   7500000	&	
			   	     and new-totite-ytd		  	  <= 15000000	&	
else round(((new-totite-ytd        - 7500000       ) * tithe2-factor)/10000,0,near) 	&	
			          if     old-totite-ytd                   <   7500000	&
			   	     and new-totite-ytd		  	  >   7500000	&
			   	     and new-totite-ytd		  	  <= 15000000	&
else round(((                        7500000       ) * tithe2-factor)/10000,0,near) 	&	
			          if     old-totite-ytd                   <   7500000	&
			   	     and new-totite-ytd		  	  >  15000000	

; (titheable income for 3rd 75K (150K - 49166700 range) has 3 options:
; 1-if prev ytd tithe is in the same 150K -491K range as new ytd inc then tithe3 income is this mths new income (new-old)
; 2-if old ytd tithe was < 150K and new ytd is in 150K - 491K  range, then tithe3 inc is amount of income within that 150K-491Krange
; 3-if old ytd tithe inc < 150K and new ytd is over 491K range, then total 75K of range is titheable income
def amt-tithe3-actual-ytd  zoned*10 numeric = 	       			      		&
     round(((new-totite-ytd        - old-totite-ytd) * tithe3-factor)/10000,0,near) 	&
			          if     old-totite-ytd                   >  15000000	&
			             and old-totite-ytd                   <= 49166700	&
			   	     and new-totite-ytd		  	  >  15000000	&	
			   	     and new-totite-ytd		  	  <= 49166700	&	
else round(((new-totite-ytd        - 15000000      ) * tithe3-factor)/10000,0,near) 	&	
			          if     old-totite-ytd                   <  15000000	&
			   	     and new-totite-ytd		  	  >= 15000000	&
			   	     and new-totite-ytd		  	  <= 49166700	&
else round(((                        7500000       ) * tithe3-factor)/10000,0,near) 	&	
			          if     old-totite-ytd                   <  15000000	&
			   	     and new-totite-ytd		  	  >  49166700	

;def amt-tithe3-actual-ytd  zoned*10 numeric = 	       					&
;     round(((new-totite-ytd       - 15000000 )  * tithe3-factor)/10000,0,near) 		&
;	 if (new-totite-ytd       > 15000000  and new-totite-ytd        <= 49166700)	&
;else round(((49166700 - amt-ytd of f119-totite) * tithe3-factor)/10000,0,near) 		&
;	if amt-ytd of f119-totite > 15000000 and amt-ytd of f119-totite <= 49166700

def amt-tithe1-actual-mtd  zoned*10 numeric = 	&
    amt-tithe1-actual-ytd  - tithe1-ytd
def amt-tithe2-actual-mtd  zoned*10 numeric = 	&
    amt-tithe2-actual-ytd  - tithe2-ytd
def amt-tithe3-actual-mtd  zoned*10 numeric = 	&
    amt-tithe3-actual-ytd  - tithe3-ytd

subfile brad_tithe1 keep include &
doc-nbr of u122b_prim_doc  ,&
x-primary-doc-nbr 	of u122b_prim_doc, &
x-amt-net-summary-doc	of u122b_prim_doc, &
doc-ohip-nbr of u122b_prim_doc  ,&
amt-ytd  of f119-totite ,&
x-amt-net of u122b_prim_doc, &
new-totite-ytd, &
tot1, &
tot2, &
tot3, &
amt-tithe1-actual-mtd,   &
amt-tithe1-actual-ytd  

subfile brad_tithe2 keep include &
doc-nbr of u122b_prim_doc  ,&
x-primary-doc-nbr 	of u122b_prim_doc, &
x-amt-net-summary-doc	of u122b_prim_doc, &
doc-ohip-nbr of u122b_prim_doc  ,&
amt-ytd  of f119-totite ,&
x-amt-net of u122b_prim_doc, &
new-totite-ytd, &
tot1, &
tot2, &
tot3, &
amt-tithe2-actual-mtd,   &
amt-tithe2-actual-ytd  

subfile brad_tithe3 keep include &
doc-nbr of u122b_prim_doc  ,&
x-primary-doc-nbr 	of u122b_prim_doc, &
x-amt-net-summary-doc	of u122b_prim_doc, &
doc-ohip-nbr of u122b_prim_doc  ,&
amt-ytd  of f119-totite ,&
x-amt-net of u122b_prim_doc, &
new-totite-ytd, &
tot1, &
tot2, &
tot3, &
amt-tithe3-actual-mtd,   &
amt-tithe3-actual-ytd  

define x-rec-type  char*1 = "A"

def comp-code1 char*6 = tithe1-code if payroll-flag = 'A'	&
		else    tithd1-code if payroll-flag = 'C'

def comp-seq1 zoned*2 unsigned  = tithe1-seq  if payroll-flag = 'A'	&
	              else 	  tithd1-seq  if payroll-flag = 'C'

def comp-group1 char*1 = tithe1-group if payroll-flag = 'A'	&
		else     tithd1-group if payroll-flag = 'C'


subfile f119_tithe_1_2_3_trans  keep   	          &
	if   amt-tithe1-actual-mtd <> 0 	  &
	  or amt-tithe1-actual-ytd <> 0  include  &
	 doc-ohip-nbr      of u122b_prim_doc 	, &
         x-primary-doc-nbr of u122b_prim_doc		, &
         comp-code1                     	, &
         comp-seq1                              , &
         comp-group1                            , &
         x-rec-type				, &
         amt-tithe1-actual-mtd    		, &
         amt-tithe1-actual-ytd    

def comp-code2 char*6 = tithe2-code if payroll-flag = 'A'	&
		else    tithd2-code if payroll-flag = 'C'

def comp-seq2 zoned*2 unsigned  = tithe2-seq  if payroll-flag = 'A'	&
	              else 	  tithd2-seq  if payroll-flag = 'C'

def comp-group2 char*1 = tithe2-group if payroll-flag = 'A'	&
		else     tithd2-group if payroll-flag = 'C'

subfile f119_tithe_1_2_3_trans alias f119tithe2        append  &
	if amt-tithe2-actual-mtd <> 0 		  &
	or amt-tithe2-actual-ytd <> 0  include	  &
	 doc-ohip-nbr      of u122b_prim_doc 	, &
         x-primary-doc-nbr of u122b_prim_doc	, &
         comp-code2                    		, & 
         comp-seq2                              , &
         comp-group2                            , &
         x-rec-type				, &
         amt-tithe2-actual-mtd    		, &
         amt-tithe2-actual-ytd    

def comp-code3 char*6 = tithe3-code if payroll-flag = 'A'	&
		else    tithd3-code if payroll-flag = 'C'

def comp-seq3 zoned*2 unsigned  = tithe3-seq  if payroll-flag = 'A'	&
	              else 	  tithd3-seq  if payroll-flag = 'C'

def comp-group3 char*1 = tithe3-group if payroll-flag = 'A'	&
		else     tithd3-group if payroll-flag = 'C'

subfile f119_tithe_1_2_3_trans alias f119tithe3        append  &
	if amt-tithe3-actual-mtd <> 0 		  &
	or amt-tithe3-actual-ytd <> 0  include	  &
	 doc-ohip-nbr      of u122b_prim_doc 	, &
         x-primary-doc-nbr of u122b_prim_doc	, &
         comp-code3                    		, & 
         comp-seq3                              , &
         comp-group3                            , &
         x-rec-type				, &
         amt-tithe3-actual-mtd    		, &
         amt-tithe3-actual-ytd    


output f110-compensation alias f110-totite-add add   &
	if 1=1
	item doc-nbr	     = doc-nbr of u122b_prim_doc
;	item doc-nbr	     = x-primary-doc-nbr  of u122b_prim_doc
        item ep-nbr          = current-ep-nbr
        item ep-nbr-entry    = current-ep-nbr
;either "TOTITE"  or "TOTITD"
        item comp-code       = comp-code of u122b_prim_doc
        item comp-type       = totite-type if payroll-flag = 'A'	&
			else   totitd-type if payroll-flag = 'C'
        item process-seq     = totite-seq  if payroll-flag = 'A'	&
			else   totitd-seq  if payroll-flag = 'C'
        item factor          = 10000
        item factor-override = " "
        item comp-units      = 0

        item amt-gross         =  new-totite-mtd
        item amt-net           =  new-totite-ytd 

        item compensation-status = " "
        item last-mod-date    = sysdate

;either 'TITHE1' or 'TITHD1'
output f110-compensation alias f110-tithe1-add add  & 
     if     (   amt-tithe1-actual-mtd <> 0           &
             or amt-tithe1-actual-ytd <> 0           &
            )                                        &
	and  1=1 
	item doc-nbr	     = doc-nbr of u122b_prim_doc
;	item doc-nbr	     = x-primary-doc-nbr  of u122b_prim_doc
        item ep-nbr          = current-ep-nbr
        item ep-nbr-entry    = current-ep-nbr
        item comp-code       = comp-code1
        item comp-type       = tithe1-type if payroll-flag = 'A'	&
			else   tithd1-type if payroll-flag = 'C'
        item process-seq     = comp-seq1 
        item factor          = 10000
        item factor-override = " "
        item comp-units      = 0
        item amt-net           =  amt-tithe1-actual-mtd
        item amt-gross         =  amt-tithe1-actual-ytd
        item compensation-status = " "
        item last-mod-date    = sysdate

;either 'TITHE2' or 'TITHD2'
output f110-compensation alias f110-tithe2-add add   &
     if     (   amt-tithe2-actual-mtd <> 0           &
             or amt-tithe2-actual-ytd <> 0           &
            )                                        &
	and  1=1
	item doc-nbr	     = doc-nbr of u122b_prim_doc
;	item doc-nbr	     = x-primary-doc-nbr  of u122b_prim_doc
        item ep-nbr          = current-ep-nbr
        item ep-nbr-entry    = current-ep-nbr
        item comp-code       = comp-code2
        item comp-type       = tithe2-type if payroll-flag = 'A'	&
			else   tithd2-type if payroll-flag = 'C'
        item process-seq     = comp-seq2  
        item factor          = 10000
        item factor-override = " "
        item comp-units      = 0
        item amt-net           =  amt-tithe2-actual-mtd
        item amt-gross         =  amt-tithe2-actual-ytd
        item compensation-status = " "
        item last-mod-date    = sysdate

;either 'TITHE3' or 'TITHD3'
output f110-compensation alias f110-tithe3-add add   &
     if     (   amt-tithe3-actual-mtd <> 0           &
             or amt-tithe3-actual-ytd <> 0           &
            )                                        &   
        and  1=1                                     
	item doc-nbr	     = doc-nbr of u122b_prim_doc
;	item doc-nbr	     = x-primary-doc-nbr  of u122b_prim_doc
        item ep-nbr          = current-ep-nbr
        item ep-nbr-entry    = current-ep-nbr
        item comp-code       = comp-code3
        item comp-type       = tithe3-type if payroll-flag = 'A'	&
			else   tithd3-type if payroll-flag = 'C'
        item process-seq     = comp-seq3 
        item factor          = 10000
        item factor-override = " "
        item comp-units      = 0
        item amt-net           =  amt-tithe3-actual-mtd
        item amt-gross         =  amt-tithe3-actual-ytd
        item compensation-status = " "
        item last-mod-date    = sysdate
        item last-mod-time    = systime / 10000
        item last-mod-user-id = "u122b gen'd"

;---------------------------------------------------------
; TITHE1 or TITHD1
output f119-tithe1 update                               &
     if     (   amt-tithe1-actual-mtd <> 0           &
             or amt-tithe1-actual-ytd <> 0           &
            )                                        &
       and  1=1  and record f119-tithe1 exists on errors report
  item amt-mtd         final amt-tithe1-actual-mtd
  item amt-ytd         final amt-tithe1-actual-ytd

output f119-doctor-ytd   alias f119-tithe1-add add    &
      if     (   amt-tithe1-actual-mtd <> 0           &
              or amt-tithe1-actual-ytd <> 0           &
             )					      &
       and  not record f119-tithe1 exists and 1=1  on errors report
  item doc-ohip-nbr    final doc-ohip-nbr     of u122b_prim_doc
  item doc-nbr         final '000'
  item rec-type	       final 'D'
  item comp-code       final comp-code1
  item process-seq     final comp-seq1 
  item comp-code-group final comp-group1 
  item amt-mtd         final amt-tithe1-actual-mtd 
  item amt-ytd         final amt-tithe1-actual-ytd 
;---------------------------------------------------------
; TITHE2 or TITHD2
output f119-tithe2 update                               &
     if     (   amt-tithe2-actual-mtd <> 0           &
             or amt-tithe2-actual-ytd <> 0           &
            )                                        &
       and  1=1 and  record f119-tithe2 exists on errors report
  item amt-mtd         final amt-tithe2-actual-mtd
  item amt-ytd         final amt-tithe2-actual-ytd

output f119-doctor-ytd   alias f119-tithe2-add add    &
      if     (   amt-tithe2-actual-mtd <> 0           &
              or amt-tithe2-actual-ytd <> 0           &
             )					      &
       and  not record f119-tithe2 exists and 1=1 on errors report
  item doc-ohip-nbr    final doc-ohip-nbr     of u122b_prim_doc
  item doc-nbr         final '000'
  item rec-type	       final 'D'
  item comp-code       final comp-code2 
  item process-seq     final comp-seq2  
  item comp-code-group final comp-group2  
  item amt-mtd         final amt-tithe2-actual-mtd 
  item amt-ytd         final amt-tithe2-actual-ytd 
;---------------------------------------------------------
; TITHE3 or TITHD3
output f119-tithe3 update                               &
     if     (   amt-tithe3-actual-mtd <> 0           &
             or amt-tithe3-actual-ytd <> 0           &
            )                                        &
       and  1=1 and record f119-tithe3  exists on errors report
  item amt-mtd         final amt-tithe3-actual-mtd
  item amt-ytd         final amt-tithe3-actual-ytd

output f119-doctor-ytd   alias f119-tithe3-add add    &
      if     (   amt-tithe3-actual-mtd <> 0           &
              or amt-tithe3-actual-ytd <> 0           &
             )					      &
       and  not record f119-tithe3 exists and 1=1 on errors report
  item doc-ohip-nbr    final doc-ohip-nbr     of u122b_prim_doc
  item doc-nbr         final '000'
  item rec-type	       final 'D'
  item comp-code       final comp-code3 
  item process-seq     final comp-seq3 
  item comp-code-group final comp-group3 
  item amt-mtd         final amt-tithe3-actual-mtd 
  item amt-ytd         final amt-tithe3-actual-ytd 

;----------------------------------------------------------------------------------

request u122b_add_summary_doctor_recs_to_f119	&
		on edit        errors report	&
                on calculation errors report

access *f119_tithe_one_comp_code_per_doc_nbr			&
        link doc-ohip-nbr, 					&
	     doc-nbr,						&
	     comp-code,  					&
	     'D'                            			&
          to doc-ohip-nbr, 					&
	     doc-nbr, 						&
	     comp-code, 					&
	     rec-type of f119-doctor-ytd   alias f119-totite opt &
 	 link comp-code						&
	  to  comp-code of f190-comp-codes 

select if     doc-nbr = '000'       

; TOTITE/TOTITD
output f119-totite    update                               &
       if 1=1 and record f119-totite exists on errors report
  item amt-mtd         final x-amt-net       
  item amt-ytd         final x-amt-net       ; we're not sure what it should be added to amt-ytd(TODO) 

output f119-doctor-ytd   alias f119-totite-add add    &
       if not record f119-totite exists and 1=1 on errors report
  item doc-ohip-nbr    final doc-ohip-nbr      	of f119_tithe_one_comp_code_per_doc_nbr
  item doc-nbr         final "000"
  item rec-type	       final 'D'
  item comp-code       final comp-code   	of f119_tithe_one_comp_code_per_doc_nbr
  item process-seq     final process-seq 	of f190-comp-codes 
  item comp-code-group final comp-code-group of f190-comp-codes   
  item amt-mtd         final x-amt-net      
  item amt-ytd         final x-amt-net        

;---------------------------------------------------------
build  $pb_obj/u122b
