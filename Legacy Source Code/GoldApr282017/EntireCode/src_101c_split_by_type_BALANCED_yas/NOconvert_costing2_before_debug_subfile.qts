; Program: costing2.qts
; Purpose: - creates files for costing the RAT REJECTION Charges
;				 (MANUAL rejections processed in costing3.qts)
;
;  DATE     BY WHOM     DESCRIPTION
;99/mar/28  YASEMIN     ORIGINAL
;00/feb/03  B.E.	- rat error '40' now charged against doctor
;
cancel clear

run costing2

set default
set process nolimit
set lock file update

;-------------------------------------------------------------------------------
; obtain current costing "constants" and pass to subsequent requests
;
use $use/get_const_rec_7_values_globals.qts
use $use/get_const_rec_7_values.qts

request costing2_1     &
        on calculation errors report    &
        on edit errors report

access f088-rat-rejected-claims-hist-hdr			&
;link (nconvert(ascii(clmhdr-batch-nbr,9)[4:3]))		&
	link clmhdr-doc-nbr					&
	to doc-nbr of f020-doctor-mstr 			        &
	link ohip-err-code to rat-code of f096-ohip-pay-code opt

 select 						  &
   if     clmhdr-date-period-end >= w-current-fiscal-start-yymmdd	  &
     and  clmhdr-date-period-end <= w-current-costing-cutoff-yymmdd	  &
     and  ohip-err-code <> "  "	   & ;rejection
     and  ohip-err-code <> "00"          &
     and  ohip-err-code <> "EV"          &
;     and  ohip-err-code <> "40"          &
     and  ohip-err-code <> "48"          &
     and  ohip-err-code <> "51"          &
     and  ohip-err-code <> "I5"          &
     and  ohip-err-code <> "80"          &
     and  (    (   ohip-err-code <> "35"       		&
                or (    doc-dept = 5         		&
                     or doc-dept = 2			&
 		  )        				&
               )						&
           and (    not record f096-ohip-pay-code exists   	&
                or (    ohip-err-code = "35" 	   		&
   	           and (    doc-dept = 5   	   		&
                          or doc-dept = 2				&
 		       )			   			&
 	           )						&
 	      )							&
 	  )

; (select doctor's active during costing analysis period)
; 
select   f020-doctor-mstr                                               &
     if    (   doc-date-fac-start  = 0000000                            &
            or doc-date-fac-start <= w-current-fiscal-start-yymmdd      &
           )                                                            &
       and (  doc-date-fac-term   = 00000000                            &
            or doc-date-fac-term >= w-current-fiscal-start-yymmdd       &
           )  

;def x-claim-id char*11 = clmhdr-claim-id[1:11] 
def x-claim-id char*11 =  ascii(clmhdr-batch-nbr)	&
        		+ ascii(clmhdr-claim-nbr)

subfile costing2a keep & ;append at doc-nbr  &
include                                     &
	 clmhdr-batch-nbr , clmhdr-claim-nbr, &
	 ped , 				      &
	 clmhdr-date-period-end, &
	 w-current-fiscal-start-yymmdd         ,&
	 w-current-costing-cutoff-yymmdd ,  &
         doc-nbr,                           &
         doc-name,                          &
         doc-inits,                         &
         doc-dept,                          &
	 doc-clinic-nbr,		    &
         x-claim-id ,                       &
	 ohip-err-code


request costing2_2     &
        on calculation errors report    &
        on edit errors report

access *costing2a

def nbr-svc zoned*6 unsigned = 0
def nbr-clm zoned*6 unsigned = 0
def nbr-dtl zoned*6 unsigned = 0

sort   on doc-nbr	&
       on x-claim-id

temp nbr-reject zoned*4 unsigned
item nbr-reject = nbr-reject + 1  reset at doc-nbr

def amt-ytd      zoned*8 signed = 0 
def misc-amt-ytd zoned*8 signed = 0 
def mohr-amt-ytd zoned*8 signed = 0 
def total-amt-ytd zoned*8 signed = 0

def man-reject   zoned*6 unsigned = 0

subfile costing2 keep append at doc-nbr  	&
include                                         &
         doc-nbr,                           &
         doc-name,                          &
         doc-inits,                         &
         doc-dept,                          &
	 doc-clinic-nbr,		    &
         nbr-svc,                         & 
         nbr-clm,                         &
         nbr-dtl,                         &   
         nbr-reject,			  &
         amt-ytd,			  &
	 misc-amt-ytd,			  & 
	 mohr-amt-ytd,			  & 
	 total-amt-ytd,			  &
	 man-reject

build $obj/costing2
