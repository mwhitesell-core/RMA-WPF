;#> program-id.     m027.qks
;    program purpose : entry of a doctor's contacts including their own address/phone etc
;
;    modification historY
;        date   who     description
; 2005/jan/19   b.e.	- original

can clear
screen $pb_obj/m027 receiving x-doc-nbr, x-doc-name  

temp x-doc-nbr char*3
temp x-doc-name char*30

file f027-contact-mstr occurs 4
  access viaindex  contact-key  using  filler, x-doc-nbr, contact-type, contact-location   
  item filler initial " "
  item doc-nbr initial x-doc-nbr

title "Contacts for: "                                                               at 1,36
align (,,50)
field x-doc-name  display  predisplay
title "Type  Loc  Surname/Given name             Address 1/2/3/Postal Code         " at 3,3

align (1,,5) (,,10) (,,14) (,,45) 
cluster occurs with f027-contact-mstr
;field filler of f027-contact-mstr 		required nochange
;field doc-nbr of f027-contact-mstr		required nochange
field contact-type of f027-contact-mstr 	required nochange
field contact-location of f027-contact-mstr 	required nochange 		&
      lookup noton f027-contact-mstr 				 		&
	via filler, doc-nbr,  contact-type , contact-location 			&
	using filler of f027-contact-mstr, doc-nbr of f027-contact-mstr, 	&
	contact-type  of f027-contact-mstr , contact-location of f027-contact-mstr

field contact-given-names 
field contact-addr-1
align (,4,10) (,,14) (,,45) 
field contact-inits label "Inits"
field contact-surname
field contact-addr-2
align (,4,14) (,,45) 
field contact-email-addr label "Email"
field contact-addr-3
align (,4,14) (,28,32) (,,45) 
field contact-phone-nbr  label "Phone"
field contact-phone-ext  label "Ext"
field postal-code
skip 1
build
