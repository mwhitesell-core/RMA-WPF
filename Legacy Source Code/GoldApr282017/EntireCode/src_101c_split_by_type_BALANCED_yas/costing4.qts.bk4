; Program: costing4.qts
; Purpose: - get clinic 22 regular/miscellaneous/MOHR revenue from 
;	     f050 revenue history file for doctors 'active' during 
;            current costing analysis period
;          - if a doctor is a 'clinic 22 doctor' then any revenue entered
;	     into OTHER clinics is RE-ENTERED INTO CLINIC 22. Therefore
;	     the other clinic revenue MUST BE IGNORED. Those doctors
;	     who bill ONLY IN NON-22 CLINICS must have their revenue
;	     picked up from these non-22 clinics. Costing3 has pre-determined
;	     doctors that bill in clinic 22 and placed their nbrs into the
;	     file doc-totals-tmp so that this pgm can check this file
;	     and make the appropriate selection of revenue .
;
;	     
;DATE       BY WHOM      DESCRIPTION
;99/03/28   YASEMIN      ORIGINAL
;03/dec/17  A.A.	 alpha doctor nbr
;

cancel clear

run costing4

set default
set process nolimit
set lock file update

;-------------------------------------------------------------------------------
; obtain current costing "constants" and pass to subsequent requests
;
use $use/get_const_rec_7_values_globals.qts
use $use/get_const_rec_7_values.qts

request costing4_1     &
        on calculation errors report    &
        on edit errors report

access f050-doc-revenue-mstr-history                            &
  link docrev-doc-nbr 					  	&
    to doc-nbr    of f020-doctor-mstr  				&
  link docrev-doc-nbr						&
    to doc-nbr    of doc-totals-tmp opt ; this file contains doctors
				        ; who bill in 22 clinic

;choose docrev-key '22@'
; y2k
;select if ep-yr = 1999
;
; (take revenue from the period that matches the current costing
;  analysis period - also select non-22 clinics only if doctor
;  bills only in non-22 clinics. Doctors BILLING IN CLINIC 22 has 
;  been pre-determined in costing3 and  those doctor nbrs written 
;  into doc-totals-tmp)
select if    iconst-date-period-end = w-current-costing-ped 	&
         and (   (    docrev-clinic-1-2 =  "22"			&
		 )						&
	      or (    docrev-clinic-1-2 <> "22"			&
		  and NOT record doc-totals-tmp exists		&
                 ) 						&
	     )
; (select doctor who  was 'active' anytime within the costing
;  analysis period - ie. start of fiscal year to current costing
;  analysis ped's yymm)

use $use/select_f020_active_for_costing_analysis_period.use


;sort on docrev-doc-nbr
sorted on  DOCREV-KEY on docrev-doc-nbr

def nbr-svc zoned*6 unsigned = 0
def nbr-clm zoned*6 unsigned = 0
def nbr-dtl zoned*6 unsigned = 0
def nbr-reject zoned*4 unsigned = 0

 
temp amt-ytd     zoned*8 signed 
item amt-ytd = amt-ytd +  docrev-ytd-in-rec + docrev-ytd-out-rec &
                if docrev-location <> "MISC"	&
               		reset at docrev-doc-nbr

temp misc-amt-ytd zoned*8 signed 
item misc-amt-ytd = misc-amt-ytd + docrev-ytd-out-rec	&
             if    docrev-location =  "MISC" 		&
	       and docrev-oma-code <> "MOHR"  &
               		reset at docrev-doc-nbr

temp mohr-amt-ytd zoned*8 signed 
item mohr-amt-ytd = mohr-amt-ytd + docrev-ytd-out-rec	&
               if    docrev-location = "MISC" 		&
		 and docrev-oma-code = "MOHR"  		&
               		reset at docrev-doc-nbr

def total-amt-ytd zoned*8 signed= amt-ytd + misc-amt-ytd + mohr-amt-ytd

def man-reject   zoned*6 unsigned = 0

subfile costing2 keep append at docrev-doc-nbr  &
include                                     &
; 2004/01/07 - MC - add alias doc-nbr so global compile at costing7 will have no error
;         docrev-doc-nbr,                    &
         docrev-doc-nbr   alias doc-nbr,    &
         doc-name,                          &
         doc-inits,                         &
         doc-dept,                          &
	 doc-clinic-nbr,		    &
         nbr-svc,                         & 
         nbr-clm,                         &
         nbr-dtl,                         &   
         nbr-reject,			  &
         amt-ytd,			  &
	 misc-amt-ytd,			  & 
	 mohr-amt-ytd,			  & 
	 total-amt-ytd,			  &
	 man-reject

; subfile used for testing only
; subfile brad keep if docrev-doc-nbr = 236 include &
; iconst-date-period-end , w-current-costing-ped    , &
;     docrev-clinic-1-2,  DOCREV-YTD-OUT-REC   ,  &
;        DOCREV-YTD-IN-REC


build $obj/costing4
