; DOC: utl0007_70.qzs
; DOC: REVENUE BY DOC/DEPT/OMACD/TECH/PROF AND TOTAL FOR MTD FIGURES
; DOC: FOR ALL AGENTS TOTALS AT DOC NO. (CLINIC 60 ONLY)
; DOC: RUN FOR: ACCOUNTING DEPT MARY BROWNRIDGE
;
;PROGRAM PURPOSE : COMPUTERIZE REPORT WHICH WAS CREATED MANUALLY

;DATE       WHO       DESCRIPTION
;2007/Mar   Y.B.      utl0007 
;

can clear
set rep nolimit
set page length 62 width 132
set rep device disc name utl0007_70
set formfee


access f050tp-doc-revenue-mstr                   &
   link docrevtp-doc-nbr                         &
to doc-nbr of f020-doctor-mstr opt               &
   link docrevtp-clinic-nbr                      &
to iconst-clinic-nbr-1-2 of iconst-mstr-rec opt

choose docrevtp-key "71@" to "75@"


sort on docrevtp-doc-nbr on docrevtp-oma-cd

def x-tech zoned*8 signed =                                   &
                           (docrevtp-in-tech-amt-billed(1) +  &
                           docrevtp-out-tech-amt-billed(1) +  &
                           docrevtp-in-tech-amt-adjusts(1) +  &
                           docrevtp-out-tech-amt-adjusts(1))

def t-svc zoned*5 unsigned =                               &
                           (docrevtp-in-tech-nbr-svc(1) +  &
                           docrevtp-out-tech-nbr-svc(1))

def x-prof zoned*8 signed =                                   &
                           (docrevtp-in-prof-amt-billed(1) +  &
                           docrevtp-out-prof-amt-billed(1) +  &
                           docrevtp-in-prof-amt-adjusts(1) +  &
                           docrevtp-out-prof-amt-adjusts(1))

def p-svc zoned*5 unsigned =                               &
                           (docrevtp-in-prof-nbr-svc(1) +  &
                           docrevtp-out-prof-nbr-svc(1))

def x-total zoned*8 signed = (x-tech + x-prof)

def svc-tot zoned*5 signed = (t-svc + p-svc)

define x-name cha*30 = pack(doc-name + " " + doc-inits)

select if DOCREVTP-AGENT-CD <> "6" &
  and  ( x-prof <> 0 or x-tech <> 0)

page heading                                               &
	tab 1  "P.E.D"				           &
	tab 7  iconst-date-period-end                      &
	tab 35 sysname				           &
	tab 85 "RUN DATE:"                                 &
	tab 96 sysdate                                     &
        tab 108 "PAGE:" tab 113 syspage pic "^^^"          &
skip 2  tab 35 "MONTHLY REVENUE RECONCILIATION No Agent 6" &
        tab 85 "utl0007_70"                                &
        tab 118 "LOCATION M599"                            &
skip 1  tab 118 "MUM020202"                                &
skip 2                                                     &
        tab 1   "DOC #"                                    &
        tab 8   "DEPT"                                     &
        tab 14  "DOC NAME/INITS"                           &
        tab 40  "OMA CODE"                                 &
        tab 50  "# SVC"                                    &
        tab 65  "TECHNICAL"                                &
        tab 77  "# SVC"                                    &
        tab 89  "PROFESSIONAL"                             &
        tab 103 "# SVC"                                    &
        tab 118 "TOTALS"                                   &
skip 2

report

footing at docrevtp-oma-cd                                        &
  tab 1   docrevtp-doc-nbr print at docrevtp-doc-nbr    	  &
  tab 9   doc-dept         print at docrevtp-doc-nbr              &
  tab 14  x-name           print at docrevtp-doc-nbr              &
  tab 40  docrevtp-oma-cd                                         &
  tab 50  t-svc   subtotal pic "^^^^^"                            &
  tab 63  x-tech  subtotal pic "^^^,^^^.^^"                       &
  tab 77  p-svc   subtotal pic "^^^^^"                            &
  tab 91  x-prof  subtotal pic "^^^,^^^.^^"                       &
  tab 103 svc-tot subtotal pic "^^^^^"                            &
  tab 114 x-total subtotal pic "^^^,^^^.^^"

footing at docrevtp-doc-nbr skip 2                      &
  tab 30  "T995A"                                       &
  tab 40  "TOTALS:"                                     &
  tab 50  t-svc   subtotal pic "^^^^^"                  &
  tab 62  x-tech  subtotal pic " ^^^,^^^.^^" float "$"  &
  tab 77  p-svc   subtotal pic "^^^^^"                  &
  tab 90  x-prof  subtotal pic " ^^^,^^^.^^" float "$"  &
  tab 103 svc-tot subtotal pic "^^^^^"                  &
  tab 113 x-total subtotal pic " ^^^,^^^.^^" float "$"  &
skip 2

final footing                                                &
  tab 34 "GRAND TOTALS:"                                     &
  tab 50  t-svc   subtotal pic "^^^^^"                       &
  tab 58  x-tech  subtotal pic " ^^^,^^^,^^^.^^" float "$"   &
  tab 77  p-svc   subtotal pic "^^^^^"                       &
  tab 86  x-prof  subtotal pic " ^^^,^^^,^^^.^^" float "$"   &
  tab 103 svc-tot subtotal pic "^^^^^"                       &
  tab 109 x-total subtotal pic " ^^^,^^^,^^^.^^" float "$"   &
skip 2

build $pb_obj/utl0007_70


  
