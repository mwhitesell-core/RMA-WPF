; U020C_USE.QTS
; COMMON PART OF U020B.QTS AND U022B.QTS (CREATE TAPE SUBFILE)
; 97/MAR/01 Y.B.  - ADD GROUP AA21 NEW CLINIC 82
; 97/JUN/01 K.M.  - ADDED THE TRANSLATED-GROUP-NBR TO THE SORT AND
;                   CHANGED W-GROUP-NBR TO TRANSLATED-GROUP-NBR
; 97/AUG/08 B.E.  - OUTPUT/ADD OF F099 FILE MADE CONDITIONAL UPON
;		    RECORD IN F099 NOT ALREADY EXISTING
; 99/Feb/04 B.E.  - temp 'fake out' of y2k format (hard coded "19" added to dates)
; 99/jun/20 B.E.  - removal of temp 'fake out' - now processes actual 8 digit dates
;		  - moved some 'hard coded' rma values into 
;		    copybook file (operator_constant.def) and 
;		    default_postal_code.def
; 99/jul/21 B.E. - new moh spec: manual review flag must be blank not "N"
; 99/Oct/20 M.C. - change clmhdr-date-admit from 6 to 8
; 99/oct/20 B.E. - fixed calculation of w-batch-ind to create a 12 character
;		   indicator
; 99/jul/21 B.E. - new moh spec: manual review flag must be blank not "N"
; 99/nov/24 B.E. - increased size of w-dtl field by 2 characters
; 03/aug/07 M.C. - add to sort on contract-code before other sort fields
; 04/jan/08 M.C. - alpha doc nbr - clmhdr-claim-id
; 11/May/31 MC1  - add a new request in the front 
;		 - modify request create_tape to add tmp-counters-alpha in the access
;	         - define w-moh-sli-code and pass in HEH record instead of w-moh-location-code
; 12/Dec/18 MC2  - blank out patient info(health nbr, version cd & birth  date) if health nbr = 1111111116
;	           for CME claims for w-claim-header-rec-1

; 2011/05/31 - MC1 - add this new request to check sli
request check_sli   on calculation errors report

access *u020b				&
	link clmdtl-oma-cd,		&
	     clmdtl-oma-suff,		&
	     w-moh-location-code 	&
	 to  clmdtl-oma-cd,		&
	     clmdtl-oma-suff,		&
	     loc-service-location-indicator of f201-sli-oma-code-suff opt

def claim-key char*10 = clmhdr-claim-id[1:10]

sorted on claim-key

temp sli-count zoned*2 unsigned
item sli-count = sli-count + 1 if record f201-sli-oma-code-suff exists  &
		reset at claim-key

output tmp-counters-alpha add at claim-key on errors report
   item tmp-counter-key-alpha final claim-key
   item tmp-counter-1 final sli-count
; 2011/05/30 - end

request create_tape on calculation errors report

; 2011/05/31 - MC1 - add tmp-counters-alpha in the access
;access *u020b
access *u020b link clmhdr-claim-id[1:10] to tmp-counter-key-alpha of tmp-counters-alpha
; 2011/05/31 - end

def w-clmhdr-claim-id char*19 = clmhdr-claim-id + pat-version-cd

;def w-dtl char*27  = w-clmhdr-claim-id + ascii(clmdtl-line-no,2) + &
def w-dtl char*29  = w-clmhdr-claim-id + ascii(clmdtl-line-no,2) + &
		     clmdtl-sv-date

;97/JUN/12 - K..M. - ADDED THE TRANSLATED-GROUP-NBR TO THE SORT

; 2003/08/07 - MC - add to sort on contract-code
;sort	on translated-group-nbr    	&
sort    on contract-code		&
        on translated-group-nbr    	&
; 2003/08/07 - end
	on batctrl-batch-nbr		&
	on w-clmhdr-claim-id		&
	on w-dtl

temp w-batch-count zoned*4
  item w-batch-count  count at batctrl-batch-nbr initial 1

temp w-b-count zoned *4
  item w-b-count count reset at batctrl-batch-nbr

temp w-b-count-r zoned *4
  item w-b-count-r count reset at batctrl-batch-nbr

temp w-h-count zoned*4
  item w-h-count count at w-clmhdr-claim-id reset at batctrl-batch-nbr

temp w-r-count zoned*4
  item w-r-count count at w-clmhdr-claim-id if w-pat-ohip-mmyy <> ' ' &
	reset at batctrl-batch-nbr

temp w-t-count zoned*4
  item w-t-count count at w-dtl reset at batctrl-batch-nbr

temp w-a-count zoned*4
  item w-a-count count at w-clmhdr-claim-id 	&
      if    w-pat-ohip-mmyy <> ' ' 		&
	and pat-prov-cd      = 'ON'		&
	reset at batctrl-batch-nbr

temp w-count
  item w-count count reset at w-clmhdr-claim-id

; y2k
;def w-batch-ind char*10 = ascii(((sysdate * 10000) + w-batch-count),10)
def  w-batch-ind char*12 = ascii(((sysdate * 10000) + w-batch-count),12)

; 99/jun/20
;def w-oper-nbr char*6 = '000805'
use $use/operator_constant.def

def w-clmdtl-oma-suff char*1 = clmdtl-oma-suff	&
	  if clmdtl-oma-suff <> 'M'		&
	else 'A'

def  w-batch-ident      char*3  = 'HEB'
def w-moh-office-code   char*1  = 'G'
def w-release-id        char*3  = "V03"

; 1999/jul/09 B.E. - use value now in u020a.sf picked up from f030 in u020a.qts
;def w-moh-location-code char*4  = "    "

; 2011/05/31 - MC1 - determine what to pass based on if record f201 found or not
;	if tmp-counter1 of tmp-counters-alpha = 0 means no record found in f201
;	if tmp-counter1 of tmp-coutners-alpha > 0 means record found in f201
def w-moh-sli-code char*4 = "    " if tmp-counter-1 = 0		& 
		else        w-moh-location-code
; 2011/05/31 - end

def w-reserved-occ-hdr1 char*4  = "    "
def w-reserved-occ-dtl1 char*10 = "          "

def w-claim-1-ident char*3 = 'HEH'
def w-claim-2-ident char*3 = 'HER'
def w-item-ident char*3 = 'HET'
def w-addr-ident char*3 = 'HEA'
def w-trail-ident char*3 = 'HEE'

def w-pay-program char*3 =	&
;	     'HCP' IF (BATCTRL-AGENT-CD = 0 AND PAT-PROV-CD = 'ON') &
;	ELSE 'WCB' IF BATCTRL-AGENT-CD = 2			&
;	ELSE 'RMB' IF PAT-PROV-CD <> 'ON'
	     'RMB' if pat-prov-cd <> 'ON'		&
	else 'WCB' if batctrl-agent-cd = 2		&
	else 'HCP'

def w-payee char*1 = 'P'

def w-pat-ohip-nbr char*12 = w-pat-ohip-mmyy[1:8] if pat-prov-cd = 'ON'&
	else w-pat-ohip-mmyy[1:12]

def w-subscr-addr-1 char*25 = subscr-addr1
def w-subscr-addr-2 char*25 = w-subscr-addr2
def w-subscr-addr-3 char*18 = w-subscr-addr3

def w-pat-health-nbr char*10 =	&
	ascii(pat-health-nbr,10) if pat-health-nbr <> 0	&
; 2012/12/18 - MC2
                           and      pat-health-nbr <> 1111111116 &
; 2012/12/18 - end
	else ' '


def w-clmhdr-refer-doc-nbr char*6 =	&
	ascii(clmhdr-refer-doc-nbr of u020b,6)	&
	if clmhdr-refer-doc-nbr of u020b <> 0	&
	else ' '

def w-clmhdr-date-admit char*8 =	&
	clmhdr-date-admit if clmhdr-date-admit <> '00000000'	&
	else ' '

; 1999/jul/21 B.E.
def w-clmhdr-manual-review char*1 	&
    = "Y" if clmhdr-manual-review = "Y"	&
 else " "


; 99/jun/20
use $use/default_postal_code.def
def w-subscr-postal-cd char*6 =	&
	subscr-postal-cd if subscr-postal-cd <> ' 0 0 0'	&
;	else 'L8S4K1'
        else w-default-postal-code

; 2012/12/18 - MC2 
def w-pat-info char*20  =		&
	  w-pat-health-nbr		&
	+ pat-version-cd		&
	+ ascii(pat-birth-date,8)	&
	if pat-health-nbr <> 1111111116	&
   else ' '
; 20120/12/18 - end
	
	 
def w-batch-header-rec char*79 		&
	= w-batch-ident			&
	+ w-release-id			&
	+ w-moh-office-code		&
	+ w-batch-ind			&
	+ w-oper-nbr			&
        + translated-group-nbr		&
	+ ascii(batctrl-doc-nbr-ohip,6)	&
	+ ascii(clmhdr-doc-spec-cd,2)

def w-claim-header-rec-1 char*79	&
	= w-claim-1-ident		&
; 2012/12/18 - MC2
;	+ w-pat-health-nbr		&
;	+ pat-version-cd		&
;	+ ascii(pat-birth-date,8)	&
        + w-pat-info			&
; 2012/12/18 - end
; 2004/01/08 - MC
;!      + clmhdr-claim-id[4:8]		&
        + clmhdr-claim-id[3:8]		&
; 2004/01/08 - end
	+ w-pay-program			&
        + w-payee			&
	+ w-clmhdr-refer-doc-nbr	&
	+ w-clmhdr-hosp			&
	+ w-clmhdr-date-admit		&
	+ '    '			&
	+ w-clmhdr-manual-review	&
; 2011/05/31 - MC1 - use w-moh-sli-code instead
;	+ w-moh-location-code		&
	+ w-moh-sli-code 		&
; 2011/05/31 - end
	+ w-reserved-occ-hdr1

def w-claim-header-rec-2 char*79	&
	= w-claim-2-ident		&
	+ w-pat-ohip-nbr		&
	+ pat-surname[1:9]		&
	+ pat-given-name[1:5]		&
	+ w-pat-sex			&
	+ pat-prov-cd

def w-item-rec char*79			&
	= w-item-ident			&
	+ clmdtl-oma-cd			&
	+ w-clmdtl-oma-suff		&
	+ '  '				&
	+ ascii(w-clmdtl-fee-ohip,6)	&
	+ ascii(clmdtl-nbr-serv,2)	&
	+ clmdtl-sv-date		&
	+ ascii(clmdtl-diag-cd,3)	&
	+ w-reserved-occ-dtl1


def w-trailer-rec char*79		&
	= w-trail-ident			&
	+ ascii(w-h-count,4)		&
	+ ascii(w-r-count,4)		&
	+ ascii(w-t-count,5)		

subfile u020_tp keep if w-b-count = 1 include w-batch-header-rec

subfile u020_tp alias u020_tape_h1 keep if w-count = 1 	&
	include w-claim-header-rec-1

subfile u020_tp alias u020_tape_h2 keep			&
	if w-count = 1 and w-pat-ohip-mmyy <> ' '	&
	include w-claim-header-rec-2

subfile u020_tp alias u020_tape_i keep at w-dtl include w-item-rec

subfile u020_tp alias u020_tape_t keep at batctrl-batch-nbr	&
	include w-trailer-rec

