; DOC: DOCTORLIST.QZS
; DOC: CREATE DOCTOR LISTING REPORT
; DOC: RUN FOR: MANAGEMENT/STAFF
;
;PROGRAM PURPOSE : DOCTOR LISTING REPORT
;
;DATE       WHO       DESCRIPTION
;92/03/16   YASEMIN   ORIGINAL
;
;
can clear
set rep nolimit
set rep device disc name doctorlist
set page length 62 width 80
set formfee

access f020-doctor-mstr

define start-from date = parm prompt    &
;y2k
 'ENTER START FROM DATE (YYYYMMDD) OR "0" FOR ALL: '

define start-to   date = parm prompt     &
;y2k
 'ENTER START TO   DATE (YYYYMMDD) OR "0" FOR ALL: '

define term-from  date = parm prompt      &
;y2k
 'ENTER TERM  FROM DATE (YYYYMMDD) OR "0" FOR ALL: '

define term-to    date = parm prompt      &
;y2k
 'ENTER TERM  TO   DATE (YYYYMMDD) OR "0" FOR ALL: '

define x-clinic zoned*2 unsigned = parm prompt   &
       'SELECT CLINIC "22" OR "60 TO 65" OR "0" FOR ALL: '

define x-dept zoned*2 unsigned = parm prompt       &
       'SELECT DEPT FROM 1 TO 14 OR "0" FOR ALL: '

define x-class zoned*1 unsigned  = parm prompt   &
'SELECT "1" FULL OR "2" PART OR "3" CS OR "4" PS OR "0" FOR ALL: '

select if  (x-clinic = doc-clinic-nbr or x-clinic = 0)	    &
      and  (x-dept  = doc-dept or x-dept = 0)               &
      and (    (x-class = 1 and doc-full-part-ind = "F")    &
            or (x-class = 2 and doc-full-part-ind = "P")    &
            or (x-class = 3 and doc-full-part-ind = "C")    &
            or (x-class = 4 and doc-full-part-ind = "S")    &
            or (x-class = 0                            ))   &
      and (  (    (   doc-date-fac-start ge start-from      &
                   or start-from = 0)                       &
              and (   doc-date-fac-start le start-to        &
                   or start-to = 0)                         &
              and (   doc-date-fac-term = 0)                &
	     )						    &
          or (    (   doc-date-fac-start <> 0)              &
              and (   doc-date-fac-term ge term-from        &
                   or term-from = 0)                        &
              and (   doc-date-fac-term le term-to          &
                   or term-to = 0)  			    &
	     )						    &
          )

define x-name cha*24 = pack(doc-name+" "+doc-inits)

sort on doc-clinic-nbr on x-name

page heading                                               &
	tab 1  "RUN DATE:"                                 &
	tab 11  sysdate                                    &
        tab 36 "DOCTOR LISTING"                           &
        tab 71 "PAGE:" tab 76 syspage pic "^^"             &
skip 2                                                     &
        tab 1   "CL#"                                      &
        tab 5   "DEPT"                                     &
        tab 11  "DR#"                                      &
        tab 16  "PHYSICIAN NAME"                           &
        tab 40  "OHIP #"                                   &
        tab 46  "SPEC"                                     &
        tab 52  "CLASS"                                    &
        tab 59  "START"                                    &
        tab 69  "TERM"                                     &
skip 1                                                     &
        tab 16 "SUB-SPECIALTY"                             &
        tab 46 "CODES"                                     &
        tab 59 "DATE"                                      &
        tab 69 "DATE"                                      &
skip 1                                                     &
        tab 16 "ADDITIONAL CLINICS"                        &
skip 2

;HEADING AT DOC-CLINIC-NBR SKIP 2    &
; TAB 40  "CLINIC" TAB 47 DOC-CLINIC-NBR SKIP 2

report            &
  tab 1   doc-clinic-nbr                                          &
  tab 6   doc-dept                                                &
  tab 11  doc-nbr fill "0"                                        &
  tab 16  x-name                                                  &
  tab 38  doc-ohip-nbr                                            &
  tab 47  doc-spec-cd                                             &
  tab 54  doc-full-part-ind                                       &
;y2k
  tab 59  doc-date-fac-start pic "^^^^/^^/^^"                       &
;y2k
  tab 69  doc-date-fac-term  pic "^^^^/^^/^^" bwz                   &
skip 1                                                            &
  tab 16  doc-sub-specialty                                       &
  tab 46  doc-spec-cd-2 bwz                                       &
skip 1                                                            &
  tab 16  doc-clinic-nbr-2 bwz                                    &
  tab 19  doc-clinic-nbr-3 bwz                                    &
  tab 12  doc-clinic-nbr-4 bwz                                    &
  tab 15  doc-clinic-nbr-5 bwz                                    &
  tab 18  doc-clinic-nbr-6 bwz                                    &
skip 2

footing at doc-clinic-nbr skip page

build $pb_obj/doctorlist


  
