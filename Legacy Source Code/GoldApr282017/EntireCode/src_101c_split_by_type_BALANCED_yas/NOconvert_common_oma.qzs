; DOC: COMMON_OMA.QZS
; DOC: MOST COMMON OMA CODES USED
; DOC: RUN FOR: THEKLA
;
;PROGRAM PURPOSE : MOST COMMON OMA CODES USED
;
;
;DATE       BY WHOM   DESCRIPTION
;95/06/07   YASEMIN   ORIGINAL
;
;
can clear
set default
set report nolimit
set report device disc name oma
set page length 60 width 132
set formfeed

access f050-doc-revenue-mstr   &
  link docrev-oma-code to fee-oma-cd of f040-oma-fee-mstr opt  &
  link docrev-doc-nbr to doc-nbr of f020-doctor-mstr opt

def x-name cha*30 =pack(doc-name +" "+ doc-inits)
def x-services zoned*5 signed = docrev-ytd-out-svc + docrev-ytd-in-svc

sort on docrev-clinic-1-2  &
     on x-name             &
     on docrev-oma-cd

page heading &
skip 1  tab 1   "COMMON_OMA.QZS"         &
        tab 25  "OHIP CODES USED"        &
        tab 50  sysdate                  &
        tab 66  syspage                  &
skip 1  tab 25  "---------------"        &
skip 2

heading at x-name    skip page           &
        tab 1  "******"                  &
        tab 8  doc-nbr fill '0'          &
        tab 13 "DR."                     &
        tab 17 x-name                    &
skip 3  tab 1  "CODE"                    &
        tab 8  "OHIP CODE DESCRIPTION"   &
        tab 60 "SERVICES"                &
skip 1  tab 1  "----"                    &
        tab 8  "---------------------"   &
        tab 60 "--------"                &
skip 2

footing at docrev-oma-cd                  &
skip 1   tab 1  docrev-oma-cd             &
         tab 8  fee-desc                  &
         tab 60 x-services subtotal pic "^^^^^"    &
skip 1

footing at x-name    &
skip 2   tab 1  "TOTAL SERVICES FOR DR."   &
         tab 24 x-name  &
         tab 60 x-services subtotal pic "^^^^^"  &
skip 2

build $pb_obj/oma


  
