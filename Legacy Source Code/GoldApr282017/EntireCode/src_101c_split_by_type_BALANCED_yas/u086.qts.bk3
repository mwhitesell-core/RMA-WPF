;#> PROGRAM-ID.     U086.QTS
;
;	((C)) Dyad Technologies
;
;    PROGRAM PURPOSE:	When patient eligibility information is changed a
;			driver record is written to f086. F086 is used by this
;			program to update all patient claims that were being
;	 		"H"eld so that they will be "R"esubmitted. Any held
;			claim which belongs to the current cycle (claim's
;			f001 record's status is checked) have the status
;			set to "blank" rather than "R" so they can be
;			processed by the OHIP submission program rather than
;			the resubmits program.
;			Any of the patient's claims in the 'rejected claims' 
;			file are also deleted by the program.
;
;    MODIFICATION HISTORY
;        DATE   WHO          DESCRIPTION
;     93/APR/28 AGK.         - ORIGINAL (SMS 141)
;     93/MAY/28 M.CHAN	     - PDR 573
;			     - A NEW REQUEST TO RESUBMIT SUBMITTED
;			       CLAIMS THAT HAVE NOT BEEN PAID
;     96/DEC/10 M. CHAN	     - REVISED THE ACCESS AND SELECT STMT
;     98/DEC/08 M. CHAN	     - add a new request to sort f086-pat-id to create
;			       unique ikey into the subfile, and using the 
;			       subfile as the driver file for each request
;			     - add on errors report on each output statement
;			     - use pkey index of f002-claims-mstr instead of 
;			       bkey index in request resubmit_submitted_claims
;  1999/jan/31 B.E.	- y2k
;  1999/Feb/01 M.C.          - change to access to key-p-clm-data instead of the
;			       individual segments for p key
;  1999/nov/25 B.E.	- added select if clmhdr-date-cash-tape-payment = blanks;			  to existing test for all zeros

can clear
set default
set process nolimit

run u086

request sort_f086_pat_id
; reduce driver file to 1 record per patient

access f086-pat-id

sort on clmhdr-pat-ohip-id-or-chart

subfile sortf086 keep at clmhdr-pat-ohip-id-or-chart		&
  include clmhdr-pat-ohip-id-or-chart



request patient_claims_update
;
; (00/may/29 B.E. access f001 to obtain claim's batch status to determine
;		  if it's part of the current cycle. Set status to blank if 
;		  current status, otherwise "R"esubmit)
access *sortf086               		 			&
  link clmhdr-pat-ohip-id-or-chart        			&
   to  clmhdr-pat-ohip-id-or-chart  of rejected-claims		&
  link ("B"), 							&
	(nconvert(ascii(claim-nbr,10)[1:2] + "0" + ascii(claim-nbr,10)[3:6])), &
	(nconvert(ascii(claim-nbr,10)[9:2])),  			&
	"00000", "0" 						&
   to   key-clm-type, key-clm-batch-nbr, 			&
        key-clm-claim-nbr, key-clm-serv-code,    		&
        key-clm-adj-nbr of f002-claims-mstr			&
   link clmhdr-batch-nbr of f002-claims-mstr                    &
    to  batctrl-batch-nbr of f001-batch-control-file   opt

use def_batctrl_batch_status.def nol

def submit-status char*1 						&
     = " " if    record f001-batch-control-file exists			&
	     and batctrl-batch-status < batctrl-batch-status-ohip-sent	&
  else "R"

output f002-claims-mstr update	on errors report 
   item clmhdr-tape-submit-ind final submit-status		&
	if clmhdr-tape-submit-ind = "H"	

   item clmhdr-confidential-flag final "N" 			&
        if clmhdr-confidential-flag = "Y"



request delete_rejected_claims

access *sortf086  						&
  link clmhdr-pat-ohip-id-or-chart        			&
   to clmhdr-pat-ohip-id-or-chart  of rejected-claims

output rejected-claims delete on errors report

request resubmit_submitted_claims

; 98/12/08  access f086-pat-id						&
access *sortf086  	 					&
;99/02/01  link "P", nconvert(clmhdr-pat-ohip-id-or-char2:9]),		&
  link "P", (clmhdr-pat-ohip-id-or-chart[2:17])				&
;99/02/01       nconvert(clmhdr-pat-ohip-id-or-chart[11:2]),		&
;99/02/01       (clmhdr-pat-ohip-id-or-chart[13:4] + " "), " "		&
; 98/12/08   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr,	&
;98/12/08        key-clm-serv-code, key-clm-adj-nbr of f002-claims-mstr
;99/02/01   to  key-p-clm-type, key-p-clm-batch-nbr, key-p-clm-claim-nbr, &
;99/02/01       key-p-clm-serv-code, key-p-clm-adj-nbr of f002-claims-mstr
   to  key-p-clm-type, key-p-clm-data of f002-claims-mstr   

sel f002-claims-mstr if clmhdr-tape-submit-ind = "S"            &
    and (clmhdr-agent-cd =  0  or clmhdr-agent-cd =  2)         &
;   AND CLMHDR-DATE-CASH-TAPE-PAYMENT = " "
;y2k
    and (   clmhdr-date-cash-tape-payment = "00000000"		&
         or clmhdr-date-cash-tape-payment = " ")

output f002-claims-mstr update on errors report
   item clmhdr-tape-submit-ind final "R"

build $pb_obj/u086
  
