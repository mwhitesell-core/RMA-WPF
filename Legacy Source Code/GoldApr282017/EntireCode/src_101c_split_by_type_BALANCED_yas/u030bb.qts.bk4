;#> Program-id.     u030bb.qts
;
; ((C)) Dyad Technologies
;
;  Program Purpose:  - RMA charges doctors for researching and correcting
;		       claims rejected on the RAT tape if they can't be
;		       automatically adjusted.  An incorrect claim usually
;		       has an error code however any claim not fully paid
;		       is considered to be in error.
;		     - so that these rejects can be tracked and costed
;		       this pgms adds the rejected claims to f088-hdr/dtl files
			; NOTE: the error is charged even if the
			; the error codes are blank/00 as long as
			; the amount paid <> amount billed
;                    - Note: unable to add new request on u030b as too
;                      many files would be declared so this separate
;                      program was created.
;
; MODIFICATION HISTORY
;     DATE    WHO    DESCRIPTION
; 2000/jan/21 B.A.   - original
; 2000/mar/10 B.E.   - add new fields to f088, obtain claims's PED via f002
; 2000/mar/30 M.C.   - change the linkage syntax    
;		     - add 3 define items and select statement in request
;		       f088_add_dtl....
can clear
run u030bb

set default
set lock file update

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;request get_clinics_PED on calculation errors report & 
;                        on edit errors report
; get the current PED of the Clinic being processed for update into f088 hdr

use $use/get_iconst_clinic_values_globals.qts	;nol
use $use/get_iconst_clinic_values.qts		;nol

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

set process nolimit

request f088_add_hdr				&
		on calculation errors report 	& 
                on edit errors report

; (link to claims hdr to obtain claim's PED - NOTE: this step could be
;  eliminated if u030b.qts put the ped in the part-paid-hdr file)

access *u030_no_adj                              & 
     link part-hdr-claim-id                      &
       to part-hdr-claim-id of part-paid-hdr     &
     link 'B', 					 &
 	   nconvert(part-hdr-claim-id[1:9]),	 &
; 2000/03/30 - MC wrong  linkage
;          nconvert(part-hdr-claim-id[7:2]),	 &
           nconvert(part-hdr-claim-id[10:2]),    &
	   '00000', 				 &
	   '0'       			 	 &
       to key-clm-type, 			 &
	  key-clm-batch-nbr, 			 &
	  key-clm-claim-nbr,      		 & 
	  key-clm-serv-code, 			 &
	  key-clm-adj-nbr of f002-claims-mstr   opt

use $use/f088_charge_status.def nol

output f088-rat-rejected-claims-hist-hdr add on errors report

 item clmhdr-batch-nbr final nconv(part-hdr-claim-id of part-paid-hdr[1:9])
 item clmhdr-claim-nbr final nconv(part-hdr-claim-id of part-paid-hdr[10:2])
 item clmhdr-doc-nbr   final nconv(part-hdr-claim-id of part-paid-hdr[4:3])

; (even if the ohip-error-code is 00 or blank the claim may be researched if 
;  the amt paid <> billed so these values are stored)
 item part-hdr-amt-bill of f088-rat-rejected-claims-hist-hdr	&
	final part-hdr-amt-bill of u030_no_adj 
 item part-hdr-amt-paid of f088-rat-rejected-claims-hist-hdr	&
	final part-hdr-amt-paid of u030_no_adj 
 item ohip-err-code final ' '; actual error code not stored - the existance
			     ; of this record is based upon u030b determined
			     ; full payment was not made and no automatic
			     ; adjustment can be made - therefore RMA research
			     ; research is needed
			     ; If subsequently RMA determines that the doctor
			     ; is NOT responsible for the error they can
			     ; cancel charges using "X" in charge field
 item charge-status final charged	; if record is in u030_no_adj then
					; charging to investigate error

 item ped 			  of f088-rat-rejected-claims-hist-hdr    &
     final w-ped 
 item clmhdr-date-period-end	  of f088-rat-rejected-claims-hist-hdr    &
     final clmhdr-date-period-end of f002-claims-mstr

 item clmhdr-serv-date 		of f088-rat-rejected-claims-hist-hdr    &
     final part-hdr-serv-date	of part-paid-hdr

 item entry-date      final sysdate
 item entry-time-long final systime
 item entry-user-id   final 'u030bb'


request f088_add_dtl_ignore_duplicate_key_error	    &
			       on calculation errors report &
                               on edit errors report
; NOTE: d001 allows 'repetitive' service entries on a single record. When these
;	claims are sent to OHIP the repetitive services are "exploded" into
;	separate records. Therefore when records are read from the RAT it is
;	possible to get duplicates based upon link via service code only

access *u030_no_adj                                                          &
  link part-hdr-clinic-nbr,  part-hdr-claim-nbr                              &
    to part-dtl-clinic-nbr, part-dtl-claim-nbr of part-paid-dtl 	     &
     link 'B',                                   &
           nconvert(part-hdr-claim-id[1:9]),     &
; 2000/03/30 - MC wrong  linkage
;          nconvert(part-hdr-claim-id[7:2]),	 &
           nconvert(part-hdr-claim-id[10:2]),    &
	   part-dtl-oma-cd[1:5],		 &
           '0'                          	 &
       to key-clm-type,                          &
          key-clm-batch-nbr,                     &
          key-clm-claim-nbr,                     &
          key-clm-serv-code, 			 &
	  key-clm-adj-nbr of f002-claims-mstr   opt

; 2000/03/30 - MC - defined 3 consecutive dates and select statements

def consec-date-1 date =  nconvert(clmdtl-sv-date[1:6]+    &
				   clmdtl-consec-dates-r[2:2])  &
	if clmdtl-sv-day(1) <> 0

def consec-date-2 date =  nconvert(clmdtl-sv-date[1:6]+    &
				   clmdtl-consec-dates-r[5:2])  &
	if clmdtl-sv-day(2) <> 0

def consec-date-3 date =  nconvert(clmdtl-sv-date[1:6]+    &
				   clmdtl-consec-dates-r[8:2])  &
	if clmdtl-sv-day(3) <> 0

select if part-dtl-serv-date = nconvert(clmdtl-sv-date) or	&
	  part-dtl-serv-date = consec-date-1  or	&  
	  part-dtl-serv-date = consec-date-2  or	&  
	  part-dtl-serv-date = consec-date-3


output f088-rat-rejected-claims-hist-dtl add on errors report

 item clmhdr-batch-nbr    final nconv(part-dtl-claim-id  of part-paid-dtl[1:9])
 item clmhdr-claim-nbr    final nconv(part-dtl-claim-id  of part-paid-dtl[10:2])
 item clmhdr-adj-oma-cd   final part-dtl-oma-cd[1:4]
 item clmhdr-adj-oma-suff final part-dtl-oma-cd[5:1]
 item clmhdr-adj-adj-nbr  final '0'

 item ped 		   of f088-rat-rejected-claims-hist-dtl    &
     final w-ped 

 item ohip-err-code final part-dtl-explan-cd 

; 2000/mar/06 B.E.
 item       part-dtl-amt-paid of f088-rat-rejected-claims-hist-dtl	&
     final  part-dtl-amt-paid of part-paid-dtl
 item       part-dtl-amt-bill of f088-rat-rejected-claims-hist-dtl	&
     final  part-dtl-amt-bill of part-paid-dtl
 item auto-adj-flag	final "N"

 item clmhdr-doc-nbr  final nconv(part-dtl-claim-id of part-paid-dtl[4:3])

 item clmdtl-date-period-end  	final clmdtl-date-period-end of f002-claims-mstr
 item clmdtl-sv-date  		final asc(part-dtl-serv-date,8) 
 item entry-date     		final sysdate
 item entry-time-long final systime
 item entry-user-id final 'u030bb'


request extract_explan_cd on calculation errors report on edit errors report
; (update header with the error code of the detail with the largest shortfall 
;  ie. with the largest descrepancy between paid and billed)

access part-paid-dtl

; (select only details with an error code)
select  if    part-dtl-explan-cd <> '  ' &
          and part-dtl-explan-cd <> '00'

def x-bal = part-dtl-amt-paid - part-dtl-amt-bill

; (sort on x-bal so that the dtl with largest difference between paid/billed
;  is sorted last and then the error code of that dtl will be used to update
;  the header record)

sort on part-dtl-clinic-nbr &
     on part-dtl-claim-nbr  &
     on x-bal               &
     on part-dtl-oma-cd

subfile extract-explan-cd keep at part-dtl-claim-nbr include &
     part-dtl-clinic-nbr, &
     part-dtl-claim-nbr,  &
     part-dtl-explan-cd


request update_explan_cd on calculation errors report on edit errors report


access *extract-explan-cd               &
 link  asc(part-dtl-clinic-nbr,2) + ascii(part-dtl-claim-nbr,9), sysdate       &
  to rat-rejected-claim, ped of f088-rat-rejected-claims-hist-hdr

output f088-rat-rejected-claims-hist-hdr update on errors report noitems
 item ohip-err-code final part-dtl-explan-cd

build $pb_obj/u030bb
