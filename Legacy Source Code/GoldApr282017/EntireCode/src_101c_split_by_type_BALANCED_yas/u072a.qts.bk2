;#> PROGRAM-ID.    U072A.QTS
;
;       ((C)) Dyad Technologies
;
; PROGRAM PURPOSE : to reload the non-deleted claims placed into subfile
;		    by u072.qts
;
;    MODIFICATION HISTORY
;    DATE     WHO    DESCRIPTION
; 2000/07/18  B.A.   - original
; 2002/07/05  M.C.   - add on errors report on output statements and
;                      add calculation/edit on errors report
;                      on request statements
; 2002/07/08  M.C.   - include agent 4 when creating f002-claim-shadow
; 2003/dec/23 A.A.   - alpha doctor nbr
; 2004/feb/04 M.C.   - add new request to delete purged claims records
;		       from rejected-claims
; 2009/Dec/02 M.C.   - add new request to delete purged claims records
;		       from f002-claims-extra

cancel clear
set process nolimit
set lock file update

; ------------------------------------------------
request load_u072_retain_claim_hdr  on calculation errors report &
                                   on edit errors report


access *u072-retain-claim-hdr

output f002-claims-mstr add on errors report

;2002/07/08 - MC
;output f002-claim-shadow add if clmhdr-agent-cd = 6 &
output f002-claim-shadow add if (clmhdr-agent-cd = 6 or clmhdr-agent-cd = 4) &
;2002/07/08 - end
       and clmhdr-batch-type = 'C' on errors report

item clm-shadow-clinic final            &
;!        nconv(ascii(clmhdr-batch-nbr of u072-retain-claim-hdr)[1:2])
        nconv(clmhdr-batch-nbr of u072-retain-claim-hdr[1:2])
item clm-shadow-subdivision final       &
        (clmhdr-date-cash-tape-payment of u072-retain-claim-hdr[4:1])
item clm-shadow-patient final clmhdr-pat-ohip-id-or-chart &
        of u072-retain-claim-hdr
item clm-shadow-batch-nbr final clmhdr-batch-nbr of u072-retain-claim-hdr
item clm-shadow-claim-nbr final clmhdr-claim-nbr of u072-retain-claim-hdr

;------------------------------------------------
request load_u072_retain_claim_dtl on calculation errors report &
                                   on edit errors report

access *u072-retain-claim-dtl

output f002-claims-mstr add on errors report

;------------------------------------------------
request load_u072_retain_claim_desc on calculation errors report &
                                   on edit errors report

access *u072-retain-claim-desc

output f002-claims-mstr add on errors report

;------------------------------------------------
; 2004/02/04 - MC - add the new request

request delete_purged_claims    on calculation errors report &
                                   on edit errors report

access *u072-delete-claim-hdr 		&
	link (clmhdr-batch-nbr + ascii(clmhdr-claim-nbr,02) )	&
	 to  claim-nbr of rejected-claims

output rejected-claims delete on errors report

; 2004/02/04 - end

;------------------------------------------------
; 2009/12/02 - MC - add the new request

request delete_purged_claims-extra    on calculation errors report &
                                   on edit errors report

access *u072-delete-claim-hdr 		&
	link (clmhdr-batch-nbr + ascii(clmhdr-claim-nbr,02) )	&
	 to  clmhdr-rma-clm-nbr of f002-claims-extra

output f002-claims-extra delete on errors report

; 2009/12/02 - end

build $obj/u072a
