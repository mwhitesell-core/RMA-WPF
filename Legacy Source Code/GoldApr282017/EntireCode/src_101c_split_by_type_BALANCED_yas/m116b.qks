;#> PROGRAM-ID.     m116b.qks
;
;	((C)) Dyad Technologies
;
;    PROGRAM PURPOSE : maintenance of f116-dept-expense-rules-dtl
;
;    MODIFICATION HISTORY
;      DATE   WHO	DESCRIPTION
; 2008/jun/05 M.C.	- original
;

can clear

screen $pb_obj/m116b receiving f116-dept-expense-rules-hdr ;predisplay

file f116-dept-expense-rules-hdr master

file f116-dept-expense-rules-dtl primary   occurs 15  noitem
select if 	  dept-expense-calc-code of f116-dept-expense-rules-dtl	&
		= dept-expense-calc-code of f116-dept-expense-rules-hdr	&
   	and       dept-nbr of f116-dept-expense-rules-hdr		&
   		= dept-nbr of f116-dept-expense-rules-dtl		&
   	and	  dept-nbr of f116-dept-expense-rules-hdr		&
   		= dept-nbr of f116-dept-expense-rules-dtl		&
   	and	  doc-afp-paym-group of f116-dept-expense-rules-hdr	&
   	 	= doc-afp-paym-group of f116-dept-expense-rules-dtl	&
   	and	  doc-nbr of f116-dept-expense-rules-hdr		&
   		= doc-nbr of f116-dept-expense-rules-dtl
   Item dept-expense-calc-code initial dept-expense-calc-code of f116-dept-expense-rules-hdr
   Item dept-nbr initial dept-nbr of f116-dept-expense-rules-hdr
   Item doc-afp-paym-group initial doc-afp-paym-group of f116-dept-expense-rules-hdr
   Item doc-nbr initial doc-nbr of f116-dept-expense-rules-hdr


file f190-comp-codes reference
   access viaindex comp-code   using comp-code of f116-dept-expense-rules-dtl

   item desc-long of f116-dept-expense-rules-dtl initial desc-long of f190-comp-codes
   item desc-short of f116-dept-expense-rules-dtl initial desc-short of f190-comp-codes

temp x-srch-code char*6

use $pb_src/std_hilite nol

title "Dept Expense Rules - Dtl    " at ,42
title "Dept Expense            Dept   AFP    Doc    "  at 3,4
title "Calc Code               Nbr    Group  Nbr    "  at 4,4

skip to 5
align (,,4) (,,28)  (,,35) (,,42) 
field dept-expense-calc-code of f116-dept-expense-rules-hdr id same display   
field dept-nbr of f116-dept-expense-rules-hdr id same display            
field doc-afp-paym-group of f116-dept-expense-rules-hdr id same display 
field doc-nbr of f116-dept-expense-rules-hdr id same display                   

skip to 7
title 'Comp       Tithe     Display    Description (long) ' at 7,4
title 'Code    In/Exclude    Hide                         ' at 8,4

skip to 9
align (1,,4) (,,16) (,,28) (,,36)
cluster occurs with f116-dept-expense-rules-dtl                   
field comp-code of f116-dept-expense-rules-dtl required nochange  		&
      lookup on f190-comp-codes message "Comp Code does not exist" ,		&
             noton f116-dept-expense-rules-dtl via dept-expense-calc-code,  	&
      dept-nbr , doc-afp-paym-group , doc-nbr, comp-code using 			&
      dept-expense-calc-code of f116-dept-expense-rules-dtl , 			&
      dept-nbr of f116-dept-expense-rules-dtl , 				&
      doc-afp-paym-group of f116-dept-expense-rules-dtl , 			&
      doc-nbr of f116-dept-expense-rules-dtl,					&
      comp-code of f116-dept-expense-rules-dtl
field tithe-in-ex-clude-flag of f116-dept-expense-rules-dtl upshift
field flag-display-hide of f116-dept-expense-rules-dtl  upshift
field desc-long of f116-dept-expense-rules-dtl      predisplay

procedure input comp-code
begin
  if fieldtext = "."
  then begin
    let x-srch-code = " "
    run screen $pb_obj/sy033.qkc clear lines 13 to 23 passing x-srch-code mode f
    if x-srch-code ne " "
    then let fieldtext = x-srch-code
    else error "Error - A Compensation Code is required.^G^G"
  end
end

procedure process comp-code
begin
   let desc-long of f116-dept-expense-rules-dtl = desc-long of f190-comp-codes  
end


build detail list

