;Program: u931.qts
;
;untest version - u931_temp still running until this program tested successfully
;
;Purpose: select records from u010daily_payroll_feed subfile which was created
;	  from u010daily.qts
;
; 2002/jan/14 M.C.	- original (clone from older version of u931.qts)
; 2003/sep/22 B.E.	- original version ran against claims history subfile
;			  and created the correct amounts. This version uses
;			  the u010 daily payroll fee subfile and was tested
;			  2003/sep/22 and failed to give the same results.
;			  It is likely that the u010daily program is at fault
;			  and needs adjustment - afterwhich this program
;			  would run fine.
;
;			  In the meantime, u931_temp.qts in 101 src is being
;			  used in production
; 2004/jan/08 M.C.	- alpha doc nbr

cancel clear
set lock file update
set process nolimit

run u931 

request explode_claims on calculation errors report on edit errors report  

access *u010daily_payroll_feed  


def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

def x-clmdtl-sv-nbr-1 zoned*2 unsigned =			&
			nconvert(clmdtl-consec-dates-r[1:1]) &
			if consec-flag = "Y"			&
			else 0

def x-clmdtl-sv-nbr-2 zoned*2 unsigned = 			&
			nconvert(clmdtl-consec-dates-r[4:1])  &
			if consec-flag = "Y"			&
			else 0

def x-clmdtl-sv-nbr-3 zoned*2 unsigned = 			&
			nconvert(clmdtl-consec-dates-r[7:1])  &
			if consec-flag = "Y"			&
			else 0

def x-tot-serv = clmdtl-nbr-serv + x-clmdtl-sv-nbr-1  +   &
		 x-clmdtl-sv-nbr-2 + x-clmdtl-sv-nbr-3

def x-serv-fee = clmdtl-fee-ohip / x-tot-serv

def x-fee-ohip   float = x-serv-fee * clmdtl-nbr-serv
def x-fee-ohip-1 float = x-serv-fee * x-clmdtl-sv-nbr-1
def x-fee-ohip-2 float = x-serv-fee * x-clmdtl-sv-nbr-2
def x-fee-ohip-3 float = x-serv-fee * x-clmdtl-sv-nbr-3

def x-clmdtl-sv-day-1 = nconvert(clmdtl-consec-dates-r[2:2]) &
			if consec-flag = "Y"			&
			else 0
def x-clmdtl-sv-day-2 = nconvert(clmdtl-consec-dates-r[5:2]) &
			if consec-flag = "Y"			&
			else 0
def x-clmdtl-sv-day-3 = nconvert(clmdtl-consec-dates-r[8:2]) &
			if consec-flag = "Y"			&
			else 0

def x-serv-date-1 char*8 = clmdtl-sv-date[1:6] +	&
			clmdtl-consec-dates-r[2:2]
def x-serv-date-2 char*8 = clmdtl-sv-date[1:6] +	&
			clmdtl-consec-dates-r[5:2]
def x-serv-date-3 char*8 = clmdtl-sv-date[1:6] +	&
			clmdtl-consec-dates-r[8:2]

;2004/01/08 - MC
;!def x-doc-nbr zoned*3 unsigned = nconvert(clmhdr-doc-nbr)
def x-doc-nbr char*3 = clmhdr-doc-nbr
; 2004/01/08 - end


sel if clmhdr-clinic-nbr = '22' and clmhdr-payroll = "B"   &
	and (   (batctrl-batch-type = 'P' and  batctrl-adj-cd = 'M') &
              or batctrl-batch-type = 'A'		&
	      or batctrl-batch-type = 'C' 		&
	    )

subfile icuapp keep include				&
	x-doc-nbr,					&
	clmhdr-doc-dept of u010daily_payroll_feed,	&
	clmdtl-oma-cd of u010daily_payroll_feed,	&
	clmdtl-sv-date of u010daily_payroll_feed,	&
	clmdtl-nbr-serv of u010daily_payroll_feed,	&
	x-fee-ohip,					&
	clmhdr-loc of u010daily_payroll_feed,		&
	batctrl-batch-type of u010daily_payroll_feed,	&
	clmhdr-pat-ohip-id-or-chart of u010daily_payroll_feed

subfile icuapp alias consec-1 keep          		&
	if x-clmdtl-sv-day-1  <> 0 include		&
	x-doc-nbr,					&
	clmhdr-doc-dept of u010daily_payroll_feed,	&
	clmdtl-oma-cd of u010daily_payroll_feed,	&
	x-serv-date-1,   				&
	x-clmdtl-sv-nbr-1,				&
	x-fee-ohip-1,					&
	clmhdr-loc of u010daily_payroll_feed,		&
	batctrl-batch-type of u010daily_payroll_feed,	&
	clmhdr-pat-ohip-id-or-chart of u010daily_payroll_feed

subfile icuapp alias consec-2 keep          		&
	if x-clmdtl-sv-day-2  <> 0 include		&
	x-doc-nbr,					&
	clmhdr-doc-dept of u010daily_payroll_feed,	&
	clmdtl-oma-cd of u010daily_payroll_feed,	&
	x-serv-date-2,   				&
	x-clmdtl-sv-nbr-2,				&
	x-fee-ohip-2,					&
	clmhdr-loc of u010daily_payroll_feed,		&
	batctrl-batch-type of u010daily_payroll_feed,	&
	clmhdr-pat-ohip-id-or-chart of u010daily_payroll_feed

subfile icuapp alias consec-3 keep          		&
	if x-clmdtl-sv-day-3  <> 0 include		&
	x-doc-nbr,					&
	clmhdr-doc-dept of u010daily_payroll_feed,	&
	clmdtl-oma-cd of u010daily_payroll_feed,	&
	x-serv-date-3,    				&
	x-clmdtl-sv-nbr-3,				&
	x-fee-ohip-3,					&
	clmhdr-loc of u010daily_payroll_feed,		&
	batctrl-batch-type of u010daily_payroll_feed,	&
	clmhdr-pat-ohip-id-or-chart of u010daily_payroll_feed

request nonexplode_consec_days

access *icuapp link clmdtl-oma-cd to fee-oma-cd of f040-oma-fee-mstr opt

output icu-app-file add					&
    if fee-special-m-suffix-ind = 'Y' or clmdtl-nbr-serv of icuapp  = 1 &
    on errors report

   item doc-nbr final x-doc-nbr
   item clmhdr-doc-dept final clmhdr-doc-dept of icuapp
   item fee-oma-cd final clmdtl-oma-cd of icuapp  
   item clmdtl-sv-date final clmdtl-sv-date of icuapp
   item clmdtl-nbr-serv final clmdtl-nbr-serv of icuapp
   item clmdtl-fee-ohip final x-fee-ohip
   item clmhdr-pat-ohip-id-or-chart final clmhdr-pat-ohip-id-or-chart of icuapp
   item clmhdr-loc final clmhdr-loc of icuapp
   item clmhdr-batch-type final batctrl-batch-type of icuapp

output icu-app-file-explode add				&
    if fee-special-m-suffix-ind <> 'Y' and clmdtl-nbr-serv of icuapp > 1  &
    on errors report

   item doc-nbr final x-doc-nbr
   item clmhdr-doc-dept final clmhdr-doc-dept of icuapp
   item fee-oma-cd final clmdtl-oma-cd of icuapp 
   item clmdtl-sv-date final clmdtl-sv-date of icuapp
   item clmdtl-nbr-serv final clmdtl-nbr-serv of icuapp
   item clmdtl-fee-ohip final x-fee-ohip
   item clmhdr-pat-ohip-id-or-chart final clmhdr-pat-ohip-id-or-chart of icuapp
   item clmhdr-loc final clmhdr-loc of icuapp
   item clmhdr-batch-type final batctrl-batch-type of icuapp


build $pb_obj/u931
