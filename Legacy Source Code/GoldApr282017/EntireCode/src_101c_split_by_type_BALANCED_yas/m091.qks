;#> PROGRAM-ID.     M091.QKS
;
;	((C)) Dyad Technologies
;
;    PROGRAM PURPOSE : MAINTENANCE OF F091-DIAG-CODES-MSTR
;
;    MODIFICATION HISTORY
;	  DATE		WHO	DESCRIPTION
;	91/05/12	D.B.	ORIGINAL
;	91/08/12	D.B.	CHANGE DESCRIPTION TO 120 CHAR
;

can clear

screen $pb_obj/m091

file f091-diag-codes-mstr occurs 9

temp w-desc-1 char*60 occurs with f091-diag-codes-mstr
temp w-desc-2 char*60 occurs with f091-diag-codes-mstr

use $pb_src/std_hilite nol

skip to 3
title "DIAGNOSTIC CODE MAINTENANCE" at 3,26
skip to 5
title "DIAG CODE   DESCRIPTION" at 5,3
skip to 6

cluster occurs with f091-diag-codes-mstr vertical

align (1,,6) (,,15)
field diag-cd of f091-diag-codes-mstr required nochange lookup noton &
      f091-diag-codes-mstr viaindex diag-cd
field w-desc-1
align (,,15)
field w-desc-2

procedure postfind
begin
  for f091-diag-codes-mstr
  begin
    let w-desc-1 = diag-cd-desc[1:60]
    let w-desc-2 = diag-cd-desc[61:120]
    end
  end

procedure preupdate
begin
  for f091-diag-codes-mstr
  begin
    let diag-cd-desc = w-desc-1 + w-desc-2
    end
  end

build
  
