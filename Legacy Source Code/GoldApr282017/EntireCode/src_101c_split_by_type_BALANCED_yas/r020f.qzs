;#> PROGRAM-ID.     R020F.QZS
;
;	((C)) Dyad Technologies
;
;    PROGRAM PURPOSE : SUMMARY OF REJECTED CLAIMS (IE NOT SUBMITTED
;			TO OHIP DUE TO MESS CODE IN PAT MSTR)
;
;    MODIFICATION HISTORY
;        DATE   WHO          DESCRIPTION
;     93/MAY/06 M.C.	     - SMS 141 (ORIGINAL)
;     95/SEP/21 Y.B.         - ADD CLINIC TOTALS
;     98/Aug/18 B.E.	     - changed page length from 63 to 66 line
;     98/sep/16 y.b.         - changed page width from 80 to 132
;     04/may/13 M.C.	     - alpha doc nbr

can clear
set default
set report nolim
set rep dev disc name ru020c
set page length 63 width 132
set page length 63 width 80
set formfeed

access *u020c1				&
	link iconst-clinic-nbr-1-2 to iconst-mstr-rec

sorted on iconst-clinic-nbr-1-2, batctrl-batch-nbr, clmhdr-claim-id of u020c1

;!def w-claim-nbr char*8 = clmhdr-claim-id[4:8]
def w-claim-nbr char*8 = clmhdr-claim-id[3:8]

page heading	&
  tab   1 "RU020C/"		&
  tab   8 iconst-clinic-nbr-1-2	&
  tab  44 "REJECTED CLAIMS"    	&
  tab  99 "RUN DATE"		&
;y2k
  tab 108 sysdate		&
  tab 120 "PAGE"		&
  tab 126 syspage		&
skip 1	&
  tab  42 iconst-clinic-name	&
skip 1	&
  tab  44 "CLINIC #"		&
  tab  56 iconst-clinic-nbr-1-2	&
  tab  70 "CYCLE ="		&
  tab  79 iconst-clinic-cycle-nbr	&
  tab  97 "FOR THE PERIOD ENDING:"	&
;y2k
  tab 120 iconst-date-period-end pic "^^^^/^^/^^"	&
skip 2	&
  tab  10 "SEQUENCE"			&
  tab  25 "DEPT NBR"			&
  tab  40 "CLAIM NBR"			&
  tab  60 "AMOUNT REQUESTED"		&
skip 2

report                        		&
  tab  10 count noreset	            	&
  tab  27 clmhdr-doc-dept		&
  tab  40 w-claim-nbr pic "^^^ ^^^ ^^"	&
  tab  62 clmhdr-tot-claim-ar-ohip of u020c1	&
	pic "^,^^^,^^^.^^ " trailing sign "-"

footing at iconst-clinic-nbr-1-2	&
skip 2	&
  tab  10 "CLINIC TOTAL:"		&
  tab  46 count                   	&
  tab  62 clmhdr-tot-claim-ar-ohip of u020c1		&
	subtotal  pic "^,^^^,^^^.^^ "	&
	trailing sign "-"				&
skip page


final footing                    	&
skip 2	&
  tab  10 "GRAND TOTAL:"		&
  tab  46 count                   	&
  tab  62 clmhdr-tot-claim-ar-ohip of u020c1		&
	subtotal  pic "^,^^^,^^^.^^ "	&
	trailing sign "-"				&
skip page

build $pb_obj/r020f

  
