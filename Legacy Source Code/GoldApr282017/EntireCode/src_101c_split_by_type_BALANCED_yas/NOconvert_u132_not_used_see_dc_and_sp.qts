;#> PROGRAM-ID.     u132.qts
;
;       ((C)) Dyad Technologies
;
;    PURPOSE: Uploads transactions from a 'text' file into the default
;	      compensation file (f113) so that transactions will be triggered
;	      during the current EP payroll run 
;
;    MODIFICATION HISTORY
;        DATE   WHO     DESCRIPTION
; 2004/sep/20	b.e.	- original
; 2005/jul/31   b.e.	- changed amt-net in u132 to signed numeric and
;			  changed variable .. not tested???
; 2006/may/18   b.e.    - added link to doctor to make sure it exists (leena
;			  keeps putting doctor '000' into WB
; 2006/nov/20   b.e.	- corrected above change .. if doctor not found
;			  it goes into error file for reporting but
;			  also it is NOT processed into f113
;

cancel clear
run u132

set default
set process nolimit

global temp w-current-ep-nbr        numeric
global temp w-current-ep-nbr-minus1 numeric

;-------------------------------------------------------------------------------
; OBTAIN THE CURRENT EP NUMBER AND PASS TO SUBSEQUENT REQUESTS
;
request u132_const_mstr_get_ep_nbr              &
                on edit        errors report    &
                on calculation errors report
access constants-mstr-rec-6
choose const-rec-nbr 6

item w-current-ep-nbr        final  current-ep-nbr
item w-current-ep-nbr-minus1 final  current-ep-nbr - 1
;


;-------------------------------------------------------------------------------
;
; verify transaction contain valid data
;
request u132_process_verify_data                &
                on edit        errors report    &
                on calculation errors report
set lock record update

access u132dat							&
	link comp-code 						&
	  to comp-code of f190-comp-codes     opt		&
	link doc-nbr,						&
	     w-current-ep-nbr,					&
	     comp-code						&
	  to doc-nbr,						&
	     ep-nbr-from,					&
	     comp-code	 of f113-default-comp opt		&
	link doc-nbr						&
	to   doc-nbr     of f020-doctor-mstr  opt

use $use/def_compensation_status.def

; (error if invalid doctor or invalid comp code)
select if  not record f190-comp-codes  exists		&
	or not record f020-doctor-mstr exists

subfile u132_errors keep include 			&
  w-current-ep-nbr,                                     &
  doc-nbr               of u132dat,                     &
  doc-name              of u132dat,                     &
  doc-inits             of u132dat,                     &
  doc-given-names       of u132dat,                     &
  comp-code             of u132dat,                     &
  signed-amt-net        of u132dat


request u132_process                           &
                on edit        errors report    &
                on calculation errors report
set lock record update

access u132dat							&
	link comp-code 						&
	  to comp-code of f190-comp-codes     opt		&
	link doc-nbr,						&
	     w-current-ep-nbr,					&
	     comp-code						&
	  to doc-nbr,						&
	     ep-nbr-from,					&
	     comp-code	 of f113-default-comp opt		&
;	(don't process record unless valid doctor)
	link doc-nbr						&
	to   doc-nbr     of f020-doctor-mstr  

use $use/def_compensation_status.def

subfile u132_audit keep include 			&
  w-current-ep-nbr,					&
  doc-nbr of u132dat,					&
  comp-code of u132dat,					&
  signed-amt-net of u132dat
  

output f113-default-comp add alias f113-add                   &
    if     not record f113-default-comp  exists               &
	and 1=1 
        item doc-nbr		final  doc-nbr 		of u132dat
        item ep-nbr-from	final  w-current-ep-nbr
        item ep-nbr-to		final  w-current-ep-nbr
        item ep-nbr-entry	final  w-current-ep-nbr
        item comp-code		final  comp-code 	of u132dat
        item factor		final  10000
        item factor-override	final  " "
        item comp-units		final  0
        item amt-gross		final  signed-amt-net		of u132dat
        item amt-net		final  signed-amt-net		of u132dat
        item last-mod-date    final  sysdate
        item last-mod-time    final  systime / 10000
        item last-mod-user-id final  "u132    Gen'd"

;output f113-default-comp	update alias f113-update       &
;    if     record f113-default-comp exists			&
;	and 1=1
;        item last-mod-user-id final  "u132    Gen'd"

build $pb_obj/u132
