; Program: audit_f050hist
; Purpose: print an audit of f050 history rev values for each clinic
cancel clear
set rep nolimit
set rep dev disc name audit_f050hist
set page length 62 width 132

access f050-doc-revenue-mstr-history

sort	on iconst-date-period-end	&
	on ep-yr			&
        on docrev-clinic-1-2	

def x-svc-mtd-in  zoned*4 signed = docrev-mtd-in-svc    &
                                   if docrev-location ne "MISC"

def x-svc-mtd-out zoned*4 signed = docrev-mtd-out-svc   &
                                   if docrev-location ne "MISC"

def x-amt-mtd-in  zoned*8 signed = docrev-mtd-in-rec    &
                                   if docrev-location ne "MISC"

def x-amt-mtd-out zoned*8 signed = docrev-mtd-out-rec   &
                                   if docrev-location ne "MISC"

def x-misc-svc-mtd zoned*4 signed = docrev-mtd-out-svc  &
                              if docrev-location = "MISC"

def x-misc-amt-mtd zoned*8 signed = docrev-mtd-out-rec  &
                              if docrev-location = "MISC"

def x-svc-ytd-in  zoned*5 signed = docrev-ytd-in-svc    &
                                   if docrev-location ne "MISC"

def x-svc-ytd-out zoned*5 signed = docrev-ytd-out-svc   &
                                   if docrev-location ne "MISC"

def x-amt-ytd-in  zoned*8 signed = docrev-ytd-in-rec    &
                                   if docrev-location ne "MISC"

def x-amt-ytd-out zoned*8 signed = docrev-ytd-out-rec   &
                                   if docrev-location ne "MISC"

def x-misc-svc-ytd zoned*5 signed =  &
                         docrev-ytd-out-svc  if docrev-location = "MISC"

def x-misc-amt-ytd zoned*8 signed =   &
                         docrev-ytd-out-rec  if docrev-location = "MISC"

def x-svc-total-mtd zoned*4 signed=   &
                         (x-svc-mtd-in + x-svc-mtd-out + x-misc-svc-mtd)

def x-svc-total-ytd zoned*5 signed=   &
                         (x-svc-ytd-in + x-svc-ytd-out + x-misc-svc-ytd)

def x-amt-total-mtd zoned*8 signed=   &
                         (x-amt-mtd-in + x-amt-mtd-out + x-misc-amt-mtd)

def x-amt-total-ytd zoned*8 signed=   &
                         (x-amt-ytd-in + x-amt-ytd-out + x-misc-amt-ytd)

page heading                                                    &
        tab 01  "audit_f050hist"                                &
        tab 35  "Audit of f050-revenue-mstr-HISTORY - as of :"  &
        tab 78  sysdate                                         &
        tab 110 syspage                                         &
skip 2  tab 01  "Clinic / EP-YR / PED"	                	&
        tab 33  "# SVC-----IN-PATIENT"                          &
        tab 60  "# SVC----OUT-PATIENT"                          &
        tab 84  "MISCELLANEOUS"                                 &
        tab 104 "# SVC---TOTAL-AMOUNT"                          &
skip 2

footing at docrev-clinic-1-2		 		                  &
        tab 1   docrev-clinic-1-2 ep-yr iconst-date-period-end            &
        tab 24  "MONTH"                                                   &
        tab 32  x-svc-mtd-in    subtotal pic "^^,^^^"                      &
        tab 41  x-amt-mtd-in    subtotal pic "^^^^^,^^^.^^ " trail "-"     &
        tab 59  x-svc-mtd-out   subtotal pic "^^,^^^"                      &
        tab 68  x-amt-mtd-out   subtotal pic "^^^^^,^^^.^^ " trail "-"     &
        tab 85  x-misc-amt-mtd  subtotal pic "^^^^^,^^^.^^ " trail "-"     &
        tab 103 x-svc-total-mtd subtotal pic "^^^,^^^"                      &
        tab 112 x-amt-total-mtd subtotal pic "^^^^^,^^^.^^ " trail "-"     &
skip 1  tab 24  "YEAR"                                                    &
        tab 31  x-svc-ytd-in    subtotal pic "^^^,^^^"                     &
        tab 41  x-amt-ytd-in    subtotal pic "^^^^^,^^^.^^ " trail "-"     &
        tab 58  x-svc-ytd-out   subtotal pic "^^^,^^^"                     &
        tab 68  x-amt-ytd-out   subtotal pic "^^^^^,^^^.^^ " trail "-"     &
        tab 85  x-misc-amt-ytd  subtotal pic "^^^^^,^^^.^^ " trail "-"     &
        tab 102 x-svc-total-ytd subtotal pic "^^^,^^^"                     &
        tab 112 x-amt-total-ytd subtotal pic "^^^^^,^^^.^^ " trail "-"     &
skip 2

footing at iconst-date-period-end skip page

build $obj/audit_f050hist
