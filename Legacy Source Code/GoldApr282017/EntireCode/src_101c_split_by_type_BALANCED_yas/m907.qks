; Program: m907.qks
; Purpose: Show next available doctor numbers. The sequence in which
;	   they appear is the sequence in which the numbers should be assigned
;	     so that the 'oldest' number is used first.
;	    By entering a status of "A"ssigned the number is taken off this
;	    this list.
;
; 1999/June/02 S.B.    - Added the use file
;                        def_doc_avail_status.def to 
;                        prevent hard coding of doc-status.
; 1999/dec/14 B.E.	- status now 'assigned', not taken
;			- when assigned, set date-assigned automatically
; 2000/jan/18 B.E.	- don't show assigned doctors - use m908 to show
; 2003/dec/10 A.A.	- alpha doctor nbr

cancel clear

screen $pb_obj/m907 activities find, change
use $use/def_doc_status.def

file f021-avail-doctor-mstr occurs 17
;S.B.
;   select if doc-status <> 'T'
; select if doc-status <> doc-status-terminated
select if doc-status <> doc-status-assigned

title "Available Doctor Numbers"       at 1,45
title "(in 'to be assigned' sequence)" at 2,45
skip 1
title 'DATE NBR   DOC         DEPT                                  DATE  ' at ,5
skip
title 'AVAILABLE  NBR  STATUS  NBR  DOCTOR FULL NAME              ASSIGNED' at ,5
skip 1
align (1,,4)   (,,16) (,,23) (,,29) (,,34)               (,,62) 
cluster occurs with f021-avail-doctor-mstr at ,1
;field date-added display
field date-available 	display
field doc-no         	display
field doc-status
field doc-dept-no bwz
field doc-full-name
;field date-taken 
field date-assigned 	display

procedure edit doc-status
begin
 if    doc-status <> doc-status-available	&
   and doc-status <> doc-status-assigned   
 then
    error "Availability status must be ' '(available) or 'A'ssigned"
end

Procedure process doc-status
begin
    if doc-status = doc-status-assigned
    then begin
       let     date-assigned = sysdate
       display date-assigned
    end
    if doc-status = doc-status-available
    then begin
;      (undoing assignment - undo old values)
       let     date-assigned = 0
       display date-assigned
       let     doc-dept-no = 0
       display doc-dept-no 
       let     doc-full-name = " "
       display doc-full-name 
       let     date-assigned = 0
       display date-assigned
    end
end

build
  
