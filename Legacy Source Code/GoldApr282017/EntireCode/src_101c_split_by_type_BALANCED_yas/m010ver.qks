;#> PROGRAM-ID.     M010VER.QKS
;
;
;   PROGRAM PURPOSE : TO CHANGE VERSION CODE
;
;   MODIFICATION HISTORY
;        DATE       WHO        DESCRIPTION
;	93/05/28   M.C.		-ORIGINAL

cancel clear
set default


screen $pb_obj/m010ver activities find, change


file f010-pat-mstr
    access viaindex pat-health-nbr using pat-health-nbr	&
	request pat-health-nbr

file f086-pat-id designer

temp w-old-version-cd char*2


use $pb_src/std_hilite.qks nol

title 'PATIENT MASTER MAINTENANCE' at 1,54

skip to 4
align (1,4,20)
field pat-health-nbr of f010-pat-mstr label 'HEALTH NBR:' nochange
field pat-version-cd of f010-pat-mstr label 'VERSION CD:'
field pat-last-version-cd of f010-pat-mstr display label 'LAST VER CD:'
field pat-surname of f010-pat-mstr         display label 'SURNAME:'
field pat-given-name of f010-pat-mstr      display label 'FIRST NAME:'

procedure postfind
begin
    let w-old-version-cd = pat-version-cd
end

procedure preupdate
begin
    if pat-version-cd <> w-old-version-cd
    then let pat-last-version-cd = w-old-version-cd
    let pat-date-last-maint = sysdate
    let pat-date-last-elig-maint = sysdate
    let pat-mess-code = ' '
    let pat-no-of-letter-sent = 0
    let clmhdr-pat-ohip-id-or-chart of f086-pat-id = &
		key-pat-mstr of f010-pat-mstr
    let pat-last-birth-date of f086-pat-id =         &
		pat-last-birth-date of f010-pat-mstr
    let pat-last-version-cd of f086-pat-id =         &
		pat-last-version-cd of f010-pat-mstr
    let pat-old-surname of f086-pat-id =		    &
		pat-surname
    let pat-old-given-name of f086-pat-id = 	    &
		pat-given-name
    let pat-old-health-nbr of f086-pat-id  =         &
		pat-health-nbr
    let pat-old-chart-nbr of f086-pat-id   =          &
		pat-chart-nbr
    let pat-old-addr1 of f086-pat-id       =         &
		subscr-addr1
    let pat-old-addr2 of f086-pat-id       =         &
		subscr-addr2
    let pat-old-addr3 of f086-pat-id       =         &
		subscr-addr3
    put f086-pat-id
end

build




  
