;PURPOSE: REPORT DETAIL OF DISKETTE IF NO HARD COPY SEND

;DATE:		WHO:		MODIFICATION
;97/03/21	YASEMIN		ORIGINAL
;
;

can clear
set rep nolimit
set rep page length 62
set rep page width 132
set rep dev disc name suspdtl
set formfee


access f002-suspend-dtl                            &
  link (nconvert(ascii(clmdtl-batch-nbr,9)[4:3]))  &
    to doc-nbr of f020-doctor-mstr opt             &
;  LINK CLMDTL-ACCOUNTING-NBR                       &
;    TO CLMHDR-ACCOUNTING-NBR OF F002-SUSPEND-HDR OPT
  link clmdtl-doc-ohip-nbr, clmdtl-accounting-nbr  &
    to clmhdr-doc-ohip-nbr, clmhdr-accounting-nbr  &
                             of f002-suspend-hdr opt

;SORT ON CLMDTL-DOC-OHIP-NBR ON CLMDTL-ACCOUNTING-NBR
sort on doc-nbr on clmdtl-accounting-nbr

def x-name cha*25 = pack(doc-name+" "+doc-inits)

def x-prov cha*2 =    clmhdr-health-care-prov         &
                   if clmhdr-health-care-prov ne "ON" &
                 else " "

page heading                   &
skip 1  tab 1 	sysdate        &
        tab 20  "VIEW SUSPEND DETAIL" &
skip 2  tab 1	"ACCT NBR"     &
        tab 11  "PATIENT"      &
	tab 23  "OMA CD"       &
        tab 30  "SVCS"         &
        tab 36  "SV DATE"      &
        tab 46  "DIAG"         &
        tab 55  "AMOUNT"       &
        tab 64  "REFER#"       &
        tab 73  "LOC "         &
        tab 80  "I/O"          &
        tab 86  "ADMIT "       &
        tab 96  "PROV"         &
skip 1  tab 1	"========"     &
        tab 11  "======="      &
	tab 23  "======"       &
        tab 30  "===="         &
        tab 36  "========="    &
        tab 46  "===="         &
        tab 55  "======"       &
        tab 64  "======"       &
        tab 73  "===="         &
        tab 80  "==="          &
        tab 86  "========"     &
        tab 96  "===="         &
skip 2

;HEADING AT CLMDTL-DOC-OHIP-NBR  SKIP 1  &
heading at doc-nbr              skip 1  &
tab 1 clmdtl-doc-ohip-nbr pic "^^^^^^"  &
tab 10 doc-dept fill "0"                &
tab 13 "/"                              &
tab 15 doc-nbr pic "^^^" fill "0"       &
tab 22 x-name                           &
tab 50 "SPECIALY: "                     &
tab 60 doc-spec-cd                      &
tab 64 doc-spec-cd-2                    &
tab 68 doc-spec-cd-3                    &
tab 72 "CLINICS: "                      &
tab 81 doc-clinic-nbr                   &
tab 85 doc-clinic-nbr-2                 &
tab 89 doc-clinic-nbr-3                 &
tab 93 doc-clinic-nbr-4                 &
tab 97 doc-clinic-nbr-5                 &
tab 101 doc-clinic-nbr-6                &
skip 2


report                           skip 2   &
tab 1	clmdtl-accounting-nbr print at clmdtl-accounting-nbr &
tab 11 	clmhdr-pat-acronym                  &
tab 23 	clmdtl-oma-cd                       &
tab 27  clmdtl-oma-suff                     &
tab 31	clmdtl-nbr-serv  pic "^^" fill "0"  &
tab 36	clmdtl-sv-date   pic "^^/^^/^^"     &
tab 46	clmdtl-diag-cd-alpha pic "^^^"      &
tab 52	clmdtl-fee-ohip pic "^^,^^^.^^"     &
tab 64	clmhdr-refer-doc-nbr pic "^^^^^^" fill "0" &
tab 73	clmhdr-loc                          &
tab 81	clmhdr-i-o-pat-ind                  &
tab 86	clmhdr-date-admit pic "^^/^^/^^"    &
tab 97	x-prov                              &
skip 1

;FOOTING AT CLMDTL-DOC-OHIP-NBR                      &
footing at doc-nbr                                  &
skip 1 tab 52  "---------"                          &
skip 1 tab 1   "NO. SVCS:"                          &
       tab 12  clmdtl-nbr-serv sub pic "^^^^"       &
       tab 19  "NO. CLMS:"                          &
       tab 27  count at clmdtl-accounting-nbr       &
       tab 51  clmdtl-fee-ohip sub pic "^^^,^^^.^^" &
skip 1 tab 52  "========="                          &
skip page

final footing skip page                                  &
        tab 1  "TOTAL NBR OF SERVICES:"                  &
        tab 25  clmdtl-nbr-serv sub pic "^^^^^^"         &
skip 2  tab 1  "TOTAL NUMBR OF CLAIMS:"                  &
        tab 25  count at clmdtl-accounting-nbr           &
skip 2  tab 1  "TOTAL DOLLAR AMOUNT  :"                  &
        tab 25  clmdtl-fee-ohip sub pic "^^,^^^,^^^.^^"  &
skip 2

build $pb_obj/suspend_dtl
  
