; DOC: AUDIT_DOC.QZS
; DOC: ACTIVE DOCTORS FROM JUL95 OT JUN 96 BY DEPT/BY CLAS
; DOC:
; DOC: RUN FOR: GORDON / DR. UPTON
;
;PROGRAM PURPOSE :


;
;DATE       BY WHOM      DESCRIPTION
;97/01/20   YASEMIN      ORIGINAL
;
;
can clear
set rep nolimit
set page length 62 width 132
set rep device disc name audit_doc
set formfee


access f020-doctor-mstr

def x-ind cha*18 = "FULL TIME"         if doc-full-part-ind = "F"  &
              else "PART TIME"         if doc-full-part-ind = "P"  &
              else "CLINICAL SCHOLARS" if doc-full-part-ind = "C"

select if           doc-clinic-nbr = 22                 &
           and (    doc-full-part-ind = "F"             &
                 or doc-full-part-ind = "P"             &
                 or doc-full-part-ind = "C")            &
           and (    doc-dept = 1 or doc-dept = 2 or doc-dept = 3   &
                 or doc-dept = 4 or doc-dept = 5 or doc-dept = 6   &
                 or doc-dept = 7 or doc-dept = 8 or doc-dept = 9)  &
           and (       (     doc-date-fac-term = 0          &
                         and doc-date-fac-start lt 960701)  &
                    or (     doc-date-fac-term-mm ge 07     &
                         and doc-date-fac-term-yy eq 95)    &
                     or (    doc-date-fac-term-mm le 06     &
                         and doc-date-fac-term-yy eq 96))

def x-name cha*30 = pack (doc-name +" "+doc-inits)

sort on doc-dept on doc-full-part-ind on x-name on doc-nbr

page heading                                              	   &
	tab 1   "ACTIVE PHYSCIAN'S JULY 1, 1995 TO JUNE 30, 1996"  &
        tab 60  sysdate                                            &
skip 1  tab 1   "==============================================="  &
skip 3                                                             &
        tab 1   "DEPT"                                             &
        tab 6   "NBR"                                              &
        tab 11  "PHYSICIAN NAME"                                   &
        tab 35  "CLASS"                                            &
        tab 45  "START"                                            &
        tab 55  "TERM"                                             &
skip 1                                                             &
        tab 1   "===="                                             &
        tab 6   "==="                                              &
        tab 11  "=============="                                   &
        tab 35  "====="                                            &
        tab 45  "====="                                            &
        tab 55  "===="                                             &
skip 2

footin at doc-dept skip page

footing at doc-full-part-ind          &
skip 2  tab 1 "TOTAL "                &
        tab 7  x-ind                  &
        tab 25 count at doc-nbr       &
skip 2

report                                                 &
skip 1 tab   2 doc-dept  fill "0"                      &
       tab   6 doc-nbr   fill "0"                      &
       tab  11 x-name                                  &
       tab  37 doc-full-part-ind                       &
       tab  45 doc-date-fac-start pic "^^/^^/^^" bwz   &
       tab  55 doc-date-fac-term  pic "^^/^^/^^" bwz   &
skip 1

final footing                                      &
skip 4  tab 1  "TOTAL NO. OF PHYSICIAN'S:  "       &
        tab 29 count at doc-nbr                    &
skip 2

build $pb_obj/audit_doc
