; Name   : RTIMESHT2.QZS
; Purpose: PRINT SELECTED TIMESHEET REPORT
;
; MODIFICATION HISTORY
; YY/MMM/DD  	By whom        	Why
; ??/???/??  	?.?           	-
; 92/02/28	M. CHAN		- SAF 1005
;				- ADD CAN CLEAR
;				- SET REPORT NOLIMIT
;				- USE CHOOSE ON DATE
;				- REPORT PRINT ON LETTER SIZE
;				- ELIMINATE SOME CONTENTS

can clear
set default
set report device printer
set page length 63 width 80
set report nolimit

access ftimesht &
	link tsht-emp to emp-num of femployees

choose tsht-key parm &
	prompt "Select Key: period-end + employee # (YYMMDDnnnn): "

sort on tsht-emp on tsht-end on pwo-num on activity on function

page heading tab 1 sysdate format yymmmdd tab 25 sysname tab  70 "PAGE: " &
	tab  75 syspage skip 2 tab 30 "EMPLOYEE TIMESHEET REPORT" skip 3

heading at tsht-end tab 1 "EMPLOYEE: " tab 12 emp-num &
        tab 21 name tab 50  "FOR WEEK ENDING: " tab  71 tsht-end &
	skip 3 tab 1 "N/C PWO    FUNC  ACTV  CLIENT"     &
	tab 34 "SAT   SUN   MON   TUE" &
        tab 58 "WED   THU   FRI   TOT"  &
	skip 2

report tab   2 billable &
       tab   4 pwo-num  bwz &
       tab  12 function &
       tab  18 activity &
       tab  25 client  &
       tab  32 sat bwz &
       tab  38 sun bwz &
       tab  44 mon bwz &
       tab  50 tue bwz &
       tab  56 wed bwz &
       tab  62 thu bwz &
       tab  68 fri bwz &
       tab  74 tot bwz &
       skip 1

footing at tsht-end " " skip 1 &
       tab  10 'TOTAL HOURS ------->'        &
       tab  31 sat subtotal pic '^^^.^^' bwz &
       tab  37 sun subtotal pic '^^^.^^' bwz &
       tab  43 mon subtotal pic '^^^.^^' bwz &
       tab  49 tue subtotal pic '^^^.^^' bwz &
       tab  55 wed subtotal pic '^^^.^^' bwz &
       tab  61 thu subtotal pic '^^^.^^' bwz &
       tab  67 fri subtotal pic '^^^.^^' bwz &
       tab  73 tot subtotal pic '^^^.^^' bwz &
       skip 4

footing at tsht-emp " " skip page

build $pb_obj/rtimesht2
  
