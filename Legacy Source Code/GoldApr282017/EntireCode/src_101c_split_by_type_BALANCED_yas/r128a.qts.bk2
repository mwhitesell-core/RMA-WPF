;#> PROGRAM-ID.     r128a.qts 
;
;       ((C)) Dyad Technologies
;
;    PURPOSE: first pass for inactive doctors that do not have earnings for 3 most recent months
;
;             STAGE 1 - create subfile of values from f119-doctor-ytd-history
;
;    MODIFICATION HISTORY
;        DATE    SAF #  WHO      DESCRIPTION
;   2014/Jul/16  ____   M.C.     - original

cancel clear

run u128_inactive_doctors

set default
set process nolimit
set lock record update

global temp w-current-ep-nbr            numeric
global temp w-last-3-ep-nbr	 	numeric

;-------------------------------------------------------------------------------
request get_constants_values_ep_nbr          		&
                on edit        errors report 		&
                on calculation errors report

access constants-mstr-rec-6

choose const-rec-nbr 6

def ep-mm =   mod(current-ep-nbr,100)

item w-current-ep-nbr  = current-ep-nbr
item w-last-3-ep-nbr   = current-ep-nbr -  3    if ep-mm >= 4	&
		else     current-ep-nbr - 90    if ep-mm <= 3   


;-------------------------------------------------------------------------------
request access_f119_hist                     		&
                on edit        errors report 		&
                on calculation errors report

access f119-doctor-ytd-history				&
	link doc-nbr					&
	 to  doc-nbr of f020-doctor-mstr

choose doc-nbr, ep-nbr (w-last-3-ep-nbr to w-current-ep-nbr)

sorted on doc-nbr on ep-nbr

sel f119-doctor-ytd-history if                    	&                   
	(comp-code = 'TOTINC'  and amt-mtd <= 0)	&
     or (comp-code = 'INCEXP'  and amt-mtd <= 0)	&
     or (comp-code = 'PAYEFT'  and amt-mtd <= 0)	&
     or (comp-code = 'ADVOUT'  and amt-mtd >  0)

sel f020-doctor-mstr if					&
	(doc-dept = 14 or doc-dept =15)  		&
     and doc-ep-pay-code = '2'

subfile r128a       keep include			&
     doc-nbr of f119-doctor-ytd-history, 		&
     doc-dept, doc-name,                 		&
     doc-date-fac-start, doc-date-fac-term,		&
     ep-nbr of f119-doctor-ytd-history, 		&
     comp-code, amt-mtd,  amt-ytd

;-------------------------------------------------------------------------------
request determine_inactive                   		&
                on edit        errors report 		&
                on calculation errors report

access *r128a         

sorted on doc-nbr on ep-nbr

temp inactive-count
item inactive-count = inactive-count + 1 at ep-nbr reset at doc-nbr

subfile r128a_inactive   keep at doc-nbr include	&
	r128a,  inactive-count

build $obj/r128a 
