; Program: u140_e.qts
; Purpose:	
cancel clear
set process nolimit
run u140_e

set default
set process nolimit

global temp w-current-ep-nbr        zoned unsigned
global temp w-current-ep-nbr-minus1 zoned unsigned
global temp w-current-ep-nbr-plus1  zoned unsigned
global temp w-ep-fiscal-nbr         zoned unsigned
global temp afpcon-seq    numeric
global temp afpcon-type   char*1

;-------------------------------------------------------------------------------
; OBTAIN THE CURRENT EP NUMBER AND PASS TO SUBSEQUENT REQUESTS
;
request u114a_const_values_get_ep_nbr            &
                on edit        errors report    &
                on calculation errors report
access constants-mstr-rec-6             &
        link current-ep-nbr             &
        to   ep-nbr of f191-earnings-period
choose const-rec-nbr 6
item w-current-ep-nbr        = current-ep-nbr   of constants-mstr-rec-6
item w-current-ep-nbr-minus1 = current-ep-nbr   of constants-mstr-rec-6&
                               - 1
item w-current-ep-nbr-plus1  = current-ep-nbr   of constants-mstr-rec-6&
                               + 1

item w-ep-fiscal-nbr = ep-fiscal-nbr            of f191-earnings-period

; DETERMINE THE 'PROCESS-SEQ' AND 'TRANSACTION TYPE'
; FOR THE TRANSACTIONS BEING CREATED IN THIS RUN

request u140_get_afpcon                        	&
                on edit        errors report    &
                on calculation errors report
access f190-comp-codes
choose comp-code "AFPCON"
item afpcon-seq  = process-seq
item afpcon-type = comp-type

;------------------------------------------------
request u140_update_f114                        &
                on edit        errors report    &
                on calculation errors report

;access afp-a2s-file					&
;	link (nconvert(doc-afp-paym-solo))		&
;	to   doc-ohip-nbr	of f020-doctor-mstr
;select if  doc-afp-paym-group of afp-a2s-file		&
;	 = doc-afp-paym-group of f020-doctor-mstr

access f075-afp-doc-mstr				&
    link doc-afp-paym-group   				&
    to   doc-afp-paym-group	of f074-afp-group-mstr  

def x-selected-payroll char*1  				&
	= parm prompt "Enter PAYROLL ID:"  upshift

select if    afp-payment-amt <> 0					&
	and  x-selected-payroll = batctrl-payroll of f074-afp-group-mstr

output f114-special-payments add
  item doc-nbr			final doc-nbr  		of f075-afp-doc-mstr
  item comp-code		final "AFPCON"
  item ep-nbr-from 		final w-current-ep-nbr   
  item ep-nbr-to	 	final w-current-ep-nbr   
  item comp-units		final afp-payment-percentage 	&
							of f075-afp-doc-mstr  
  item amt-gross		final afp-payment-amt-total	&
						 	of f075-afp-doc-mstr  
  item amt-net  		final afp-payment-amt 	of f075-afp-doc-mstr  
  item last-mod-date    	final sysdate
  item last-mod-time		final systime / 10000
  item last-mod-user-id		final "U140_b  gen'd"
  
build $obj/u140_e
