;PROGRAM-ID.	U708.QTS
;
;PROGRAM PURPOSE: TO SET THE STATUS TO "C"OMPLETED IF DETAIL AND
;                 HEADER RECORDS HAVE VALID VALUES.
;
;	MODIFICATION HISTORY
;	  DATE	   SMS #	WHO	DESCRIPTION
;       90.10.18   000          D.B.    ORIGINAL
;	93.07.21   142		M.C.	CHECK ON DIAG-CD-ALPHA,
;					NBR-SERV-ALPHA,
;					FEE-OMA-ALPHA, FEE-OHIP-ALPHA
;	93.09.20   142          M.C.    ADD THE SELECTION CRITERIA THE
;					STATUS NOT EQUAL TO "R" IN THE
;					SECOND REQUEST "U708_UPDATE_HEADER"
;   1999/jan/28          	B.E.    - y2k 
;   1999/May/27 		S.B.    - Added the use file
;                        		  def_clmhdr_status.def to 
;                        		  prevent hardcoding of clmhdr-status.

can clear

run u708

set process nolimit

request u708_check_detail

access f002-suspend-dtl

select  if clmdtl-oma-cd[1:1] = "?"			&
	or clmdtl-oma-suff[1:1] ="?"			&
	or clmdtl-agent-cd-alpha[1:1] = "?"		&
 	or clmdtl-nbr-serv-alpha[1:1] = "?"		&
	or clmdtl-sv-yy-alpha[1:1] = "?"		&
	or clmdtl-sv-mm-alpha[1:1] = "?"		&
	or clmdtl-sv-dd-alpha[1:1] = "?"		&
	or clmdtl-consecutive-sv-date-alpha(1) = "???"	&
	or clmdtl-consecutive-sv-date-alpha(2) = "???"	&
	or clmdtl-consecutive-sv-date-alpha(3) = "???"	&
;	OR CLMDTL-AMT-TECH-BILLED-ALPHA[1:1] = "?"	&
 	or clmdtl-fee-oma-alpha[1:1] = "?"		&
 	or clmdtl-fee-ohip-alpha[1:1] = "?"		&
   	or clmdtl-diag-cd-alpha[1:1] = "?"

subfile u708 keep include clmdtl-doc-ohip-nbr, clmdtl-accounting-nbr

request u708_update_header

access *u708	&
	link clmdtl-doc-ohip-nbr, clmdtl-accounting-nbr	&
	to   clmhdr-doc-ohip-nbr, clmhdr-accounting-nbr	&
	of f002-suspend-hdr

;S.B.
use $use/def_clmhdr_status.def

;select if     clmhdr-status <> "X" 	&
;	  and clmhdr-status <> "R"
select if     clmhdr-status <> clmhdr-status-error 	&
	  and clmhdr-status <> clmhdr-status-resubmit

output f002-suspend-hdr update
;S.B.
; item clmhdr-status final "N"
 item clmhdr-status final clmhdr-status-nocancel



request f708_check_header

access f002-suspend-hdr

;S.B.
use $use/def_clmhdr_status.def

;select  if  clmhdr-status                   eq " "	&
select  if   (     clmhdr-status               = clmhdr-status-default	&
	        or clmhdr-status	       = updated  		&
	     ) 						&
  	  and clmhdr-refer-doc-nbr-alpha[1:1] <> "?"	&
	  and clmhdr-diag-cd-alpha[1:1]       <> "?"	&
	  and clmhdr-loc[1:1]                 <> "?"	&
	  and clmhdr-hosp[1:1]                <> "?"	&
	  and clmhdr-agent-cd-alpha[1:1]      <> "?"	&
	  and clmhdr-i-o-pat-ind[1:1]         <> "?"	&
	  and clmhdr-pat-key-type[1:1]        <> "?"	&
	  and clmhdr-pat-key-type[1:1]        <> "O"	&
;	  AND CLMHDR-PAT-KEY-DATA[1:1]        <> "?"	&
	  and clmhdr-doc-spec-cd-alpha[1:1]   <> "?"	&
	  and clmhdr-msg-nbr		    <> "?"	&
	  and clmhdr-reprint-flag 	    <> "?"	&
	  and clmhdr-sub-nbr[1:1]             <> "?"	&
	  and clmhdr-auto-logout		    <> "?"	&
	  and clmhdr-fee-complex		    <> "?"	&
	  and clmhdr-date-admit[1:1]          <> "?"

output f002-suspend-hdr update
;S.B.
; item clmhdr-status final "C"
 item clmhdr-status final clmhdr-status-complete

build $pb_obj/u708
