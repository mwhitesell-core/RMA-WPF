;#> PROGRAM-ID.     u131b.qts
;
;       ((C)) Dyad Technologies
;
;    PURPOSE: SUB-PROCESS WITHIN "EARNINGS GENERATION" PROCESS.
;             Creates the 'adjustment for conversion' of AFP inscope
;	      billings: Regular inscope billings are paid under comp code
;	      "AFPIN". This program creates an automatic 10% charge against
;	      that 'AFPIN' amount in a 'AFPADJ' transaction.
;
;	This module takes subfile create in u131a and adds/updates record
;		in f110
;
;    MODIFICATION HISTORY
;        DATE    WHO      DESCRIPTION
; 2004/02/20     b.e.    - original
; 2004/mar/25    b.e.	 - add access to f110-afpadj alias to see if doctor
;			   has overrride to adfadj factor
; 2004/nov/23    M.C.    - change the item doc-nbr defintion when creating f110-add

cancel clear
set stacksize 500
run u131b

set default
set process nolimit

;-------------------------------------------------------------------------------
;
request u131b_process                           &
                on edit        errors report    &
                on calculation errors report

access *u131a							&	
  link debug-link-key 						&
         to   compensation-key                          	&
               of f110-compensation opt    

use $use/def_compensation_status.def


;----------- APFADJ doesn't exist ------------
output f110-compensation add alias f110-add             &
   if not record f110-compensation exists					
; 2004/11/22 - MC
;        item doc-nbr	     final  doc-nbr of f110-compensation
        item doc-nbr	     final  doc-nbr of u131a   
; 2004/11/22 - end
        item ep-nbr          final  w-current-ep-nbr
        item ep-nbr-entry    final  w-current-ep-nbr
        item comp-code       final  "AFPADJ"
        item comp-type       final  afpadj-type
        item process-seq     final  afpadj-seq
        item factor          final  afpadj-factor
        item factor-override final  " "
        item comp-units      final  0    ; BUT COULD STORE NBR SVCS ?????!!!!
        item amt-gross       final  x-amt-gross
        item amt-net         final  x-amt-net
        item compensation-status final  compensation-status-accepted
        item last-mod-date    final  sysdate
        item last-mod-time    final  systime / 10000
        item last-mod-user-id final  "u131a    Gen'd"

;----------- APFADJ  exist ------------
output f110-compensation update alias f110-update	&
    if record f110-compensation exists					
        item factor-override final  "*"
        item comp-units      final  0    ; BUT COULD STORE NBR SVCS ?????!!!!
        item amt-gross       final  x-amt-gross
        item amt-net         final  x-amt-net
        item compensation-status final  compensation-status-accepted
        item last-mod-date    final  sysdate
        item last-mod-time    final  systime / 10000
        item last-mod-user-id final  "u131a  Updated"

build $pb_obj/u131b
