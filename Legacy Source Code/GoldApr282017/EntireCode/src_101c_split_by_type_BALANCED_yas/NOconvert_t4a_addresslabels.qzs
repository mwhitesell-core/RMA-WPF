; DOC: ADDRLABELS.QZS
; DOC: CREATE MAILING DOCTOR ADDRESS LABELS
; DOC: RUN FOR: MANAGEMENT/STAFF
;
;PROGRAM PURPOSE : MAILING DOCTOR ADDRESS LABELS
;
;DATE        WHO       DESCRIPTION
;92/mar/18   YASEMIN   ORIGINAL
;05/feb/07             - print for t4a's mail out print one set company = 1 and
;                        set company = 2

can clear
set default
set rep nolimit
set rep device disc name t4a_addrlabels
set page length 6 width 40
set nohead
set noformfeed

access f020-doctor-mstr             &
  link doc-dept to dept-nbr         &
    of f070-dept-mstr   opt

define x-addr zoned*1 unsigned = parm prompt            &
   	'Select office addrs "1" or home addrs "2": '

define x-clinic zoned*2 unsigned = parm prompt          &
 'Select clinic "22" or "60" or OR "80" OR "81" OR "82" OR "0" For all: '

define x-dept zoned*2 unsigned = parm prompt            &
       'Select dept from 1 to 14 or "0" for all: '

define x-class zoned*1 unsigned  = parm prompt          &
'Select "1" FULL or "2" PART or "3" CS or "4" PS or "0" For all: '

define x-company  zoned*1 unsigned  = parm prompt          &
'Select "1" RMA or "2" for RMAINC or "0" For all: '

define x-name cha*30 = pack("DR. " + doc-inits + " " + doc-name)

define x-docdept char*6 = pack(doc-nbr + "/" + ascii(doc-dept,2))

define x-addr-1 char*30 = doc-addr-office-1 if x-addr= 1	 &
	else doc-addr-home-1 if x-addr = 2

define x-addr-2 char*30 = doc-addr-office-2 if x-addr= 1	 &
	else doc-addr-home-2 if x-addr = 2

define x-addr-3 char*30 = doc-addr-office-3 if x-addr= 1	 &
	else doc-addr-home-3 if x-addr = 2

define x-addr-pc-1 char*6  = doc-addr-office-pc-123 if x-addr= 1 &
	else doc-addr-home-pc-123 if x-addr = 2

define x-addr-pc-2 char*6  = doc-addr-office-pc-456 if x-addr= 1 &
	else doc-addr-home-pc-456 if x-addr = 2

define x-clinic-22 num*2 = doc-clinic-nbr if doc-clinic-nbr = 22

define x-clinic-60 num*2 = doc-clinic-nbr   if (     doc-clinic-nbr   >= 60    &
                                                 and doc-clinic-nbr   <= 65)   &
                      else doc-clinic-nbr-2 if (     doc-clinic-nbr-2 >=60     &
                                                 and doc-clinic-nbr-2 <= 65)   &
                      else doc-clinic-nbr-3 if (     doc-clinic-nbr-3 >=60     &
                                                 and doc-clinic-nbr-3 <= 65)   &
                      else doc-clinic-nbr-4 if (     doc-clinic-nbr-4 >=60     &
                                                 and doc-clinic-nbr-4 <= 65)   &
                      else doc-clinic-nbr-5 if (     doc-clinic-nbr-5 >=60     &
                                                 and doc-clinic-nbr-5 <= 65)   &
                      else doc-clinic-nbr-6 if (     doc-clinic-nbr-6 >=60     &
                                                 and doc-clinic-nbr-6 <= 65)   
           
define x-clinic-80 num*2 = doc-clinic-nbr   if doc-clinic-nbr   = 80 &
                      else doc-clinic-nbr-2 if doc-clinic-nbr-2 = 80 &
                      else doc-clinic-nbr-3 if doc-clinic-nbr-3 = 80 &
                      else doc-clinic-nbr-4 if doc-clinic-nbr-4 = 80 &
                      else doc-clinic-nbr-5 if doc-clinic-nbr-5 = 80 &
                      else doc-clinic-nbr-6 if doc-clinic-nbr-6 = 80 

define x-clinic-81 num*2 = doc-clinic-nbr   if doc-clinic-nbr   = 81 &
                      else doc-clinic-nbr-2 if doc-clinic-nbr-2 = 81 &
                      else doc-clinic-nbr-3 if doc-clinic-nbr-3 = 81 &
                      else doc-clinic-nbr-4 if doc-clinic-nbr-4 = 81 &
                      else doc-clinic-nbr-5 if doc-clinic-nbr-5 = 81 &
                      else doc-clinic-nbr-6 if doc-clinic-nbr-6 = 81 

define x-clinic-82 num*2 = doc-clinic-nbr   if doc-clinic-nbr   = 82 &
                      else doc-clinic-nbr-2 if doc-clinic-nbr-2 = 82 &
                      else doc-clinic-nbr-3 if doc-clinic-nbr-3 = 82 &
                      else doc-clinic-nbr-4 if doc-clinic-nbr-4 = 82 &
                      else doc-clinic-nbr-5 if doc-clinic-nbr-5 = 82 &
                      else doc-clinic-nbr-6 if doc-clinic-nbr-6 = 82 

select if (     (    x-clinic = x-clinic-22                     & 
                 or  x-clinic = x-clinic-60                     &
                 or  x-clinic = x-clinic-80                     &
                 or  x-clinic = x-clinic-81                     &
                 or  x-clinic = x-clinic-82                     &
                 or  x-clinic = 0                               &
                 or (x-clinic = 60 and x-clinic-60 >= 60)       &  
                )                                               & 
           and  (    (   x-dept = doc-dept 			&
		      or x-dept = 0				&
		     )                         		    	& 
                 and (doc-dept ne 31 				&
		     )						&
           	)                                               &
           and  (   x-company = dept-company 			&
		 or x-company = 0)                       	&
           and  (   doc-date-fac-term  = 00000000 		&	
	         or doc-date-fac-term >= 20031201		&
		)   						&
           and  (   (x-class = 1 and doc-full-part-ind = "F")	&
                 or (x-class = 2 and doc-full-part-ind = "P")	&
                 or (x-class = 3 and doc-full-part-ind = "C")	&
                 or (x-class = 4 and doc-full-part-ind = "S")	&
                 or (x-class = 0)				&
                )						&
          )


sort on doc-ohip-nbr    

footing at doc-ohip-nbr   &
  tab 4   x-docdept                 skip                          &
  tab 4   x-name                    skip                          &
  tab 4   x-addr-1       	    skip                          &
  tab 4   x-addr-2                  skip                          &
  tab 4   x-addr-3                  skip                          &
  tab 4   x-addr-pc-1                                             &
  tab 8   x-addr-pc-2               skip page


build $pb_obj/t4a_addrlabels

