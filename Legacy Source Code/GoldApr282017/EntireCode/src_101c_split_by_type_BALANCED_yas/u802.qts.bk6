;#> PROGRAM-ID.     U802.QTS
;
;	((C)) Dyad Technologies
;
;    PROGRAM PURPOSE : CREATE AUTOMATIC ADJUSTMENT FROM ADJ-CLAIM-FILE
;
;    MODIFICATION HISTORY
;        DATE   WHO          DESCRIPTION
;     93/AUG/25 M.C.         - ORIGINAL (SMS 143)
;     94/JAN/14 M.C.	     - SMS 144
;			     - ADD THE ROUND OPTION TO CALCULATE AMOUNT
;     94/FEB/04 M.C.	     - ADD LOGIC FOR MULTIPLE CLINIC ADJUSTMENT
;			     - RESET THE BATCH NBR FOR EACH CLINIC
;     99/mar/08 C.M.	     - Y2K CONVERSION AND I-CONST-CLINIC-NBR
;		             - HAS BEEN CONVERTED FROM 9(4) TO X(4) 
;   1999/May/19 S.B.	     - Re-checked the Y2K. 
;    1999/May/21 S.B.        - Added the use file
;                              def_batctrl_batch_status.def to 
;                              prevent hardcoding of batctrl-batch-status.

can clear
set default
set process nolimit


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request calc_batch_nbr on calculation errors report on edit errors report

access adj-claim-file

def clm-id char*11 = ascii(adj-batch-nbr,9) + ascii(adj-claim-nbr,2)
def x-clinic-nbr char*2 = ascii(adj-batch-nbr,9)[1:2]

;SORT ON CLM-ID
sort on x-clinic-nbr on clm-id

temp x-count
  item x-count count at clm-id reset at x-clinic-nbr

temp x-batch-count
  item x-batch-count = ceiling(x-count / 99) at clm-id

subfile u802_srt_adj keep at clm-id include               &
        adj-claim-file,  x-count, x-batch-count, x-clinic-nbr
	

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request create_b_adjustment on calculation errors report on edit errors report
                            ; Create a batch control record for every
			    ; 99 claim. Create a clmhdr and a clmdtl
			    ; record, and inverted clmdtl key to the
			    ; each adjusting clmdtl record in the
			    ; subfile. At the end of the request,
			    ; update the batch nbr in Constant Mstr


access *u802_srt_adj					&
   link  'B', adj-batch-nbr, adj-claim-nbr, '00000', '0' &
    to   key-clm-type, key-clm-batch-nbr, 		&
	 key-clm-claim-nbr, key-clm-serv-code, key-clm-adj-nbr &
         of f002-claims-mstr alias f002-clmhdr		&
  link   nconvert(ascii(key-clm-batch-nbr,9)[4:3])	&
   to    doc-nbr of f020-doctor-mstr			&
   and   floor(adj-batch-nbr / 10000000)  to iconst-mstr-rec

;SORTED ON X-BATCH-COUNT
sorted on x-clinic-nbr on x-batch-count

temp x-batch-nbr int*6
  item x-batch-nbr = iconst-clinic-batch-nbr + x-batch-count   &
	       at x-batch-count				 &
;              RESET AT INITIAL TO  ICONST-CLINIC-BATCH-NBR
               reset at x-clinic-nbr  to  iconst-clinic-batch-nbr

define x-clinic-batch-nbr int*9 =			&
	nconvert(ascii(adj-batch-nbr,9)[1:2] +		&
	         ascii(x-batch-nbr,7))

define x-mod       = mod(x-count,99)

define x-claim-nbr = x-mod if x-mod <> 0 else 99

define x-tot-claim-ar-oma int*7 signed =		&
  round(clmhdr-tot-claim-ar-oma of f002-clmhdr * adj-amt-bal /  &
	 clmhdr-tot-claim-ar-ohip of f002-clmhdr)

define x-amt-tech-billed  int*7 signed =		&
  round(clmhdr-amt-tech-billed  of f002-clmhdr * adj-amt-bal /  &
	 clmhdr-tot-claim-ar-ohip of f002-clmhdr)

;S.B.
use $use/def_batctrl_batch_status.def

output f001-batch-control-file add at x-batch-count on errors report
   item batctrl-batch-nbr 	   initial x-clinic-batch-nbr
   item batctrl-batch-type 	   initial 'A'
;  item batctrl-clinic-nbr         initial iconst-clinic-nbr
   item batctrl-clinic-nbr         initial nconvert(iconst-clinic-nbr)
   item batctrl-adj-cd 		   initial 'B'
   item batctrl-agent-cd           initial adj-agent-cd
;  item batctrl-date-batch-entered initial ascii(sysdate,6)
   item batctrl-date-batch-entered initial ascii(sysdate,8)
;  item batctrl-date-period-end    initial ascii(iconst-date-period-end,6)
   item batctrl-date-period-end    initial ascii(iconst-date-period-end,8)
   item batctrl-cycle-nbr 	   initial iconst-clinic-cycle-nbr
;  item batctrl-ar-yy-mm 	   initial '0000'
   item batctrl-ar-yy-mm           initial '000000'
   item batctrl-adj-cd-sub-type    initial 'A'
   item batctrl-nbr-claims-in-batch final x-claim-nbr
   item batctrl-last-claim-nbr      final x-claim-nbr
;S.B.
;   item batctrl-batch-status	    final "1"
   item batctrl-batch-status	    final batctrl-batch-status-balanced
   item batctrl-amt-act             subtotal adj-amt-bal
   item batctrl-amt-est             subtotal adj-amt-bal
   item batctrl-calc-ar-due         subtotal adj-amt-bal
   item batctrl-calc-tot-rev        subtotal adj-amt-bal



output f002-claims-mstr alias f002-adj-hdr add noitems on errors report
;Preset claim header data from batctrl record
   item key-clm-type	           initial 'B'
   item key-clm-batch-nbr 	   initial x-clinic-batch-nbr
   item key-clm-claim-nbr	   initial x-claim-nbr
   item key-clm-serv-code	   initial '00000'
   item key-clm-adj-nbr		   initial '0'
   item clmhdr-orig-batch-nbr	   initial batctrl-batch-nbr
   item clmhdr-orig-claim-nbr	   initial x-claim-nbr
   item clmhdr-batch-type          initial batctrl-batch-type
   item clmhdr-adj-cd-sub-type     initial batctrl-adj-cd-sub-type
   item clmhdr-agent-cd            initial batctrl-agent-cd
   item clmhdr-adj-cd              initial batctrl-adj-cd
   item clmhdr-date-period-end	   initial nconvert(batctrl-date-period-end)
   item clmhdr-cycle-nbr	   initial batctrl-cycle-nbr
; Preset claim header data with values from claim being adjusted
   item clmhdr-claim-id  	   initial clmhdr-claim-id of f002-clmhdr
   item clmhdr-diag-cd  	   initial clmhdr-diag-cd of f002-clmhdr
   item clmhdr-hosp         	   initial clmhdr-hosp of f002-clmhdr
   item clmhdr-i-o-pat-ind         initial clmhdr-i-o-pat-ind of f002-clmhdr
   item clmhdr-loc                 initial clmhdr-loc of f002-clmhdr
   item clmhdr-pat-ohip-id-or-chart initial clmhdr-pat-ohip-id-or-chart of f002-clmhdr
   item clmhdr-pat-acronym         initial adj-pat-acronym
   item clmhdr-doc-dept            initial doc-dept of f020-doctor-mstr
; Assign values to the remaining fields
   item clmhdr-reference	   initial ' '
;  item clmhdr-date-admit          initial '000000'
;  item clmhdr-date-cash-tape-payment initial ascii(sysdate,6)
;  item clmhdr-date-sys            initial ascii(sysdate,6)
   item clmhdr-date-admit          initial '00000000'
   item clmhdr-date-cash-tape-payment initial ascii(sysdate,8)
   item clmhdr-date-sys            initial ascii(sysdate,8)
   item clmhdr-status-ohip         initial '00'
   item clmhdr-tape-submit-ind     initial 'N'
   item clmhdr-doc-nbr-ohip        initial 0
   item clmhdr-doc-spec-cd         initial 0
   item clmhdr-refer-doc-nbr       initial 0
   item clmhdr-curr-payment        initial 0
   item clmhdr-amt-tech-paid       initial 0
   item clmhdr-manual-and-tape-payments initial 0
;
   item clmhdr-amt-tech-billed 	   initial x-amt-tech-billed
   item clmhdr-tot-claim-ar-oma    initial x-tot-claim-ar-oma
   item clmhdr-tot-claim-ar-ohip   initial adj-amt-bal


output f002-claims-mstr alias f002-adj-dtl add noitems on errors report
   item key-clm-type               initial 'B'
   item key-clm-batch-nbr          initial x-clinic-batch-nbr
   item key-clm-claim-nbr          initial x-claim-nbr
   item key-clm-serv-code          initial adj-oma-cd-suff of u802_srt_adj
   item key-clm-adj-nbr            initial '0'
   item clmdtl-batch-nbr           initial clmhdr-batch-nbr of f002-adj-hdr
   item clmdtl-claim-nbr           initial clmhdr-claim-nbr of f002-adj-hdr
   item clmdtl-oma-cd              initial adj-oma-cd-suff of u802_srt_adj[1:4]
   item clmdtl-oma-suff            initial adj-oma-cd-suff of u802_srt_adj[5:1]
   item clmdtl-adj-nbr             initial 1
   item clmdtl-agent-cd            initial clmhdr-agent-cd of f002-adj-hdr
   item clmdtl-adj-cd              initial clmhdr-adj-cd of f002-adj-hdr
;  item clmdtl-sv-date             initial ascii(adj-serv-date,6)
   item clmdtl-sv-date             initial ascii(adj-serv-date,8)
   item clmdtl-nbr-serv            initial 0
   item clmdtl-consec-dates        initial 0
;   item clmdtl-date-period-end     initial ascii(clmhdr-date-period-end of f002-adj-hdr,6)
   item clmdtl-date-period-end     initial ascii(clmhdr-date-period-end of f002-adj-hdr,8)
   item clmdtl-cycle-nbr           initial clmhdr-cycle-nbr of f002-adj-hdr
   item clmdtl-orig-batch-id       initial clmhdr-orig-batch-id of f002-adj-hdr
   item clmdtl-fee-oma             initial clmhdr-tot-claim-ar-oma of f002-adj-hdr
   item clmdtl-fee-ohip            initial clmhdr-tot-claim-ar-ohip of f002-adj-hdr
   item clmdtl-amt-tech-billed     initial clmhdr-amt-tech-billed of f002-adj-hdr
   item clmdtl-diag-cd 	   	   initial adj-diag-cd
   item clmdtl-rev-group-cd        initial ' '
   item clmdtl-line-no		   initial adj-line-no


output f002-clmhdr update on errors report
   item clmhdr-tot-claim-ar-oma    subtotal x-tot-claim-ar-oma
   item clmhdr-tot-claim-ar-ohip   subtotal adj-amt-bal
   item clmhdr-amt-tech-billed     subtotal x-amt-tech-billed



;OUTPUT ICONST-MSTR-REC UPDATE AT FINAL ON ERRORS REPORT
output iconst-mstr-rec update at x-clinic-nbr on errors report
   item iconst-clinic-batch-nbr     final x-batch-nbr


;Create Clmdtl Key in Subfile, so Cobol pgm can write inverted to
;the adjusting Clmdtl
subfile u030_dtl_key keep include 			&
   key-clm-type      of f002-adj-dtl,			&
   key-clm-batch-nbr of f002-adj-dtl,			&
   key-clm-claim-nbr of f002-adj-dtl,			&
   key-clm-serv-code of f002-adj-dtl,			&
   key-clm-adj-nbr   of f002-adj-dtl



build $pb_obj/u802
