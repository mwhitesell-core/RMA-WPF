;#> PROGRAM-ID.     R124A.QZS
;
;       ((C)) Dyad Technologies
;
;    PURPOSE: Print the STATEMENT OF EARNINGS for the physician
;             - PHASE I: create subfile of MTD and YTD values plus
;                        messages from f119-YTD file.

;    MODIFICATION HISTORY
;        DATE    SAF #  WHO      DESCRIPTION
;     93/MAR/17  ____   B.E.     - original
;     93/APR/14  ____   B.E.     - added hard code CR/LF to end of each line
;     93/APR/15  ____   B.E.     - added underlining
;     93/JUN/09  ____   B.E.     - added ytd values
;     93/JUN/09  ____   B.E.     - GTYPEA is a YTD calculation so
;                                  so set print MTD value in YTD column
;                                  and blank MTD column
;     93/NOV/01  ____   B.E.     - reverse sign of GTYPE values for presenation
;                                  on the Stmnt
;     93/NOV/26  ____   B.E.     - correct printing of negative sign
;     93/DEC/16  ____   B.E.     - print blank instead of MTD = 0.00 if
;				   value is not applicable
;     93/DEC/24  ____   B.E.     - DON'T select for printing any COMP-CODE
;				   with a GROUP-CODE of "Z".
;     94/JAN/11  ____   B.E.     - print STATUS comp-code for PAY CODE 4
;				 - changed descriptions of STATUS code
;
;     94/JAN/18  ----   M.C.     - add 'AND SEL' statement
;     94/FEB/08  ____   B.E.     - eliminate reverseral of sign for GTYPE. U116
;				   changed to update F119-DOCTOR-YTD with
;				   reversed sign.
;                                - "STATUS" now printed for all Pay Codes if non-zero amt
;                                - "STATUS" now printed as positive value only
;
;     94/MAR/15  ----   M.C.     - DO NOT SUPPRESS PRINTING ON YTD TOTADV
;
;     95/AUG/11  ----   YAS.     - ADD 18 DEFINE AND ADD TO SUBFILE
;
;     95/NOV/20  ----   M.C.	 - ADD F020-DOCTOR-EXTRA TO ACCESS, AND
;				   ADD 8 DEFINE ITEMS AND TO SUBFILE,
;				   INCLUDE DOC-DEPT TO SUBFILE
;     95/NOV/21  ----   B.E.	 - FIX 'AND' LOGIC FOR SELECT TO SUPPRESS
;				   PRINTING ALL ZERO AMOUNT TRANSACTIONS
;     95/JAN/16 ----    M.C.	 - DO NOT INCLUDE 'GTYPEA' RECORDS
;     98/jul/13 ----    yas	 - mod x-date-title for 1998-1999
;     99/jul/19 ----    yas	 - mod x-date-title for 1999-2000 
;     99/nov/22 ----    M.C.     - mod x-date-title to be dynamic to pick up 
;				   calendar year form constants-mstr-rec-6
;     00/feb/04 ----    yas	 - mod x-full-part-ind
;                                  change full time to GFT and 
;                                  Part time to Non-GFT
;     00/Mar/30 ----    yas	 - mod x-doc-id-info
;                                  comment out x-full-part-ind
; 00/nov/15 B.E.	- moved x-date-title to r124b_xxx program and 
;			  placed 'first/last-ep-nbr-of-cal-yr' in subfile since
;			  the fiscal period can change based upon clinic 
;			  and the r124b_xxx pgms are already customized by
;			  clinic. This saves having unique r124a pgms for
;			  each clinic
; 03/jan/24 B.E.	- PAYPOT/YTDEAR descriptions changed depending 
;			  upon 'company' of doctor
; 03/nov/10 b.e.	- alpha doctor nbr conversion
; 07/jan/118 b.e.	- replace leading part of bank account with "**"
;			  to make the full account number not visible
; 08/may/27 M.C. 	- only select rec-type = 'A' as per Brad'request
; 11/Jul/26 MC1		- Mary wants a comma when doc ytd amounts goes up to a million
; 2014/apr/14	be1	- exclude paycode 7 doctors from this run - they have their own version of r124a.qzs
; 2014/apr/15 	be2	- correct link to f112 to improve speed of access
; 2015/Apr/29	MC2	- hide DEFIC if amt-mtd = 0 and amt-ytd <> 0

cancel clear
set default
set report nolimit

access f119-doctor-ytd                                          &
        link comp-code of f119-doctor-ytd                       &
        to   comp-code of f190-comp-codes             opt       &
        link doc-nbr to doc-nbr of f020-doctor-mstr   opt       &
        link doc-nbr to doc-nbr of f020-doctor-extra  opt       &
        link doc-dept of f020-doctor-mstr                       &
        to   dept-nbr of f070-dept-mstr opt                     &
        link (  ascii(doc-bank-nbr    of f020-doctor-mstr,4)	&
              + ascii(doc-bank-branch of f020-doctor-mstr,5) )  &
        to   bank-cd of f080-bank-mstr opt                      &
        link (6) to const-rec-nbr of constants-mstr-rec-6       &
;be2
        link   current-ep-nbr,                          	&
               doc-nbr of f119-doctor-ytd               	&
        to   ep-nbr ,                                   	&
             doc-nbr of f112-pycdceilings               

use $pb_src/f119_doctor_ytd.def nol

; (select all comp-codes for processing except STATUS which is wanted
; only amt is non-zero. Also include messages)
;SELECT IF    (REC-TYPE = F119-REC-MSG)                    	     &
;          OR (    (COMP-CODE-GROUP OF F190-COMP-CODES <> "Z")  	     &
;              AND (    COMP-CODE OF F119-DOCTOR-YTD <> 'STATUS'	     &
;	           OR (    COMP-CODE OF F119-DOCTOR-YTD = 'STATUS'   &
;	               AND (   AMT-MTD OF F119-DOCTOR-YTD <> 0 	     &
;	                    OR AMT-YTD OF F119-DOCTOR-YTD <> 0       &
;		           )					     &
;		      )					     &
;		   )						     &
;	      )
;DEFINE W-TEST-COMP-CODE CHAR*2 = COMP-CODE OF F119-DOCTOR-YTD[1:2]
select if    (    (rec-type = f119-rec-msg)                    	     	&
              or (    (comp-code-group of f190-comp-codes <> "Z")  	&
	          and (comp-code of f119-doctor-ytd <> "GTYPEA")	&
	          and (    amt-mtd of f119-doctor-ytd <> 0      	&
; MC2
;	               or  amt-ytd of f119-doctor-ytd <> 0              &
	               or (    amt-ytd of f119-doctor-ytd <> 0          &
			   and comp-code of f119-doctor-ytd <> 'DEFIC'  &
			  )						&
; MC2 - end
		      )        						&
; 2008/05/27 - MC - include rec-type = 'A'
		  and (rec-type of f119-doctor-ytd = 'A'		&
		      )							&
; 2008/05/27 - end
	          )							&
             ) 								&
;            be1
        AND  (doc-pay-code of f112-pycdceilings <> "7"			&
             )


use $pb_src/printer_codes.def nol

; **YAS DEFINES NEXT 18 DEFINE STATEMENTS AND ADD TO SUBFILE AUG/95

;def x-deposit int*8 signed size 4 = doc-payeft
def x-deposit int*10 signed        = doc-payeft

def x-deposit-date date = doc-ep-date-deposit
def x-ped int*8 signed size 4 = doc-ep-ped
def x-bank-account cha*12 = lj(doc-bank-acct)
def x-bank-account-privacy-masked cha*12 				      	&
       = "************"                         if x-bank-account[01:1] = " " 	&
  else   "************"                         if x-bank-account[02:1] = " "	& 
  else   "************"                         if x-bank-account[03:1] = " "	&
  else   "************"                         if x-bank-account[04:1] = " "	& 
  else   "************"                         if x-bank-account[05:1] = " "	& 
  else   "********"      +(x-bank-account[2:4]) if x-bank-account[06:1] = " "	&	
  else   "********"      +(x-bank-account[3:4]) if x-bank-account[07:1] = " "	&	
  else   "********"      +(x-bank-account[4:4]) if x-bank-account[08:1] = " "	&	
  else   "********"      +(x-bank-account[5:4]) if x-bank-account[09:1] = " "	&	
  else   "********"      +(x-bank-account[6:4]) if x-bank-account[10:1] = " "	&	
  else   "********"      +(x-bank-account[7:4]) if x-bank-account[11:1] = " "	&	
  else   "********"      +(x-bank-account[8:4]) if x-bank-account[12:1] = " "	&	
  else   "********"      +(x-bank-account[9:4]) 

def x-bank char*30 = bank-name		
def x-branch char*30 = bank-address1	 

def x-ytdcea-pos-prt cha*3  = ytdcea-prt-format[1:3]
def x-ytdcea-val-prt cha*10 = ytdcea-prt-format[4:10]

def x-ceicea-pos-prt cha*3  = ceicea-prt-format[1:3]
def x-ceicea-val-prt cha*10 = ceicea-prt-format[4:10]

def x-ytdcex-pos-prt cha*3  = ytdcex-prt-format[1:3]
def x-ytdcex-val-prt cha*10 = ytdcex-prt-format[4:10]

def x-ceicex-pos-prt cha*3  = ceicex-prt-format[1:3]
def x-ceicex-val-prt cha*10 = ceicex-prt-format[4:10]

;def x-date-title char*49 =  &
;             "For the Fiscal Year July 1, " +		&
;		ascii(first-ep-nbr-of-cal-yr,6)[1:4]  +		&
;             " to June 30, " + ascii(last-ep-nbr-of-cal-yr,6)[1:4]

def x-dept-name char*30 = dept-name if record f070-dept-mstr exists &
                     else "UNKNOWN DEPARTMENT"

define x-full-part-ind char*25				&
     = "GFT"        if doc-full-part-ind = "F" &
  else "CLINICAL SCHOLAR" if doc-full-part-ind = "C" &
  else "PLASTIC SURGERY"  if doc-full-part-ind = "S" &
  else "Non-GFT"        if doc-full-part-ind = "P" &
  else "CLASSIFICATION UNKNOWN"

define x-doc-id-info char*50 =  "Dr. "                              &
                              + truncate(doc-inits) + " "           &
                              + truncate(doc-name) + "    "         &
                              + ascii(doc-dept,2) + "-"             &
                              + doc-nbr + "  "            
;                              + ascii(doc-nbr ,3) + "  "            
;                              + x-full-part-ind

; MC ADDED 8 DEFINE ITEMS

def x-ytdreq-pos-prt cha*3  = ytdreq-prt-format[1:3]
def x-ytdreq-val-prt cha*10 = ytdreq-prt-format[4:10]

def x-ceireq-pos-prt cha*3  = ceireq-prt-format[1:3]
def x-ceireq-val-prt cha*10 = ceireq-prt-format[4:10]

def x-ytdtar-pos-prt cha*3  = ytdtar-prt-format[1:3]
def x-ytdtar-val-prt cha*10 = ytdtar-prt-format[4:10]

def x-ceitar-pos-prt cha*3  = ceitar-prt-format[1:3]
def x-ceitar-val-prt cha*10 = ceitar-prt-format[4:10]

def x-period char*1 = "."

def x-mtd-dollars                                               &
      = amt-mtd of f119-doctor-ytd / 100                        &
                        if    rec-type = "A"                    &
                          and comp-code <> "GTYPEA"             &
   else 0
def x-mtd-cents                                                 &
      = mod(abs(amt-mtd of f119-doctor-ytd),100)                &
                        if    rec-type = "A"                    &
                          and comp-code <> "GTYPEA"             &
   else 0

def x-ytd-dollars                                               &
      = amt-mtd of f119-doctor-ytd / 100                        &
                        if    rec-type  = "A"                   &
                          and comp-code = "GTYPEA"              &
   else abs(amt-ytd of f119-doctor-ytd) / 100                   &
                        if    rec-type  = "A"                   &
                          and comp-code = "STATUS"              &
   else amt-ytd of f119-doctor-ytd / 100                        &
                        if rec-type = "A"                       &
   else 0
def x-ytd-cents                                                 &
      = mod(abs(amt-mtd of f119-doctor-ytd),100)                &
                        if    rec-type  = "A"                   &
                        and comp-code = "GTYPEA"                &
   else mod(abs(amt-ytd of f119-doctor-ytd),100)                &
                        if rec-type = "A"                       &
   else 0

; (DETERMINE HOW MANY NUMBERS TO BE PRINTED, OFFSET BY WIDTH OF 1 NUMBER. IE 21)
;def x-mtd-dollars-abs int*8 = abs(x-mtd-dollars)
def x-mtd-dollars-abs int*10 = abs(x-mtd-dollars)
;def x-ytd-dollars-abs int*8 = abs(x-ytd-dollars)
def x-ytd-dollars-abs int*10 = abs(x-ytd-dollars)
;def x-mtd-ctr1        num*1 =  21 if x-mtd-dollars-abs < 10      &
def x-mtd-ctr1        num*2 =  21 if x-mtd-dollars-abs < 10      &
                        else   42 if x-mtd-dollars-abs < 100     &
                        else   63 if x-mtd-dollars-abs < 1000    &
                        else   84 if x-mtd-dollars-abs < 10000   &
                        else  105 if x-mtd-dollars-abs < 100000  &
                        else  126 if x-mtd-dollars-abs < 1000000 &
                        else  147 ;  X-MTD-DOLLARS-ABS < 10000000

;def x-ytd-ctr1        num*1 =  21 if x-ytd-dollars-abs < 10      &
def x-ytd-ctr1        num*2 =  21 if x-ytd-dollars-abs < 10      &
                        else   42 if x-ytd-dollars-abs < 100     &
                        else   63 if x-ytd-dollars-abs < 1000    &
                        else   84 if x-ytd-dollars-abs < 10000   &
                        else  105 if x-ytd-dollars-abs < 100000  &
                        else  126 if x-ytd-dollars-abs < 1000000 &
                        else  147 ;  X-YTD-DOLLARS-ABS < 10000000

; (CHECK IF NEGATIVE SIGN WILL BE PRINTED)
def x-mtd-ctr2 num*1 = x-mtd-ctr1 + 14 if x-mtd-dollars < 0      &
                  else x-mtd-ctr1
def x-mtd-sign char*1 = "-"            if x-mtd-dollars < 0      &
                   else " "
def x-ytd-ctr2 num*1 = x-ytd-ctr1 + 14 if x-ytd-dollars < 0      &
                  else x-ytd-ctr1
def x-ytd-sign char*1 = "-"            if x-ytd-dollars < 0      &
                   else " "

; (CHECK IF COMMA REQUIRED FOR VALUE)
def x-mtd-ctr3 num*1 = x-mtd-ctr2 if x-mtd-dollars-abs < 1000   &
                  else x-mtd-ctr2 + 14
def x-ytd-ctr3 num*1 = x-ytd-ctr2 if x-ytd-dollars-abs < 1000   &
                  else x-ytd-ctr2 + 14

; (If printing any "TOT"al line (COMP-CODE = "TOTxxx") except for
;  'total advances' (TOTADV), then UNDERLINE the previously printed line
;   then print total)

def x-mtd-lit1 char*88              &
     = hpiii-column-100             & ; position to column for decimal alignment
         if   comp-code[1:3] <> "TOT"    &
           or comp-code      =  "TOTADV" &
  else hpiii-column-100             & ; position to column
     + hpiii-up-a-row               & ; up a row
     + hpiii-backward-dot-135       & ; back up dots
     + hpiii-underline-on           & ; underline on
     + hpiii-forward-dot-195        & ; forward dots (underlining)
     + hpiii-underline-off          & ; underline off
     + hpiii-down-a-row             & ; down a row
     + hpiii-column-100               ; re-position to column for decimal alignment

def x-ytd-lit1 char*88              &
     = hpiii-column-153             & ; position to column for decimal alignment
         if   comp-code[1:3] <> "TOT"    &
           or comp-code      =  "TOTADV" &
  else hpiii-column-153             & ; position to column
     + hpiii-up-a-row               & ; up a row
     + hpiii-backward-dot-135       & ; back up dots
     + hpiii-underline-on           & ; underline on
     + hpiii-forward-dot-195        & ; forward dots (underlining)
     + hpiii-underline-off          & ; underline off
     + hpiii-down-a-row             & ; down a row
     + hpiii-column-153               ; re-position to column for decimal alignment


def x-mtd-lit2 char*10 = x-esc + '*p-' + truncate(ascii(x-mtd-ctr3,3)) + 'X'
def x-ytd-lit2 char*10 = x-esc + '*p-' + truncate(ascii(x-ytd-ctr3,3)) + 'X'

; (REMOVE LEADING ZEROS FROM DOLLAR VALUE AND REPLACE WITH SPACES)
;def x-mtd-dollars-abs-a char*6 = ascii(x-mtd-dollars-abs,6)
def x-mtd-dollars-abs-a char*7 = ascii(x-mtd-dollars-abs,7)
;def x-ytd-dollars-abs-a char*6 = ascii(x-ytd-dollars-abs,6)
def x-ytd-dollars-abs-a char*7 = ascii(x-ytd-dollars-abs,7)
;def x-mtd-dollars-abs-b char*7                                               &
def x-mtd-dollars-abs-b char*8                                               &
      =  x-mtd-sign + x-mtd-dollars-abs-a      if x-mtd-dollars-abs >999999  &
   else  x-mtd-sign + x-mtd-dollars-abs-a[2:6] if x-mtd-dollars-abs > 99999  &
   else  x-mtd-sign + x-mtd-dollars-abs-a[3:5] if x-mtd-dollars-abs >  9999  &
   else  x-mtd-sign + x-mtd-dollars-abs-a[4:4] if x-mtd-dollars-abs >   999  &
   else  x-mtd-sign + x-mtd-dollars-abs-a[5:3] if x-mtd-dollars-abs >    99  &
   else  x-mtd-sign + x-mtd-dollars-abs-a[6:2] if x-mtd-dollars-abs >     9  &
   else  x-mtd-sign + x-mtd-dollars-abs-a[7:1]
;def x-ytd-dollars-abs-b char*7                                               &
def x-ytd-dollars-abs-b char*8                                               &
      =  x-ytd-sign + x-ytd-dollars-abs-a      if x-ytd-dollars-abs >999999  &
   else  x-ytd-sign + x-ytd-dollars-abs-a[2:6] if x-ytd-dollars-abs > 99999  &
   else  x-ytd-sign + x-ytd-dollars-abs-a[3:5] if x-ytd-dollars-abs >  9999  &
   else  x-ytd-sign + x-ytd-dollars-abs-a[4:4] if x-ytd-dollars-abs >   999  &
   else  x-ytd-sign + x-ytd-dollars-abs-a[5:3] if x-ytd-dollars-abs >    99  &
   else  x-ytd-sign + x-ytd-dollars-abs-a[6:2] if x-ytd-dollars-abs >     9  &
   else  x-ytd-sign + x-ytd-dollars-abs-a[7:1]
;def x-mtd-dollars-abs-char char*7 = rj(x-mtd-dollars-abs-b)
def x-mtd-dollars-abs-char char*8 = rj(x-mtd-dollars-abs-b)
;def x-ytd-dollars-abs-char char*7 = rj(x-ytd-dollars-abs-b)
def x-ytd-dollars-abs-char char*8 = rj(x-ytd-dollars-abs-b)

; (build print format of amount with comma in number if number $1000 or more)
;define x-mtd-prt-dollars char*8                                               &
define x-mtd-prt-dollars char*9                                                &
     =" " + x-mtd-dollars-abs-char                 if x-mtd-dollars-abs < 1000 &
;  else      x-mtd-dollars-abs-char[1:4] + "," + x-mtd-dollars-abs-char[5:3]
  else      x-mtd-dollars-abs-char[1:5] + "," + x-mtd-dollars-abs-char[6:3]

;define x-ytd-prt-dollars char*8                                               &
; 2011/07/26 - MC1 
;define x-ytd-prt-dollars char*9                                                &
;     = " " + x-ytd-dollars-abs-char                if x-ytd-dollars-abs < 1000 &
;; else       x-ytd-dollars-abs-char[1:4] + "," + x-ytd-dollars-abs-char[5:3]
;  else       x-ytd-dollars-abs-char[1:5] + "," + x-ytd-dollars-abs-char[6:3]
define x-ytd-prt-dollars char*10                                               &
     = "  " + x-ytd-dollars-abs-char                if x-ytd-dollars-abs < 1000 &
  else       x-ytd-dollars-abs-char[1:2] + "," + x-ytd-dollars-abs-char[3:3]  + "," + x-ytd-dollars-abs-char[6:3] &
		if x-ytd-dollars-abs >= 1000000					   &
  else " " + x-ytd-dollars-abs-char[1:5] + "," + x-ytd-dollars-abs-char[6:3]
; 2011/07/26 - end

define x-mtd-prt-cents   char*2 = ascii(x-mtd-cents,2)
define x-ytd-prt-cents   char*2 = ascii(x-ytd-cents,2)



; (COUNT THE NUMBER OF COMP-CODE LINES PRINTED SO THAT SHADING
;  AREA CAN BE DETERMINED FOR LASER PRINTER.  THE TWO COUNTERS SHOW
;  THE NUMBER OF LINES PRINTED WHEN SHADING SHOULD START (INCREQ) AND
;  WHEN IS SHOULD END (STATUS)

def x-count-increq = 1 if comp-code-group <= "J"                &
                else 0
def x-count-status = 1 if comp-code-group <= "L"                &
                else 0
def x-count-increq-status = 1 if comp-code-group >= "J" 	&
			and      comp-code-group <= "L"
def x-increq = 1 if comp-code-group = 'J'
def x-inctar = 1 if comp-code-group = 'K'
def x-status = 1 if comp-code-group = 'L'


; override the default comp-code description 
@if dg and hsc

define x-msg-text char*50                                                  &
     = "Your YTD Ceiling Payments are underpaid by "                   &
              if comp-code of f119-doctor-ytd="STATUS" and amt-ytd > 0 &
  else "Your YTD Ceiling Payments are overpaid by "                    &
              if comp-code of f119-doctor-ytd="STATUS" and amt-ytd < 0 &
  else "Current Earnings Payable"                                      &
              if    comp-code of f119-doctor-ytd = "PAYPOT"            &
  else text of f119-doctor-ytd                                         &
;             IF COMP-CODE-GROUP OF F190-COMP-CODES = "K"              &
              if rec-type = f119-rec-msg                               &
  else desc-long of f190-comp-codes

@elseif dg and rma
define x-msg-text char*50                                              &
;    = "Your YTD Ceiling Payments are underpaid by "                   &
;             IF COMP-CODE OF F119-DOCTOR-YTD="STATUS" AND AMT-YTD > 0 &
    =  "Your YTD Ceiling Payments are overpaid by "                    &
              if comp-code of f119-doctor-ytd="STATUS" and amt-ytd < 0 &
  else "Current Ceiling Payable"                                       &
              if    comp-code of f119-doctor-ytd = "PAYPOT"            &
                and dept-company  = 1				       &
  else "Current Earnings Payable"                                      &
              if    comp-code of f119-doctor-ytd = "PAYPOT"            &
                and dept-company <> 1				       &
  else "YTD Ceiling Paid"                                      	       &
              if    comp-code of f119-doctor-ytd = "YTDEAR"            &
                and dept-company =  1				       &
  else "YTD Earnings Paid"                                     	       &
              if    comp-code of f119-doctor-ytd = "YTDEAR"            &
                and dept-company <> 1				       &
  else text of f119-doctor-ytd                                         &
;             IF COMP-CODE-GROUP OF F190-COMP-CODES = "K"              &
              if rec-type = f119-rec-msg                               &
  else desc-long of f190-comp-codes
@else
ERROR - REQUIRES CONDITIONAL COMPILE - use bqzs macro!!!
@endif

define x-line-text char*190                                     &
    =  x-msg-text                                               &
     + truncate(x-ytd-lit1)                                     &
     + truncate(x-ytd-lit2)                                     &
     + truncate(lj(x-ytd-prt-dollars))                          &
     + x-period                                                 &
     + x-ytd-prt-cents  if    rec-type of f119-doctor-ytd = "A" &
			  and (   comp-code = "YTDEAR"	        &
			       or comp-code = "INCREQ"          &
			       or comp-code = "INCTAR"          &
			       or comp-code = "STATUS"          &
			       or comp-code = "GTYPEA")         &
 else  x-msg-text                                               &
     + truncate(x-mtd-lit1)                                     &
     + truncate(x-mtd-lit2)                                     &
     + truncate(lj(x-mtd-prt-dollars))                 		&
     + x-period                                                 &
     + x-mtd-prt-cents  if    rec-type of f119-doctor-ytd = "A" &
			  and (   comp-code = "ADVOUT"          &
			       or comp-code = "ADVANC")         &
 else  x-msg-text                                               &
     + truncate(x-mtd-lit1)                                     &
     + truncate(x-mtd-lit2)                                     &
     + truncate(lj(x-mtd-prt-dollars))                     	&
     + x-period                                                 &
     + x-mtd-prt-cents                                          &
     + truncate(x-ytd-lit1)                                     &
     + truncate(x-ytd-lit2)                                     &
     + truncate(lj(x-ytd-prt-dollars))                          &
     + x-period                                                 &
     + x-ytd-prt-cents  if rec-type of f119-doctor-ytd = "A"    &
 else text of f119-doctor-ytd

sorted on doc-nbr of f119-doctor-ytd

set subfile name r124a keep

report summary                                                  &
  doc-nbr               of f119-doctor-ytd                      &
  comp-code             of f119-doctor-ytd                      &
  comp-code-group       of f190-comp-codes                      &
  process-seq           of f119-doctor-ytd                      &
  x-count-increq subtotal reset at doc-nbr                      &
  x-count-status  subtotal reset at doc-nbr                     &
  x-line-text                                                   &
  x-deposit                                                     &
  x-deposit-date                                                &
  x-ped                                                         &
; 2007/jan/18 replaced account with masked value
;  x-bank-account                                                &
  x-bank-account-privacy-masked					&
  x-bank                                                        &
  x-branch                                                      &
  x-dept-name                                                   &
  x-full-part-ind                                               &
  x-doc-id-info                                                 &
;  x-date-title                                                  &
  first-ep-nbr-of-cal-yr					&
  last-ep-nbr-of-cal-yr						&
  x-ytdcea-pos-prt                                              &
  x-ytdcea-val-prt                                              &
  x-ceicea-pos-prt                                              &
  x-ceicea-val-prt                                              &
  x-ytdcex-pos-prt                                              &
  x-ytdcex-val-prt                                              &
  x-ceicex-pos-prt                                              &
  x-ceicex-val-prt						&
  x-ytdreq-pos-prt                                              &
  x-ytdreq-val-prt                                              &
  x-ceireq-pos-prt                                              &
  x-ceireq-val-prt                                              &
  x-ytdtar-pos-prt                                              &
  x-ytdtar-val-prt                                              &
  x-ceitar-pos-prt                                              &
  x-ceitar-val-prt						&
  doc-dept							&
  x-count-increq-status subt reset at doc-nbr			&
  x-increq       subt  reset at doc-nbr				&
  x-inctar       subt  reset at doc-nbr				&
  x-status       subt  reset at doc-nbr		

build $pb_obj/r124a

  
