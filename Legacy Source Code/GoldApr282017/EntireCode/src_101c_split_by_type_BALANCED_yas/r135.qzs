; 2015/Jan/28	MC	original
;			Portal RA rejects
; 2015/Apr/15   MC1     add the parameter for selection to run 'REGULAR' or 'PORTAL'
;                       change define item x-doc-dept-nbr
; 2015/Apr/22   MC2     before it took an hours to run due to large record complex for sort,
;                       change program into A/B so that extract the require items in A and sort in B

can clear 
set rep nolimit

access f088-rat-rejected-claims-hist-dtl                                 &
        link (clmhdr-batch-nbr+ascii(clmhdr-claim-nbr,2), ped)		 &
         to  rat-rejected-claim, ped of f088-rat-rejected-claims-hist-hdr &
        link clmhdr-doc-nbr                                              &
         to  doc-nbr of f020-doctor-mstr   opt                           &
        link ('B', clmhdr-batch-nbr, clmhdr-claim-nbr, '00000', '0')     &
         to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr,         &
             key-clm-serv-code, key-clm-adj-nbr of f002-claims-mstr opt  &
        link clmhdr-pat-ohip-id-or-chart                                 &
         to  key-pat-mstr of f010-pat-mstr opt				 &
        link ohip-err-code                                               &
         to  rat-code of f096-ra-reject-code opt

choose clmhdr-batch-nbr, clmhdr-claim-nbr, clmhdr-oma-suff-adj, ped parm prompt 'From PED: ' range toprompt 'To PED: '  1 time                
select f088-rat-rejected-claims-hist-hdr if charge-status = 'Y'

sel if ohip-err-code of f088-rat-rejected-claims-hist-hdr  =  &
       ohip-err-code of f088-rat-rejected-claims-hist-dtl       

def x-code cha*5 = clmhdr-adj-oma-cd + clmhdr-adj-oma-suff

def x-desc char*70 = rat-explanation

; MC2
set subfile name r135 keep

rep summ clmhdr-doc-nbr					&
	rat-rejected-claim       			&
	doc-dept					&
	doc-name					&
	x-code                       			&
	clmdtl-sv-date                        		&
	pat-health-nbr        				&
	pat-version-cd        				&
	ohip-err-code         				&
	x-desc                				

build $obj/r135a


access *r135

set rep dev disc name r135
set rep page length 62
set rep page width 140 
set formfeed

;MC1
def x-run-type char*7 = parm prompt 'Enter either "REGULAR" or "PORTAL" : '
;MC1 - end

def x-doc-dept-nbr char*7= '~' + clmhdr-doc-nbr + ascii(doc-dept,2) + '~'       &
;MC1
        if x-run-type = 'PORTAL'                &
    else ' '

; MC2 - end

sort on clmhdr-doc-nbr reset page  on rat-rejected-claim

page heading                                                    &
        tab 1   "R135"                                          &
        tab 45  "****  RA  REJECTS  **** "               	&
        tab 100 "RUN DATE:"                                     &
        tab 112  sysdate                                        &
        tab 124 "PAGE:" tab 130 syspage pic "^^^"               &
        tab 133 x-doc-dept-nbr                                  &
skip 2								&
	tab  1  'Dept: '					&		
	tab  7  doc-dept              				&
	tab 12  'Doc#: '    					&
	tab 18  clmhdr-doc-nbr        				&
	tab 25  doc-name              				&
	tab 133 x-doc-dept-nbr					&
skip 2								&
	tab  1  'Claim Nbr'					&
	tab 15  'OMA cd'   					&
	tab 24  'Serv Date'					&
	tab 36  'Health Nbr'					&
	tab 50  'Ver'						&
	tab 56  'Error Code & Description'			&
	tab 133 x-doc-dept-nbr					&
skip 2
		
footing at rat-rejected-claim 					&
	tab  1  rat-rejected-claim       			&
	tab 15  x-code                       			&
	tab 24  clmdtl-sv-date   pic "^^^^/^^/^^"     		&
	tab 36  pat-health-nbr        				&
	tab 50  pat-version-cd        				&
	tab 56  ohip-err-code         				&
	tab 62  x-desc                				&
	tab 133 x-doc-dept-nbr	      				&
skip 1
  
footing at clmhdr-doc-nbr					&
skip 1	tab  1 'Total Nbr of RA rejects: '			&
	tab 40 count at rat-rejected-claim reset at clmhdr-doc-nbr pic '^^^^^'  	&
	tab 133 x-doc-dept-nbr	      				&
skip page 

build $pb_obj/r135b

