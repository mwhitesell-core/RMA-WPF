; DOC: R011C.QZS
; DOC: PHYSICIAN REVENUE ANALYSIS BY DOCTOR (SUMMARY REPORT)
; DOC: SORT BY CLINIC
; DOC: RUN FOR: MUMC DIAGNOSTICS
;
;PROGRAM PURPOSE : REVENUE ANALYSIS BY DOCTOR (SUMMARY REPORT)
;                  R011.CB CONVERSTION TO POWERHOUSE R011C.QZS
;                  THIS IS THE FIRST PART OF 3 PROGRAMS APPEND
;                  R011C.TXT END OF R011A.TXT
;
;DATE       WHO       DESCRIPTION
;92/05/27   YASEMIN   ORIGINAL
;
;
; Feb/19/2001 A.A  Increased size of docrev-mtd-in-svc and
;                                    docrev-mtd-out-svc to 4 digits
can clear
set rep nolimit
set page length 62 width 132
set rep device disc name r011c
set formfee
set noblank

access f050-doc-revenue-mstr                             &
  link docrev-doc-nbr                                    &
    to doc-nbr of f020-doctor-mstr   opt                 &
  link (nconvert(docrev-clinic-1-2))                     &
    to iconst-clinic-nbr-1-2 of iconst-mstr-rec   opt    &
  link docrev-dept                                          &
    to dept-nbr of f070-dept-mstr  opt

choose docrev-key "60@" to "65@"

sorted on docrev-clinic-1-2

def x-class cha*18 = "FULL TIME"         if doc-full-part-ind = "F"   &
                else "PART TIME"         if doc-full-part-ind = "P"   &
                else "CLINICAL SCHOLARS" if doc-full-part-ind = "C"   &
                else "OTHER"             if doc-full-part-ind = "S"

; Feb/19/2001 A.A
;def x-svc-mtd-in  zoned*3 signed = docrev-mtd-in-svc    &
def x-svc-mtd-in  zoned*4 signed = docrev-mtd-in-svc    &
                                   if docrev-location ne "MISC"

; Feb/19/2001 A.A
;def x-svc-mtd-out zoned*3 signed = docrev-mtd-out-svc   &
def x-svc-mtd-out zoned*4 signed = docrev-mtd-out-svc   &
                                   if docrev-location ne "MISC"

def x-amt-mtd-in  zoned*8 signed = docrev-mtd-in-rec    &
                                   if docrev-location ne "MISC"

def x-amt-mtd-out zoned*8 signed = docrev-mtd-out-rec   &
                                   if docrev-location ne "MISC"

; Feb/19/2001 A.A
;def x-misc-svc-mtd zoned*3 signed =(docrev-mtd-in-svc + docrev-mtd-out-svc) &
def x-misc-svc-mtd zoned*4 signed =(docrev-mtd-in-svc + docrev-mtd-out-svc) &
                              if docrev-location = "MISC"

def x-misc-amt-mtd zoned*8 signed =(docrev-mtd-in-rec + docrev-mtd-out-rec) &
                              if docrev-location = "MISC"

def x-svc-ytd-in  zoned*5 signed = docrev-ytd-in-svc    &
                                   if docrev-location ne "MISC"

def x-svc-ytd-out zoned*5 signed = docrev-ytd-out-svc   &
                                   if docrev-location ne "MISC"

def x-amt-ytd-in  zoned*8 signed = docrev-ytd-in-rec    &
                                   if docrev-location ne "MISC"

def x-amt-ytd-out zoned*8 signed = docrev-ytd-out-rec   &
                                   if docrev-location ne "MISC"

def x-misc-svc-ytd zoned*5 signed =  &
    (docrev-ytd-in-svc + docrev-ytd-out-svc) if docrev-location = "MISC"

def x-misc-amt-ytd zoned*8 signed =   &
    (docrev-ytd-in-rec + docrev-ytd-out-rec) if docrev-location = "MISC"

; Feb/19/2001 A.A
;def x-svc-total-mtd zoned*3 signed= (x-svc-mtd-in + x-svc-mtd-out + x-misc-svc-mtd)
def x-svc-total-mtd zoned*4 signed= (x-svc-mtd-in + x-svc-mtd-out + x-misc-svc-mtd)

def x-svc-total-ytd zoned*5 signed= (x-svc-ytd-in + x-svc-ytd-out + x-misc-svc-ytd)

def x-amt-total-mtd zoned*8 signed= (x-amt-mtd-in + x-amt-mtd-out + x-misc-amt-mtd)

def x-amt-total-ytd zoned*8 signed= (x-amt-ytd-in + x-amt-ytd-out + x-misc-amt-ytd)

;y2k
def x-period cha*1 = "."

def x-name cha*30 =pack(doc-name +" "+ doc-inits)

page heading                                                    &
	tab 1   "R011"  			          	&
;	TAB 8   "/"     			          	&
;	TAB 9   DOCREV-CLINIC-1-2                               &
	tab 13  "P.E.D"				          	&
;y2k
	tab 20  iconst-date-period-end pic "^^^^/^^/^^"     	&
	tab 47  "PHYSICIAN REVENUE ANALYSIS - BY DOCTOR"  	&
	tab 103 "RUN DATE:"                               	&
;y2k
	tab 112  sysdate                                  	&
        tab 124 "PAGE:" tab 130 syspage pic "^^^"      	 	&
skip 1  tab 60  "SUMMARY REPORT"                                &
;       TAB 105 "CLINIC:"                                       &
;       TAB 113  DOCREV-CLINIC-1-2                              &
;       TAB 116  "-"                                            &
;       TAB 118  ICONST-CLINIC-NBR                              &
;SKIP 1  TAB 57  "CHED-MCMAS DIAG MUMC"                  	&
skip 2  tab 1   "CLINIC CLASS TOTALS"                  		&
skip 2  tab 39  "# SVC---IN-PATIENT"                   		&
        tab 64  "# SVC---OUT-PATIENT"              		&
        tab 87  "# SVC--MISCELLANEOUS"                  	&
        tab 113 "# SVC--TOTAL-AMOUNT"                  		&
skip 2  tab 1   "SUMMARY BY CLINIC"                             &
skip 2

footing at docrev-clinic-1-2     &
skip 2  tab 8   docrev-clinic-1-2                                &
        tab 29  "MONTH"                                          &
        tab 41  x-svc-mtd-in    subtotal pic "^,^^^"             &
        tab 47  x-amt-mtd-in    subtotal pic "^^^^,^^^.^^ " trail "-"       &
        tab 66  x-svc-mtd-out   subtotal pic "^,^^^"             &
        tab 72  x-amt-mtd-out   subtotal pic "^^^^,^^^.^^ " trail "-"       &
        tab 89  x-misc-svc-mtd  subtotal pic "^,^^^"             &
        tab 97  x-misc-amt-mtd  subtotal pic "^^^^,^^^.^^ " trail "-"       &
        tab 113 x-svc-total-mtd subtotal pic "^,^^^"             &
        tab 120 x-amt-total-mtd subtotal pic "^^^^,^^^.^^ " trail "-"       &
skip 1  tab 30  "YEAR"                                           &
        tab 40  x-svc-ytd-in    subtotal pic "^^,^^^"            &
        tab 47  x-amt-ytd-in    subtotal pic "^^^^,^^^.^^ " trail "-"       &
        tab 65  x-svc-ytd-out   subtotal pic "^^,^^^"            &
        tab 72  x-amt-ytd-out   subtotal pic "^^^^,^^^.^^ " trail "-"       &
        tab 88  x-misc-svc-ytd  subtotal pic "^^,^^^"            &
        tab 97  x-misc-amt-ytd  subtotal pic "^^^^,^^^.^^ " trail "-"       &
        tab 112 x-svc-total-ytd subtotal pic "^^,^^^"            &
        tab 120 x-amt-total-ytd subtotal pic "^^^^,^^^.^^ " trail "-"       &
skip 2

final footing                    &
skip 1  tab 1   "* TOTAL FOR ALL CLINICS *"                      &
        tab 29  "MONTH"                                          &
        tab 41  x-svc-mtd-in    subtotal pic "^,^^^"             &
        tab 47  x-amt-mtd-in    subtotal pic "^^^^,^^^.^^ " trail "-"       &
        tab 65  x-svc-mtd-out   subtotal pic "^^,^^^"             &
        tab 72  x-amt-mtd-out   subtotal pic "^^^^,^^^.^^ " trail "-"       &
        tab 89  x-misc-svc-mtd  subtotal pic "^,^^^"             &
        tab 97  x-misc-amt-mtd  subtotal pic "^^^^,^^^.^^ " trail "-"       &
        tab 112 x-svc-total-mtd subtotal pic "^^,^^^"             &
        tab 120 x-amt-total-mtd subtotal pic "^^^^,^^^.^^ " trail "-"       &
skip 1  tab 30  "YEAR"                                           &
        tab 40  x-svc-ytd-in    subtotal pic "^^,^^^"            &
        tab 47  x-amt-ytd-in    subtotal pic "^^^^,^^^.^^ " trail "-"       &
        tab 64  x-svc-ytd-out   subtotal pic "^^^,^^^"            &
        tab 72  x-amt-ytd-out   subtotal pic "^^^^,^^^.^^ " trail "-"       &
        tab 88  x-misc-svc-ytd  subtotal pic "^^,^^^"            &
        tab 97  x-misc-amt-ytd  subtotal pic "^^^^,^^^.^^ " trail "-"       &
        tab 111 x-svc-total-ytd subtotal pic "^^^,^^^"            &
        tab 120 x-amt-total-ytd subtotal pic "^^^^,^^^.^^ " trail "-"

build $pb_obj/r011c

  
