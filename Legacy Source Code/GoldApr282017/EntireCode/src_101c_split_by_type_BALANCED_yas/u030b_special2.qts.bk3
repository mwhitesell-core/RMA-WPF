;#> PROGRAM-ID.     U030B_special1.QTS
;
; ((C)) Dyad Technologies
;
;    PROGRAM PURPOSE : SECOND PASS OF U030b_special2.qts
;                    THIS IS THE MAIN PROGRAM OF THE ORIGINAL
;                       U030.CB
;
;    MODIFICATION HISTORY
;        DATE   WHO          DESCRIPTION
;     
;    2000/aug/23 M.C.	  - add a new prepass to calculate the tech/prof amount
;			    before creating automatic adjustment records
;    2000/sep/14 M.C.	  - first time run this pgm in live, error in rounding
;			    don't understand why rounding is a problem now but
;			    not in u030b_reapply.qts, same formula in both pgms


can clear
set default
set process nolimit
set lock file update

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


; 2000/08/23 M.C.	- add this new prepass to calculate proper
;			  technical & professional amt billed


request calc_tech_prof on calculation errors report on edit errors report

access *u030_srt_adj link x-adj-serv-code to fee-oma-cd of f040-oma-fee-mstr &
 link   'B', nconvert(part-hdr-claim-id[1:9]),        &
        nconvert(part-hdr-claim-id[10:2]), '00000', '0'   &
    to   key-clm-type, key-clm-batch-nbr,             &
        key-clm-claim-nbr, key-clm-serv-code, key-clm-adj-nbr &
         of f002-claims-mstr 

def x-prof-ind char*1 = 'Y'				&
	 if fee-icc-sec = 'DR' 		    		&
	 or fee-icc-sec = 'DU'				&
	 or fee-icc-sec = 'NM'				&
	 or fee-icc-sec = 'PF'				&
	 or fee-tech-ind = 'N'  

def x-tech-ind char*1 = 'Y'				&
	 if fee-icc-sec = 'DR' 		    		&
	 or fee-icc-sec = 'DU'				&
	 or fee-icc-sec = 'NM'				&
	 or fee-icc-sec = 'PF'				&
	 or fee-tech-ind = 'Y'  

def x-prof-prev   = fee-prev-h-fee-2        		&
	 if fee-icc-sec = 'DR' 		    		&
	 or fee-icc-sec = 'DU'				&
	 or fee-icc-sec = 'NM'				&
	 or fee-icc-sec = 'PF'				&
	else fee-prev-h-fee-1 

def x-prof-incr = fee-prev-h-fee-2 * .0195		&
	 if fee-icc-sec = 'DR' 		    		&
	 or fee-icc-sec = 'DU'				&
	 or fee-icc-sec = 'NM'				&
	 or fee-icc-sec = 'PF'				&
	else fee-prev-h-fee-1 * .0195

def x-prof-curr   = fee-curr-h-fee-2        		&
	 if fee-icc-sec = 'DR' 		    		&
	 or fee-icc-sec = 'DU'				&
	 or fee-icc-sec = 'NM'				&
	 or fee-icc-sec = 'PF'				&
	else fee-curr-h-fee-1 

; 2000/09/14 - MC
;def x-prof-stage-1 = round(x-prof-incr,0,near) 			
def x-prof-stage-1 zoned*5 signed = round(x-prof-incr/10)       	
;;
def x-prof-ascii   char*5 = ascii(x-prof-stage-1,5)
def x-prof-stage-2 = nconvert(x-prof-ascii[1:4] + '0')	&
	if x-prof-ascii[5:1] >= '0' and x-prof-ascii[5:1] <= '2' &
	else nconvert(x-prof-ascii[1:4] + '5')		&
	if x-prof-ascii[5:1] >= '3' and x-prof-ascii[5:1] <= '7' &
	else nconvert(x-prof-ascii[1:4] + '0')+ 10		&
	if x-prof-ascii[5:1] >= '8' and x-prof-ascii[5:1] <= '9' 

def x-prof = x-prof-stage-2      			&
	 if    x-prof-ind = 'Y'   			&
	   and clmdtl-sv-date >= '20000401'		&
	   and clmhdr-date-sys < '20000828'		&
	else x-prof-prev				&
	 if    x-tech-ind = 'Y'				&
	   and x-prof-ind = 'Y'				&
	   and part-dtl-explan-cd = '35' 		&
	   and (clmdtl-sv-date <= '20000331'  		&
	         or  (clmdtl-sv-date >= '20000401'	&
		 and  clmhdr-date-sys < '20000828'))	&
	else x-prof-curr				& 
	 if    x-tech-ind = 'Y'				&
	   and x-prof-ind = 'Y'				&
	   and part-dtl-explan-cd = '35' 		&
	   and clmdtl-sv-date  >= '20000401'		&
   	   and clmhdr-date-sys >= '20000828'	


; 2000/09/14 - MC
;def x-tech-decr integer signed size 4 = round(fee-curr-h-fee-1 * -.07)	&
def x-tech-decr zoned*6 signed = round(fee-curr-h-fee-1 * -.07 / 10)	&
;;
	if clmdtl-sv-date >= '20000401'			&
; 2000/09/14 - MC
; else  round(fee-prev-h-fee-1 * -.07)		&
  else  round(fee-prev-h-fee-1 * -.07 / 10)	&
;;
	if clmdtl-sv-date <= '20000331'			
	
; 2000/09/14 - MC
;def x-tech integer signed  size 4 = fee-curr-h-fee-1    &
def x-tech zoned*6 signed         = fee-curr-h-fee-1    &
;;
	if    x-tech-ind = 'Y' 				&
	  and x-prof-ind = 'Y'				&
	  and part-dtl-explan-cd = '35'			&
	  and clmdtl-sv-date >= '20000401'		&
	else fee-prev-h-fee-1				&
	if    x-tech-ind = 'Y' 				&
	  and x-prof-ind = 'Y'				&
	  and part-dtl-explan-cd = '35'			&
	  and clmdtl-sv-date <= '20000331'		&
	else x-tech-decr if x-tech-ind = 'Y'  

def x-tot-adj = x-tech + x-prof
def x-nbr-serv = round(x-bal-diff / x-tot-adj)

def x-tech-amt = x-bal-diff if fee-tech-ind = 'Y'	&
 	 		  or (x-prof = 0		&
			 and  clmdtl-sv-date <= '20000331') &
	else (x-bal-diff - (x-nbr-serv * x-prof))       & 
			    if x-prof <> 0		& 
			   and x-tech-ind = 'Y' 	&
                           and x-prof-ind = 'Y' ;&
;		      	   and clmdtl-sv-date >= '20000401'

def x-prof-amt = x-bal-diff if x-tech-ind = ' '   	& 
			   and x-prof-ind = 'Y'		& 
	                   and clmdtl-sv-date >= '20000401' &
	else x-nbr-serv * x-prof  if x-prof <> 0	&
			   and x-tech-ind = 'Y' 	&
                           and x-prof-ind = 'Y' ;	&
;		      	   and clmdtl-sv-date >= '20000401'

subfile u030mc keep include	&
	u030_srt_adj,		&
	fee-tech-ind,		&
	fee-icc-sec, 		&
	fee-curr-h-fee-1,       &
	fee-curr-h-fee-2,	&
	x-tech, x-prof,		&
	x-tech-ind, x-prof-ind, &
 	x-tech-amt, x-prof-amt


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



request create_b_adjustment on calculation errors report on edit errors report
                            ; Create a batch control record for every
                          ; 99 claim. Create a clmhdr and a clmdtl
                        ; record, and inverted clmdtl key to the
                        ; each adjusting clmdtl record in the
                           ; subfile. At the end of the request,
                           ; update the batch nbr in Constant Mstr
                         ; Note:  Create adjustment only if the
                          ;        Rma Billed = Ohip Billed
                       ; 91/12/16 - Comment out the linkage to
                         ; F002-CLMDTL, some define statements and
                       ; selection statement.  Create a new subfile
                            ; that store all adjusted batches.
                      ; 93/02/19 - link to doctor master to use
                       ; the department instead of from claims



access *u030mc                                       &
  link  'B', nconvert(part-hdr-claim-id[1:9]),        &
        nconvert(part-hdr-claim-id[10:2]), '00000', '0' &
    to   key-clm-type, key-clm-batch-nbr,             &
        key-clm-claim-nbr, key-clm-serv-code, key-clm-adj-nbr &
         of f002-claims-mstr alias f002-clmhdr          &
  link   nconvert(part-hdr-claim-id[4:3])              &
   to    doc-nbr of f020-doctor-mstr                   &
   and   nconvert(part-hdr-claim-id[1:2]) to iconst-mstr-rec

sorted on x-batch-count on part-hdr-claim-id


temp x-batch-nbr int*6
  item x-batch-nbr = iconst-clinic-batch-nbr + x-batch-count   &
        at x-batch-count                          &
               reset at initial to  iconst-clinic-batch-nbr

define x-clinic-batch-nbr int*9 =                  &
       nconvert(part-hdr-claim-id[1:2] +               &
                ascii(x-batch-nbr,7))

define x-mod       = mod(x-count,99)

define x-claim-nbr = x-mod if x-mod <> 0 else 99

; B.E. 98/Sep/10
; For clinics 60 thru 65, 
; if any of the details had an ohip reason code of '80' then all
; of the adjustment is applied against the tech component of claim

define x-ohip-bal int*7 signed =                      &
;      (PART-HDR-AMT-BILL - PART-HDR-AMT-PAID) * -1
    x-bal-diff * -1

define x-tot-claim-ar-oma int*7 signed =         	&
  round(  (  clmhdr-tot-claim-ar-oma  of f002-clmhdr	&
	   / clmhdr-tot-claim-ar-ohip of f002-clmhdr)	&
	* x-ohip-bal)

; For Clinics 61-65, if reason code = 80 then the full amount
; of writeoff is applied against the tech portion, otherwise
; based upon ratio of original ohip billed to original tech billed
;define x-amt-tech-billed  int*7 signed =         	&
;   x-ohip-bal 						&
;if    part-dtl-explan-cd = "80"			&
;  and part-hdr-claim-id[1:2] >= "60"		&
;  and part-hdr-claim-id[1:2] <= "65"		&
;else							&
;   round(  (  clmhdr-amt-tech-billed   of f002-clmhdr	&
;     / clmhdr-tot-claim-ar-ohip of f002-clmhdr)	&
; 	  * x-ohip-bal)


define x-amt-tech-billed  int*7 signed = x-tech-amt * -1


;S.B.
use $use/def_batctrl_batch_status.def
use $use/def_clmhdr_status_ohip.def

subfile u030mcadj keep include	&
;u030_srt_adj,			&
u030mc, 			&
x-clinic-batch-nbr ,		&
x-claim-nbr     ,		&
clmhdr-tot-claim-ar-oma,	&
x-tot-claim-ar-oma,		&
clmhdr-tot-claim-ar-ohip,	&
x-ohip-bal,			&
clmhdr-amt-tech-billed,		&
x-amt-tech-billed       
 
output f001-batch-control-file add at x-batch-count on errors report
   item batctrl-batch-nbr       initial x-clinic-batch-nbr
   item batctrl-batch-type            initial 'A'
   item batctrl-clinic-nbr         initial iconst-clinic-nbr
   item batctrl-adj-cd                   initial 'B'
;y2k
;  item batctrl-date-batch-entered initial ascii(sysdate,6)
   item batctrl-date-batch-entered initial ascii(sysdate,8)
;  item batctrl-date-period-end    initial ascii(iconst-date-period-end,6)
   item batctrl-date-period-end    initial ascii(iconst-date-period-end,8)

   item batctrl-cycle-nbr       initial iconst-clinic-cycle-nbr
; y2k
;  item batctrl-ar-yy-mm         initial '0000'
   item batctrl-ar-yy-mm         initial '000000'

   item batctrl-adj-cd-sub-type    initial 'A'
   item batctrl-nbr-claims-in-batch final x-claim-nbr
   item batctrl-last-claim-nbr      final x-claim-nbr
;S.B.
;   item batctrl-batch-status       final "1"
   item batctrl-batch-status       final batctrl-batch-status-balanced
   item batctrl-amt-act             subtotal x-ohip-bal
   item batctrl-amt-est             subtotal x-ohip-bal
   item batctrl-calc-ar-due         subtotal x-ohip-bal
   item batctrl-calc-tot-rev        subtotal x-ohip-bal
   item batctrl-agent-cd           final clmhdr-agent-cd of f002-clmhdr


output f002-claims-mstr alias f002-adj-hdr add noitems on errors report
;Preset claim header data from batctrl record
   item key-clm-type                   initial 'B'
   item key-clm-batch-nbr            initial x-clinic-batch-nbr
   item key-clm-claim-nbr     initial x-claim-nbr
   item key-clm-serv-code    initial '00000'
   item key-clm-adj-nbr                  initial '0'
   item clmhdr-orig-batch-nbr        initial batctrl-batch-nbr
   item clmhdr-orig-claim-nbr          initial x-claim-nbr
   item clmhdr-batch-type          initial batctrl-batch-type
   item clmhdr-adj-cd-sub-type     initial batctrl-adj-cd-sub-type
   item clmhdr-adj-cd              initial batctrl-adj-cd
   item clmhdr-date-period-end    initial nconvert(batctrl-date-period-end)
   item clmhdr-cycle-nbr       initial batctrl-cycle-nbr
; Preset claim header data with values from claim being adjusted
   item clmhdr-claim-id        initial clmhdr-claim-id of f002-clmhdr
   item clmhdr-diag-cd            initial clmhdr-diag-cd of f002-clmhdr
   item clmhdr-hosp                initial clmhdr-hosp of f002-clmhdr
   item clmhdr-i-o-pat-ind         initial clmhdr-i-o-pat-ind of f002-clmhdr
   item clmhdr-agent-cd            initial clmhdr-agent-cd of f002-clmhdr
   item clmhdr-loc                 initial clmhdr-loc of f002-clmhdr
   item clmhdr-pat-ohip-id-or-chart initial clmhdr-pat-ohip-id-or-chart of f002-clmhdr
   item clmhdr-pat-acronym         initial clmhdr-pat-acronym of f002-clmhdr
   item clmhdr-doc-dept            initial doc-dept of f020-doctor-mstr
; Assign values to the remaining fields
   item clmhdr-reference    initial part-dtl-explan-cd of u030mc
; y2k
;  item clmhdr-date-admit          initial '000000'
   item clmhdr-date-admit          initial '00000000'
;y2k
;  item clmhdr-date-cash-tape-payment initial ascii(sysdate,6)
   item clmhdr-date-cash-tape-payment initial ascii(sysdate,8)
;  item clmhdr-date-sys            initial ascii(sysdate,6)
   item clmhdr-date-sys            initial ascii(sysdate,8)
;S.B.
;   item clmhdr-status-ohip         initial '00'
   item clmhdr-status-ohip         initial clmhdr-status-ohip-accepted
   item clmhdr-tape-submit-ind     initial 'N'
   item clmhdr-doc-nbr-ohip        initial 0
   item clmhdr-doc-spec-cd         initial 0
   item clmhdr-refer-doc-nbr       initial 0
   item clmhdr-curr-payment        initial 0
   item clmhdr-amt-tech-paid       initial 0
   item clmhdr-manual-and-tape-payments initial 0
;
   item clmhdr-amt-tech-billed         initial x-amt-tech-billed
   item clmhdr-tot-claim-ar-oma    initial x-tot-claim-ar-oma
   item clmhdr-tot-claim-ar-ohip   initial x-ohip-bal
; 99/12/20 
   item key-p-clm-type 		   initial 'Z'
   item key-p-clm-data   	   initial clmhdr-pat-ohip-id-or-chart of f002-clmhdr[2:14]

output f002-claims-mstr alias f002-adj-dtl add noitems on errors report
   item key-clm-type               initial 'B'
   item key-clm-batch-nbr          initial x-clinic-batch-nbr
   item key-clm-claim-nbr          initial x-claim-nbr
   item key-clm-serv-code          initial x-adj-serv-code of u030mc
   item key-clm-adj-nbr            initial '0'
   item clmdtl-batch-nbr           initial clmhdr-batch-nbr of f002-adj-hdr
   item clmdtl-claim-nbr           initial clmhdr-claim-nbr of f002-adj-hdr
   item clmdtl-oma-cd              initial x-adj-serv-code  of u030mc[1:4]
   item clmdtl-oma-suff            initial x-adj-serv-code of u030mc[5:1]
   item clmdtl-adj-nbr             initial 1
   item clmdtl-agent-cd            initial clmhdr-agent-cd of f002-adj-hdr
   item clmdtl-adj-cd              initial clmhdr-adj-cd of f002-adj-hdr
;  ITEM CLMDTL-SV-DATE             INITIAL CLMDTL-SV-DATE OF F002-CLMDTL
   item clmdtl-sv-date             initial clmdtl-sv-date of u030mc
   item clmdtl-nbr-serv            initial 0
   item clmdtl-consec-dates        initial 0

; y2k
   item clmdtl-date-period-end     initial ascii(			       &
;				       clmhdr-date-period-end of f002-adj-hdr,6)
				       clmhdr-date-period-end of f002-adj-hdr,8)
   item clmdtl-cycle-nbr           initial clmhdr-cycle-nbr of f002-adj-hdr
   item clmdtl-orig-batch-id       initial clmhdr-orig-batch-id of f002-adj-hdr
   item clmdtl-fee-oma             initial clmhdr-tot-claim-ar-oma of f002-adj-hdr
   item clmdtl-fee-ohip            initial clmhdr-tot-claim-ar-ohip of f002-adj-hdr
   item clmdtl-amt-tech-billed     initial clmhdr-amt-tech-billed of f002-adj-hdr
   item clmdtl-diag-cd             initial clmdtl-diag-cd of u030mc
   item clmdtl-rev-group-cd        initial ' '
   item clmdtl-line-no              initial clmdtl-line-no of u030mc
; 99/12/20 
   item key-p-clm-type 		   initial 'Z'
; 2000/02/14 - MC - the old formula gives data conversion error
;   item key-p-clm-data   	   initial ascii(x-clinic-batch-nbr,2) +    &   
   item key-p-clm-data   	   initial ascii(x-clinic-batch-nbr,9) +    &   
					   ascii(x-claim-nbr,2) +	&
					   x-adj-serv-code of u030mc  &
					   + '0'


output iconst-mstr-rec update at final on errors report
   item iconst-clinic-batch-nbr     final x-batch-nbr


;Create Clmdtl Key in Subfile, so Cobol pgm can write inverted to
;the adjusting Clmdtl
subfile u030_dtl_key keep include                        &
   key-clm-type      of f002-adj-dtl,                  &
   key-clm-batch-nbr of f002-adj-dtl,                  &
   key-clm-claim-nbr of f002-adj-dtl,                  &
   key-clm-serv-code of f002-adj-dtl,                  &
   key-clm-adj-nbr   of f002-adj-dtl



subfile u030_adj_batches keep at x-batch-count     &
       include batctrl-batch-nbr

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request hold_claim_status on calculation errors report on edit errors report
                           ;  If partial payment, set the claim to 'H'old
                       ;  and update OHIP claim nbr in F020-CLAIMS-EXTRA

access *u030_no_adj                                         &
   link  'B', nconvert(part-hdr-claim-id[1:9]),        &
        nconvert(part-hdr-claim-id[10:2]), '00000', '0' &
    to   key-clm-type, key-clm-batch-nbr,             &
        key-clm-claim-nbr, key-clm-serv-code, key-clm-adj-nbr &
         of f002-claims-mstr                            &
   link  nconvert(part-hdr-claim-id)                   &
    to   clmhdr-rma-clm-nbr of f002-claims-extra opt

output f002-claims-mstr update on errors report
   item clmhdr-tape-submit-ind final 'H'
   item clmhdr-status-ohip final part-hdr-explan-cd

output f002-claims-extra add update on errors report
   item clmhdr-ohip-clm-nbr final part-hdr-ohip-clm-nbr
   item clmhdr-rma-clm-nbr initial nconvert(part-hdr-claim-id)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request part_adj_batch on calculation errors report on edit errors report
                           ;  Store the adjusted batches into the file
                           ;  PART_ADJ_BATCH

access *u030_adj_batches link 'B', batctrl-batch-nbr    &
   to key-clm-type, key-clm-batch-nbr of f002-claims-mstr

sel if clmhdr-oma-suff-adj = '000000'

output part-adj-batch add on errors report
   item  part-adj-claim-id final clmhdr-claim-id[1:11]
   item  part-adj-bal final clmhdr-tot-claim-ar-ohip


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

request update_claim_bal on calculation errors report on edit errors report
                    ;  Update the adjusted balance to the
                    ;  original claim header

;access *u030_update_clmhdr                           &
access *u030mc                                       &
   link  'B', nconvert(part-hdr-claim-id[1:9]),        &
        nconvert(part-hdr-claim-id[10:2]), '00000', '0' &
    to   key-clm-type, key-clm-batch-nbr,             &
        key-clm-claim-nbr, key-clm-serv-code, key-clm-adj-nbr &
         of f002-claims-mstr
; 98/Sep/10 added sort in order to reset x-found-80-code counter
sorted on part-hdr-claim-id 

; B.E. 98/Sep/10
; For clinics 60 thru 65, 
; if any of the details had an ohip reason code of '80' then all
; of the adjustment is applied against the tech component of claim
; Counter will be > 0 if any 80 codes found for claim
temp x-found-80-code
item x-found-80-code = x-found-80-code + 1 		&
	if    part-dtl-explan-cd = "80"			&
	  and part-hdr-claim-id[1:2] >= "60"		&
	  and part-hdr-claim-id[1:2] <= "65"		&
		reset at part-hdr-claim-id

;define x-ohip-bal int*7 signed =                    	&
;       x-claim-bal * -1



temp x-ohip-bal int*7 signed
item x-ohip-bal = x-ohip-bal  + (x-bal-diff * -1)	&
	reset at part-hdr-claim-id


;define x-tot-claim-ar-oma int*7 signed =                &
;  round(  (  clmhdr-tot-claim-ar-oma 			&
;           * x-ohip-bal)				&
;        / clmhdr-tot-claim-ar-ohip  )


temp x-tot-claim-ar-oma int*7 signed
item x-tot-claim-ar-oma = x-tot-claim-ar-oma + 		&
   round(  (  clmhdr-tot-claim-ar-oma 			&
            * x-bal-diff * -1)				&
         / clmhdr-tot-claim-ar-ohip  )			&
	reset at part-hdr-claim-id


; if any reason codes = 80 then the full amount
; of writeoff is applied against the tech portion, otherwise
; based upon ratio of original ohip billed to original tech billed
;define x-amt-tech-billed  int*7 signed =                &
;  round(  (  clmhdr-amt-tech-billed			&
;           * x-ohip-bal) 				&
;	/ clmhdr-tot-claim-ar-ohip  )			&
;		if x-found-80-code = 0			&
; else	x-ohip-bal

temp x-amt-tech-billed int*7 signed
item x-amt-tech-billed = x-amt-tech-billed  +		&
	(x-tech-amt * -1) reset at part-hdr-claim-id

  	
subfile u030mchdr keep at part-hdr-claim-id include	&
x-found-80-code,		&
;u030_update_clmhdr,		&
u030mc,       			&
clmhdr-tot-claim-ar-oma,	&
x-tot-claim-ar-oma,		&
clmhdr-tot-claim-ar-ohip,	&
x-ohip-bal,			&
clmhdr-amt-tech-billed,		&
x-amt-tech-billed       
 
output f002-claims-mstr  update at part-hdr-claim-id on errors report
;  item clmhdr-tot-claim-ar-oma    subtotal x-tot-claim-ar-oma
   item clmhdr-tot-claim-ar-oma    final    x-tot-claim-ar-oma  &
				+ clmhdr-tot-claim-ar-oma 
;  item clmhdr-tot-claim-ar-ohip   subtotal x-ohip-bal
   item clmhdr-tot-claim-ar-ohip   final    x-ohip-bal		&
				+ clmhdr-tot-claim-ar-ohip
;  item clmhdr-amt-tech-billed     subtotal x-amt-tech-billed
   item clmhdr-amt-tech-billed     final    x-amt-tech-billed   &
				+ clmhdr-amt-tech-billed

;  item clmhdr-date-cash-tape-payment final ascii(sysdate,8)

build $pb_obj/u030b_special2
