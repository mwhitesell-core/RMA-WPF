; 2014/Sep/23 	MC	- check  primary doctor (u100_b.qzs)
;			- to be run as last part of $cmd/verify_payroll_ok_to_run
;			- user is asked to pass 2 parameters a
; 2014/Oct/9   yas      - If doc has only one number and have primary flag set then do not print on this report
;                         We need the primary number so that system will calculate the TITHE for the doctor 

cancel clear
set report nolimit

access f020-doctor-mstr                         &
        link doc-nbr                            &
          to  doc-nbr of f020-doctor-extra  optional

choose doc-ohip-nbr

def payroll-flag char*1 = parm prompt 'Enter Payroll (A = 101c, C = solo): '
def term-date-cutoff date = parm prompt 'Enter terminate date cutoff: '

sorted on doc-ohip-nbr

sel if  	                                      &
 	(	doc-date-fac-term = 0 		      &
      	     or doc-date-fac-term > term-date-cutoff  &
	)					      &
   and						      &
        (   (    (   (doc-dept = 4)                   &
                  or (doc-dept = 42)                  &
                  or (    (    doc-dept = 14          &
                            or doc-dept = 15          &
                          )                           &
                      and doc-afp-paym-group = 'H111' &
                     )                                &
                 )                                    &
              and payroll-flag = 'A'                  &
            )                                         &
         or (    (    doc-dept = 31                   &
                  and doc-afp-paym-group = 'H132'     &
                 )                                    &
             and payroll-flag = 'C'                   &
            )					      &
       )	

set subfile name u100_b keep
rep summ doc-ohip-nbr doc-nbr doc-dept doc-afp-paym-group doc-flag-primary doc-date-fac-start doc-date-fac-term  &
term-date-cutoff payroll-flag
go


access *u100_b

sort on doc-ohip-nbr on doc-flag-primary

def doctor-count = 1

set subfile name u100_b2 keep at doc-ohip-nbr

rep summ doc-ohip-nbr doc-nbr doc-dept doc-afp-paym-group doc-flag-primary doc-date-fac-start doc-date-fac-term  &
term-date-cutoff payroll-flag doctor-count subt reset at doc-ohip-nbr
go

; select for multiple doctors
access *u100_b2

sel if doctor-count <> 1 

set subfile name u100_b_3a keep

rep summ doc-ohip-nbr doctor-count term-date-cutoff payroll-flag

go

;select unique doctor that have not assigned primary flag to 'Y' 
access *u100_b2

sel if doc-flag-primary <> 'Y'

set subfile name u100_b_3b keep

rep summ all

go

access *u100_b_3a link doc-ohip-nbr to doc-ohip-nbr of f020-doctor-mstr	&
        link doc-nbr                            &
          to  doc-nbr of f020-doctor-extra  optional


sel if  	                                      &
 	(	doc-date-fac-term = 0 		      &
      	     or doc-date-fac-term > term-date-cutoff  &
	)					      &
   and						      &
        (   (    (   (doc-dept = 4)                   &
                  or (doc-dept = 42)                  &
                  or (    (    doc-dept = 14          &
                            or doc-dept = 15          &
                          )                           &
                      and doc-afp-paym-group = 'H111' &
                     )                                &
                 )                                    &
              and payroll-flag = 'A'                  &
            )                                         &
         or (    (    doc-dept = 31                   &
                  and doc-afp-paym-group = 'H132'     &
                 )                                    &
             and payroll-flag = 'C'                   &
            )					      &
       )	

set subfile name u100_b_3b append

rep summ doc-ohip-nbr doc-nbr doc-dept doc-afp-paym-group doc-flag-primary doc-date-fac-start doc-date-fac-term  &
term-date-cutoff payroll-flag doctor-count 

go

access *u100_b_3b
set rep dev disc name u100_b
set rep page width 142 length 60
set formfeed

sort on doc-ohip-nbr

page heading                                    &
tab  1 'U100_b'                                 &
tab 10 'Run Date: '                             &
tab 20 sysdate                                  &
tab 70 'Page: '                                 &
tab 79 syspage pic '^^'                         &
skip 2                                          &
tab 10 'Check if the Primary flag assigned to the correct doctor number'       &
skip 3 keep column heading

rep doc-ohip-nbr doc-nbr doc-dept doc-afp-paym-group doc-flag-primary doc-date-fac-start doc-date-fac-term  

footing at doc-ohip-nbr  skip 2
go

exit

