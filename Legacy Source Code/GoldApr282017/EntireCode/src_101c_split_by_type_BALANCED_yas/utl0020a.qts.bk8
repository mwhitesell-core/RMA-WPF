;#> PROGRAM-ID.   utl0020a.qts
;
;	((C)) Dyad Technologies
;
;    R.M.A. VERSION
;
;    PROGRAM PURPOSE :
;			- extract data from the payroll system and download to pc
;			  for upload into spreadsheet
;			- in this program individual requests gather different information
;			  for the doctor and place it into a unique record for the doctor 
;			  within file tmp-pc-download-file 
;
;
;    MODIFICATION HISTORY
;        DATE   WHO          DESCRIPTION
;     92/MAR/02 B.E.         - ORIGINAL
;     92/DEC/22 B.E.         - VIRT. MEM. PROBLEM - REMOVE DEPT 6
;     94/FEB/11 M.C.	     - ADD PAY-SUB-CODE IN SUBFILE
;     94/OCT/21 B.E.         - VIRT. MEM. PROBLEM - REMOVE DEPT 5
;     95/OCT/25 M.C.	     - ACCESS STMT HAS EXPANDED TO 28 FILES,
;			       ONLY ONE SUBFILE TO BE CREATED. DG2PC1B
;			       WILL SPLIT OUT SUBFILES BY DEPT
;     95/OCT/25 M.C.         - INCLUDE CEIEXP, GTYPEA, GUARANTEE-FLAG
;
;     95/NOV/02 YAS.         - REMOVE FROM ACCESS AND CALC-EARN
;                              MICV/MICM/MISJ/MISP/MOHR INSTEAD
;                              ADD MANTRA/MANTRR
;     95/NOV/06 YAS.         - COMMENT GTYPEA-DOL-YTD & GTYPEA-CNT-YTD
;			       AND REPLACE BY GTYPEA-DOL-MTH &
;			       & GTYPEA-CNT-MTH
;			     - CHANGE THE FORMULA FOR MISC-DOL-YTD &
;			       MISC-CNT-YTD, USE (TOTINC - BILL) INSTEAD
;			       OF GTYPEA
;			     - ADD F119-BILL TO ACCESS STATEMENT
;     95/NOV/22 YAS.         - ADD F119-SPEINC TO ACCESS STAEMENT
;			       AND ADD SPEINC TO EARN-DOL-YTD
;     95/DEC/18 YAS.         - ADD PAYRED, TRRMAN, TRAMAN, REDPAY,MAN009
;	                       TAKE OUT MAN001/002/003/004
;     96/JAN/19 YAS.         - ADD SPEING TO ACCESS AND EARN-DOL-YTD
;     96/FEB/16 YAS.         - ADD MAN002
;     96/MAR/18 YAS.         - ADD TRAMNN, TAKE OUT EARREF/EFTCAN/SPEING
;   1999/May/19 S.B.	     - Y2K checked.
;	2003/SEP/25 b.e.		-re-write logic so that information is gathered into a unique
;						     record within file tmp-pc-download-file for each doctor
;						    - that file is then downloaded to the PC and places into spreadsheet

can clear

run download_to_pc_1

set default
set stacksize 10000
set verify errors
set process nolimit
set lock record update

;-------------------------------------------------------------------------------

; obtain current costing "constants" and pass to subsequent requests
;
use $use/get_const_rec_7_values_globals.qts
use $use/get_const_rec_7_values.qts

;-------------------------------------------------------------------------------
request download_doc_1  on edit        errors report &
                        on calculation errors report
;
;-------------------------------------------------------------------------------

access constants-mstr-rec-6                             &
        link (current-ep-nbr - 1)                       &
        viaindex f112-ep-doc-nbr                        &
        to   ep-nbr of f112-pycdceilings                &
        link doc-nbr of f112-pycdceilings               &
        to   doc-nbr of f020-doctor-mstr		&
        link doc-nbr of f112-pycdceilings               &
        to 	 doc-nbr of f020-doctor-extra opt	&
        link doc-nbr of f112-pycdceilings,              &
             "TOTINC"                                   &
        to   doc-nbr,                                   &
             comp-code     of f119-doctor-ytd           &
                        alias f119-totinc       optional&
        link doc-nbr of f112-pycdceilings,              &
             "INCEXP"                                   &
        to   doc-nbr,                                   &
             comp-code     of f119-doctor-ytd           &
                        alias f119-incexp       optional&
        link doc-nbr of f112-pycdceilings,              &
             "PAYPOT"                                   &
        to   doc-nbr,                                   &
             comp-code     of f119-doctor-ytd           &
                        alias f119-paypot       optional&
        link doc-nbr of f112-pycdceilings,              &
             "GTYPEA"                                   &
        to   doc-nbr,                                   &
             comp-code    of f119-doctor-ytd            &
                        alias f119-gtypea       optional			

use $use/select_f020_active_for_costing_analysis_period.use

output tmp-pc-download-file add update
; constants master
   item current-ep-nbr  final	 current-ep-nbr  of constants-mstr-rec-6
   
; f020 doctor master
   item doc-nbr 	final	doc-nbr 	of f020-doctor-mstr
   item doc-name	final 	doc-name 	of f020-doctor-mstr
   item doc-inits	final	doc-inits	of f020-doctor-mstr		
   item doc-dept	final	doc-dept	of f020-doctor-mstr		
   item doc-ohip-nbr	final	doc-ohip-nbr	of f020-doctor-mstr		
   item doc-sin-nbr	final	doc-sin-nbr	of f020-doctor-mstr		
   item doc-clinic-nbr	final	doc-clinic-nbr	of f020-doctor-mstr		
   item doc-spec-cd	final	doc-spec-cd	of f020-doctor-mstr		
   item doc-yrly-ceiling-computed					    &
			final	doc-yrly-ceiling-computed of f020-doctor-mstr

; f020 doctor extra
   item doc-yrly-require-revenue					&
   			final	doc-yrly-require-revenue of f020-doctor-extra

; f112 paycode ceiling
    item doc-pay-code		final	doc-pay-code	 of f112-pycdceilings
    item doc-pay-sub-code	final   doc-pay-sub-code of f112-pycdceilings

; f119 doctor ytd
   item amt-ytd-totinc	final	amt-ytd of f119-totinc
   item amt-ytd-incexp	final	amt-ytd of f119-incexp
   item amt-ytd-paypot	final	amt-ytd of f119-paypot
   item amt-mtd-gtypea	final	amt-mtd of f119-gtypea

;-------------------------------------------------------------------------------
request download_doc_2 on edit        errors report &
                        on calculation errors report
;
;-------------------------------------------------------------------------------

access f110-compensation						&
	link 	 6     							&
	to	 const-rec-nbr of constants-mstr-rec-6			&
        link doc-nbr of f110-compensation				&
        to   doc-nbr of f020-doctor-mstr

select 								&
   if    ep-nbr of f110-compensation 				&
		= (current-ep-nbr of constants-mstr-rec-6 - 1) 	&
     and (   comp-code of f110-compensation = "CEIEXP"		&
          or comp-code of f110-compensation = "YTDCEX"		&
         )

use $use/select_f020_active_for_costing_analysis_period.use

output tmp-pc-download-file add update  alias tmp-ceiexp	&
  if comp-code of f110-compensation = "CEIEXP"
; f110 compensation
    item amt-gross-ceiexp       final   amt-gross of f110-compensation

output tmp-pc-download-file add update  alias tmp-ytdcex	&
  if comp-code of f110-compensation = "YTDCEX"
; f110 compensation
    item amt-net-ytdcex         final   amt-net   of f110-compensation

;-------------------------------------------------------------------------------
request download_doc_3 on edit        errors report &
                        on calculation errors report
;
;-------------------------------------------------------------------------------
access f119-doctor-ytd-history                                          &
        link     6                                                      &
        to       const-rec-nbr of constants-mstr-rec-6			&
        link doc-nbr of f119-doctor-ytd-history				&
        to   doc-nbr of f020-doctor-mstr

def x-last-ep-of-last-fiscal-year zoned*6 =                                  &
nconvert(ascii (  nconvert(ascii(current-ep-nbr of constants-mstr-rec-6)[1:4] &
                         )                                                   &
               - 1                                                           &
              )                                                              &
        + "13"								     &
        )

select 								&
   if    ep-nbr of f119-doctor-ytd-history 			&
		= x-last-ep-of-last-fiscal-year 		&
     and comp-code of f119-doctor-ytd-history  = "TOTINC"  

use $use/select_f020_active_for_costing_analysis_period.use

output tmp-pc-download-file add update  
; f119 history
    item amt-ytd-totinc-end-of-last-fiscal-year			&
		      final   amt-ytd of f119-doctor-ytd-history 

build $obj/utl0020a
