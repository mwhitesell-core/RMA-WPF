; Name   : PM27.QZS
; Purpose: Employee UTILIZATION REPORT
; 	   CREATE TEMPORARY FILE OF TIMESHEET SUMMARIES BY EMP-NUM
; 	   FOR THE EMPLOYEE UTILIZATION REPORT
;
; MODIFICATION HISTORY
; YY/MMM/DD  	By whom        	Why
; ??/???/??  	?.?           	-
; 92/02/28	M. CHAN		- SAF 1005
;				- ADD CAN CLEAR
;				- SET REPORT NOLIMIT


can clear
set default
set report nolimit

access ftimesht &
       link  tsht-emp to emp-num of femployees
define temp-start-date zoned*6 unsigned = parm &
	prompt "ENTER REPORTING PERIOD - start date (YYMMDD): "
define temp-end-date zoned*6 unsigned = parm &
        prompt "                          end  date (YYMMDD): "

select if (tsht-end >= temp-start-date and tsht-end <= temp-end-date)
define temp-l01 numeric*8 =     &
       tot if (function = "L01" and pwo-num = 0)  &
       else 0
define temp-l02 numeric*8 =     &
       tot if (function = "L02" and pwo-num = 0)  &
       else 0
define temp-l03 numeric*8 =     &
       tot if (function = "L03" and pwo-num = 0)  &
       else 0
define temp-l04 numeric*8 =     &
       tot if (function = "L04" and pwo-num = 0)  &
       else 0
define temp-l05 numeric*8 =     &
       tot if (function = "L05" and pwo-num = 0)  &
       else 0
define temp-l06 numeric*8 =     &
       tot if (function = "L06" and pwo-num = 0)  &
       else 0
define temp-pbill numeric*8 =     &
       tot if (pwo-num ne 0 and billable ne "*")  &
       else 0
define temp-pnon numeric*8 =     &
       tot if (pwo-num ne 0 and billable = "*")  &
       else 0
define temp-adm numeric*8 =     &
       tot if (pwo-num = 0 and matchpattern(function,"A@")) &
       else 0
define temp-mrk numeric*8 =     &
       tot if (pwo-num = 0 and  &
              (not matchpattern(function,"M11")) and &
              matchpattern(function,"M@")) &
       else 0
define temp-m11 numeric*8 =     &
       tot if (pwo-num = 0 and  &
              matchpattern(function,"M11")) &
       else 0
define temp-trn numeric*8 =     &
       tot if (pwo-num = 0 and matchpattern(function,"T@")) &
       else 0
define temp-total numeric*8 = tot
sort on tsht-emp
report summary tsht-emp temp-start-date temp-end-date name company group &
       temp-l01 subtotal temp-l02 subtotal temp-l03 subtotal &
       temp-l04 subtotal temp-l05 subtotal temp-l06 subtotal &
       temp-pbill subtotal temp-pnon subtotal temp-adm subtotal &
       temp-mrk   subtotal temp-m11 subtotal  temp-trn  subtotal &
       temp-total subtotal
set subfile at tsht-emp name temputilize  keep
build $pb_obj/pm27
  
