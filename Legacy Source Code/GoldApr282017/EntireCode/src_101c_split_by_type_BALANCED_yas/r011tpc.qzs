; doc: r011c.qzs
; doc: physician revenue analysis by doctor (summary report)
; doc: sort by clinic
; doc: run for: mumc diagnostics
;
;PROGRAM PURPOSE : REVENUE ANALYSIS BY DOCTOR (SUMMARY REPORT)
;                  R011.CB CONVERSTION TO POWERHOUSE R011C.QZS
;                  THIS IS THE FIRST PART OF 3 PROGRAMS APPEND
;                  R011C.TXT END OF R011A.TXT
;
;DATE       WHO       DESCRIPTION
;01/05/11   YASEMIN   copy from r011c.qzs
;                     access f050tp instead of f050
;
can clear
set rep nolimit
set page length 62 width 132
set rep device disc name r011tpc
set formfee
set noblank

access f050tp-doc-revenue-mstr                             &
  link docrevtp-doc-nbr                                    &
    to doc-nbr of f020-doctor-mstr   opt                   &
  link docrevtp-clinic-nbr                                 &
    to iconst-clinic-nbr-1-2 of iconst-mstr-rec   opt    

sorted on docrevtp-clinic-nbr


def t-amt-mtd zoned*8 signed =   &
                              (docrevtp-in-tech-amt-billed  (1)  +  &
                               docrevtp-out-tech-amt-billed (1)  +  &
                               docrevtp-in-tech-amt-adjusts (1)  +  &
                               docrevtp-out-tech-amt-adjusts(1))

def p-amt-mtd  zoned*8 signed =  &
                             (docrevtp-in-prof-amt-billed  (1) +  &
                              docrevtp-out-prof-amt-billed (1) +  &
                              docrevtp-in-prof-amt-adjusts (1) +  &
                              docrevtp-out-prof-amt-adjusts(1))

def t-svc-mtd zoned*8 signed =   &
                              (docrevtp-in-tech-nbr-svc (1)  +  &
                               docrevtp-out-tech-nbr-svc(1)  +  &
                               docrevtp-in-prof-nbr-svc (1)  +  &
                               docrevtp-out-prof-nbr-svc(1))

def total-amt-mtd zoned*8 signed = (t-amt-mtd + p-amt-mtd)

def t-amt-ytd zoned*8 signed =   &
                              (docrevtp-in-tech-amt-billed (2) +  &
                              docrevtp-out-tech-amt-billed (2) +  &
                              docrevtp-in-tech-amt-adjusts (2) +  &
                              docrevtp-out-tech-amt-adjusts(2))

def p-amt-ytd  zoned*8 signed =  &
                             (docrevtp-in-prof-amt-billed (2) +  &
                             docrevtp-out-prof-amt-billed (2) +  &
                             docrevtp-in-prof-amt-adjusts (2) +  &
                             docrevtp-out-prof-amt-adjusts(2))

def t-svc-ytd zoned*8 signed =   &
                              (docrevtp-in-tech-nbr-svc (2)  +  &
                               docrevtp-out-tech-nbr-svc(2)  +  &
                               docrevtp-in-prof-nbr-svc (2)  +  &
                               docrevtp-out-prof-nbr-svc(2))

def total-amt-ytd zoned*8 signed = (t-amt-ytd + p-amt-ytd)

page heading                                    &
   tab 01 "F050tp file"                         &
   tab 35 "Audit of f050TP-revenue-mstr:"       & 
   tab 77 sysdate                               &
 skip 2                                         &
   tab  16 "--------------- M T D -------------------" &
   tab  61 "--------------- Y T D -------------------" &
 skip 1                                         &
   tab  25 "Technical"                          &
   tab  37 "Professional"                       &
   tab  52 "       Total"                       &
   tab  70 "Technical"                          &
   tab  82 "Professional"                       &
   tab  96 "       Total"                       &
 skip 1                                         &
   tab  22 "Bill/Adjusts"                       &
   tab  37 "Bill/Adjusts"                       &
   tab  51 "      Amt/Svc"                      &
   tab  67 "Bill/Adjusts"                       &
   tab  82 "Bill/Adjusts"                       &
   tab  96 "      Amt/Svc"                      &
 skip 2

footing at docrevtp-clinic-nbr                                       &
        tab 01  docrevtp-clinic-nbr                                  &
        tab 05  iconst-date-period-end                               &
        tab 18  t-amt-mtd              subtotal pic "^^,^^^,^^^.^^ " &
        tab 35  p-amt-mtd              subtotal pic "^^,^^^,^^^.^^ " &
        tab 52  total-amt-mtd          subtotal pic "^^,^^^,^^^.^^ " &
        tab 69  t-amt-ytd              subtotal pic "^^,^^^,^^^.^^ " &
        tab 86  p-amt-ytd              subtotal pic "^^,^^^,^^^.^^ " &
        tab 103 total-amt-ytd          subtotal pic "^^,^^^,^^^.^^ " &
skip 1  tab 52  t-svc-mtd              subtotal pic "^^,^^^,^^^"     &
        tab 103 t-svc-ytd              subtotal pic "^^,^^^,^^^"     &
skip 2


build $pb_obj/r011tpc

  
