; DOC: BILLINGLIST.QZS
; DOC: CREATE BILLING LISTING REPORT
; DOC: RUN FOR: MANAGEMENT/STAFF
;
;PROGRAM PURPOSE : BILLING LISTING REPORT
;                 *** IF YOU ADD PROMT CHAGE ALL COROSPONDING .CLI"S ***
;
;DATE       WHO       DESCRIPTION
;92/03/16   YASEMIN   ORIGINAL
;94/JUL/26  B.E.      Pgm access F112 with no OPTIONAL field and without regard
;		      to which EP-NBR record it would pickup.  When a doctor
;		      had fo F112 records they didn"t appear in report.
;		      Since no F112 info appeared in report the access
;		      was commented out.
;		      NOTE: If F112 info needed in the future make sure
;		      to access Constants Mstr to pickup CURRENT-ER-NBR
;		      to obtain"s the doctors current F112 record.
;95/09/13    YAS      ADD TERM PROMPT
;

can clear
set rep nolimit
set rep device disc name billinglist
set page length 60 width 80
set formfeed

access f020-doctor-mstr 		; &
;  LINK DOC-NBR TO DOC-NBR OF F112-PYCDCEILINGS   OPT

;y2k
define term-from  date = parm prompt      &
 'ENTER TERM  FROM DATE (YYYYMMDD) OR "0" FOR ALL: '

define x-clinic zoned*2 unsigned = parm prompt   &
       'SELECT CLINIC "22" OR "60 TO 65" OR "0" FOR ALL: '

define x-dept zoned*2 unsigned = parm prompt       &
       'SELECT DEPT FROM 1 TO 14 OR "0" FOR ALL: '

define x-class zoned*1 unsigned  = parm prompt   &
'SELECT "1" FULL OR "2" PART OR "3" CS OR "4" PS OR "0" FOR ALL: '

select if (    (x-clinic = doc-clinic-nbr or x-clinic = 0)	&
           and (x-dept   = doc-dept       or x-dept = 0)	&
           and (   (x-class = 1 and doc-full-part-ind = "F")	&
		or (x-class = 2 and doc-full-part-ind = "P")	&
       		or (x-class = 3 and doc-full-part-ind = "C")	&
       		or (x-class = 4 and doc-full-part-ind = "S")	&
       		or (x-class = 0)				&
	       )						&
           and (   doc-date-fac-term ge term-from               &
                or term-from = 0                                &
                or doc-date-fac-term = 0 )                      &
	  )

define x-name cha*24 = pack(doc-name+" "+doc-inits)

sort on doc-clinic-nbr

page heading                                               &
	tab 1  "RUN DATE:"                                 &
	tab 11  sysdate                                    &
        tab 36 "BILLING LISTING"                           &
        tab 71 "PAGE:" tab 76 syspage pic "^^"             &
skip 2                                                     &
        tab 1   "CL#"                                      &
        tab 5   "DEPT"                                     &
        tab 11  "DR#"                                      &
        tab 16  "PHYSICIAN NAME"                           &
        tab 40  "OHIP #"                                   &
        tab 48  "SPEC"                                     &
        tab 54  "CLASS"                                    &
        tab 61  "START"                                    &
        tab 71  "TERM"                                     &
skip 1                                                     &
        tab 16 "SUB-SPECIALTY"                             &
        tab 48 "CODES"                                     &
        tab 61 "DATE"                                      &
        tab 71 "DATE"                                      &
skip 1                                                     &
        tab 16 "ADDITIONAL CLINICS"                        &
skip 2

;HEADING AT DOC-CLINIC-NBR SKIP 2    &
; TAB 40  "CLINIC" TAB 47 DOC-CLINIC-NBR SKIP 2

report            &
  tab 1   doc-clinic-nbr                                          &
  tab 6   doc-dept                                                &
  tab 11  doc-nbr fill "0"                                        &
  tab 16  x-name                                                  &
  tab 40  doc-ohip-nbr                                            &
  tab 49  doc-spec-cd                                             &
  tab 56  doc-full-part-ind                                       &
;y2k
  tab 61  doc-date-fac-start pic "^^^^^^^^"                       &
;y2k
  tab 71  doc-date-fac-term  pic "^^^^^^^^" bwz                   &
skip 1                                                            &
  tab 16  doc-sub-specialty                                       &
  tab 49  doc-spec-cd-2 bwz                                       &
skip 1                                                            &
  tab 16  doc-clinic-nbr-2 bwz                                    &
  tab 19  doc-clinic-nbr-3 bwz                                    &
  tab 22  doc-clinic-nbr-4 bwz                                    &
  tab 25  doc-clinic-nbr-5 bwz                                    &
  tab 28  doc-clinic-nbr-6 bwz                                    &
skip 2

footing at doc-clinic-nbr skip page

build $pb_obj/billinglist


  
