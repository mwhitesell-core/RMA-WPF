;#> PROGRAM-ID. R099.qzs
;
;       ((C)) Dyad Technologies
;
;    PROGRAM PURPOSE :  PRODUCE AN AUDIT REPORT SUBTOTALING
;                       ALL PATIENTS DELETED AND RETAIN AFTER
;                       PATIENT PURGE
;
;    MODIFICATION HISTORY
;        DATE   WHO       DESCRIPTION
;   2000/07/27  BANNIS    ORIGIONAL
;   2016/11/29  MC1	  change the picture in the last request (step d)

cancel clear
set report nolimit

access *u099-delete-patients

define d-delete-count num = 1
define d-retain-count num = 0
define d-total num = 0

sort on d-delete-count

set subfile name u099-purge-validate keep at d-delete-count
rep summary d-delete-count subtotal     &
            d-retain-count subtotal     &
            d-total subtotal

build $obj/r099a


access *u099-retain-patients

define d-delete-count num = 0
define d-retain-count num = 1
define d-total num = 0

sort on d-retain-count

set subfile name u099-purge-validate append at d-retain-count
rep summary d-delete-count subtotal     &
            d-retain-count subtotal     &
            d-total subtotal

build $obj/r099b


access f010-pat-mstr

define d-delete-count num = 0
define d-retain-count num = 0
define d-total num = 1

sort on d-total

set subfile name u099-purge-validate append at d-total
rep summary d-delete-count subtotal     &
            d-retain-count subtotal     &
            d-total subtotal

build $obj/r099c


access *u099-purge-validate

define d-sort-item num = 1

sort on d-sort-item

page heading                                            &
        tab   1 "RU099"                                 &
        tab  40 "PATIENT PURGE AUDIT REPORT"            &
        tab 100 "RUN DATE"                              &
        tab 109 sysdate                                 &
        tab 124 "PAGE"                                  &
        tab 129 syspage pic "^^^"   			&
skip 2							&
 	tab  30 "TOTAL"					&
	tab  55 "DELETED"				&
	tab  80 "RETAINED"				&
skip 1							&
	tab  30 "-----"					&
	tab  55 "-------"				&
	tab  80 "--------"
 
footing at d-sort-item					&
        tab   1 "PATIENTS"                              &
; MC1
;  	tab  28 d-total	subtotal pic "^^^,^^^"		&
   	tab  28 d-total	subtotal pic "^,^^^,^^^"	&
; MC1 - end
        tab  55 d-delete-count subtotal pic "^^^,^^^"	&
        tab  81 d-retain-count subtotal pic "^^^,^^^"	

set page width 132
set formfeed
set rep dev disc name ru099

build $obj/r099d
	
