; DOC: ADDRLABELS.QZS
; DOC: CREATE MAILING DOCTOR ADDRESS LABELS
; DOC: RUN FOR: MANAGEMENT/STAFF
;
;PROGRAM PURPOSE : MAILING DOCTOR ADDRESS LABELS
;
;DATE        WHO       DESCRIPTION
;92/mar/18   YASEMIN   ORIGINAL
;98/aug/12   B.E.      - conversion to unix, changed to "set noformfeed"
;01/feb/21   yas       - add check all 5 additional clinics when selecting
;                        a clinic. added 5 def statements.  
;03/Mar/20   yas       - don't print labels for dept 26 and 27
;04/jan/06   M.C.      - alpha doc nbr
;05/jul/26   b.e.      - acccess new f027/f028 files for doc info
;10/feb/09   yas.      - change x-clinic to >= 60 and <=66        

can clear
set default
set rep nolimit
set rep device disc name addrlabels
set page length 6 width 40
set nohead
set noformfeed

access f020-doctor-mstr                                         &
        link doc-nbr                                            &
          to doc-nbr of f020-doctor-extra       opt             &
        link (" ",                                              &
             doc-nbr,                                           &
             "D")                                               &
          to filler,                                            &
             doc-nbr,                                           &
             contacts-type of f027-contacts-mstr                &
                                alias f027-doc  opt             &
        link (" ",                                              &
            doc-nbr,                                            &
            "D",                                                &
            "H" )                                               &
          to filler,                                            &
            doc-nbr,                                            &
            contacts-type,                                      &
            contacts-location of f028-contacts-info-mstr        &
                                alias f028-doc-home      opt    &
        link (" ",                                              &
            doc-nbr,                                            &
            "D",                                                &
            "O")                                                &
          to filler,                                            &
            doc-nbr,                                            &
            contacts-type,                                      &
            contacts-location of f028-contacts-info-mstr        &
                                alias f028-doc-office    opt        

define x-addr zoned*1 unsigned = parm prompt            &
   	'Select office addrs "1" or home addrs "2": '

define x-clinic zoned*2 unsigned = parm prompt          &
 'Select clinic "22" or "60" or OR "80" OR "81" OR "82" OR "0" For all: '

define x-dept zoned*2 unsigned = parm prompt            &
       'Select dept from 1 to 14 or "0" for all: '

define x-class zoned*1 unsigned  = parm prompt          &
'Select "1" FULL or "2" PART or "3" CS or "4" PS or "0" For all: '

define x-name cha*30 = pack("DR. " + doc-inits + " " + doc-name)

; 2004/01/06 - MC
;!define x-docdept char*6 = pack(ascii(doc-nbr,3) + "/" + ascii(doc-dept,2))
define x-docdept char*6 = pack(doc-nbr + "/" + ascii(doc-dept,2))
; 2004/01/06 - end

;define x-addr-1 char*30 = doc-addr-office-1 if x-addr= 1	 &
;	else doc-addr-home-1 if x-addr = 2
;define x-addr-2 char*30 = doc-addr-office-2 if x-addr= 1	 &
;	else doc-addr-home-2 if x-addr = 2
;define x-addr-3 char*30 = doc-addr-office-3 if x-addr= 1	 &
;	else doc-addr-home-3 if x-addr = 2
;define x-addr-pc-1 char*6  = doc-addr-office-pc-123 if x-addr= 1 &
;	else doc-addr-home-pc-123 if x-addr = 2
;define x-addr-pc-2 char*6  = doc-addr-office-pc-456 if x-addr= 1 &
;	else doc-addr-home-pc-456 if x-addr = 2
def x-addr-1 char*30						&
     = contacts-addr-1 of f028-doc-office 	if x-addr = 1	&
  else contacts-addr-1 of f028-doc-home  
def x-addr-2 char*30						&
     = contacts-addr-2 of f028-doc-office 	if x-addr = 1	&
  else contacts-addr-2 of f028-doc-home  
def x-addr-3 char*30						&
     = contacts-addr-3 of f028-doc-office 	if x-addr = 1	&
  else contacts-addr-3 of f028-doc-home  
def x-addr-pc-1 char*30						&
     = postal-code of f028-doc-office[1:3] 	if x-addr = 1	&
  else postal-code of f028-doc-home  [1:3] 
def x-addr-pc-2 char*30						&
     = postal-code of f028-doc-office[4:3] 	if x-addr = 1	&
  else postal-code of f028-doc-home  [4:3]

define x-clinic-22 num*2 = doc-clinic-nbr if doc-clinic-nbr = 22

define x-clinic-60 num*2 = doc-clinic-nbr   if (     doc-clinic-nbr   >= 60    &
                                                 and doc-clinic-nbr   <= 66)   &
                      else doc-clinic-nbr-2 if (     doc-clinic-nbr-2 >=60     &
                                                 and doc-clinic-nbr-2 <= 66)   &
                      else doc-clinic-nbr-3 if (     doc-clinic-nbr-3 >=60     &
                                                 and doc-clinic-nbr-3 <= 66)   &
                      else doc-clinic-nbr-4 if (     doc-clinic-nbr-4 >=60     &
                                                 and doc-clinic-nbr-4 <= 66)   &
                      else doc-clinic-nbr-5 if (     doc-clinic-nbr-5 >=60     &
                                                 and doc-clinic-nbr-5 <= 66)   &
                      else doc-clinic-nbr-6 if (     doc-clinic-nbr-6 >=60     &
                                                 and doc-clinic-nbr-6 <= 66)   
           
define x-clinic-80 num*2 = doc-clinic-nbr   if doc-clinic-nbr   = 80 &
                      else doc-clinic-nbr-2 if doc-clinic-nbr-2 = 80 &
                      else doc-clinic-nbr-3 if doc-clinic-nbr-3 = 80 &
                      else doc-clinic-nbr-4 if doc-clinic-nbr-4 = 80 &
                      else doc-clinic-nbr-5 if doc-clinic-nbr-5 = 80 &
                      else doc-clinic-nbr-6 if doc-clinic-nbr-6 = 80 

define x-clinic-81 num*2 = doc-clinic-nbr   if doc-clinic-nbr   = 81 &
                      else doc-clinic-nbr-2 if doc-clinic-nbr-2 = 81 &
                      else doc-clinic-nbr-3 if doc-clinic-nbr-3 = 81 &
                      else doc-clinic-nbr-4 if doc-clinic-nbr-4 = 81 &
                      else doc-clinic-nbr-5 if doc-clinic-nbr-5 = 81 &
                      else doc-clinic-nbr-6 if doc-clinic-nbr-6 = 81 

define x-clinic-82 num*2 = doc-clinic-nbr   if doc-clinic-nbr   = 82 &
                      else doc-clinic-nbr-2 if doc-clinic-nbr-2 = 82 &
                      else doc-clinic-nbr-3 if doc-clinic-nbr-3 = 82 &
                      else doc-clinic-nbr-4 if doc-clinic-nbr-4 = 82 &
                      else doc-clinic-nbr-5 if doc-clinic-nbr-5 = 82 &
                      else doc-clinic-nbr-6 if doc-clinic-nbr-6 = 82 

select if ( (        x-clinic = x-clinic-22                                  &
                 or  x-clinic = x-clinic-60                                  &
                 or  x-clinic = x-clinic-80                                  &
                 or  x-clinic = x-clinic-81                                  &
                 or  x-clinic = x-clinic-82                                  &
                 or  x-clinic = 0                                            &
                 or (x-clinic = 60 and (x-clinic-60 >= 60))                  &  
            )                                                                & 
;;;;;;and  (x-dept  = doc-dept or x-dept = 0)                                &
      and  (      (x-dept  = doc-dept or x-dept = 0)                         &
              and (    doc-dept ne 10                                        &
                   and doc-dept ne 13                                        &
                   and doc-dept ne 21                                        &
                   and doc-dept ne 22                                        &
                   and doc-dept ne 23                                        &
                   and doc-dept ne 25                                        &
                   and doc-dept ne 26                                        &
                   and doc-dept ne 27                                        &
                   and doc-dept ne 28                                        &
                   and doc-dept ne 30                                        &
                  )                                                          &
           )                                                                 &
;;;;;;and  (doc-date-fac-term >= 20020101 and doc-date-fac-term <= 20021231) &
      and  (doc-date-fac-term = 00000000 or doc-date-fac-term >= sysdate)    &
      and  ((x-class = 1 and doc-full-part-ind = "F")	                     &
       or   (x-class = 2 and doc-full-part-ind = "P")	                     &
       or   (x-class = 3 and doc-full-part-ind = "C")	                     &
       or   (x-class = 4 and doc-full-part-ind = "S")	                     &
       or   (x-class = 0)))


sort on doc-clinic-nbr

report            						  &
  tab 1   x-docdept                 skip                          &
  tab 1   x-name                    skip                          &
  tab 1   x-addr-1       	    skip                          &
  tab 1   x-addr-2                  skip                          &
  tab 1   x-addr-3                  skip                          &
  tab 1   x-addr-pc-1                                             &
  tab 5   x-addr-pc-2               skip page


;footing at doc-clinic-nbr skip page

build $pb_obj/addrlabels


  
