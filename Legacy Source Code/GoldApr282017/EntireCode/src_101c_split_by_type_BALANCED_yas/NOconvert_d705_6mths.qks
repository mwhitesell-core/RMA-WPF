;#> PROGRAM-ID.     D705.QKS
;
;	((C)) Dyad Technologies
;
;    PROGRAM PURPOSE : TO ALLOW CORRECTIONS TO SUSPENDED CLAIM HEADER/DETAIL/ADDRESS RECS
;
;    MODIFICATION HISTORY
;        DATE   WHO          DESCRIPTION
;     90/JUN/13 D.B.         - ORIGINAL
;     90/OCT/02 B.E.         - ADDED OHIP CODE/SUFFIX, ADJ CD/SUB-TYPE FIELDS TO SCREEN
;     90/OCT/22 D.B.         - ADDED EDITS
;     91/FEB/15 B.E.         - ADDED 'ADD' DESIGNER RTN TO ACCESS SUSPENDED ADDRESS RECORDS
;     91/OCT/29 M.C.	     - CORRECT THE ACCESS STMT IN F091-DIAG-CODE
;     94/MAR/28 M.C.	     - CHANGE TO WARNING FROM ERROR WHEN THE
;			       CLMHDR-ACRONYM AND PAT-ACRONYM ARE
;			       DIFFERENT
;     97/NOV/14 B.E.	     - ADDED CLMHDR-MANUAL-REVIEW
;			       F002-SUSPEND-HDR RECORD AND ALLOWED FIELD
;			       TO BE UPDATED ON THIS SCREEN
;

can clear

screen $pb_obj/d705 activities find, change

file f002-suspend-hdr

file f020-doctor-mstr reference
	access viaindex doc-ohip-nbr using clmhdr-doc-nbr-ohip

file f091-diag-codes-mstr reference
;	ACCESS VIAINDEX DIAG-CD USING ASCII(CLMHDR-DIAG-CD,3)
	access viaindex diag-cd using clmhdr-diag-cd-alpha

file f094-msg-mstr reference

file f010-pat-mstr reference

temp w-clmhdr-doc-ohip-nbr
temp w-clmhdr-accounting-nbr char*8

title 'SUSPENDED **HEADER** MAINTENANCE' at 1,48
skip 1

align ( 1, 4, 29 )
field clmhdr-doc-ohip-nbr of f002-suspend-hdr required nochange   &
	pic '^^^^^^' label 'DOCTOR OHIP NBR'
align ( 1, 4, 29 ) (41, 44, 59) (,62,64)
field clmhdr-accounting-nbr of f002-suspend-hdr required nochange &
      id same        label 'ACCOUNTING NBR'
; B.E. REACTIVATED BATCH-NBR TO OBTAIN THE CLINIC NBR
;FIELD CLMHDR-BATCH-NBR OF F002-SUSPEND-HDR
field clmhdr-clinic-nbr-1-2 of f002-suspend-hdr label "CLINIC/DOCTOR:"
field clmhdr-doc-nbr        of f002-suspend-hdr label "/"

align ( 1, 4, 29 )
skip 1
;FIELD CLMHDR-CLAIM-NBR OF F002-SUSPEND-HDR
;FIELD CLMHDR-ADJ-OMA-CD OF F002-SUSPEND-HDR
;FIELD CLMHDR-ADJ-OMA-SUFF OF F002-SUSPEND-HDR
;FIELD CLMHDR-ADJ-ADJ-NBRF OF F002-SUSPEND-HDR
;FIELD CLMHDR-BATCH-TYPE OF F002-SUSPEND-HDR
;FIELD CLMHDR-ADJ-CD OF F002-SUSPEND-HDR           &
;	LABEL 'ADJ. CODE'
;FIELD CLMHDR-ADJ-CD-SUB-TYPE OF F002-SUSPEND-HDR &
;	LABEL 'CLAIM SOURCE "D"ISK'
;FIELD CLMHDR-DOC-NBR-OHIP OF F002-SUSPEND-HDR
field clmhdr-doc-spec-cd of f002-suspend-hdr			&
	label 'DOCTOR SPECIALITY CODE' lookup on f020-doctor-mstr
field clmhdr-refer-doc-nbr of f002-suspend-hdr pic '^^^^^^'	&
	label 'REFERRING PHYSICIAN NBR'
; 10 DIGIT CHECK MODE IS NOT DONE
field clmhdr-diag-cd-alpha of f002-suspend-hdr			&
	label 'DIAGNOSTIC CODE'					&
	lookup on f091-diag-codes-mstr
field clmhdr-loc of f002-suspend-hdr				&
	label 'LOCATION'
field clmhdr-hosp of f002-suspend-hdr				&
	label 'HOSPITAL'					&
	value 'B', 'C', 'D', 'G', 'H', 'J', 'L', 'M', 'N',      &
              'O', 'R', 'S', 'W', 'Z'
field clmhdr-agent-cd-alpha of f002-suspend-hdr			&
	label 'AGENT CODE'					&
	value '0', '1', '4', '6', '7', '8', '9'
field clmhdr-tape-submit-ind of f002-suspend-hdr		&
	label 'TAPE SUBMIT INDICATOR' value 'Y', 'N'
field clmhdr-i-o-pat-ind of f002-suspend-hdr			&
	label 'PATIENT I/O INDICATOR' values 'I', 'O'
field clmhdr-date-admit of f002-suspend-hdr			&
	label 'DATE ADMIT'
skip 1
field clmhdr-pat-key-type of f002-suspend-hdr			&
	label 'PATIENT KEY TYPE' value 'I'
field clmhdr-pat-key-data of f002-suspend-hdr			&
	label '            DATA' id same
field clmhdr-pat-acronym of f002-suspend-hdr			&
	label '         ACRONYM' id same
field clmhdr-health-care-prov of f002-suspend-hdr		&
	label '   PROVINCE CODE' id same
;FIELD CLMHDR-REFERENCE OF F002-SUSPEND-HDR
skip to 6
align (41,44,69)
field clmhdr-doc-dept-alpha of f002-suspend-hdr     display	&
	label 'DOCTOR DEPARTMENT'
field clmhdr-msg-nbr of f002-suspend-hdr &;   IF CLMHDR-AGENT-CD = 0 &
	label 'MSG NBR'
field clmhdr-reprint-flag of f002-suspend-hdr &; IF CLMHDR-AGENT-CD =0 &
	label 'REPRINT FLAG' value 'Y', 'N'
field clmhdr-sub-nbr of f002-suspend-hdr &;   IF CLMHDR-AGENT-CD = 0 &
	label 'SUB NBR' value '0' to '9'
field clmhdr-auto-logout of f002-suspend-hdr &;IF CLMHDR-AGENT-CD = 0 &
	label 'AUTO LOGOUT' value 'Y', 'N'
field clmhdr-fee-complex of f002-suspend-hdr &;IF CLMHDR-AGENT-CD = 0 &
	label 'FEE COMPLEX' value 'Y', 'N'
;FIELD CLMHDR-CURR-PAYMENT OF F002-SUSPEND-HDR
;FIELD CLMHDR-DATE-PERIOD-END OF F002-SUSPEND-HDR
;FIELD CLMHDR-CYCLE-NBR OF F002-SUSPEND-HDR
field clmhdr-date-sys of f002-suspend-hdr     display		&
	label 'SYSTEM DATE'
;FIELD CLMHDR-AMT-TECH-BILLED OF F002-SUSPEND-HDR		&
;LABEL 'AMT TECH BILLED'
;FIELD CLMHDR-AMT-TECH-PAID OF F002-SUSPEND-HDR
field clmhdr-tot-claim-ar-oma of f002-suspend-hdr		&
	label 'TOT CLAIM AR OMA'
field clmhdr-tot-claim-ar-ohip of f002-suspend-hdr		&
	label 'TOT CLAIM AR OHIP'
;FIELD CLMHDR-MANUAL-AND-TAPE-PAYMENTS OF F002-SUSPEND-HDR
;FIELD CLMHDR-STATUS-OHIP OF F002-SUSPEND-HDR
;FIELD CLMHDR-ORIG-BATCH-NBR OF F002-SUSPEND-HDR
;FIELD CLMHDR-ORIG-CLAIM-NBR OF F002-SUSPEND-HDR

skip 1
field clmhdr-manual-review  of f002-suspend-hdr
skip 1
field clmhdr-status of f002-suspend-hdr				&
	label 'RECORD STATUS' value "D", " "			&
	help "Value can be 'D'elete or ' ' after fixing 'X' claims"

procedure edit clmhdr-doc-spec-cd
begin
  get f020-doctor-mstr viaindex doc-ohip-nbr using clmhdr-doc-nbr-ohip
    if fieldvalue <> doc-spec-cd
    then begin
      error "WRONG SPECIALTY CODE"
      end
  end
 
procedure edit clmhdr-loc
begin
  get f020-doctor-mstr viaindex doc-ohip-nbr using clmhdr-doc-nbr-ohip
    if fieldtext <> doc-loc-1 and fieldtext <> doc-loc-2 and	&
       fieldtext <> doc-loc-3 and fieldtext <> doc-loc-4 and	&
       fieldtext <> doc-loc-5 and fieldtext <> doc-loc-6 and	&
       fieldtext <> doc-loc-7 and fieldtext <> doc-loc-8 and	&
       fieldtext <> doc-loc-9 and fieldtext <> doc-loc-10 and	&
       fieldtext <> doc-loc-11 and fieldtext <> doc-loc-12 and	&
       fieldtext <> doc-loc-13 and fieldtext <> doc-loc-14 and	&
       fieldtext <> doc-loc-15 and fieldtext <> doc-loc-16 and	&
       fieldtext <> doc-loc-17 and fieldtext <> doc-loc-18 and	&
       fieldtext <> doc-loc-19 and fieldtext <> doc-loc-20 and	&
       fieldtext <> doc-loc-21 and fieldtext <> doc-loc-22 and	&
       fieldtext <> doc-loc-23 and fieldtext <> doc-loc-24 and	&
       fieldtext <> doc-loc-25 and fieldtext <> doc-loc-26 and	&
       fieldtext <> doc-loc-27 and fieldtext <> doc-loc-28 and	&
       fieldtext <> doc-loc-29 and fieldtext <> doc-loc-30
     then error "WRONG LOCATION CODE"
  end
 
procedure edit clmhdr-hosp
begin
    if clmhdr-hosp <> clmhdr-loc[1:1]
      then error "WRONG HOSPITAL CODE WITH LOCATION"
 end

procedure input clmhdr-msg-nbr
begin
  if clmhdr-agent-cd = 0 and 0 <> size(fieldtext)
    then error "CANNOT CHANGE MSG NBR FOR OHIP AGENT"
 end

procedure edit clmhdr-msg-nbr
begin
  if clmhdr-agent-cd <> 0
    then begin
      get f094-msg-mstr viaindex msg-sub-key using clmhdr-msg-nbr
      if not accessok
        then error "INVALID MSG NUMBER"
      end
  end

procedure input clmhdr-reprint-flag
begin
  if clmhdr-agent-cd = 0 and 0 <> size(fieldtext)
    then error "CANNOT CHANGE REPRINT FLAG FOR OHIP AGENT"
 end

procedure input clmhdr-sub-nbr
begin
  if clmhdr-agent-cd = 0 and 0 <> size(fieldtext)
    then error "CANNOT CHANGE SUB NBR FOR OHIP AGENT"
 end

procedure input clmhdr-auto-logout
begin
  if clmhdr-agent-cd = 0 and 0 <> size(fieldtext)
    then error "CANNOT CHANGE AUTO LOGOUT FOR OHIP AGENT"
 end

procedure input clmhdr-fee-complex
begin
  if clmhdr-agent-cd = 0 and 0 <> size(fieldtext)
    then error "CANNOT CHANGE FEE COMPLEX FOR OHIP AGENT"
 end

procedure edit clmhdr-pat-key-data
begin
  get f010-pat-mstr viaindex key-pat-mstr	&
	using (clmhdr-pat-key-type + clmhdr-pat-key-data)
    if clmhdr-pat-acronym <> pat-acronym
;     THEN ERROR "CLMHDR AND PATIENT ACRONYM ARE DIFFERENT"
      then warning "CLMHDR AND PATIENT ACRONYM ARE DIFFERENT"
   end
 
procedure designer dtl
begin
  let w-clmhdr-doc-ohip-nbr = clmhdr-doc-ohip-nbr
  let w-clmhdr-accounting-nbr = clmhdr-accounting-nbr
  run screen $pb_obj/d705a passing w-clmhdr-doc-ohip-nbr,	&
 	w-clmhdr-accounting-nbr mode f
  end

procedure designer add
begin
  let w-clmhdr-doc-ohip-nbr = clmhdr-doc-ohip-nbr
  let w-clmhdr-accounting-nbr = clmhdr-accounting-nbr
  run screen $pb_obj/d705b passing w-clmhdr-doc-ohip-nbr,	&
 	w-clmhdr-accounting-nbr mode f
  end

build
