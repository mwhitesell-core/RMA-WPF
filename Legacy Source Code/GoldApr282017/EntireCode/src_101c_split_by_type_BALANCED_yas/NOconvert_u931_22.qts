; Program: u931_22.qts
; Purpose: Read claims_history_subfile, explode repetitive claims into single
;	   records and update the icu-app-file file  
;
; 95/06/14 	Y.B.	- COPY FROM U931 CHANGE TO RUN FOR CLINIC 22
;		
; 1999/May/13 	S.B.	- This is the Y2K version.  The NON Y2K version
;			  is 'u931_22_non_y2k.qts'.

cancel clear
set process limit 200000

run u931_22

request explode_claims

access  claims_history_subfile  alias claims_subfile

def x-oma-cd  char*4 = clmdtl-id[12:4]

def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"

def x-clmdtl-sv-nbr-1 zoned*2 unsigned =			&
			nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
			if consec-flag = "Y"			&
			else 0

def x-clmdtl-sv-nbr-2 zoned*2 unsigned = 			&
			nconvert(ascii(clmdtl-consec-dates,9)[4:1])  &
			if consec-flag = "Y"			&
			else 0

def x-clmdtl-sv-nbr-3 zoned*2 unsigned = 			&
			nconvert(ascii(clmdtl-consec-dates,9)[7:1])  &
			if consec-flag = "Y"			&
			else 0

def x-tot-serv = clmdtl-nbr-serv + x-clmdtl-sv-nbr-1  +   &
		 x-clmdtl-sv-nbr-2 + x-clmdtl-sv-nbr-3

def x-serv-fee = clmdtl-fee-ohip / x-tot-serv

def x-fee-ohip   float = x-serv-fee * clmdtl-nbr-serv
def x-fee-ohip-1 float = x-serv-fee * x-clmdtl-sv-nbr-1
def x-fee-ohip-2 float = x-serv-fee * x-clmdtl-sv-nbr-2
def x-fee-ohip-3 float = x-serv-fee * x-clmdtl-sv-nbr-3

def x-clmdtl-sv-day-1 = nconvert(ascii(clmdtl-consec-dates,9)[2:2]) &
			if consec-flag = "Y"			&
			else 0
def x-clmdtl-sv-day-2 = nconvert(ascii(clmdtl-consec-dates,9)[5:2]) &
			if consec-flag = "Y"			&
			else 0
def x-clmdtl-sv-day-3 = nconvert(ascii(clmdtl-consec-dates,9)[8:2]) &
			if consec-flag = "Y"			&
			else 0

;def x-serv-date-1 char*6 = clmdtl-sv-date[1:4] +	&
;			ascii(clmdtl-consec-dates,9)[2:2]
;def x-serv-date-2 char*6 = clmdtl-sv-date[1:4] +	&
;		 	ascii(clmdtl-consec-dates,9)[5:2]
;def x-serv-date-3 char*6 = clmdtl-sv-date[1:4] +	&
;			ascii(clmdtl-consec-dates,9)[8:2]
def x-serv-date-1 char*8 = clmdtl-sv-date[1:6] +	&
			ascii(clmdtl-consec-dates,9)[2:2]
def x-serv-date-2 char*8 = clmdtl-sv-date[1:6] +	&
			ascii(clmdtl-consec-dates,9)[5:2]
def x-serv-date-3 char*8 = clmdtl-sv-date[1:6] +	&
			ascii(clmdtl-consec-dates,9)[8:2]

def x-doc-nbr zoned*3 unsigned = nconvert(clmdtl-id[4:3])

 sel if clmdtl-id[1:2] = '22'

subfile icuapp keep include				&
	x-doc-nbr,					&
	clmhdr-doc-dept of claims_subfile,		&
	x-oma-cd,					&
	clmdtl-sv-date of claims_subfile,		&
	clmdtl-nbr-serv of claims_subfile,		&
	x-fee-ohip,					&
	clmhdr-pat-ohip-id-or-chart of claims_subfile

subfile icuapp alias consec-1 keep          		&
	if x-clmdtl-sv-day-1  <> 0 include		&
	x-doc-nbr,					&
	clmhdr-doc-dept of claims_subfile,		&
	x-oma-cd,					&
	x-serv-date-1,   				&
	x-clmdtl-sv-nbr-1,				&
	x-fee-ohip-1,					&
	clmhdr-pat-ohip-id-or-chart of claims_subfile

subfile icuapp alias consec-2 keep          		&
	if x-clmdtl-sv-day-2  <> 0 include		&
	x-doc-nbr,					&
	clmhdr-doc-dept of claims_subfile,		&
	x-oma-cd,					&
	x-serv-date-2,   				&
	x-clmdtl-sv-nbr-2,				&
	x-fee-ohip-2,					&
	clmhdr-pat-ohip-id-or-chart of claims_subfile

subfile icuapp alias consec-3 keep          		&
	if x-clmdtl-sv-day-3  <> 0 include		&
	x-doc-nbr,					&
	clmhdr-doc-dept of claims_subfile,		&
	x-oma-cd,					&
	x-serv-date-3,    				&
	x-clmdtl-sv-nbr-3,				&
	x-fee-ohip-3,					&
	clmhdr-pat-ohip-id-or-chart of claims_subfile

request nonexplode_consec_days

access *icuapp link x-oma-cd to fee-oma-cd of f040-oma-fee-mstr opt

sel if fee-special-m-suffix-ind = 'Y' or clmdtl-nbr-serv of icuapp  = 1

output icu-app-file add
   item doc-nbr final x-doc-nbr
   item clmhdr-doc-dept final clmhdr-doc-dept of icuapp
   item fee-oma-cd final x-oma-cd
   item clmdtl-sv-date final clmdtl-sv-date of icuapp
   item clmdtl-nbr-serv final clmdtl-nbr-serv of icuapp
   item clmdtl-fee-ohip final x-fee-ohip
   item clmhdr-pat-ohip-id-or-chart final clmhdr-pat-ohip-id-or-chart of icuapp


request explode_consec_days

access *icuapp link x-oma-cd to fee-oma-cd of f040-oma-fee-mstr opt

sel if fee-special-m-suffix-ind <> 'Y' and clmdtl-nbr-serv of icuapp > 1

output icu-app-file-explode add
   item doc-nbr final x-doc-nbr
   item clmhdr-doc-dept final clmhdr-doc-dept of icuapp
   item fee-oma-cd final x-oma-cd
   item clmdtl-sv-date final clmdtl-sv-date of icuapp
   item clmdtl-nbr-serv final clmdtl-nbr-serv of icuapp
   item clmdtl-fee-ohip final x-fee-ohip
   item clmhdr-pat-ohip-id-or-chart final clmhdr-pat-ohip-id-or-chart of icuapp

build $pb_obj/u931_22
  
