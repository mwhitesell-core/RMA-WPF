; DOC: R051CA.QZS
; DOC: PHYSICIAN REVENUE ANALYSIS
; DOC: SORT BY CLINIC/DEPT/CLASS/DOC #/OMA CODE (DETAIL REPORT)
; DOC: RUN FOR: CLINIC 22/80/81
;
;PROGRAM PURPOSE : PHYSICIAN REVENUE ANALYSIS
;                  R051A/B/C.CB CONVERSION TO POWERHOUSE
;
;
;DATE       BY WHOM   DESCRIPTION
;08/02/04   M. Chan   ORIGINAL
;
;
CAN CLEAR
SET REP NOLIMIT
SET PAGE LENGTH 62 WIDTH 132
SET REP DEVICE DISC NAME r051ca
SET FORMFEED


access  F050-DOC-REVENUE-MSTR 		 &
  LINK DOCREV-DOC-NBR                                    &
    TO DOC-NBR OF F020-DOCTOR-MSTR  OPT                  &
  LINK (NCONVERT(DOCREV-CLINIC-1-2))                     &
    TO ICONST-CLINIC-NBR-1-2 OF ICONST-MSTR-REC  OPT     &
  LINK DOCREV-DEPT                                       &
    TO DEPT-NBR OF F070-DEPT-MSTR  OPT                   &
  LINK DOCREV-OMA-CD                                     &
    TO FEE-OMA-CD OF F040-OMA-FEE-MSTR  OPT


choose docrev-key parm prompt 'Enter clinic plus @:  ' 1 time

SORT ON DOCREV-CLINIC-1-2  &
     ON DOCREV-DEPT        &
     ON DOC-FULL-PART-IND  &
     ON DOCREV-DOC-NBR     &
     ON DOCREV-OMA-CD

DEF X-CLASS CHA*18 = "FULL TIME"         IF DOC-FULL-PART-IND = "F"   &
                ELSE "PART TIME"         IF DOC-FULL-PART-IND = "P"   &
		ELSE "CLINICAL SCHOLAR"  IF DOC-FULL-PART-IND = "C"   &
		ELSE "PLASTIC SURGERY"   IF DOC-FULL-PART-IND = "P"


DEF X-AMT-MTD ZONED*8 SIGNED = DOCREV-MTD-IN-REC + DOCREV-MTD-OUT-REC
DEF X-SVC-MTD ZONED*3 SIGNED = DOCREV-MTD-IN-SVC + DOCREV-MTD-OUT-SVC
;;DEF X-AVG-MTD ZONED*8 SIGNED = X-AMT-MTD / X-SVC-MTD
;;DEF X-PER-MTD ZONED*8 SIGNED =                   &
;;    ROUND((X-AMT-MTD / X-DOC-MTD) * 100,0,NEAR)  &
;;        IF X-AMT-MTD NE 0 ELSE 0

DEF X-AMT-YTD ZONED*8 SIGNED = DOCREV-YTD-IN-REC + DOCREV-YTD-OUT-REC
DEF X-SVC-YTD ZONED*5 SIGNED = DOCREV-YTD-IN-SVC + DOCREV-YTD-OUT-SVC
;;DEF X-AVG-YTD ZONED*8 SIGNED = X-AMT-YTD / X-SVC-MTD
;;DEF X-PER-YTD ZONED*8 SIGNED =                   &
;;    ROUND((X-AMT-YTD / X-DOC-YTD) * 100,0,NEAR)  &
;;        IF X-AMT-YTD NE 0 ELSE 0

;** FOLLOWING FOR DEPARTMENT CLASS TOTALS **

DEF X-FULL-AMT-MTD ZONED*8 SIGNED = X-AMT-MTD IF DOC-FULL-PART-IND = "F"
DEF X-FULL-SVC-MTD ZONED*3 SIGNED = X-SVC-MTD IF DOC-FULL-PART-IND = "F"
;;DEF X-FULL-AVG-MTD ZONED*8 SIGNED = (X-FULL-AMT-MTD / X-FULL-SVC-MTD)

DEF X-FULL-AMT-YTD ZONED*8 SIGNED = X-AMT-YTD IF DOC-FULL-PART-IND = "F"
DEF X-FULL-SVC-YTD ZONED*5 SIGNED = X-SVC-YTD IF DOC-FULL-PART-IND = "F"
;;DEF X-FULL-AVG-YTD ZONED*8 SIGNED = (X-FULL-AMT-YTD / X-FULL-SVC-YTD)

DEF X-PART-AMT-MTD ZONED*8 SIGNED = X-AMT-MTD IF DOC-FULL-PART-IND = "P"
DEF X-PART-SVC-MTD ZONED*3 SIGNED = X-SVC-MTD IF DOC-FULL-PART-IND = "P"
;;DEF X-PART-AVG-MTD ZONED*8 SIGNED = (X-PART-AMT-MTD / X-PART-SVC-MTD)

DEF X-PART-AMT-YTD ZONED*8 SIGNED = X-AMT-YTD IF DOC-FULL-PART-IND = "P"
DEF X-PART-SVC-YTD ZONED*5 SIGNED = X-SVC-YTD IF DOC-FULL-PART-IND = "P"
;;DEF X-PART-AVG-YTD ZONED*8 SIGNED = (X-PART-AMT-YTD / X-PART-SVC-YTD)

DEF X-PERIOD CHA*1 = "."

DEF X-NAME CHA*30 =PACK(DOC-NAME +" "+ DOC-INITS)

PAGE HEADING                                              	&
	TAB 1   "R051CA "    		          	        &
	TAB 8   "/"          	          	                &
	TAB 9   DOCREV-CLINIC-1-2                           &
	TAB 13  "P.E.D"				          	&
	TAB 20  ICONST-DATE-PERIOD-END                      	&
	TAB 50  "* PHYSICIAN REVENUE ANALYSIS *"          	&
	TAB 100 "RUN DATE:"                               	&
	TAB 110  SYSDATE                                  	&
        TAB 124 "PAGE:" TAB 130 SYSPAGE PIC "^^^"      	 	&
SKIP 1  TAB 55  ICONST-CLINIC-NAME                   

HEADING AT DOCREV-DOC-NBR 					&
SKIP 2  TAB 50  "DEPT"                                          &
        TAB 55  "#"  TAB 56 DOCREV-DEPT FILL "0"                &
        TAB 59  "-"  TAB 61 DEPT-NAME                           &
SKIP 1  TAB 50  "CLASS"                                         &
        TAB 57  DOC-FULL-PART-IND                               &
        TAB 59  "-"  TAB 61 X-CLASS                             &
SKIP 2  TAB 1   "DOCTOR"                                        &
        TAB 8   DOC-NBR                                         &
        TAB 14  X-NAME                                          &
SKIP 1  TAB 56  "----------- M . T . D . -----------"           &
        TAB 94  "-------------- Y . T. D . ------------"        &
SKIP 1  TAB 58  "SVC"                                           &
        TAB 68  "$ AMT"                                         &
        TAB 98  "SVC"                                           &
        TAB 108 "$ AMT"                                         &
SKIP 1  TAB 1   "OHIP"                                          &
        TAB 7   "------------OHIP CODE DESCRIPTION------------" & 
SKIP 1

FOOTING AT DOCREV-OMA-CD                                             &
SKIP 1  TAB 1   DOCREV-OMA-CD                                        &
        TAB 7   FEE-DESC                                           &
        TAB 57  X-SVC-MTD      SUB PIC "^^^^"                        &
        TAB 64  X-AMT-MTD      SUB PIC "^^,^^^.^^ " TRAIL "-"       &
        TAB 96  X-SVC-YTD      SUB PIC "^^^^^"                       &
        TAB 103 X-AMT-YTD      SUB PIC "^^^,^^^.^^ " TRAIL "-"       &
SKIP 1

FOOTING AT DOCREV-DOC-NBR                                        &
SKIP 2  TAB 1   "DOCTOR"                                         &
        TAB 9   DOC-NBR                                          &
        TAB 15  "TOTALS"                                         &
        TAB 56  X-SVC-MTD      SUB PIC "^^^^^"                        &
        TAB 64  X-AMT-MTD      SUB PIC "^^,^^^.^^ " TRAIL "-"       &
        TAB 96  X-SVC-YTD      SUB PIC "^^^^^"                       &
        TAB 103 X-AMT-YTD      SUB PIC "^^^,^^^.^^ " TRAIL "-"       &
SKIP PAGE


build $obj/r051ca 

