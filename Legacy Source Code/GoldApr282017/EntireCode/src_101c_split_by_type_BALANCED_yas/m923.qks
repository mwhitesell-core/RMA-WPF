; Program: m923.qkc
; Purpose: maintain doctor payroll/revenue translation codes
;
; Logic: Doctors are normally paid via revenue tranferred from f002_claims
;	 into f050_revenue that eventually goes into f110_compensation (other
;	 than 'mp' (manual payment) payroll in which case the compensation
;	 is entered directly into f110).
;	 The 'regular' payroll is for claims/adjustments entered into 
;	 clinic 22.  If a 2nd clinic is used this revenue can be transferred
;	 to f050 under that clinic and then into a special f110 payroll
;	 subssystem (like the original clinic 81 or 81y2k ICU payroll)
;	 However in order to allow claims to go into a clinic but have then
;	 separated out from that clinic's payroll we allow the f923 translation
;	 file to specify by doctor, by clinic, by payroll code a DIFFERENT
;	 clinic. This different or translatted clinic is used to create the
;	 f050_revenue transactions that then feed a different F110 payroll..
;
;	 Payroll "A" feeds clinic 22 and the regular-22 payroll.
;	 Payroll "B" was initially setup to change clinic 22 / payroll "B"
;	 		claims into clinic 85 revenue records that then 
;		  	were passed into the ICU payroll
;

; modification history
; 2001/oct/20 M.C.	- original
; 2001/nov/08 B.E.	- changed agent-cd field to clmhdr-payroll
;			  and religned layout of code
;

screen $obj/m923

file f923-doc-revenue-translation occurs 15
  access via doc-nbr 		&
  using      doc-nbr 		&
  request    doc-nbr
  access sequential
title 'Payroll Revenue-Clinic Translation Maintenance' at 1,32

title '                        TRANSLATED' at 3,5
title 'DOC    CLINIC  HOSPITAL   CLINIC'   at 4,5
title 'NBR     NBR      CODE       NBR '   at 5,5

skip to 7
align (1,,5) (,,14) (,,24) (,,34)

cluster occurs with f923-doc-revenue-translation
field doc-nbr of f923-doc-revenue-translation required nochange

field clinic-nbr of f923-doc-revenue-translation id same required &
      nochange

field clmhdr-payroll of f923-doc-revenue-translation id same required 	&
	nochange 							&
	lookup noton f923-doc-revenue-translation 			&
		via doc-nbr,						&
		    clinic-nbr, 					&
		    clmhdr-payroll					&
	      using doc-nbr        of f923-doc-revenue-translation,	&
		    clinic-nbr     of f923-doc-revenue-translation,	&
		    clmhdr-payroll of f923-doc-revenue-translation

field clinic-nbr-translated of f923-doc-revenue-translation required	&
	nochange 							&
	lookup noton f923-doc-revenue-translation 			&
		via doc-nbr,						&
		    clinic-nbr-translated 				&
	      using doc-nbr               of f923-doc-revenue-translation,&
		    clinic-nbr-translated of f923-doc-revenue-translation

build
