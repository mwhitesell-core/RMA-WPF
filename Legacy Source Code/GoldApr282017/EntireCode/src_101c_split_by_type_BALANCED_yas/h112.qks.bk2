cancel clear
set verify errors
;#> PROGRAM-ID.     H112.QKS
;
;       ((C)) Dyad Technologies
;
;    PURPOSE: Query/modifications to Physician PAYCODES / CEILINGS
;
;    MODIFICATION HISTORY
;        DATE    SAF #  WHO      DESCRIPTION
;     95/JUN/29  ____   B.M.L.   - original COPIED FROM D112.QKS
;   1999/Apr/9		S.B.	 - FIX DISPLAYS AND ALIGNMENST FOR Y2K. 
;				 - Changed t-ep-nbr-yy to use the full year.
;   1999/Jun/07         S.B.     - Altered the call to scrtitle.use and
;                                  stdhilite.use to be called from $use
;                                  instead of src.
;                                - Removed the call to secfile.use because
;                                  it was not doing anything.
;   2003/nov/10 b.e.    - alpha doctor nbr

screen $pb_obj/h112     					&
	on line 7 for 17 				&
	activities find					&
	message on line 24                    		&
        receiving 	w-doc-nbr, 			&
;			W-EP-NBR-FROM,			&
			w-ep-nbr-yy,			&
			constants-mstr-rec-6, 		&
			f020-doctor-mstr

temp w-doc-nbr char*3
;TEMP W-EP-NBR-FROM
temp w-ep-nbr-yy

description of screen                                                &
"                                                                   " &
"       This screen allows a User to query        Earnings Period   " &
"       transactions that specify a doctor's PAY CODE, PAY SUB CODE," &
"       EARNINGS CEILING, EXPENSE CEILING and GUARANTEE INFORMATION." &
"                                                                   "

temp t-ep-nbr-yy char*4 reset at startup initial ascii(w-ep-nbr-yy,4)

file  f112-pycdceilings-history primary  occurs 13
        access viaindex pycdceilings-history-key using w-doc-nbr

select if t-ep-nbr-yy  = (ascii(ep-nbr of f112-pycdceilings-history,6)[1:4])

file f020-doctor-mstr     master
file constants-mstr-rec-6 designer

file f191-earnings-period reference
     access viaindex ep-nbr using ep-nbr of f112-pycdceilings-history


define x-screen-name char*55 = "PAY CODES, CEILINGS, GUARANTEES"
;use $pb_src/scrtitle.use  nol nod
use $use/scrtitle.use  nol nod

;use $pb_src/stdhilite.use nol nod
use $use/stdhilite.use nol nod

skip to 2
skip
title &
"   E/P    BILLING  ---PAY--- --EARNINGS--- -GUARANTEE- ---EXPENSES-- ALLOC RTRO" &
        at ,1
skip
title &
"   Nbr      PED    CD/FACTOR CEILING/ORIGNL   %  AMOUNT CEILING/ORIGNL PER  TO " &
         at ,1
skip

cluster occurs with f112-pycdceilings-history
;         1         2         3         4         5         6         7         8
;....v....0....v....0....v....0....v....0....v....0....v....0....v....0....v....0
;01 YYYYMM YYYY/MM/DD _X 99.9999 999,999 999,999 999 999,999 999,999 999,999 99  YYYYMM  YYYY/MM/DD HH:MM USERID--
align (1,,4) (,,11) (,,20)(,,21) (,,22) (,,30) (,,38) (,,45) (,,49) (,,57) (,,65) (,,72) (,,75)

field ep-nbr				of f112-pycdceilings-history
field iconst-date-period-end		of f191-earnings-period	&
        pic "^^^^^^^^" sig 8

field doc-pay-code			of f112-pycdceilings-history
field doc-pay-sub-code			of f112-pycdceilings-history
field factor				of f112-pycdceilings-history
field doc-yrly-ceiling			of f112-pycdceilings-history
field doc-yrly-ceiling-adjusted 	of f112-pycdceilings-history	&
        pic "^^^^^^"
field doc-yrly-ceiling-guar-perc        of f112-pycdceilings-history
field doc-yrly-ceil-guar                of f112-pycdceilings-history
field doc-yrly-expense                  of f112-pycdceilings-history
field doc-yrly-expense-adjusted         of f112-pycdceilings-history	&
        pic "^^^^^^"
field doc-yrly-expn-alloc-pers          of f112-pycdceilings-history
field retro-to-ep-nbr			of f112-pycdceilings-history
cluster

build
  
