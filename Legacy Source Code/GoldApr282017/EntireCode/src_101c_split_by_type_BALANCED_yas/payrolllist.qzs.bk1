; DOC: PAYROLLLIST.QZS
; DOC: CREATE PAYROLL LISTING REPORT
; DOC: RUN FOR: MANAGEMENT/STAFF
;
;PROGRAM PURPOSE : PAYROLL LISTING REPORT
;
;DATE       WHO       DESCRIPTION
;92/03/10   Y.B.      ORIGINAL
;
;
can clear
set rep nolimit
set rep device disc name payrolllist
set page length 60 width 80
set formfee

access f020-doctor-mstr   &
  link doc-nbr to doc-nbr of f112-pycdceilings

define x-clinic zoned*2 unsigned = parm prompt   &
       'SELECT CLINIC "22" "60 TO 65" OR "0" FOR ALL: '

define x-dept zoned*2 unsigned = parm prompt       &
       'SELECT DEPT FROM 1 TO 14 OR "0" FOR ALL: '


define x-class zoned*1 unsigned  = parm prompt   &
'SELECT "1" FULL OR "2" PART OR "3" CS OR "4" PS OR "0" FOR ALL: '

select if ((x-clinic = doc-clinic-nbr or x-clinic = 0)	&
      and  (x-dept  = doc-dept or x-dept = 0)           &
      and ((x-class = 1 and doc-full-part-ind = "F")	&
       or  (x-class = 2 and doc-full-part-ind = "P")	&
       or  (x-class = 3 and doc-full-part-ind = "C")	&
       or  (x-class = 4 and doc-full-part-ind = "S")	&
       or  (x-class = 0)))

sort on doc-clinic-nbr

page heading                                               &
	tab 1  "RUN DATE:"                                 &
	tab 11  sysdate                                    &
        tab 36 "PAYROLL LISTING"                           &
        tab 71 "PAGE:" tab 77 syspage pic "^^"             &
skip 2                                                     &
        tab 1   "CL#"                                      &
        tab 6   "DEPT"                                     &
        tab 12  "DR#"                                      &
        tab 18  "PHYSICIAN NAME"                           &
        tab 36  "INITS"                                    &
        tab 42  "CLASS"                                    &
        tab 49  "PAY CODE"                                 &
        tab 59  "START DATE"                               &
        tab 71  "TERM DATE"                                &
skip 2

;HEADING AT DOC-CLINIC-NBR SKIP 2    &
; TAB 36  "CLINIC" TAB 43 DOC-CLINIC-NBR SKIP 2

report            &
  tab 1   doc-clinic-nbr                                          &
  tab 7   doc-dept                                                &
  tab 12  doc-nbr fill "0"                                        &
  tab 18  doc-name                                                &
  tab 38  doc-inits                                               &
  tab 44  doc-full-part-ind                                       &
  tab 52  doc-pay-code                                            &
;y2k tab 59  doc-date-fac-start pic "^^/^^/^^"                       &
  tab 59  doc-date-fac-start pic "^^^^/^^/^^"                       &
;y2k tab 71  doc-date-fac-term  pic "^^/^^/^^"                       &
  tab 71  doc-date-fac-term  pic "^^^^/^^/^^"                       &
skip 2

footing at doc-clinic-nbr skip page

build $pb_obj/payrolllist


  
