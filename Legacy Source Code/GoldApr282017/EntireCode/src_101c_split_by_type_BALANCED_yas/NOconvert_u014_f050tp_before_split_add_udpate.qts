;#> PROGRAM-ID.     u014_f050tp.qts
;
;       ((C)) Dyad Technologies
;
;    PURPOSE: transfer the current status of the f050 and f050tp revenue
;	     files to the appropriate history files to keep a snap of the
;	      revenue at the end of the month.
;
;    MODIFICATION HISTORY
;     DATE     WHO      DESCRIPTION
; 2000/jan/10  B.E.	- original
; 2000/jan/18  B.E.	- constants master PED has yyyymmdd but each dd
;			  is different for each clinic and we want all
;			  clinic data to be easily accessible for the
;			  same period. Therefore the 'dd' portion of the
;			  clinic's PED is ignored and '01' is used. For
;			  the 13th month the 'update' action on the output
;			  statement is will 12th monthend values.
; 2000/jun/18 B.E.      - for clinic 61-65 the passed parameter is 60 so 
;                         the match for clinic-nbr must be only on 1st digit
;		         

cancel clear
set stacksize 10000
run u014_f050tp

set default
set lock file update

set process nolimit

; define globals for this run
use $use/get_const_rec_7_values_globals.qts

global temp x-selected-clinic       num*2 	parm			&
	prompt "Enter CLINIC(2 digits) to be transferred to history: " 
global temp x-selected-clinic-alpha char*2 

;-------------------------------------------------------------------------------
; obtain current costing "constants" and pass to subsequent requests
;
use $use/get_const_rec_7_values.qts

;-------------------------------------------------------------------------------
;

request u014_f050tp_transfer_f050tp_to_f050_hist         &
                on edit        errors report    &
                on calculation errors report

access f050tp-doc-revenue-mstr			&
    link x-selected-clinic			&
      to iconst-clinic-nbr-1-2  of iconst-mstr-rec

; (for clinic 61-65 passed parameter is 60 so match only on 1st digit)
def x-selected-clinic-1 = nconvert(ascii(x-selected-clinic,2)[1:1])
def x-docrevtp-clinic-1 = nconvert(ascii(docrevtp-clinic-nbr,2)[1:1])
select if docrevtp-clinic-nbr 					&
	=    x-selected-clinic					&
         or (    x-selected-clinic = 60				&
	     and x-selected-clinic-1= x-docrevtp-clinic-1	&
	    )


;  (PED from constants master has yyyymmdd however each clinic's dd is
;   different for the same month.
;   Therefore use hard coded '01' for dd so that each clinics data is 
;   in effect stored at the yyyymm level and thus monthly values can
;   be easily obtained across clinic)
def x-iconst-date-period-end date = 					&
     nconvert(								&
	         ascii (						&
		        ( iconst-date-period-end of iconst-mstr-rec	&
		         /100   					&
		        )						&
		       ,6)						&
    	       + '01'							&
	     )

;(the 13th monthend processing will use the 'update' action on the output
; statement to update the 12th monthend values)

output f050tp-doc-revenue-mstr-history add update	&
	using w-ep-yr, 				&
	      x-iconst-date-period-end, 	&
	      docrevtp-key of f050tp-doc-revenue-mstr 

  item ep-yr 			final w-ep-yr
  item iconst-date-period-end	final x-iconst-date-period-end

build $obj/u014_f050tp
