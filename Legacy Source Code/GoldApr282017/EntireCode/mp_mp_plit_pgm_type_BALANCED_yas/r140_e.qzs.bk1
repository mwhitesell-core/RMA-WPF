cancel clear
; Program: r140_e.qzs
; Purpose: Generate i audit report of AFP conversion payments placed into
;	   payroll's f114 special payments file

; 2004/sep/14 b.e.	- original

set rep nolimit
set report dev disc name r140_e
set page width 132 length 60

access *u140_e_audit						&
	link doc-nbr						&
	to   doc-nbr    	 of f020-doctor-mstr		&
        link doc-afp-paym-group                         	&
        to   doc-afp-paym-group  of f074-afp-group-mstr opt	&
	link doc-dept						&
	to dept-nbr of f070-dept-mstr opt

sort 	on comp-code	&
	on doc-name

def x-doc-name char*35 				&
	=   pack(  doc-name 			&
	         + ", "				&
	         + doc-inits			&
	         + " ["				&
	  	 + doc-nbr			&
		 + "]"				&
	        )

page heading 					&
    tab 01 "r140_e"				&
    tab 20 "AFP Premium Payroll transactions Audit"		&
    tab 90 "Page: "				&
    tab 95 syspage				&
  skip 1

;heading at doc-afp-paym-group 		&
;    skip page				&
;    tab  1 "Group:"			&
;    tab  8 doc-afp-paym-group 		&
;    tab 12 "-"				&
;    tab 13 afp-group-name 		&
;  skip 1 

heading at comp-code			&
  skip 2				&
    tab 01 "Compenstation Code:"	&
    tab 21 comp-code			&
  skip 2				&
    tab  11 "Physician"			&
  skip 1				&
    tab  11 "Name"			&
    tab  70 "Amount"			&
  skip 2

rep							&
    tab 11 x-doc-name 					&
    tab 61 amt-net pic " ^^^,^^^,^^^.^^ "               

footing at comp-code			&
  skip 1				&
    tab 66 "----------"			&
  skip 1				&
    tab 41 "Payment "			&
    tab 49 "["				&
    tab 50 comp-code			&
    tab 56 "]:"				&
    tab 61 amt-net  subt  pic " ^^^,^^^,^^^.^^ "                       

final footing				&
  skip 1				&
    tab 66 "=========="			&
  skip 1				&
    tab 46 "Total Payment: "	&
    tab 61 amt-net  subt  pic " ^^^,^^^,^^^.^^ "                       

build $obj/r140_e
