;#> PROGRAM-ID.     D020A.QKS
;
;       ((C)) Dyad Technologies
;
;    PURPOSE: Display of YTD system maintained values
;
;    MODIFICATION HISTORY
;        DATE    SAF #  WHO      DESCRIPTION
;     93/JAN/01  ____   B.E.     - original
;     93/DEC/31  ____   B.E.     - added FIX designer routine to allow
;				   user to adjust any field
;     95/MAY/30  ----   M.C.     - when user changes the ytd expense,
;				   also change the transactions in
;				   f119-doctor-ytd
;     95/NOV/15  ----   M.C.	 - include new items on the screen
;     98/dec/08  ----   B.E.     - automatically calculate monthly
;				   required amount when computed 
;				   ie. yearly is entered
;   1999/jan/31         B.E.	 - y2k
;   1999/Jun/07 S.B.      	 - Altered the call to scrtitle.use and
;                       	   stdhilite.use to be called from $use
;               	           instead of src.
;       	                 - Removed the call to secfile.use because
;	                           it was not doing anything.
; 2003/nov/10 b.e.	- alpha doctor nbr
;
; !!!!!!!!!!!!!!!!!!!!!
; !!!!!!!!!!!!!!!!!!!!!
;   must change all FIELD statements below to DISPLAY when finished testing
; !!!!!!!!!!!!!!!!!!!!!
;
; 2014/sep/15  MC1      - add f020-doctor-audit to capture before change records if alteredrecord
;                       - specify the file on the field statements

can clear
screen $pb_obj/d020a.qkc slave  on line 7 for 17 message on line 24	&
	receiving f020-doctor-mstr, f020-doctor-extra

file f020-doctor-mstr     master

file f020-doctor-extra    master

; MC1
use $use/savef020audit_var.use

file f020-doctor-audit    designer
; MC1 - end

file f119-doctor-ytd      designer

temp t-ytdcex-diff


description of screen                                                    &
"                                                                      " &
"       This screen allows DISPLAY of System defined and maintained    "&
"	Year-to-Date values required by the system to perform payroll  "&
"	calculations."

;use $pb_src/secfile.use   nol nod


define x-screen-name char*55 = "EARNINGS - System TOTAL Values"
;use $pb_src/scrtitle.use  nol nod
use $use/scrtitle.use  nol nod

;use $pb_src/stdhilite.use nol nod
use $use/stdhilite.use nol nod

align (1,4,30)
field doc-ytdear    of f020-doctor-mstr            label "YTD Earnings ............:"
align (1,4,30) (,44,70)
field doc-ytdinc    of f020-doctor-mstr             label "YTD Income (Net).........:"
field doc-ytdinc-g  of f020-doctor-mstr             label "YTD Income (Gross).......:"
skip 1
field doc-ytdcea   of f020-doctor-mstr              label "YTD Ceiling     -EARNINGS:"
field doc-ceicea   of f020-doctor-mstr              label "EP  Ceiling     -EARNINGS:"
field doc-ytdcex   of f020-doctor-mstr              label ' "     "        -EXPENSES:'
field doc-ceicex   of f020-doctor-mstr              label ' "     "        -EXPENSES:'
align (1,4,31) (,44,70)
field doc-yrly-ceiling-computed of f020-doctor-mstr label "Computed Ceiling-EARNINGS:"
field doc-totinc                of f020-doctor-mstr label "TOTINC ..................:"
field doc-yrly-expense-computed of f020-doctor-mstr label '   "        "   -EXPENSES:'
field doc-totinc-g      of f020-doctor-mstr         label "TOTINC-Gross ............:"
skip 1
field doc-ytdgua       of f020-doctor-mstr          label "Guaranteed Income type A :"
field doc-ytdguc       of f020-doctor-mstr          label "Guaranteed Income type C :"
field doc-ytdgub       of f020-doctor-mstr          label '    "         "    "   B :'
field doc-ytdgud       of f020-doctor-mstr          label '    "         "    "   D :'
skip 1
field doc-yrly-require-revenue of f020-doctor-extra label "Computed Required Revenue:"
field doc-yrly-target-revenue  of f020-doctor-extra label "Computed Target Revenue  :"
field doc-ytdreq      of f020-doctor-extra          label 'YTD         "        "   :'
field doc-ytdtar      of f020-doctor-extra          label 'YTD        "      "      :'
field doc-ceireq of f020-doctor-extra display        label 'Monthly     "        "   :'
field doc-ceitar of f020-doctor-extra display        label 'Monthly    "      "      :'
skip 1
align (1,4,31) (,44,71)
field doc-payeft   of f020-doctor-mstr              label "EP  EFT Payments ........:" ; NOID
field doc-ep-date-deposit of f020-doctor-mstr       label "EFT DEPOSIT Date ........:"

procedure edit doc-ytdcex
begin
   if oldvalue(doc-ytdcex of f020-doctor-mstr ) <> doc-ytdcex of f020-doctor-mstr 
   then
	let t-ytdcex-diff = doc-ytdcex of f020-doctor-mstr - oldvalue(doc-ytdcex of f020-doctor-mstr )
	
end

procedure process doc-yrly-target-revenue
begin
   let doc-ceitar of f020-doctor-extra = round(doc-yrly-target-revenue of f020-doctor-extra / 12)
   display doc-ceitar of f020-doctor-extra
end

;98/dec/08 
procedure process doc-yrly-require-revenue
begin
   let doc-ceireq of f020-doctor-extra = round(doc-yrly-require-revenue of f020-doctor-extra / 12)
   display doc-ceireq of f020-doctor-extra
end

; MC1
use $use/savef020audit_mp.use

use $use/createf020audit_mp.use

procedure initialize
begin
  do savef020audit
end

; MC1 - end

procedure preupdate
begin
   if t-ytdcex-diff <> 0
   then begin
	get f119-doctor-ytd optional via doc-nbr, comp-code   &
	   using doc-nbr of f020-doctor-mstr, "CEIEXP"
	if accessok
	then begin
	   let amt-ytd = amt-ytd + t-ytdcex-diff
	   put f119-doctor-ytd
	end
	get f119-doctor-ytd optional via doc-nbr, comp-code   &
	   using doc-nbr of f020-doctor-mstr, "TOTEXP"
	if accessok
	then begin
	   let amt-ytd = amt-ytd + t-ytdcex-diff
	   put f119-doctor-ytd
	end
	get f119-doctor-ytd optional via doc-nbr, comp-code   &
	   using doc-nbr of f020-doctor-mstr, "INCEXP"
	if accessok
	then begin
	   let amt-ytd = amt-ytd - t-ytdcex-diff
	   put f119-doctor-ytd
	end
   end
; MC1
   if alteredrecord of f020-doctor-mstr or alteredrecord of f020-doctor-extra
   then begin
;  create before change audit record
        do createf020audit_before
        let last-mod-date of f020-doctor-audit = sysdate
        let last-mod-time of f020-doctor-audit = systime / 10000
        let last-mod-user-id of f020-doctor-audit = trun(logonid) + '-d020a-1'
        let last-mod-flag of f020-doctor-audit = 'C'
        put f020-doctor-audit reset
;  create after  change audit record
        do createf020audit_after
        let last-mod-date of f020-doctor-audit = sysdate
        let last-mod-time of f020-doctor-audit = systime / 10000
        let last-mod-user-id of f020-doctor-audit = trun(logonid) + '-d020a-2'
        let last-mod-flag of f020-doctor-audit = 'C'
        put f020-doctor-audit
        end
; MC1 - end

end

build
  
