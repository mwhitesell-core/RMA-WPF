cancel clear
set verify errors
;------------------------------------------------------------------
;#> PROGRAM-ID.     H020A.QKS
;
;       ((C)) Dyad Technologies
;
;    PURPOSE: Display of YTD system maintained values
;
;    MODIFICATION HISTORY
;      DATE    SAF #  WHO      DESCRIPTION
;   95/JUL/19  ____   B.M.L.   - original copied from D020A
;   95/NOV/07  ----   M.C.	 - USE T-DOC-EP-NBR INSTEAD OF
;				   T-DOC-EP-YR
;   95/NOV/17  ----   M.C.     - INCLUDE FIELDS FROM F020-DOCTOR-EXTRA
; 1996/NOV/26  ----   M.C.     - PDR 643 - ALLOW USER TO CHANGE
; 1999/jan/31         B.E.     - y2k
; 1999/Jun/07         S.B.     - Altered the call to scrtitle.use and
;                                  stdhilite.use to be called from $use
;                                  instead of src.
;                                - Removed the call to secfile.use because
;                                  it was not doing anything.
; 2001/jul/09	B.E.	- realignment of fields to match d020a.qks
; 2003/nov/10 b.e.	- alpha doctor nbr
; 2004/sep/02 b.e.      - made all fields display so that data can't be 
;                         accidently changed
;                       - added new designer procedcure FIX to allow change
;                         of each field only after a 'secret' password has
;                         been entered
;
;------------------------------------------------------------------

screen $pb_obj/h020a.qkc      						&
	activities find, change				       &
	on line 7 for 17					&
	message on line 24					&
	receiving t-doc-nbr,					&
		  t-doc-ep-nbr


temp t-doc-nbr char*3
temp t-doc-ep-nbr
temp t-password date
temp t-sysdate date

file f020-doc-mstr-history primary
	access viaindex f020-doc-history			&
		via doc-nbr, ep-nbr				&
	using t-doc-nbr, t-doc-ep-nbr


description of screen                                                  &
"                                                                    " &
"       This screen allows DISPLAY of System defined and maintained  "&
"	Year-to-Date values required by the system to perform payroll"&
"	calculations that have been archived into the history files."



define x-screen-name char*55 = "EARNINGS - System TOTAL Values"
use $use/scrtitle.use  nol nod

use $use/stdhilite.use nol nod
align (1,4,29)
field doc-ytdear                label "YTD Earnings ........:"
align (1,4,29) (,43,68)
field doc-ytdinc                label "YTD Income (Net).....:"
field doc-ytdinc-g              label "YTD Income (Gross)...:"
skip 1
field doc-ytdcea                label "YTD Ceiling -EARNINGS:"
field doc-ceicea                label "EP  Ceiling -EARNINGS:"
field doc-ytdcex                label ' "     "    -EXPENSES:'
field doc-ceicex                label ' "     "    -EXPENSES:'
align (1,4,32) (,43,68)
field doc-yrly-ceiling-computed label "Computed Ceil-EARNINGS:"
field doc-totinc                label "TOTINC ...............:"
align (1,4,30) (,43,68)
field doc-yrly-expense-computed label '   "      "  -EXPENSES:'
field doc-totinc-g              label "TOTINC-Gross .........:"
skip 1
align (1,4,29) (,43,68)
field doc-ytdgua                label "Guaranteed Inc type A:"
field doc-ytdguc                label "Guaranteed Inc type C:"
field doc-ytdgub                label '    "       "    "  B:'
field doc-ytdgud                label '    "       "    "  D:'
skip 1
field doc-yrly-require-revenue  label "Computed Required Rev:"
field doc-yrly-target-revenue   label "Computed Target Rev  :"
field doc-ytdreq                label 'YTD         "      " :'
field doc-ytdtar                label 'YTD        "     "   :'
align (1,4,31) (,43,68)
field doc-ceireq display        label 'Monthly     "      " :'
field doc-ceitar display        label 'Monthly    "     "   :'
;skip 1
align (,,1)
field t-password noid noecho
align (1,4,29) (,43,68)
field doc-payeft                label "EP  EFT Payments ........:" ; NOID
field doc-ep-date-deposit       label "EFT DEPOSIT Date ........:"


procedure designer fix help "Use this 'secret' function to make corrections to data"
begin
  let t-sysdate = sysdate
  accept t-password
  if t-password = t-sysdate
  then begin
    accept doc-ytdear
    accept doc-ytdinc
    accept doc-ytdinc-g
    accept doc-ytdcea
    accept doc-ceicea
    accept doc-ytdcex
    accept doc-ceicex
    accept doc-yrly-ceiling-computed
    accept doc-totinc
    accept doc-yrly-expense-computed
    accept doc-totinc-g
    accept doc-ytdgua
    accept doc-ytdguc
    accept doc-ytdgub
    accept doc-ytdgud
    accept doc-yrly-require-revenue
    accept doc-yrly-target-revenue
    accept doc-ytdreq
    accept doc-ytdtar
    accept doc-ceireq
    accept doc-ceitar
    accept doc-payeft
    accept doc-ep-date-deposit
  end
end
build
  
