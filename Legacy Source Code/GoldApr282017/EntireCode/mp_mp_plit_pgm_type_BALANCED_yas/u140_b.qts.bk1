; Program: u140_b
; Purpose: After the RA is run and f075 updated with RA payments this program
;	   will determine the percentage by RMA doctor number of the RA
;	   payments at the doctor's OHIP number level.
;	   Later programs will use this percentage to split the AFP payment
; mod history
; 2004/jul/01 b.e.	- originla
; 2004/aug/12 b.e.	- changed 'count' of doctors with same ohip number
;			  to count with same ohip nbr within doc-afp-paym-group 

cancel clear

run u140_b
set process nolimit
set lock file update

request f075_calc_ra_total                   &
                on edit        errors report &
                on calculation errors report

access f075-afp-doc-mstr

sorted	on doc-ohip-nbr

temp x-ra-total
item x-ra-total				&
	= x-ra-total + ra-payment-amt	&
		reset at doc-ohip-nbr

subfile u140_b keep at doc-ohip-nbr include 		&
  doc-ohip-nbr,						&
  x-ra-total


request f075_update_doc_recs_with_ra_total   &
                on edit        errors report &
                on calculation errors report

access *u140_b				&
   link doc-ohip-nbr			&
     to doc-ohip-nbr of f075-afp-doc-mstr

def x-ra-percentage zoned*5					&
	= round(   (  ra-payment-amt of f075-afp-doc-mstr	&
	            / x-ra-total				&
	           )						&
	         * 100						&
	       )

subfile u140_b_audit_1 keep include		&
  doc-ohip-nbr of u140_b,			&
  doc-nbr,					&
  ra-payment-amt,				&
  x-ra-total,					&	
  x-ra-percentage		

output f075-afp-doc-mstr alias f075-update update
  item ra-payment-amt-total	final x-ra-total of u140_b
  item afp-payment-percentage	final x-ra-percentage


request f075_ensure_percentage_is_100        &
                on edit        errors report &
                on calculation errors report

access f075-afp-doc-mstr

sort	on doc-ohip-nbr		&
	on doc-afp-paym-group

temp x-rec-count
item x-rec-count =x-rec-count + 1 reset at doc-afp-paym-group

; add up all percentages except the last one
temp x-ra-percentage zoned*5
item x-ra-percentage						&
	=   x-ra-percentage					&
	  + afp-payment-percentage				&
		if x-rec-count <> afp-duplicate-doc-count	&
     else x-ra-percentage reset at doc-afp-paym-group

;def x-final-percentage = 1 - (x-ra-percentage / 1000)
def x-final-percentage = 100 - x-ra-percentage 

subfile u140_b_audit_2 keep include		&
  doc-ohip-nbr,					&
  x-ra-percentage,				&	
  afp-payment-percentage,			&
  x-final-percentage,				&
  x-rec-count,					&
  afp-duplicate-doc-count
  
output f075-afp-doc-mstr alias f075-update update 	&
	if x-rec-count = afp-duplicate-doc-count
  item afp-payment-percentage	final x-final-percentage

build $obj/u140_b
