; PROGRAM:  CHECK9492.QZS
; DOC:      IF REQUIRED & TARGET IN 92 = 0 AND INCREQ AND INCTAR IN 94
;           SCREEN PRINT

; DATE      WHO       MODIFICATION
; 96/03/18  YAS       ORIGINAL
;
;
cancel clear
set default
set noblanks
set report nolimit
set report dev disc name check9492
set page width 132 length 63
set formfeed
set noclose

access *subfile94            &
  link doc-nbr to doc-nbr of f020-doctor-mstr  opt

sort on doc-dept    &
     on x-doc-name  &
     on doc-nbr

page heading							&
  tab  1 'CHECK949292'                                          &
  tab 10 'RUN DATE:'						&
  tab 21  sysdate						&
  tab 43  "COMP CODE INCREQ AND INCTAR FROM 94 SCREEN"          &
  tab 121 "PAGE:"						&
  tab 127 syspage						&
skip 2  tab  1  "DOCTOR DEPARTMENT: "                           &
        tab  20  doc-dept pic "^^"                              &
        tab  35 "Actual Revenue"                                &
        tab  60 "Actual Revenue"                                &
skip 1  tab  1  "DR. NBR"                                       &
        tab  10 "DR. NAME"                                      &
        tab  35 "Less Required Rev"                             &
        tab  60 "Less Target Rev"                               &
skip 1  tab  1  "-------"                                       &
        tab  10 "--------"                                      &
        tab  35 "-----------------"                             &
        tab  60 "---------------"                               &
skip 2

heading at doc-dept  skip page

footing at doc-nbr                                                    &
skip 1  tab  1  doc-nbr fill "0"                                      &
        tab  10 x-doc-name                                            &
        tab  41 x-required sub pic "^^^,^^^.^^ " trail sign "-"       &
        tab  65 x-target   sub pic "^^^,^^^.^^ " trail sign "-"       &
skip 2

footing at doc-dept                                                   &
skip 2  tab  1  "DEPARTMENT TOTAL: "                                  &
        tab  41 x-required sub pic "^^^,^^^.^^ " trail sign "-"       &
        tab  65 x-target   sub pic "^^^,^^^.^^ " trail sign "-"       &
skip 2
final footing                                                         &
skip 1  tab  1  "GRAND TOTAL:"                                        &
        tab  38 x-required sub pic "^^,^^^,^^^.^^ " trail sign "-"    &
        tab  62 x-target   sub pic "^^,^^^,^^^.^^ " trail sign "-"    &
skip 2

build $pb_obj/check9492
  
