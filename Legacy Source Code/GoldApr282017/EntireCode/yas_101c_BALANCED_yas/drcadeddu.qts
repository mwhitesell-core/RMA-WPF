;doc     : drcadeddu.qts
;purpose : create report tracking patients who have had surgical procedure
           Laparoscopic (s091a)
;          Pass 1 - select appropriate claims and "explode" repetitive services
;who     : Dr.Cadeddu


;Date		Who		Description
;2001/01/08	Yasemin		Original
;2002/04/22	Yasemin	        list of surgeries both elective and emergency
;                               print patient name & service date only
;2002/05/29                     print elective and emergency exclude Z399/Z514/
;                               Z555/E740/E741/E747/E705/E717

cancel clear
set default
set process nolimit
set lock file update

request one

access f002-claims-mstr                           ; &
; link clmhdr-pat-ohip-id-or-chart                 &
;  link ('I' + key-p-clm-data)                      &
;    to key-pat-mstr of  f010-pat-mstr  opt

choose  key-clm-type 'B' ,  key-clm-batch-nbr 220450000 to 220450999,   &
        key-clm-claim-nbr,  key-clm-serv-code, key-clm-adj-nbr '0'


select f002-claims-mstr if                      &
                clmdtl-oma-cd      <> "0000"    & ; ignore claim header recs
           and  clmdtl-oma-cd      <> "ZZZZ"    & ; ignore description recs
           and  clmdtl-oma-cd      <> "PAID"    & ; no payments
           and  clmdtl-oma-cd      <> "MICM"    & ; no miscellaneous billings
           and  clmdtl-oma-cd      <> "MISJ"    & ; "  "             "
           and  clmdtl-oma-cd      <> "MISC"    & ; "  "             "
           and  clmdtl-oma-cd      <> "MICV"    & ; "  "             "
           and  clmdtl-oma-cd      <> "MISP"    & ; "  "             "
           and  clmdtl-oma-cd      <> "MOHR"    & ; "  "             "
           and  clmdtl-oma-cd      <> "MICB"    & ; "  "             "
           and  clmdtl-oma-cd      <> "MIBR"    & ; "  "             "
           and  clmdtl-oma-cd      <> "MINH"    & ; "  "             "
           and  clmdtl-oma-cd      <> "MHSC"    & ; "  "             "
           and  clmdtl-oma-cd      <> "NHSC"    & ; "  "             "
           and  clmdtl-oma-cd      <> "Z399"    & ; "  "             "
           and  clmdtl-oma-cd      <> "Z514"    & ; "  "             "
           and  clmdtl-oma-cd      <> "Z555"    & ; "  "             "
           and  clmdtl-oma-cd      <> "E740"    & ; "  "             "
           and  clmdtl-oma-cd      <> "E741"    & ; "  "             "
           and  clmdtl-oma-cd      <> "E747"    & ; "  "             "
           and  clmdtl-oma-cd      <> "E705"    & ; "  "             "
           and  clmdtl-oma-cd      <> "E717"    & ; "  "             "
           and  clmdtl-adj-nbr     = 0            ; ignore adjustments


def x-doc-nbr cha*3 = clmdtl-id[4:3]

def x-oma-code cha*5 = clmdtl-id[12:5]

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 =                         &
        nconvert(clmdtl-consec-dates-r[1:1])    &
        if (clmdtl-consec-dates-r[2:2] <> 'MR'  &
        and clmdtl-consec-dates-r[2:2] <> 'OP') &
        and clmdtl-consec-dates-r[1:1] <> '0'

def x-sv-nbr3 zoned*2 =                         &
        nconvert(clmdtl-consec-dates-r[4:1])    &
        if (clmdtl-consec-dates-r[5:2] <> 'MR'  &
        and clmdtl-consec-dates-r[5:2] <> 'OP') &
        and clmdtl-consec-dates-r[4:1] <> '0'

def x-sv-nbr4 zoned*2=                          &
        nconvert(clmdtl-consec-dates-r[7:1])    &
        if (clmdtl-consec-dates-r[8:2] <> 'MR'  &
        and clmdtl-consec-dates-r[8:2] <> 'OP') &
        and clmdtl-consec-dates-r[7:1] <> '0'

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

def x-fee = clmdtl-fee-ohip / x-nbr-svcs
def x-clmdtl-fee-ohip-1 zoned*7 = x-sv-nbr1 * x-fee         
def x-clmdtl-fee-ohip-2 zoned*7 = x-sv-nbr2 * x-fee  
def x-clmdtl-fee-ohip-3 zoned*7 = x-sv-nbr3 * x-fee 
def x-clmdtl-fee-ohip-4 zoned*7 = x-sv-nbr4 * x-fee 

def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2]

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2]

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2]

;sort on clmhdr-pat-ohip-id-or-chart

subfile drcadeddu keep include              &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-1                              ,&
x-sv-nbr1	                         ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code	                         ,& 
x-clmdtl-fee-ohip-1                      

subfile drcadeddu alias drcadeddu_svc2 append keep if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-2                              ,&
x-sv-nbr2                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code                               ,&
x-clmdtl-fee-ohip-2                     

subfile drcadeddu alias drcadeddu_svc3 append keep if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-3                              ,&
x-sv-nbr3                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code                               ,&
x-clmdtl-fee-ohip-3                      

subfile drcadeddu alias drcadeddu_svc4 append keep if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-4                              ,&
x-sv-nbr4                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code                               ,&
x-clmdtl-fee-ohip-4                      

request two

access *drcadeddu				 		  &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr, '00000' , '0'	  &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr,	  &
       key-clm-serv-code, key-clm-adj-nbr of f002-claims-mstr opt &
  link clmhdr-pat-ohip-id-or-chart to key-pat-mstr of f010-pat-mstr opt &
  link x-oma-code[1:4] to fee-oma-cd of f040-oma-fee-mstr opt

select if      fee-icc-sec = "SP"   &
           or (fee-icc-sec = "CP" and x-oma-code[1:1]="E") 
 
def x-pat-name char*20 = pack(pat-surname+", "+pat-given-name)

subfile drcadeddu1 keep include           &
clmdtl-id       of drcadeddu             ,&
x-pat-name                               ,&
pat-surname                              ,&
pat-given-name                           ,&
pat-health-nbr                           ,&
x-doc-nbr                                ,&
x-sv-date-1                              ,&
x-sv-nbr1	                         ,& 
clmdtl-diag-cd of drcadeddu              ,&
x-oma-code	                         ,& 
x-clmdtl-fee-ohip-1                      

build $obj/drcadeddu
