;doc     : drchurchill.qts
;purpose : billings from April 1st, 2003 to current
;          Pass 1 - select appropriate claims and "explode" repetitive services
;who     : Dr. Churchill 


;Date		Who		Description
;2004/01/30     yas             original

cancel clear
set default
set process nolimit
set lock file update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr			
 

choose  key-clm-type 'B' ,  key-clm-batch-nbr 220403000 to 220403999,   &
        key-clm-claim-nbr,  key-clm-serv-code '00000', key-clm-adj-nbr '0'

select f002-claims-mstr if       clmhdr-batch-type = 'C'               

def x-clinic cha*2 = clmhdr-claim-id[1:2]

subfile drchurchill keep include                      &
clmhdr-claim-id              of f002-claims-mstr,&
key-clm-batch-nbr            of f002-claims-mstr,& 
key-clm-claim-nbr            of f002-claims-mstr,& 
clmhdr-loc                   of f002-claims-mstr,& 
clmhdr-hosp                  of f002-claims-mstr,& 
x-clinic                                        ,& 
clmhdr-pat-ohip-id-or-chart 

request two                             &
        on edit        errors report    &
        on calculation errors report

access *drchurchill                                                           &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                         &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr                &
          of f002-claims-mstr 

select f002-claims-mstr if                   &
            clmdtl-sv-date     >= '20030401' & 
       and  clmdtl-oma-cd      <> "0000"     & ; ignore claim header recs
       and  clmdtl-oma-cd      <> "ZZZZ"     & ; ignore description recs
       and  clmdtl-oma-cd      <> "PAID"     & ; no payments
       and  clmdtl-oma-cd      <> "MICM"     & ; no miscellaneous billings
       and  clmdtl-oma-cd      <> "MISJ"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICV"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISP"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MOHR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICB"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MIBR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MINH"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MHSC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "NHSC"    


def x-oma-code cha*5 = clmdtl-id[12:5]

def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne " 00"  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 unsigned =                                    &
                        nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr3 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[4:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-sv-nbr4 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[7:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

def x-fee = clmdtl-fee-ohip / x-nbr-svcs
def x-clmdtl-fee-ohip-1 zoned*7 = x-sv-nbr1 * x-fee
def x-clmdtl-fee-ohip-2 zoned*7 = x-sv-nbr2 * x-fee
def x-clmdtl-fee-ohip-3 zoned*7 = x-sv-nbr3 * x-fee
def x-clmdtl-fee-ohip-4 zoned*7 = x-sv-nbr4 * x-fee

def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-doc-nbr zoned*3 unsigned = nconvert(clmdtl-id[4:3])


sort on clmdtl-sv-date
                                         
subfile drchurchill1 keep include              &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-1                              ,&
x-sv-nbr1	                         ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code	                         ,& 
clmhdr-loc	      of drchurchill          ,& 
x-clinic              of drchurchill          ,& 
clmhdr-hosp           of drchurchill          ,& 
clmhdr-pat-ohip-id-or-chart of drchurchill    ,& 
x-clmdtl-fee-ohip-1                      

subfile drchurchill1 alias drchurchill_svc2 append keep if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-2                              ,&
x-sv-nbr2                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code                               ,&
clmhdr-loc            of drchurchill          ,&
x-clinic              of drchurchill          ,& 
clmhdr-hosp           of drchurchill          ,& 
clmhdr-pat-ohip-id-or-chart of drchurchill    ,&
x-clmdtl-fee-ohip-2                     

subfile drchurchill1 alias drchurchill_svc3 append keep if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-3                              ,&
x-sv-nbr3                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code                               ,&
clmhdr-loc            of drchurchill          ,&
x-clinic              of drchurchill          ,& 
clmhdr-hosp           of drchurchill          ,& 
clmhdr-pat-ohip-id-or-chart of drchurchill    ,&
x-clmdtl-fee-ohip-3                      

subfile drchurchill1 alias drchurchill_svc4 append keep if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-4                              ,&
x-sv-nbr4                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code                               ,&
clmhdr-loc            of drchurchill          ,&
x-clinic              of drchurchill          ,& 
clmhdr-hosp           of drchurchill          ,& 
clmhdr-pat-ohip-id-or-chart of drchurchill    ,&
x-clmdtl-fee-ohip-4                      

request three                           &
        on edit        errors report    &
        on calculation errors report

access *drchurchill1				 		                 &
  link clmhdr-pat-ohip-id-or-chart to key-pat-mstr of f010-pat-mstr opt
 
def x-pat-name char*42 = pack(pat-surname+", "+pat-given-name)

def clmdtl-sv-yy zoned*4 unsigned = nconvert(x-sv-date-1[1:4])
def clmdtl-sv-mm zoned*4 unsigned = nconvert(x-sv-date-1[5:2])
def clmdtl-sv-dd zoned*4 unsigned = nconvert(x-sv-date-1[7:2])

def x-age zoned*4 unsigned  =  clmdtl-sv-yy - pat-birth-date-yy     &
        if   ( clmdtl-sv-mm >  pat-birth-date-mm)                   &
          or ((clmdtl-sv-mm =  pat-birth-date-mm) and               &
              (clmdtl-sv-dd >= pat-birth-date-dd))                  &
        else clmdtl-sv-yy - pat-birth-date-yy - 1

subfile drchurchill2 keep include              &
clmdtl-id       of drchurchill1               ,&
pat-birth-date                           ,&
x-age                                    ,&
x-doc-nbr             of drchurchill1         ,&
x-sv-date-1           of drchurchill1         ,&
x-sv-nbr1	      of drchurchill1         ,& 
clmdtl-diag-cd        of drchurchill1         ,&
x-oma-code	      of drchurchill1         ,& 
clmhdr-loc	      of drchurchill1         ,& 
x-clinic  	      of drchurchill1         ,& 
clmhdr-hosp           of drchurchill1         ,& 
x-clmdtl-fee-ohip-1   of drchurchill1         

build $obj/drchurchill
