;doc     : codec101.qts
;purpose : How many C101 codes were billed for fiscal 2012 - july 2011 to June 30 2012
;who     : Leena
;
; *************************************************************

;2012/10/04     Yasemin         original

cancel clear
set default
set process nolimit
set lock file update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr

choose  key-clm-type 'B' ,  key-clm-batch-nbr,                   &
  key-clm-claim-nbr, key-clm-serv-code '00000', key-clm-adj-nbr '0'

select f002-claims-mstr if  clmhdr-batch-type  = 'C'

subfile c101     keep include              &
clmhdr-claim-id              of f002-claims-mstr,&
key-clm-batch-nbr            of f002-claims-mstr,&
key-clm-claim-nbr            of f002-claims-mstr,&
clmhdr-agent-cd              of f002-claims-mstr,&
clmhdr-batch-type            of f002-claims-mstr

request two                             &
        on edit        errors report    &
        on calculation errors report

access *c101                                                             &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                         &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr                &
          of f002-claims-mstr

select f002-claims-mstr if                                              &
            clmdtl-sv-date     >= '20110701'                            &
       and  clmdtl-sv-date     <= '20120630'                            &
       and  clmdtl-oma-cd      =  "C101"                                &
       and  clmdtl-adj-nbr     =  0

def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne " 00"  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 unsigned =                                    &
                        nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr3 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[4:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-sv-nbr4 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[7:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

def x-fee = clmdtl-fee-ohip / x-nbr-svcs
def x-clmdtl-fee-ohip-1 zoned*7 = x-sv-nbr1 * x-fee    
def x-clmdtl-fee-ohip-2 zoned*7 = x-sv-nbr2 * x-fee  
def x-clmdtl-fee-ohip-3 zoned*7 = x-sv-nbr3 * x-fee 
def x-clmdtl-fee-ohip-4 zoned*7 = x-sv-nbr4 * x-fee 

def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2] &
                        if consec-flag = "Y"                              &
                        else " "

subfile c1011     keep include            &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-1                      ,&
x-sv-date-1                              ,&
clmdtl-oma-cd         of f002-claims-mstr

subfile c1011 alias geriatric_svc2 append keep if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-2                      ,&
x-sv-date-2                              ,&
clmdtl-oma-cd         of f002-claims-mstr

subfile c1011 alias geriatric_svc3 append keep if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-3                      ,&
x-sv-date-3                              ,&
clmdtl-oma-cd         of f002-claims-mstr

subfile c1011 alias geriatric_svc4 append keep if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-4                      ,&
x-sv-date-4                              ,&
clmdtl-oma-cd         of f002-claims-mstr

request three                           &
        on edit        errors report    &
        on calculation errors report


access *c1011                                                          &
  link clmdtl-id[3:3] to doc-nbr of f020-doctor-mstr opt                    

def x-clinic cha*2 = clmdtl-id[1:2]
def x-doc    cha*3 = clmdtl-id[3:3]

sort on x-doc on x-sv-date-1     


temp x-c101   num*7
item x-c101   = x-c101 + x-clmdtl-fee-ohip-1/100 &
              reset at x-doc    

temp x-c101-count   num*7
item x-c101-count   = x-c101-count + 1           &
                    reset at x-doc

def comma cha*1 = "~"
def x-num-cr integer unsigned size 2 = 13
def x-cr        char*2 = char(x-num-cr)

subfile c101a portable keep at x-doc include     &
x-doc                                               ,&
comma                                               ,&
doc-dept                                            ,&
comma                                               ,&
doc-name                                            ,&
comma                                               ,&
doc-inits                                           ,&
comma                                               ,&
x-c101                                              ,&
comma                                               ,&
x-c101-count                                        ,&
x-cr

build $obj/c101
