;doc     : drfederici.qts
;purpose : access f088 report clinic 93 (doc# 365,625,715,497,612,519,725
;          service dates from 2003/04/01 to 2004/03/31)          
;who     : Dr. Federici

;Date           Who             Description
;2004/07/26     Yasemin         Original

cancel clear
set default
set process nolimit
set lock file update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr                   &
  link clmhdr-pat-ohip-id-or-chart        &
    to key-pat-mstr of f010-pat-mstr opt  &
  link clmhdr-claim-id[3:3]               &
    to doc-nbr of f020-doctor-mstr opt

choose  key-clm-type 'B' ,  key-clm-batch-nbr "93612000" to "93612999"   &
                                              "22612000" to "22612999"   &
                                              "93519000" to "93519999"   &
                                              "22519000" to "22519999"   &
                                              "93725000" to "93725999"   &
                                              "22725000" to "22725999"   &
                                              "93365000" to "93365999"   &
                                              "22365000" to "22365999"   &
                                              "93625000" to "93625999"   &
                                              "22625000" to "22625999"   &
                                              "93715000" to "93715999"   &
                                              "22715000" to "22715999"   &
                                              "93497000" to "93497999"   &
                                              "22497000" to "22497999"   &
                                              "93267000" to "93267999"   &
                                              "22267000" to "22267999"   &
                                              "93462000" to "93462999"   &
                                              "22462000" to "22462999"   &
 ,key-clm-claim-nbr, key-clm-serv-code '00000', key-clm-adj-nbr '0'

select f002-claims-mstr if       clmhdr-batch-type = 'C'

subfile feder     keep include                    &
clmhdr-claim-id               of f002-claims-mstr,&
key-clm-batch-nbr             of f002-claims-mstr,&
key-clm-claim-nbr             of f002-claims-mstr,&
clmhdr-pat-ohip-id-or-chart

request two                             &
        on edit        errors report    &
        on calculation errors report

access *feder                                                             &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                         &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr                &
          of f002-claims-mstr

 select f002-claims-mstr if                  &
            clmdtl-sv-date  >= "20030401"    & ; details only
       and  clmdtl-sv-date  <= "20040331"    & ; details only
       and  clmdtl-oma-cd      <> "0000"     & ; details only
       and  clmdtl-oma-cd      <> "ZZZZ"     & ;   
       and  clmdtl-oma-cd      <> "PAID"     & ; no payments
       and  clmdtl-oma-cd      <> "MICM"     & ; no miscellaneous billings
       and  clmdtl-oma-cd      <> "MISJ"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICV"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISP"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MOHR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICB"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MIBR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MINH"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MHSC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "NHSC"     & ;
       and  clmdtl-adj-nbr     = 0             ; ignore adjustments

def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne " 00"  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 unsigned =                                    &
                        nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr3 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[4:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-sv-nbr4 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[7:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2] &
                        if consec-flag = "Y"                              &
                        else " "

sort on clmdtl-sv-date

subfile feder1     keep include           &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-sv-date-1                              ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmhdr-loc            of feder           ,&
clmhdr-hosp           of feder           ,&
clmhdr-pat-ohip-id-or-chart of feder      

subfile feder1 alias feder_svc2 append keep if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-sv-date-2                              ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmhdr-loc            of feder           ,&
clmhdr-hosp           of feder           ,&
clmhdr-pat-ohip-id-or-chart of feder    

subfile feder1 alias feder_svc3 append keep if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-sv-date-3                              ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmhdr-loc            of feder           ,&
clmhdr-hosp           of feder           ,&
clmhdr-pat-ohip-id-or-chart of feder   

subfile feder1 alias feder_svc4 append keep if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-sv-date-4                              ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmhdr-loc            of feder           ,&
clmhdr-hosp           of feder           ,&
clmhdr-pat-ohip-id-or-chart of feder   

request three                           &
        on edit        errors report    &
        on calculation errors report

access *feder1                                                          &
  link clmhdr-pat-ohip-id-or-chart to key-pat-mstr of f010-pat-mstr opt

def clmdtl-sv-yy zoned*4 unsigned = nconvert(x-sv-date-1[1:4])
def clmdtl-sv-mm zoned*4 unsigned = nconvert(x-sv-date-1[5:2])
def clmdtl-sv-dd zoned*4 unsigned = nconvert(x-sv-date-1[7:2])

def x-age zoned*4 unsigned  =  clmdtl-sv-yy - pat-birth-date-yy     	&
        if   ( clmdtl-sv-mm >  pat-birth-date-mm)                   	&
          or (    clmdtl-sv-mm =  pat-birth-date-mm 		    	&
	      and clmdtl-sv-dd >= pat-birth-date-dd			&
	     )                  					&
        else clmdtl-sv-yy - pat-birth-date-yy - 1

select if         x-age < 18              & 
         and (    clmdtl-diag-cd = 287    &
               or clmdtl-diag-cd = 289)

sort on clmhdr-pat-ohip-id-or-chart

def comma cha*1 = "~"
def x-num-cr integer unsigned size 2 = 13
def x-cr        char*2 = char(x-num-cr)

subfile feder2 portable keep at clmhdr-pat-ohip-id-or-chart include    &
pat-given-name                                      ,&
comma                                               ,&
pat-surname                                         ,&
comma                                               ,&
pat-birth-date                                      ,&
comma                                               ,&
x-age                                               ,&
x-cr

build $obj/feder
