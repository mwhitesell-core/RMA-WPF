; DOC: drwells1.qzs
; DOC: Select doc 397 and report health#/pat/code/amount
; DOC:  
; DOC: RUN FOR: Created for Steve Crocker (Dr.Wells research assistant) 
;
;PROGRAM PURPOSE : 


;
;DATE       BY WHOM      DESCRIPTION
;98/12/07   YASEMIN      ORIGINAL
;
can clear
set rep nolimit
set page length 62 width 80
set rep device disc name drwells1
set noformfee

access claims_history_subfile

def x-doc-nbr cha*3 = clmdtl-id[4:3]

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

define x-sv-nbr2  zoned*2 unsigned = clmdtl-sv-nbr(1)               &
                     if    clmdtl-consecutive-sv-days(1) ne 'OP'    &
                       and clmdtl-consecutive-sv-days(1) ne 'MR'    &
                       and clmdtl-consecutive-sv-days(1) ne 'BI'

define x-sv-nbr3  zoned*2 unsigned = clmdtl-sv-nbr(2)               &
                     if    clmdtl-consecutive-sv-days(1) ne 'OP'    &
                       and clmdtl-consecutive-sv-days(1) ne 'MR'    &
                       and clmdtl-consecutive-sv-days(1) ne 'BI'

define x-sv-nbr4  zoned*2 unsigned = clmdtl-sv-nbr(3)               &
                     if    clmdtl-consecutive-sv-days(1) ne 'OP'    &
                       and clmdtl-consecutive-sv-days(1) ne 'MR'    &
                       and clmdtl-consecutive-sv-days(1) ne 'BI'

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 & 
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

def x-oma-code cha*5 = clmdtl-id[12:5]

def x-pat-name cha*27 = pack(pat-surname + ",  " + pat-given-name)	
 
select if x-doc-nbr = "397"

sort on pat-surname    & 
     on pat-given-name &
     on pat-health-nbr &
     on x-oma-code 


page heading                                        &
        tab 1    sysdate                            &
        tab 15   "Prepared by R.M.A. for Dr. Wells" &
skip 3  tab 1    "Health#"                          &
        tab 15   "Patient Name"                     &  
        tab 45   "OMA Code"                         & 
        tab 55   "#services"                        &
        tab 67   "$amount"                          &
skip 2

report                                              &
        tab 1    pat-health-nbr                     &
        tab 15   x-pat-name                         &
        tab 45   x-oma-code                         &
        tab 55   x-nbr-svcs                         &
        tab 63   clmdtl-fee-oma                     &
skip 1 

final footing skip 4                                                &
        tab 35  "GRAND TOTALS"                                      &
        tab 52  x-nbr-svcs subtotal    pic "^^^,^^^"                &
        tab 67  clmdtl-fee-oma  subtotal    pic "^^^,^^^.^^"

            
build $pb_obj/drwells1




  
