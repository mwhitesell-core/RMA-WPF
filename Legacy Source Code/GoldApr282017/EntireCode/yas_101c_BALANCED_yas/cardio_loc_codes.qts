;doc     : cardio_loc_codes.qts
;purpose : create a detail claims report for cardio docs one amount per code    
;          clinic 96 have diag codes for tech and proff and doc gets paid for it
;          clinic 60 proff amount is recorded in 22 for under code T995 but T995
;          does not include the #services or what code it is        a
;          included just clinic 61-65 proff fee and took out T995 
;          included just clinic 71-75 proff fee                      
;          INCLUDE ADJUSTMENTS  
;who     : Dwayne Martins - Medicine Manager

;
; *************************************************************

;Date		Who		Description
;2008/09/09	Yasemin	       
;2008/09/10     MC	- include Misc Payment
;2008/09/10     Yas 	- include Misc Payment DID not WORK purge deletes the payments of claims dtl
;2008/09/11     Yas     - modify the select statement for "B" adjustment

cancel clear
set default
set process nolimit
set lock record update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr                   

choose  key-clm-type 'B' ,  key-clm-batch-nbr,                  &
 key-clm-claim-nbr, key-clm-serv-code '00000', key-clm-adj-nbr '0'

select f002-claims-mstr if     (      clmhdr-batch-type = 'C'                             &
				  or (clmhdr-batch-type = 'P' and clmhdr-adj-cd = 'M')    &
                                  or (clmhdr-batch-type = 'A' and clmhdr-adj-cd = 'B')    &
			       )				      &
                           and       clmhdr-doc-dept = 4              &
                           and (     clmhdr-claim-id[3:3] =  "431"    &
                                  or clmhdr-claim-id[3:3] =  "K87"    &
                                  or clmhdr-claim-id[3:3] =  "F13"    &
                                  or clmhdr-claim-id[3:3] =  "413"    &
                                  or clmhdr-claim-id[3:3] =  "415"    &
                                  or clmhdr-claim-id[3:3] =  "C25"    &
                                  or clmhdr-claim-id[3:3] =  "F14"    &
                                  or clmhdr-claim-id[3:3] =  "442"    &
                                  or clmhdr-claim-id[3:3] =  "873"    &
                                  or clmhdr-claim-id[3:3] =  "G74"    &
                                  or clmhdr-claim-id[3:3] =  "660"    &
                                  or clmhdr-claim-id[3:3] =  "F73"    &
                                  or clmhdr-claim-id[3:3] =  "M19"    &
                                  or clmhdr-claim-id[3:3] =  "E66"    &
                                  or clmhdr-claim-id[3:3] =  "620"    &
                                  or clmhdr-claim-id[3:3] =  "G25"    &
                                  or clmhdr-claim-id[3:3] =  "015"    &
                                  or clmhdr-claim-id[3:3] =  "G70"    &
                                  or clmhdr-claim-id[3:3] =  "976"    &
                                  or clmhdr-claim-id[3:3] =  "181"    &
                                  or clmhdr-claim-id[3:3] =  "F52"    &
                                  or clmhdr-claim-id[3:3] =  "809"    &
                                  or clmhdr-claim-id[3:3] =  "C53"    &
                                  or clmhdr-claim-id[3:3] =  "F80"    &
                                  or clmhdr-claim-id[3:3] =  "891"    &
                                  or clmhdr-claim-id[3:3] =  "J84"    &
                                  or clmhdr-claim-id[3:3] =  "D98"    &
                                  or clmhdr-claim-id[3:3] =  "F99"    &
                                  or clmhdr-claim-id[3:3] =  "907"    &
                                  or clmhdr-claim-id[3:3] =  "D50"    &
                                  or clmhdr-claim-id[3:3] =  "K07"    &
                                  or clmhdr-claim-id[3:3] =  "F95"    &
                                  or clmhdr-claim-id[3:3] =  "K10"    &
                                  or clmhdr-claim-id[3:3] =  "441"    &
                                  or clmhdr-claim-id[3:3] =  "476"    &
                                  or clmhdr-claim-id[3:3] =  "004"    &
                                  or clmhdr-claim-id[3:3] =  "115"    &
                                  or clmhdr-claim-id[3:3] =  "158"    &
                                  or clmhdr-claim-id[3:3] =  "366")   

subfile cardio     keep include       &
clmhdr-claim-id                      ,&
key-clm-batch-nbr                    ,&
key-clm-claim-nbr                    ,&
clmhdr-batch-type		     ,&
clmhdr-loc        

request two                             &
        on edit        errors report    &
        on calculation errors report

access *cardio                                                    &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                  &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr         &
          of f002-claims-mstr

select f002-claims-mstr if                   &
       clmdtl-date-period-end  >= '20060701' &
   and clmdtl-date-period-end  <= '20070630' &
       and  clmdtl-oma-cd      <> "0000"     & ; details only
       and  clmdtl-oma-cd      <> "ZZZZ"     & ;   
       and  clmdtl-oma-cd      <> "PAID"     & ; no payments
;      and  clmdtl-oma-cd      <> "MICM"     & ; no miscellaneous billings
;      and  clmdtl-oma-cd      <> "MISJ"     & ; "  "             "
;      and  clmdtl-oma-cd      <> "MISC"     & ; "  "             "
;      and  clmdtl-oma-cd      <> "MICV"     & ; "  "             "
;      and  clmdtl-oma-cd      <> "MISP"     & ; "  "             "
;      and  clmdtl-oma-cd      <> "MOHR"     & ; "  "             "
;      and  clmdtl-oma-cd      <> "MICB"     & ; "  "             "
;      and  clmdtl-oma-cd      <> "MIBR"     & ; "  "             "
;      and  clmdtl-oma-cd      <> "MINH"     & ; "  "             "
;      and  clmdtl-oma-cd      <> "MHSC"     & ; "  "             "
;      and  clmdtl-oma-cd      <> "NHSC"     &
;      and  clmdtl-oma-cd      <> "DHSC"     &
;      and  clmdtl-oma-cd      <> "AGEP"     &
       and  clmdtl-oma-cd      <> "T995"     ;clinic 60 proff fees

def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne " 00"  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 unsigned =                                    &
                        nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr3 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[4:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-sv-nbr4 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[7:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

def x-prof-fee = clmdtl-fee-ohip - clmdtl-amt-tech-billed                       & 
	if   (key-clm-batch-nbr[1:2] > '60' and key-clm-batch-nbr[1:2] < '66')  &
	  or (key-clm-batch-nbr[1:2] > '70' and key-clm-batch-nbr[1:2] < '76')  &
        else clmdtl-fee-ohip

def x-fee = x-prof-fee  / x-nbr-svcs 			&
	if clmhdr-batch-type = 'C' 			&
	else x-prof-fee
def x-clmdtl-fee-ohip-1 zoned*7 = x-sv-nbr1 * x-fee     &
	if clmhdr-batch-type = 'C'			&
	else x-fee
def x-clmdtl-fee-ohip-2 zoned*7 = x-sv-nbr2 * x-fee     &
	if clmhdr-batch-type = 'C'			&
	else x-fee
def x-clmdtl-fee-ohip-3 zoned*7 = x-sv-nbr3 * x-fee     &
	if clmhdr-batch-type = 'C'			&
	else x-fee
def x-clmdtl-fee-ohip-4 zoned*7 = x-sv-nbr4 * x-fee     &
	if clmhdr-batch-type = 'C'			&
	else x-fee

def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2] &
                        if consec-flag = "Y"                              &
                        else " "


subfile cardio1     keep include          &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-1                      ,&
x-sv-date-1                              ,&
x-sv-nbr1                                ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of cardio           

subfile cardio1 alias cardio_svc2 append keep if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-2                      ,&
x-sv-date-2                              ,&
x-sv-nbr2                                ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of cardio          

subfile cardio1 alias cardio_svc3 append keep if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-3                      ,&
x-sv-date-3                              ,&
x-sv-nbr3                                ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of cardio          

subfile cardio1 alias cardio_svc4 append keep if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-4                      ,&
x-sv-date-4                              ,&
x-sv-nbr4                                ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of cardio           

request three                           &
        on edit        errors report    &
        on calculation errors report

access *cardio1                                                         &
  link clmhdr-loc to loc-nbr of f030-locations-mstr opt                 &
  link clmdtl-oma-cd to fee-oma-cd of f040-oma-fee-mstr opt             &
  link clmdtl-id[3:3] to doc-nbr of f020-doctor-mstr opt                &
  link (ascii(clmdtl-diag-cd,3)) to diag-cd of f091-diag-codes-mstr opt  

def x-clinic cha*2 = clmdtl-id[1:2]
def x-doc    cha*3 = clmdtl-id[3:3]


sort on x-clinic on x-doc on clmdtl-oma-cd

temp x-amt  zoned*9 signed
item x-amt  = x-amt  + x-clmdtl-fee-ohip-1     & 
              reset at clmdtl-oma-cd

temp x-svc  zoned*9 unsigned
item x-svc  = x-svc  + x-sv-nbr1               &
              reset at clmdtl-oma-cd

def comma cha*1 = "~"
def x-num-cr integer unsigned size 2 = 13
def x-cr        char*2 = char(x-num-cr)

subfile cardio2 portable keep at clmdtl-oma-cd include            &
x-clinic                                            ,&
comma                                               ,&
x-doc                                               ,&
comma                                               ,&
doc-name                                            ,&
comma                                               ,&
doc-inits                                           ,&
comma                                               ,&
x-amt                                               ,&
comma                                               ,&
x-svc                                               ,&
comma                                               ,&
clmhdr-loc                                          ,&
comma                                               ,&
loc-name                                            ,&
comma                                               ,&
clmdtl-oma-cd                                       ,&
comma                                               ,&
fee-desc                                            ,&
comma                                               ,&
clmdtl-diag-cd                                      ,&
comma                                               ,&
diag-cd-desc                                        ,&
x-cr

build $obj/cardio_loc_codes
