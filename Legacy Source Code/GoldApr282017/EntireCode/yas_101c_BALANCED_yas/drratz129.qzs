;doc     : ratz129.qzs
;purpose : create report extracting z129 for dep1 doctors
;          wrong code sent to ohip for flu shots
;who     : Linda Norek family medicine manager


;Date           Who             Description
;2001/01/16     Yasemin         Original


set rep nolimit 


access u030-tape-145-file                                                     &
 link ('B', nconvert(rat-145-group-nbr[1:2] + '0' + rat-145-account-nbr[1:6]),&
       nconvert(rat-145-account-nbr[7:2]) , '00000', '0')                     &
  to key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr,                      &
     key-clm-serv-code, key-clm-adj-nbr of f002-claims-mstr opt               &
 link clmhdr-pat-ohip-id-or-chart                                             &
     viaindex key-pat-mstr to f010-pat-mstr opt

def x-doc-nbr cha*3 =  rat-145-account-nbr[1:3]

select  u030-tape-145-file  if                        &
                (    x-doc-nbr            = "134"     &
                  or x-doc-nbr            = "142"     &     
                  or x-doc-nbr            = "171"     &     
                  or x-doc-nbr            = "174"     &     
                  or x-doc-nbr            = "218"     &     
                  or x-doc-nbr            = "362"     &     
                  or x-doc-nbr            = "387"     &     
                  or x-doc-nbr            = "915")    &
                 and rat-145-service-cd   = "Z129A"   

set subfile name ratz129  keep 
report summar            &
x-doc-nbr                &
rat-145-doc-nbr          &
rat-145-specialty-cd     &
rat-145-health-ohip-nbr  &
rat-145-version-cd       &
pat-birth-date           &
rat-145-account-nbr      &
rat-145-claim-nbr        &
rat-145-service-date     &
rat-145-service-cd 

build $obj/ratz129a


set report nolimit
set rep page length 62
set rep page widt 132
set rep dev  disc name ratz129
set rep nohead
set formfee

access *ratz129

sort on x-doc-nbr              &
     on rat-145-service-date   &
     on rat-145-service-cd 

heading at x-doc-nbr skip 2

report                          &
tab 1  x-doc-nbr                &
tab 6  rat-145-doc-nbr          &
tab 13 rat-145-specialty-cd     &
tab 16 rat-145-health-ohip-nbr  &
tab 30 rat-145-version-cd       &
tab 33 pat-birth-date            &
tab 44 rat-145-account-nbr      &
tab 54 rat-145-claim-nbr        &
tab 68 rat-145-service-date     &
tab 80 rat-145-service-cd       &
skip 1

build $obj/ratz129b


