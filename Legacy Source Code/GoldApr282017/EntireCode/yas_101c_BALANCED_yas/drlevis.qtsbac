;doc     : drlevis.qts
;purpose : create report tracking patients who have had consults A085 & C085
;          within the last 6 months
;          Pass 1 - select appropriate claims and "explode" repetitive services
;who     : Dr. Carolyn Levis


;Date		Who		Description
;2001/05/02	Yasemin		Original


cancel clear
set default
set process nolimit
set lock file update

request one

access f002-claims-mstr                           ;&
; link clmhdr-pat-ohip-id-or-chart                 &
;  link ('I' + key-p-clm-data)                     &
;    to key-pat-mstr of  f010-pat-mstr  opt

choose  key-clm-type 'B' ,  key-clm-batch-nbr 220925000 to 220925999,   &
        key-clm-claim-nbr,  key-clm-serv-code, key-clm-adj-nbr '0'


select f002-claims-mstr if                      &
              ( clmdtl-oma-cd      =  "A085"    &           
           or   clmdtl-oma-cd      =  "C085")   &           
           and  clmdtl-sv-date     >= "20001101"    


def x-doc-nbr cha*3 = clmdtl-id[4:3]

def x-oma-code cha*5 = clmdtl-id[12:5]

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 =                         &
        nconvert(clmdtl-consec-dates-r[1:1])    &
        if (clmdtl-consec-dates-r[2:2] <> 'MR'  &
        and clmdtl-consec-dates-r[2:2] <> 'OP') &
        and clmdtl-consec-dates-r[1:1] <> '0'

def x-sv-nbr3 zoned*2 =                         &
        nconvert(clmdtl-consec-dates-r[4:1])    &
        if (clmdtl-consec-dates-r[5:2] <> 'MR'  &
        and clmdtl-consec-dates-r[5:2] <> 'OP') &
        and clmdtl-consec-dates-r[4:1] <> '0'

def x-sv-nbr4 zoned*2=                          &
        nconvert(clmdtl-consec-dates-r[7:1])    &
        if (clmdtl-consec-dates-r[8:2] <> 'MR'  &
        and clmdtl-consec-dates-r[8:2] <> 'OP') &
        and clmdtl-consec-dates-r[7:1] <> '0'

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

def x-fee = clmdtl-fee-ohip / x-nbr-svcs
def x-clmdtl-fee-ohip-1 zoned*7 = x-sv-nbr1 * x-fee         
def x-clmdtl-fee-ohip-2 zoned*7 = x-sv-nbr2 * x-fee  
def x-clmdtl-fee-ohip-3 zoned*7 = x-sv-nbr3 * x-fee 
def x-clmdtl-fee-ohip-4 zoned*7 = x-sv-nbr4 * x-fee 

def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2]

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2]

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2]

sort on clmhdr-pat-ohip-id-or-chart

subfile drlevis keep include              &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-1                              ,&
x-sv-nbr1	                         ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code	                         ,& 
x-clmdtl-fee-ohip-1                      

subfile drlevis alias drlevis_svc2 append keep if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-2                              ,&
x-sv-nbr2                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code                               ,&
x-clmdtl-fee-ohip-2                     

subfile drlevis alias drlevis_svc3 append keep if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-3                              ,&
x-sv-nbr3                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code                               ,&
x-clmdtl-fee-ohip-3                      

subfile drlevis alias drlevis_svc4 append keep if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-4                              ,&
x-sv-nbr4                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code                               ,&
x-clmdtl-fee-ohip-4                      

request two

access *drlevis				 		  &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr, '00000' , '0'	  &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr,	  &
       key-clm-serv-code, key-clm-adj-nbr of f002-claims-mstr opt &
  link clmhdr-pat-ohip-id-or-chart to key-pat-mstr of f010-pat-mstr opt
 
def x-pat-name char*20 = pack(pat-surname+", "+pat-given-name)
def x-postal-cd cha*6  = pack(SUBSCR-POST-CODE1+" "+SUBSCR-POST-CODE2)

def x-sys-yy zoned*4 unsigned = ncon(ascii(sysdate,8)[1:4])
def x-sys-mm char*2 = ascii(sysdate,8)[5:2]
def x-sys-dd char*2 = ascii(sysdate,8)[7:2]

def pat-birth-yy zoned*4 unsigned = ncon(ascii(pat-birth-date,8)[1:4])
def pat-birth-mm char*2 = ascii(pat-birth-date,8)[5:2]
def pat-birth-dd char*2 = ascii(pat-birth-date,8)[7:2]

def x-age zoned*4 unsigned  = x-sys-yy - pat-birth-yy             &
        if   (x-sys-mm  > pat-birth-mm)                   &
          or ((x-sys-mm = pat-birth-mm) and               &
              (x-sys-dd >= pat-birth-dd))                 &
        else x-sys-yy - pat-birth-yy - 1

subfile drlevis1 keep include             &
clmdtl-id       of drlevis               ,&
x-pat-name                               ,&
pat-health-nbr                           ,&
pat-birth-date                            ,&
x-age                                    ,&
subscr-addr1                             ,&
subscr-addr2                             ,&
subscr-addr3                             ,&
x-postal-cd                              ,&
pat-prov-cd                              ,&
pat-country                              ,&
x-doc-nbr                                ,&
x-sv-date-1                              ,&
x-sv-nbr1	                         ,& 
clmdtl-diag-cd of drlevis                ,&
x-oma-code	                         ,& 
x-clmdtl-fee-ohip-1                      

build $obj/drlevis
