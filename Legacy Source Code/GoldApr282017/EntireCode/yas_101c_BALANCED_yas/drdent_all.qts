;doc     : drdent.qts
;purpose : pedriatric
;          create report patients under 18 (service date minus birth date)
;          from april 1/2000 to march 31st 2001 agent0 clinic all clinics
;          Pass 1 - select appropriate claims and "explode" repetitive services
;who     : Dr. Peter Dent


;Date		Who		Description
;2002/08/27	Yasemin		Original
;2002/08/27     yas             agent 0 and 4 only include "B" adjustments

cancel clear
set default
set process nolimit
set lock file update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr			
 

choose  key-clm-type 'B' ,  key-clm-batch-nbr 810327000 to 810327999,   &
                                              220327000 to 220327999,   &
                                              810357000 to 810357999,   &
                                              220357000 to 220357999,   &
                                              640357000 to 640357999,   &
                                              810684000 to 810684999,   &
                                              220684000 to 220684999,   &
                                              810861000 to 810861999,   &
                                              220861000 to 220861999,   &
                                              810707000 to 810707999,   &
                                              220707000 to 220707999,   &
                                              810097000 to 810097999,   &
                                              220097000 to 220097999,   &
                                              820097000 to 820097999,   &
        key-clm-claim-nbr,  key-clm-serv-code, key-clm-adj-nbr '0'



select f002-claims-mstr if (                                  &     
           (clmhdr-agent-cd = 0  or clmhdr-agent-cd = 4)      &
       and (clmhdr-batch-type = 'C'  or                       &
           (clmhdr-batch-type = 'A' and clmhdr-adj-cd = 'B')) &
                           )
        
subfile drdent keep include                      &
clmhdr-claim-id              of f002-claims-mstr,&
key-clm-batch-nbr            of f002-claims-mstr,& 
key-clm-claim-nbr            of f002-claims-mstr,& 
clmhdr-loc                   of f002-claims-mstr,& 
clmhdr-hosp                  of f002-claims-mstr,& 
clmhdr-pat-ohip-id-or-chart 

request two                             &
        on edit        errors report    &
        on calculation errors report

access *drdent                                                           &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                         &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr                &
          of f002-claims-mstr 

select f002-claims-mstr if                   &
            clmdtl-sv-date     >= '20000401'  & 
       and  clmdtl-sv-date     <= '20010331' &
       and  clmdtl-oma-cd      <> "0000"     & ; ignore claim header recs
       and  clmdtl-oma-cd      <> "ZZZZ"     & ; ignore description recs
       and  clmdtl-oma-cd      <> "PAID"     & ; no payments
       and  clmdtl-oma-cd      <> "MICM"     & ; no miscellaneous billings
       and  clmdtl-oma-cd      <> "MISJ"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICV"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISP"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MOHR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICB"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MIBR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MINH"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MHSC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "NHSC"     & ; "  "             "
       and  clmdtl-adj-nbr     = 0             ; ignore adjustments


def x-oma-code cha*5 = clmdtl-id[12:5]

def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne " 00"  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 unsigned =                                    &
                        nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr3 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[4:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-sv-nbr4 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[7:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

def x-fee = clmdtl-fee-ohip / x-nbr-svcs
def x-clmdtl-fee-ohip-1 zoned*7 = x-sv-nbr1 * x-fee
def x-clmdtl-fee-ohip-2 zoned*7 = x-sv-nbr2 * x-fee
def x-clmdtl-fee-ohip-3 zoned*7 = x-sv-nbr3 * x-fee
def x-clmdtl-fee-ohip-4 zoned*7 = x-sv-nbr4 * x-fee

def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-doc-nbr zoned*3 unsigned = nconvert(clmdtl-id[4:3])


sort on clmdtl-sv-date
                                         
subfile drdent1 keep include              &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-1                              ,&
x-sv-nbr1	                         ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code	                         ,& 
clmhdr-loc	      of drdent          ,& 
clmhdr-hosp           of drdent          ,& 
clmhdr-pat-ohip-id-or-chart of drdent    ,& 
x-clmdtl-fee-ohip-1                      

subfile drdent1 alias drdent_svc2 append keep if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-2                              ,&
x-sv-nbr2                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code                               ,&
clmhdr-loc            of drdent          ,&
clmhdr-hosp           of drdent          ,& 
clmhdr-pat-ohip-id-or-chart of drdent    ,&
x-clmdtl-fee-ohip-2                     

subfile drdent1 alias drdent_svc3 append keep if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-3                              ,&
x-sv-nbr3                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code                               ,&
clmhdr-loc            of drdent          ,&
clmhdr-hosp           of drdent          ,& 
clmhdr-pat-ohip-id-or-chart of drdent    ,&
x-clmdtl-fee-ohip-3                      

subfile drdent1 alias drdent_svc4 append keep if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-4                              ,&
x-sv-nbr4                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code                               ,&
clmhdr-loc            of drdent          ,&
clmhdr-hosp           of drdent          ,& 
clmhdr-pat-ohip-id-or-chart of drdent    ,&
x-clmdtl-fee-ohip-4                      

request three                           &
        on edit        errors report    &
        on calculation errors report

access *drdent1				 		                 &
  link clmhdr-pat-ohip-id-or-chart to key-pat-mstr of f010-pat-mstr opt
 
def x-pat-name char*42 = pack(pat-surname+", "+pat-given-name)

def clmdtl-sv-yy zoned*4 unsigned = nconvert(x-sv-date-1[1:4])
def clmdtl-sv-mm zoned*4 unsigned = nconvert(x-sv-date-1[5:2])
def clmdtl-sv-dd zoned*4 unsigned = nconvert(x-sv-date-1[7:2])

def x-age zoned*4 unsigned  =  clmdtl-sv-yy - pat-birth-date-yy     &
        if   ( clmdtl-sv-mm >  pat-birth-date-mm)                   &
          or ((clmdtl-sv-mm =  pat-birth-date-mm) and               &
              (clmdtl-sv-dd >= pat-birth-date-dd))                  &
        else clmdtl-sv-yy - pat-birth-date-yy - 1

subfile drdent2 keep include              &
clmdtl-id       of drdent1               ,&
pat-birth-date                           ,&
x-age                                    ,&
x-doc-nbr             of drdent1         ,&
x-sv-date-1           of drdent1         ,&
x-sv-nbr1	      of drdent1         ,& 
clmdtl-diag-cd        of drdent1         ,&
x-oma-code	      of drdent1         ,& 
clmhdr-loc	      of drdent1         ,& 
clmhdr-hosp           of drdent1         ,& 
x-clmdtl-fee-ohip-1   of drdent1         

build $obj/drdent
