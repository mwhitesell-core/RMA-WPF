;doc     : fellows.qts
;purpose : create a detail claims report for fellows  clinic 22 and 42 and department 21
;          report claim number, health number, pat surname, first name, billing code, service date,fee, clmhdr-status-ohip 
;          SELECT if ome-code = E100C and no agent 6
; *************************************************************

;Date		Who		Description
;2010/03/31	Yasemin	        original

cancel clear
set default
set process nolimit
set lock record update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr                   

choose  key-clm-type 'B' ,  key-clm-batch-nbr "22@",   &
                                              "42@",   &
        key-clm-claim-nbr, key-clm-serv-code '00000', key-clm-adj-nbr '0'

select f002-claims-mstr if       clmhdr-batch-type = 'C'        &
                             and clmhdr-agent-cd <> 6           &
                             and CLMHDR-DOC-DEPT = 21
 
subfile fellows_a     keep include                    &
clmhdr-claim-id               of f002-claims-mstr,&
key-clm-batch-nbr             of f002-claims-mstr,&
key-clm-claim-nbr             of f002-claims-mstr,&
clmhdr-doc-nbr-ohip           of f002-claims-mstr,&
clmhdr-loc                    of f002-claims-mstr,&
clmhdr-hosp                   of f002-claims-mstr,&
clmhdr-serv-date	      of f002-claims-mstr,&
CLMHDR-DATE-ADMIT      	      of f002-claims-mstr,&
CLMHDR-DATE-ADMIT      	      of f002-claims-mstr,&
clmhdr-status-ohip     	      of f002-claims-mstr,&
clmhdr-pat-ohip-id-or-chart   of f002-claims-mstr

request two                             &
        on edit        errors report    &
        on calculation errors report

access *fellows_a                                                    &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                         &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr                &
          of f002-claims-mstr

select f002-claims-mstr if                   &
            clmdtl-sv-date     >= '20070401' &
       and  clmdtl-oma-cd      <> "0000"     & ; details only
       and  clmdtl-oma-cd      <> "ZZZZ"     & ;   
       and  clmdtl-oma-cd      <> "PAID"     & ; no payments
       and  clmdtl-oma-cd      <> "MICM"     & ; no miscellaneous billings
       and  clmdtl-oma-cd      <> "MISJ"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICV"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISP"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MOHR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICB"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MIBR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MINH"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MHSC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "NHSC"     &
       and  clmdtl-oma-cd      <> "DHSC"     &
       and  clmdtl-oma-cd      <> "AGEP"     &
       and  clmdtl-oma-cd      <> "MICA"     &
       and  clmdtl-oma-cd      <> "MICC"     &
       and  clmdtl-oma-cd      <> "MICD"     &
       and  clmdtl-oma-cd      <> "MICE"     &
       and  clmdtl-oma-cd      <> "MICF"     &
       and  clmdtl-oma-cd      <> "MICG"     &
       and  clmdtl-oma-cd      <> "MICH"     &
       and  clmdtl-oma-cd      <> "MICJ"     &
       and  clmdtl-oma-cd      <> "MICK"     &
       and  clmdtl-oma-cd      <> "MICL"     &
       and  clmdtl-adj-nbr     =  0            ; exclude adjustments

def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne " 00"  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 unsigned =                                    &
                        nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr3 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[4:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-sv-nbr4 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[7:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

def x-fee = clmdtl-fee-ohip / x-nbr-svcs
def x-clmdtl-fee-ohip-1 zoned*7 = x-sv-nbr1 * x-fee    
def x-clmdtl-fee-ohip-2 zoned*7 = x-sv-nbr2 * x-fee  
def x-clmdtl-fee-ohip-3 zoned*7 = x-sv-nbr3 * x-fee 
def x-clmdtl-fee-ohip-4 zoned*7 = x-sv-nbr4 * x-fee 

def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2] &
                        if consec-flag = "Y"                              &
                        else " "


subfile fellows_1     keep include      &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
clmhdr-doc-nbr-ohip   of fellows_a      ,&
x-clmdtl-fee-ohip-1                      ,&
x-sv-date-1                              ,&
x-sv-nbr1                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of fellows_a      ,&
clmhdr-hosp           of fellows_a      ,&
clmhdr-status-ohip    of fellows_a      ,&
clmhdr-pat-ohip-id-or-chart of fellows_a      

subfile fellows_1 alias fellows_svc2 append keep if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
clmhdr-doc-nbr-ohip   of f002-claims-mstr,&
x-clmdtl-fee-ohip-2                      ,&
x-sv-date-2                              ,&
x-sv-nbr2                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of fellows_a           ,&
clmhdr-hosp           of fellows_a           ,&
clmhdr-status-ohip    of fellows_a      ,&
clmhdr-pat-ohip-id-or-chart of fellows_a    

subfile fellows_1 alias fellows_svc3 append keep if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
clmhdr-doc-nbr-ohip   of fellows_a      ,&
x-clmdtl-fee-ohip-3                      ,&
x-sv-date-3                              ,&
x-sv-nbr3                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of fellows_a           ,&
clmhdr-hosp           of fellows_a           ,&
clmhdr-status-ohip    of fellows_a      ,&
clmhdr-pat-ohip-id-or-chart of fellows_a   

subfile fellows_1 alias fellows_svc4 append keep if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
clmhdr-doc-nbr-ohip   of fellows_a      ,&
x-clmdtl-fee-ohip-4                      ,&
x-sv-date-4                              ,&
x-sv-nbr4                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of fellows_a           ,&
clmhdr-hosp           of fellows_a           ,&
clmhdr-status-ohip    of fellows_a      ,&
clmhdr-pat-ohip-id-or-chart of fellows_a   

request three                           &
        on edit        errors report    &
        on calculation errors report

access *fellows_1                                                         &
  link clmhdr-pat-ohip-id-or-chart to key-pat-mstr of f010-pat-mstr opt     &
  link clmdtl-id[3:3] to doc-nbr of f020-doctor-mstr opt                    

def x-claim-nbr cha*10 = clmdtl-id[1:10]
def x-code cha*5 = clmdtl-id[11:5]

select if x-code = "E001C"

def comma cha*1 = "~"
def x-num-cr integer unsigned size 2 = 13
def x-cr        char*2 = char(x-num-cr)

subfile fellows_2    portable keep  include          &
x-claim-nbr                                         ,&
comma                                               ,&
doc-dept                                            ,&
comma                                               ,&
doc-name                                            ,&
comma                                               ,&
doc-inits                                           ,&
comma                                               ,&
pat-health-nbr                                      ,&
comma                                               ,&
pat-version-cd                                      ,&
comma                                               ,&
PAT-SURNAME                                         ,&
comma                                               ,&
PAT-GIVEN-NAME                                      ,&
comma                                               ,&
clmdtl-oma-cd                                       ,&
comma                                               ,&
x-code                                              ,&
comma                                               ,&
x-sv-nbr1                                           ,&
comma                                               ,&
x-clmdtl-fee-ohip-1                                 ,&
comma                                               ,&
x-sv-date-1                                         ,&
comma                                               ,&
clmhdr-status-ohip                                  ,&
x-cr


build $obj/fellows
