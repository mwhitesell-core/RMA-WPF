;doc     : pediatric_total_billing_ped.qts
;purpose : create a detail claims report for all claims all clinics for         
;          department of pediatrics docs                                        
;          clinic 60 prof amount is recorded in 22 for under code T995 but T995
;          does not include the #services or what code it is   
;          included just clinic 61-65 prof fee and took out T995 
;          ******* INCLUDE ADJUSTMENTS *******
;          ***  ADJUSTMENT is included for if you select service date otherwise you have to change it for the PED selection
;who     : For Pediatric manager and Ken Macfarland

;
; *************************************************************

;Date		Who	       Description
;2006/10/18	Yasemin	       cardio_codes_adjust_payment.qts
;2010/01/07     MC	       to extract regular claim detail, adjustment detail 
;2010/02/04     MC	       Regular claim detail will have 0 in clmdtl-adj-nbr and key-clm-adj-nbr
;			       Adjustment claim detail will have 1 in clmdtl-adj-nbr and key-clm-adj-nbr
;			       Original adjustment claim detail will have 1 in clmdtl-adj-nbr and 0 in key-clm-adj-nbr
;			       All Adjustment claim detail will have the same service date as the original claim detail
;			       but the period date will be different (usaully later than the original claim detail one)
; Note:  It is okay to select regular or adjustment claim details by either service date or PED from the second pass below

cancel clear
set default
set process nolimit
set lock record update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr                   

choose  key-clm-type 'B' ,  key-clm-batch-nbr, key-clm-claim-nbr,  &
                    key-clm-serv-code '00000', key-clm-adj-nbr '0'

select f002-claims-mstr if    (    clmhdr-batch-type = 'C'             &   
			      )					       &
                          and (    clmhdr-doc-dept = 7                 &
                                or clmhdr-doc-dept = 70                &
                                or clmhdr-doc-dept = 71                &
                                or clmhdr-doc-dept = 72                &
                                or clmhdr-doc-dept = 73                &
                                or clmhdr-doc-dept = 74                &
                                or clmhdr-doc-dept = 75		       &
   		             )               

subfile pediatric_ped     keep include    &
clmhdr-claim-id                      ,&
key-clm-batch-nbr                    ,&
key-clm-claim-nbr                    ,&
clmhdr-loc        

request two                             &
        on edit        errors report    &
        on calculation errors report

access *pediatric_ped                                                &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                  &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr         &
          of f002-claims-mstr

select f002-claims-mstr if                   &
	   (clmdtl-date-period-end >= '20090401' and	&
	    clmdtl-date-period-end <= '20100331'	&
	   )						&
       and  clmdtl-oma-cd      <> "0000"     & ; details only
       and  clmdtl-oma-cd      <> "ZZZZ"     & ;   
       and  clmdtl-oma-cd      <> "PAID"     & ; no payments
       and  clmdtl-oma-cd      <> "MICM"     & ; no miscellaneous billings
       and  clmdtl-oma-cd      <> "MISJ"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICV"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISP"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICB"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MIBR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MINH"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MHSC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "NHSC"     &
       and  clmdtl-oma-cd      <> "DHSC"     &
       and  clmdtl-oma-cd      <> "MICA"     &
       and  clmdtl-oma-cd      <> "MICC"     &
       and  clmdtl-oma-cd      <> "MICD"     &
       and  clmdtl-oma-cd      <> "MICE"     &
       and  clmdtl-oma-cd      <> "MICF"     &
       and  clmdtl-oma-cd      <> "MICG"     &
       and  clmdtl-oma-cd      <> "MICH"     &
       and  clmdtl-oma-cd      <> "MICJ"     &
       and  clmdtl-oma-cd      <> "MICK"     &
       and  clmdtl-oma-cd      <> "MICL"     &
       and  clmdtl-oma-cd      <> "MOHR"     & ; exclude MOHR 
       and  clmdtl-oma-cd      <> "AGEP"     & ; exclude AGEP
       and  clmdtl-oma-cd      <> "T995"       ; clinic 60 prof fees
;      and  clmdtl-adj-nbr     =  0	       ; regular claim detail only 
;      and  clmdtl-adj-nbr     =  1	       ; adjustment or miscellaneous payment detail only

def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne " 00"  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

; regular claim detail (clmdtl-adj-nbr = 0) should have non-zero nbr-of-serv
; but for adjustment claim and misc payment  detail(clmdtl-adj-nbr = 1) nbr-serv and consecute date are always ZERO
define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 unsigned =                                    &
                        nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr3 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[4:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-sv-nbr4 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[7:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

def x-prof-fee = clmdtl-fee-ohip - clmdtl-amt-tech-billed & 
	if key-clm-batch-nbr[1:2] > '60' and key-clm-batch-nbr[1:2] < '66'  &
     else clmdtl-fee-ohip

def x-fee = x-prof-fee  / x-nbr-svcs


; regular claim detail (clmdtl-adj-nbr = 0) should have non-zero nbr-of-serv
; but for adjustment claim and misc payment  detail(clmdtl-adj-nbr = 1) nbr-serv and consecute date are always ZERO
def x-clmdtl-fee-ohip-1 zoned*7 = x-sv-nbr1 * x-fee     &
                 if clmdtl-adj-nbr = 0                   &
;        else    clmdtl-fee-ohip  - this may give wrong result
        else   x-prof-fee 

def x-clmdtl-fee-ohip-2 zoned*7 = x-sv-nbr2 * x-fee  
def x-clmdtl-fee-ohip-3 zoned*7 = x-sv-nbr3 * x-fee 
def x-clmdtl-fee-ohip-4 zoned*7 = x-sv-nbr4 * x-fee 

def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2] &
                        if consec-flag = "Y"                              &
                        else " "


subfile pediatric1_ped     keep include          &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-1                      ,&
x-sv-date-1                              ,&
x-sv-nbr1                                ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of pediatric_ped           

subfile pediatric1_ped alias pediatric_svc2 append keep if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-2                      ,&
x-sv-date-2                              ,&
x-sv-nbr2                                ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of pediatric_ped          

subfile pediatric1_ped alias pediatric_svc3 append keep if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-3                      ,&
x-sv-date-3                              ,&
x-sv-nbr3                                ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of pediatric_ped          

subfile pediatric1_ped alias pediatric_svc4 append keep if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-4                      ,&
x-sv-date-4                              ,&
x-sv-nbr4                                ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of pediatric_ped       

request three                           &
        on edit        errors report    &
        on calculation errors report


access *pediatric1_ped                                                         &
  link clmdtl-oma-cd to fee-oma-cd of f040-oma-fee-mstr opt             &
  link clmdtl-id[3:3] to doc-nbr of f020-doctor-mstr opt                &
  link (ascii(clmdtl-diag-cd,3)) to diag-cd of f091-diag-codes-mstr opt  

def x-clinic cha*2 = clmdtl-id[1:2]
def x-doc    cha*3 = clmdtl-id[3:3]


sort on x-clinic on x-doc on clmdtl-oma-cd

temp x-amt  zoned*9 signed
item x-amt  = x-amt  + x-clmdtl-fee-ohip-1     & 
              reset at clmdtl-oma-cd

temp x-svc  zoned*9 unsigned
item x-svc  = x-svc  + x-sv-nbr1               &
              reset at clmdtl-oma-cd

def comma cha*1 = "~"
def x-num-cr integer unsigned size 2 = 13
def x-cr        char*2 = char(x-num-cr)

subfile pediatric2_ped portable keep at clmdtl-oma-cd include            &
x-clinic                                            ,&
comma                                               ,&
x-doc                                               ,&
comma                                               ,&
doc-name                                            ,&
comma                                               ,&
doc-inits                                           ,&
comma                                               ,&
x-amt                                               ,&
comma                                               ,&
x-svc                                               ,&
comma                                               ,&
clmdtl-oma-cd                                       ,&
comma                                               ,&
fee-desc                                            ,&
comma                                               ,&
clmdtl-diag-cd                                      ,&
comma                                               ,&
diag-cd-desc                                        ,&
x-cr

build $obj/pediatric_total_billing_ped
