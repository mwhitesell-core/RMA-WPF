; DOC: JOHN3DOC.QZS
; DOC: REPORT BILLINGS AGENT 0 CLASS FULL & SCHOLARS BY MTD YTD FIGURES
; DOC: CREATED FOR PAUL DANAHUE

;     **** DO NOT MAKE CHANGES TO THIS WITHOUT CONSULTING YAS *****

;PROGRAM PURPOSE : PRINT REP BY DOC BY MONTH 4TH PASS
;                  (JOHN.QZS JOHN1.QZS JOHN2.QZS)
;
;DATE       BY WHOM      DESCRIPTION
;93/09/28   YASEMIN      ORIGINAL
;93/04/01   YASEMIN      MODIFY FOR 1994
;95/03/29   YASEMIN      MODIFY FOR 1995
;96/03/25   YASEMIN      MODIFY FOR 1996
;97/04/18   YASEMIN      MODIFY FOR 1997
;98/04/16   YASEMIN      MODIFY FOR 1998
;99/03/25   YASEMIN      MODIFY FOR 1999
;00/03/30   yasemin      modify for 2000
;01/03/23   yasemin      modify for 2001
;02/03/25   yasemin      modify for 2002
;mar/27/03  yasemin      modify for 2003

can clear
set rep nolimit
set page length 62 width 132
set rep device disc name john3doc
set formfeed
set noblank

access *john1			          &
   link x-doc-nbr 		          &
   to   doc-nbr of f020-doctor-mstr

sort   on doc-dept        	  &
       on x-doc-nbr               &
       on x-sort-seq		  &
       on x-mm


def x-mmm   cha*9 = "JANUARY"   if x-mm  = "01"      &
               else "FEBRUARY"  if x-mm  = "02"      &
               else "MARCH"     if x-mm  = "03"      &
               else "APRIL"     if x-mm  = "04"      &
               else "MAY"       if x-mm  = "05"      &
               else "JUNE"      if x-mm  = "06"      &
               else "JULY"      if x-mm  = "07"      &
               else "AUGUST"    if x-mm  = "08"      &
               else "SEPTEMBER" if x-mm  = "09"      &
               else "OCTOBER"   if x-mm  = "10"      &
               else "NOVEMBER"  if x-mm  = "11"      &
               else "DECEMBER"  if x-mm  = "12"

def x-doc-name cha*30 = pack(doc-name +" "+ doc-inits)

def x-diff-mth =  x-mth-apr03-mar04 - x-mth-apr02-mar03

def x-diff-ytd =  x-ytd-apr03-mar04 - x-ytd-apr02-mar03

def x-perc-mth	int*8 signed				              &
       = round((x-diff-mth / x-mth-apr02-mar03) * 100,0,near)	      &
		if  x-mth-apr02-mar03 ne 0                            &
    else 100

def x-perc-ytd	int*8 signed				               &
       = round((x-diff-ytd / x-ytd-apr02-mar03) * 100,0,near)	       &
		if x-ytd-apr02-mar03 ne 0                              &
    else 100

page heading                                              &
        tab 1    sysdate                                  &
        tab 42  "MONTH AND YTD BILLINGS FOR APRIL 2002 - MARCH 2004" &
        tab 127  syspage                                  &
skip 2  tab 42   "DOC DEPT/NBR:"                          &
        tab 56   doc-dept fill "0"	                  &
        tab 58   "/"                                      &
        tab 59   x-doc-nbr fill "0" pic "^^^"             &
        tab 65   x-doc-name                               &
skip 3  tab 12   "MONTH TOTAL"                            &
        tab 31   "MONTH TOTAL"                            &
        tab 74   "YTD TOTAL"                              &
        tab 93   "YTD TOTAL"                              &
skip 1  tab 1    "MONTH"                                  &
        tab 12   "APR2002-MAR2003"                        &
        tab 31   "APR2003-MAR2004"                        &
        tab 49   "DIFFERENCE"                             &
        tab 62   "PERCENT"                                &
        tab 74   "APR2002-MAR2003"                        &
        tab 93   "APR2003-MAR2004"                        &
        tab 111  "DIFFERENCE"                             &
        tab 124  "PERCENT"                                &
skip 1                                                    &
        tab 1    "-----"                                  &
        tab 12   "---------------"                        &
        tab 31   "---------------"                        &
        tab 49   "----------"                             &
        tab 62   "-------"                                &
        tab 74   "---------------"                        &
        tab 93   "---------------"                        &
        tab 111  "----------"                             &
        tab 124  "-------"                                &
skip 2

heading at x-doc-nbr skip page

report                                                    &
	tab 1    x-mmm                      	  	  &
	tab 11   x-mth-apr02-mar03 pic "^,^^^,^^^.^^"	  &
	tab 30   x-mth-apr03-mar04 pic "^,^^^,^^^.^^"	  &
	tab 45   x-diff-mth        pic "^^,^^^^,^^^.^^"	  &
 	tab 62   x-perc-mth        pic "^^^^^ %"          &
	tab 73   x-ytd-apr02-mar03 pic "^,^^^,^^^.^^" 	  &
 	tab 92   x-ytd-apr03-mar04 pic "^,^^^,^^^.^^" 	  &
        tab 108  x-diff-ytd        pic "^^,^^^,^^^.^^" 	  &
 	tab 124  x-perc-ytd        pic "^^^^^ %"          &
skip 2

build $pb_obj/john3doc
  
