;doc     : clinic_71_billings.qts          
;purpose : create a detail claims report for all clinic 71 proff fee only (report proff and tech)      
;who     : leena                                        

;
; *************************************************************

;Date		Who	       Description
;2011/06/13	Yasemin	       detail_peds_billings_ped.qts  
;2010/01/07     MC	       to extract regular claim detail, adjustment detail 
;2010/02/04     MC	       Regular claim detail will have 0 in clmdtl-adj-nbr and key-clm-adj-nbr
;			       Adjustment claim detail will have 1 in clmdtl-adj-nbr and key-clm-adj-nbr
;			       Original adjustment claim detail will have 1 in clmdtl-adj-nbr and 0 in key-clm-adj-nbr
;			       All Adjustment claim detail will have the same service date as the original claim detail
;			       but the period date will be different (usaully later than the original claim detail one)
; Note:  It is okay to select regular or adjustment claim details by either service date or PED from the second pass below
;

cancel clear
set default
set process nolimit
set lock record update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr                   

choose  key-clm-type 'B' ,  key-clm-batch-nbr "71@", key-clm-claim-nbr,  &
                    key-clm-serv-code '00000', key-clm-adj-nbr '0'

select f002-claims-mstr if clmhdr-batch-type = 'C'             

subfile clinic71      keep include    &
clmhdr-claim-id                      ,&
key-clm-batch-nbr                    ,&
key-clm-claim-nbr                    ,&
clmhdr-loc        

request two                             &
        on edit        errors report    &
        on calculation errors report

access *clinic71                                                  &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                  &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr         &
          of f002-claims-mstr

select f002-claims-mstr if                   &
            clmdtl-sv-date     >= '20100701' &
       and  clmdtl-sv-date     <= '20110630' &       
       and  clmdtl-oma-cd      <> "0000"     & ; details only
       and  clmdtl-oma-cd      <> "ZZZZ"     & ;   
       and  clmdtl-oma-cd      <> "PAID"     & ; no payments
       and  clmdtl-oma-cd      <> "MICM"     & ; no miscellaneous billings
       and  clmdtl-oma-cd      <> "MISJ"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICV"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISP"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICB"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MIBR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MINH"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MHSC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "NHSC"     &
       and  clmdtl-oma-cd      <> "DHSC"     &
       and  clmdtl-oma-cd      <> "MICA"     &
       and  clmdtl-oma-cd      <> "MICC"     &
       and  clmdtl-oma-cd      <> "MICD"     &
       and  clmdtl-oma-cd      <> "MICE"     &
       and  clmdtl-oma-cd      <> "MICF"     &
       and  clmdtl-oma-cd      <> "MICG"     &
       and  clmdtl-oma-cd      <> "MICH"     &
       and  clmdtl-oma-cd      <> "MICJ"     &
       and  clmdtl-oma-cd      <> "MICK"     &
       and  clmdtl-oma-cd      <> "MICL"     &
       and  clmdtl-oma-cd      <> "MOHR"     & ; exclude MOHR 
       and  clmdtl-oma-cd      <> "AGEP"     & ; exclude AGEP
       and  clmdtl-oma-cd      <> "T995"     & ; clinic 60 prof fees
       and  clmdtl-adj-nbr     =  0	       ; regular claim detail only 
;      and  clmdtl-adj-nbr     =  1	       ; adjustment or miscellaneous payment detail only

def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne " 00"  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "00 "  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

; regular claim detail (clmdtl-adj-nbr = 0) should have non-zero nbr-of-serv
; but for adjustment claim and misc payment  detail(clmdtl-adj-nbr = 1) nbr-serv and consecute date are always ZERO
define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 unsigned =                                    &
                        nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr3 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[4:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-sv-nbr4 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[7:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4
;;;;;;Print proff amounts
def x-prof-fee = clmdtl-fee-ohip - clmdtl-amt-tech-billed & 
	if key-clm-batch-nbr[1:2] > '70' and key-clm-batch-nbr[1:2] < '76'  &
     else clmdtl-fee-ohip

def x-fee = x-prof-fee  / x-nbr-svcs if clmdtl-adj-nbr = 0 &
      else x-prof-fee

; regular claim detail (clmdtl-adj-nbr = 0) should have non-zero nbr-of-serv
; but for adjustment claim and misc payment  detail(clmdtl-adj-nbr = 1) nbr-serv and consecute date are always ZERO
def x-clmdtl-fee-ohip-1 zoned*7 = x-sv-nbr1 * x-fee     &
                 if clmdtl-adj-nbr = 0                   &
        else   x-prof-fee 

def x-clmdtl-fee-ohip-2 zoned*7 = x-sv-nbr2 * x-fee  
def x-clmdtl-fee-ohip-3 zoned*7 = x-sv-nbr3 * x-fee 
def x-clmdtl-fee-ohip-4 zoned*7 = x-sv-nbr4 * x-fee 

;;;;;;Print tech  amounts
def x-tech = clmdtl-amt-tech-billed / x-nbr-svcs if clmdtl-adj-nbr = 0 &
	else clmdtl-amt-tech-billed

def x-clmdtl-fee-tech-1 zoned*7 = x-sv-nbr1 * x-tech     &
                 if clmdtl-adj-nbr = 0                   &
        else   x-tech      

def x-clmdtl-fee-tech-2 zoned*7 = x-sv-nbr2 * x-tech  
def x-clmdtl-fee-tech-3 zoned*7 = x-sv-nbr3 * x-tech 
def x-clmdtl-fee-tech-4 zoned*7 = x-sv-nbr4 * x-tech 

def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2] &
                        if consec-flag = "Y"                              &
                        else " "


subfile clinic711   keep include          &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-1                      ,&
x-clmdtl-fee-tech-1                      ,&
x-sv-date-1                              ,&
x-sv-nbr1                                ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of clinic71           

subfile clinic711 alias clinic71_svc2 append keep if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-2                      ,&
x-clmdtl-fee-tech-2                      ,&
x-sv-date-2                              ,&
x-sv-nbr2                                ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of clinic71          

subfile clinic711 alias clinic71_svc3 append keep if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-3                      ,&
x-clmdtl-fee-tech-3                      ,&
x-sv-date-3                              ,&
x-sv-nbr3                                ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of clinic71          

subfile clinic711 alias clinic71_svc4 append keep if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-4                      ,&
x-clmdtl-fee-tech-4                      ,&
x-sv-date-4                              ,&
x-sv-nbr4                                ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of clinic71       

request three                           &
        on edit        errors report    &
        on calculation errors report


access *clinic711                                                       &
  link clmdtl-oma-cd to fee-oma-cd of f040-oma-fee-mstr opt             &
  link clmdtl-id[3:3] to doc-nbr of f020-doctor-mstr opt                &
  link (ascii(clmdtl-diag-cd,3)) to diag-cd of f091-diag-codes-mstr opt  

def x-clinic cha*2 = clmdtl-id[1:2]
def x-doc    cha*3 = clmdtl-id[3:3]

def comma cha*1 = "~"
def x-num-cr integer unsigned size 2 = 13
def x-cr        char*2 = char(x-num-cr)

subfile clinic712      portable keep include         &
clmdtl-id                                           ,&
comma                                               ,&
x-clinic                                            ,&
comma                                               ,&
x-doc                                               ,&
comma                                               ,&
doc-name                                            ,&
comma                                               ,&
doc-inits                                           ,&
comma                                               ,&
x-sv-date-1                                         ,&
comma                                               ,&
x-clmdtl-fee-ohip-1                                 ,&
comma                                               ,&
x-clmdtl-fee-tech-1                                 ,&
comma                                               ,&
x-sv-nbr1                                           ,&
comma                                               ,&
clmdtl-oma-cd                                       ,&
comma                                               ,&
fee-tech-ind                                        ,&
comma                                               ,&
fee-desc                                            ,&
comma                                               ,&
clmhdr-loc                                          ,&
;clmdtl-diag-cd                                      ,&
;diag-cd-desc                                        ,&
x-cr

build $obj/clinic71_billings
