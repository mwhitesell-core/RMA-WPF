;doc     : demaria_dtl.qts
;purpose : rep billings from 2005/10/01 to 2007/12/31
;who     : Leena - to give to Demaria

;
; *************************************************************

;Date		Who		Description
;2008/06/10     Yasemin	        

cancel clear
set default
set process nolimit
set lock file update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr                   

choose  key-clm-type 'B' ,  key-clm-batch-nbr "91739@",                  &
                                              "94739@",                  &
                                              "91953@",                  &
                                              "94953@",                  &
                                              "91H27@",                  &
                                              "94H27@",                  &
 key-clm-claim-nbr, key-clm-serv-code '00000', key-clm-adj-nbr '0'

select f002-claims-mstr if       clmhdr-batch-type = 'C'    

subfile demaria     keep include                    &
clmhdr-claim-id               of f002-claims-mstr,&
key-clm-batch-nbr             of f002-claims-mstr,&
key-clm-claim-nbr             of f002-claims-mstr,&
clmhdr-loc                    of f002-claims-mstr,&
clmhdr-hosp                   of f002-claims-mstr,&
clmhdr-pat-ohip-id-or-chart

request two                             &
        on edit        errors report    &
        on calculation errors report

access *demaria                                                          &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                         &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr                &
          of f002-claims-mstr

select f002-claims-mstr if                   &
            clmdtl-sv-date     >= '20051001' &
       and  clmdtl-sv-date     <= '20071231' &
       and  clmdtl-oma-cd      <> "0000"     & ; details only
       and  clmdtl-oma-cd      <> "ZZZZ"     & ;   
       and  clmdtl-oma-cd      <> "PAID"     & ; no payments
       and  clmdtl-oma-cd      <> "MICM"     & ; no miscellaneous billings
       and  clmdtl-oma-cd      <> "MISJ"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICV"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISP"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MOHR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICB"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MIBR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MINH"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MHSC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "NHSC"     &
       and  clmdtl-oma-cd      <> "DHSC"     &
       and  clmdtl-oma-cd      <> "AGEP"     &
       and  clmdtl-oma-cd      <> "MICA"     &
       and  clmdtl-oma-cd      <> "MICC"     &
       and  clmdtl-oma-cd      <> "MICD"     &
       and  clmdtl-oma-cd      <> "MICE"     &
       and  clmdtl-oma-cd      <> "MICF"     &
       and  clmdtl-oma-cd      <> "MICG"     

def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne " 00"  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 unsigned =                                    &
                        nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr3 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[4:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-sv-nbr4 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[7:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

def x-fee = clmdtl-fee-ohip / x-nbr-svcs
def x-clmdtl-fee-ohip-1 zoned*7 = x-sv-nbr1 * x-fee    
def x-clmdtl-fee-ohip-2 zoned*7 = x-sv-nbr2 * x-fee  
def x-clmdtl-fee-ohip-3 zoned*7 = x-sv-nbr3 * x-fee 
def x-clmdtl-fee-ohip-4 zoned*7 = x-sv-nbr4 * x-fee 

def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2] &
                        if consec-flag = "Y"                              &
                        else " "


subfile demaria1     keep include           &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-1                      ,&
x-sv-date-1                              ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of demaria           ,&
clmhdr-hosp           of demaria           ,&
clmhdr-pat-ohip-id-or-chart of demaria      

subfile demaria1 alias demaria_svc2 append keep if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-2                      ,&
x-sv-date-2                              ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of demaria           ,&
clmhdr-hosp           of demaria           ,&
clmhdr-pat-ohip-id-or-chart of demaria    

subfile demaria1 alias demaria_svc3 append keep if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-3                      ,&
x-sv-date-3                              ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of demaria           ,&
clmhdr-hosp           of demaria           ,&
clmhdr-pat-ohip-id-or-chart of demaria   

subfile demaria1 alias demaria_svc4 append keep if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-4                      ,&
x-sv-date-4                              ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of demaria           ,&
clmhdr-hosp           of demaria           ,&
clmhdr-pat-ohip-id-or-chart of demaria   

request three                           &
        on edit        errors report    &
        on calculation errors report


access *demaria1                                                          &
  link clmhdr-pat-ohip-id-or-chart to key-pat-mstr of f010-pat-mstr opt   &
  link clmdtl-id[3:3] to doc-nbr of f020-doctor-mstr opt                    

def x-clinic cha*2 = clmdtl-id[1:2]
def x-doc    cha*3 = clmdtl-id[3:3]
def x-pat-name char*42 = pack(pat-surname+", "+pat-given-name)

sort on x-sv-date-1     

def clmdtl-sv-yy zoned*4 unsigned = nconvert(x-sv-date-1[1:4])
def clmdtl-sv-mm zoned*4 unsigned = nconvert(x-sv-date-1[5:2])
def clmdtl-sv-dd zoned*4 unsigned = nconvert(x-sv-date-1[7:2])

def x-age zoned*4 unsigned  =  clmdtl-sv-yy - pat-birth-date-yy     &
        if   ( clmdtl-sv-mm >  pat-birth-date-mm)                   &
          or ((clmdtl-sv-mm =  pat-birth-date-mm) and               &
              (clmdtl-sv-dd >= pat-birth-date-dd))                  &
        else clmdtl-sv-yy - pat-birth-date-yy - 1


def comma cha*1 = "~"
def x-num-cr integer unsigned size 2 = 13
def x-cr        char*2 = char(x-num-cr)

subfile demaria2 portable keep  include              &
clmdtl-id                                           ,&
comma                                               ,&
x-clinic                                            ,&
comma                                               ,&
x-doc                                               ,&
comma                                               ,&
pat-birth-date                                      ,&
comma                                               ,&
x-age                                               ,&
comma                                               ,&
x-pat-name                                          ,&
comma                                               ,&
doc-inits                                           ,&
comma                                               ,&
x-sv-date-1                                         ,&
comma                                               ,&
clmdtl-oma-cd                                       ,&
comma                                               ,&
x-clmdtl-fee-ohip-1                                 ,&
comma                                               ,&
clmdtl-diag-cd                                      ,&
x-cr

build $obj/demaria_dtl
