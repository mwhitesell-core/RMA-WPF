;doc     : clinic_60_70_billings.qts          
;purpose : create a list of all MUMC MDU billings submitted by RMA (by service date) for the period of April 1/14 to Aug 31/14
;who     : Yasemin                                      
;
; *************************************************************

;Date		Who	       Description
;2014/09/17	MC             extract regular claim detail & adjustment detail
;                              Regular claim detail will have 0 in clmdtl-adj-nbr and key-clm-adj-nbr
;			       Adjustment claim detail will have 1 in clmdtl-adj-nbr and key-clm-adj-nbr
;			       All Adjustment claim detail will have the same service date as the original claim detail
;			       but the period date will be different (usaully later than the original claim detail one)
; Note:  It is okay to select regular or adjustment claim details by either service date or PED from the second pass below
;

cancel clear
set default
set process nolimit
set lock record update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr                   

choose  key-clm-type 'B' ,  key-clm-batch-nbr "61@" to "66@", "71@" to "75@",    &
        key-clm-claim-nbr,  key-clm-serv-code '00000', key-clm-adj-nbr '0'

select f002-claims-mstr if clmhdr-batch-type = 'C'             &
                    and   (clmhdr-serv-date >= 20140401 and clmhdr-serv-date <= 20140831)

subfile clinic_hdr    keep include    &
key-clm-batch-nbr                    ,&
key-clm-claim-nbr                    ,&
clmhdr-loc        		     ,&
clmhdr-submit-date		     ,&
clmhdr-i-o-pat-ind		     ,&
clmhdr-pat-ohip-id-or-chart  	     ,&
clmhdr-tot-claim-ar-ohip

request two                             &
        on edit        errors report    &
        on calculation errors report

access *clinic_hdr                                                & 
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                  &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr         &
          of f002-claims-mstr

select f002-claims-mstr if                   &
            clmdtl-sv-date     >= '20140401' &
       and  clmdtl-sv-date     <= '20140831' &       
       and  clmdtl-oma-cd      <> "0000"     & ; details only
       and  clmdtl-oma-cd      <> "ZZZZ"     & ;   
       and  clmdtl-oma-cd      <> "PAID"     & ; no payments
       and  clmdtl-oma-cd      <> "MICM"     & ; no miscellaneous billings
       and  clmdtl-oma-cd      <> "MISJ"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICV"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISP"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICB"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MIBR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MINH"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MHSC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "NHSC"     &
       and  clmdtl-oma-cd      <> "DHSC"     &
       and  clmdtl-oma-cd      <> "MICA"     &
       and  clmdtl-oma-cd      <> "MICC"     &
       and  clmdtl-oma-cd      <> "MICD"     &
       and  clmdtl-oma-cd      <> "MICE"     &
       and  clmdtl-oma-cd      <> "MICF"     &
       and  clmdtl-oma-cd      <> "MICG"     &
       and  clmdtl-oma-cd      <> "MICH"     &
       and  clmdtl-oma-cd      <> "MICJ"     &
       and  clmdtl-oma-cd      <> "MICK"     &
       and  clmdtl-oma-cd      <> "MICL"     &
       and  clmdtl-oma-cd      <> "MOHR"     & ; exclude MOHR 
       and  clmdtl-oma-cd      <> "AGEP"     & ; exclude AGEP
       and  clmdtl-oma-cd      <> "T995"   ; & ; clinic 60 prof fees
;      and  clmdtl-adj-nbr     =  0	       ; regular claim detail only 
;      and  clmdtl-adj-nbr     =  1	       ; adjustment or miscellaneous payment detail only

def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne " 00"  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "00 "  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

; regular claim detail (clmdtl-adj-nbr = 0) should have non-zero nbr-of-serv
; but for adjustment claim and misc payment  detail(clmdtl-adj-nbr = 1) nbr-serv and consecute date are always ZERO

def x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2  zoned*2 unsigned =                                   &
                        nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr3  zoned*2 unsigned =                                   &
                        nconvert(ascii(clmdtl-consec-dates,9)[4:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr4  zoned*2 unsigned =                                   &
                        nconvert(ascii(clmdtl-consec-dates,9)[7:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

; determine professional amount
def x-prof-fee   zoned*7 signed = clmdtl-fee-ohip - clmdtl-amt-tech-billed  

def x-each-prof  zoned*7 signed = x-prof-fee  / x-nbr-svcs 	&
				  if clmdtl-adj-nbr = 0 	&
		      	     else x-prof-fee

; regular claim detail (clmdtl-adj-nbr = 0) should have non-zero nbr-of-serv
; but for adjustment claim and misc payment  detail(clmdtl-adj-nbr = 1) nbr-serv and consecute date are always ZERO

def x-clmdtl-fee-ohip-1 zoned*7 signed = x-sv-nbr1 * x-each-prof     		&
                 			 if clmdtl-adj-nbr = 0           	&
		           	  else   x-prof-fee 

def x-clmdtl-fee-ohip-2 zoned*7 signed = x-sv-nbr2 * x-each-prof  
def x-clmdtl-fee-ohip-3 zoned*7 signed = x-sv-nbr3 * x-each-prof 
def x-clmdtl-fee-ohip-4 zoned*7 signed = x-sv-nbr4 * x-each-prof 

; determine  technical  amount
def x-each-tech zoned*7 signed = clmdtl-amt-tech-billed / x-nbr-svcs 	&
				 if clmdtl-adj-nbr = 0 			&
		            else clmdtl-amt-tech-billed


def x-clmdtl-fee-tech-1 zoned*7 signed = x-sv-nbr1 * x-each-tech     &
                			 if clmdtl-adj-nbr = 0       &
        			  else   clmdtl-amt-tech-billed

def x-clmdtl-fee-tech-2 zoned*7 signed = x-sv-nbr2 * x-each-tech  
def x-clmdtl-fee-tech-3 zoned*7 signed = x-sv-nbr3 * x-each-tech 
def x-clmdtl-fee-tech-4 zoned*7 signed = x-sv-nbr4 * x-each-tech 

def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2] &
                        if consec-flag = "Y"                              &
                        else " "

def oma-cd-suff char*5 = clmdtl-oma-cd + clmdtl-oma-suff

subfile clinic_dtl  keep include          &
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
key-clm-adj-nbr       of f002-claims-mstr,&
x-clmdtl-fee-ohip-1                      ,&
x-clmdtl-fee-tech-1                      ,&
x-sv-date-1                              ,&
x-sv-nbr1                                ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
oma-cd-suff                              ,&
clmhdr-submit-date    of clinic_hdr      ,&
clmhdr-i-o-pat-ind    of clinic_hdr      ,&
clmhdr-pat-ohip-id-or-chart of clinic_hdr     ,&
clmhdr-tot-claim-ar-ohip    of clinic_hdr     ,&
clmhdr-loc            of clinic_hdr         

subfile clinic_dtl  alias clinic_dtl_svc2 append keep if  x-sv-nbr2 <> 0 include &
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
key-clm-adj-nbr       of f002-claims-mstr,&
x-clmdtl-fee-ohip-2                      ,&
x-clmdtl-fee-tech-2                      ,&
x-sv-date-2                              ,&
x-sv-nbr2                                ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
oma-cd-suff                              ,&
clmhdr-submit-date    of clinic_hdr      ,&
clmhdr-i-o-pat-ind    of clinic_hdr      ,&
clmhdr-pat-ohip-id-or-chart of clinic_hdr     ,&
clmhdr-tot-claim-ar-ohip    of clinic_hdr     ,&
clmhdr-loc            of clinic_hdr        

subfile clinic_dtl alias clinic_dtl_svc3 append keep if  x-sv-nbr3 <> 0 include &
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
key-clm-adj-nbr       of f002-claims-mstr,&
x-clmdtl-fee-ohip-3                      ,&
x-clmdtl-fee-tech-3                      ,&
x-sv-date-3                              ,&
x-sv-nbr3                                ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
oma-cd-suff                              ,&
clmhdr-submit-date    of clinic_hdr      ,&
clmhdr-i-o-pat-ind    of clinic_hdr      ,&
clmhdr-pat-ohip-id-or-chart of clinic_hdr     ,&
clmhdr-tot-claim-ar-ohip    of clinic_hdr     ,&
clmhdr-loc            of clinic_hdr        

subfile clinic_dtl alias clinic_dtl_svc4 append keep if  x-sv-nbr4 <> 0 include &
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
key-clm-adj-nbr       of f002-claims-mstr,&
x-clmdtl-fee-ohip-4                      ,&
x-clmdtl-fee-tech-4                      ,&
x-sv-date-4                              ,&
x-sv-nbr4                                ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
oma-cd-suff                              ,&
clmhdr-submit-date    of clinic_hdr      ,&
clmhdr-i-o-pat-ind    of clinic_hdr      ,&
clmhdr-pat-ohip-id-or-chart of clinic_hdr     ,&
clmhdr-tot-claim-ar-ohip    of clinic_hdr     ,&
clmhdr-loc            of clinic_hdr     

request three                           &
        on edit        errors report    &
        on calculation errors report


access *clinic_dtl                                                      &
  link key-clm-batch-nbr[3:3] to doc-nbr of f020-doctor-mstr opt        &
  link clmhdr-pat-ohip-id-or-chart to key-pat-mstr of f010-pat-mstr opt & 
  link clmhdr-loc to loc-nbr of f030-locations-mstr opt                 &
  link (ascii(clmdtl-diag-cd,3)) to diag-cd of f091-diag-codes-mstr opt

def x-clinic     cha*2  = key-clm-batch-nbr[1:2]
def x-claim-nbr  cha*10 = key-clm-batch-nbr + ascii(key-clm-claim-nbr,02)

def comma 	 cha*1 = "~"
def x-num-cr     integer unsigned size 2 = 13
def x-cr         char*2 = char(x-num-cr)

subfile clinic_billings  portable keep include       &
x-clinic                                            ,&
comma                                               ,&
x-claim-nbr                                         ,&
comma                                               ,&
key-clm-adj-nbr					    ,&
comma                                               ,&
doc-ohip-nbr                                        ,&
comma                                               ,&
doc-name                                            ,&
comma                                               ,&
doc-inits                                           ,&
comma                                               ,&
x-sv-date-1                                         ,&
comma                                               ,&
x-clmdtl-fee-ohip-1                                 ,&
comma                                               ,&
x-clmdtl-fee-tech-1                                 ,&
comma                                               ,&
oma-cd-suff                                         ,&
comma                                               ,&
clmdtl-diag-cd                                      ,&
comma                                               ,&
diag-cd-desc                                        ,&
comma                                               ,&
clmhdr-i-o-pat-ind                                  ,&
comma                                               ,&
clmhdr-submit-date                                  ,&
comma                                               ,&
clmhdr-loc                                          ,&
comma                                               ,&
loc-name                                            ,&
comma                                               ,&
pat-health-nbr                                      ,&
comma                                               ,&
pat-version-cd                                      ,&
comma                                               ,&
pat-birth-date                                      ,&
x-cr

build $obj/clinic_60_70_billings
