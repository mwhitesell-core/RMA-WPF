;doc     : g040_code.qts
;purpose : The Pain Care Clinic does a "Urine Drug Test" (G040) for which most of the funds go towards the kit 
;          that they use for the testing.  Dr. Al-Jazrawi has requested that we include in our Statement 
;          to them the revenue amount for the G040 per physician.  Yasemin suggested that she could write a program 
;          to extract the G040 information for each physician each month.  Helena to included this info in the monthly
;          statement to the clinic.  
;who     : Helena               
;
; *************************************************************

cancel clear
set default
set process nolimit
set lock record update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr

choose  key-clm-type 'B' ,  key-clm-batch-nbr "26@",                 &
                                              "98@",                 &
 key-clm-claim-nbr, key-clm-serv-code '00000', key-clm-adj-nbr '0'

select f002-claims-mstr if         clmhdr-batch-type  = 'C'   &
                              and  clmhdr-doc-dept = 80

subfile g040     keep include              &
clmhdr-claim-id              of f002-claims-mstr,&
key-clm-batch-nbr            of f002-claims-mstr,&
key-clm-claim-nbr            of f002-claims-mstr,&
clmhdr-agent-cd              of f002-claims-mstr,&
clmhdr-batch-type            of f002-claims-mstr

request two                             &
        on edit        errors report    &
        on calculation errors report

access *g040                                                             &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr, 'G040A'                &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr,               &
       key-clm-serv-code                                                 &
          of f002-claims-mstr

def x-yyyymm char*6  = parm prompt 'Enter PED (YYYYMM): '

select f002-claims-mstr if                                              &
            clmdtl-date-period-end[1:6] = x-yyyymm                      
;       and  clmdtl-oma-cd      =  "G271"                               &
;       and  clmdtl-adj-nbr     =  0

def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne " 00"  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 unsigned =                                    &
                        nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr3 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[4:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-sv-nbr4 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[7:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

; regular claim detail (clmdtl-adj-nbr = 0) should have non-zero nbr-of-serv
; but for adjustment claim and misc payment  detail(clmdtl-adj-nbr = 1) nbr-serv and consecute date are always ZERO

def x-fee = clmdtl-fee-ohip  / x-nbr-svcs if clmdtl-adj-nbr = 0 &
      else clmdtl-fee-ohip

def x-clmdtl-fee-ohip-1 zoned*7 = x-sv-nbr1 * x-fee     &
                 if clmdtl-adj-nbr = 0                   &
        else   x-fee

def x-clmdtl-fee-ohip-2 zoned*7 = x-sv-nbr2 * x-fee  
def x-clmdtl-fee-ohip-3 zoned*7 = x-sv-nbr3 * x-fee 
def x-clmdtl-fee-ohip-4 zoned*7 = x-sv-nbr4 * x-fee 

def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2] &
                        if consec-flag = "Y"                              &
                        else " "

subfile g0401     keep include            &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-1                      ,&
x-sv-date-1                              ,&
clmdtl-oma-cd         of f002-claims-mstr

subfile g0401 alias code_svc2 append keep if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-2                      ,&
x-sv-date-2                              ,&
clmdtl-oma-cd         of f002-claims-mstr

subfile g0401 alias code_svc3 append keep if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-3                      ,&
x-sv-date-3                              ,&
clmdtl-oma-cd         of f002-claims-mstr

subfile g0401 alias code_svc4 append keep if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-4                      ,&
x-sv-date-4                              ,&
clmdtl-oma-cd         of f002-claims-mstr

request three                           &
        on edit        errors report    &
        on calculation errors report


access *g0401                                                          &
  link clmdtl-id[3:3] to doc-nbr of f020-doctor-mstr opt                    

def x-clinic cha*2 = clmdtl-id[1:2]
def x-doc    cha*3 = clmdtl-id[3:3]

;sort on x-doc on x-sv-date-1     
sort on x-clinic on x-doc on x-sv-date-1     


temp x-g040   num*7
item x-g040   = x-g040 + x-clmdtl-fee-ohip-1     &
              reset at x-doc    

temp x-g040-count   num*7
item x-g040-count   = x-g040-count + 1           &
                    reset at x-doc

def comma cha*1 = "~"
def x-num-cr integer unsigned size 2 = 13
def x-cr        char*2 = char(x-num-cr)

subfile g040a portable keep at x-doc include     &
x-clinic                                            ,&
comma                                               ,&
doc-dept                                            ,&
comma                                               ,&
x-doc                                               ,&
comma                                               ,&
doc-name                                            ,&
comma                                               ,&
doc-inits                                           ,&
comma                                               ,&
x-g040                                              ,&
comma                                               ,&
x-g040-count                                        ,&
x-cr

build $obj/g040_code
