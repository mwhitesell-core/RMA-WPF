; PROGRAM:  CHECK94.QZS
; DOC:      PRINT REV LESS REQUIRED AND REV LESS TARTEG FROM 94 SCREEN

; DATE          WHO       MODIFICATION
; 96/02/22      YAS       ORIGINAL
; 1999/Feb/18   S.B.      - Checked for Y2K.
;
;
cancel clear
set default
set noblanks
set report nolimit
set report dev disc name check94
set page width 132 length 63
set formfeed
set noclose

access f119-doctor-ytd       &
  link doc-nbr to doc-nbr of f020-doctor-mstr opt

select if   comp-code = "INCREQ"    &
         or comp-code = "INCTAR"

def x-required zoned*8 signed = amt-ytd if comp-code = "INCREQ"
def x-target   zoned*8 signed = amt-ytd if comp-code = "INCTAR"

define x-doc-name char*20 =  "DR. " + doc-name + " "		    &
                           + doc-inits			            &
                                 if record f020-doctor-mstr exists  &
   	       else "UNKNOWN!"

sort on doc-dept    &
     on x-doc-name  &
     on doc-nbr

page heading							&
  tab  1 'CHECK94'		                                &
  tab 10 'RUN DATE:'						&
  tab 21  sysdate						&
  tab 43  "COMP CODE INCREQ AND INCTAR FROM 94 SCREEN"          &
  tab 121 "PAGE:"						&
  tab 127 syspage						&
skip 2  tab  1  "DOCTOR DEPARTMENT: "                           &
        tab  20  doc-dept pic "^^"                              &
        tab  35 "Actual Revenue"                                &
        tab  60 "Actual Revenue"                                &
skip 1  tab  1  "DR. NBR"                                       &
        tab  10 "DR. NAME"                                      &
        tab  35 "Less Required Rev"                             &
        tab  60 "Less Target Rev"                               &
skip 1  tab  1  "-------"                                       &
        tab  10 "--------"                                      &
        tab  35 "-----------------"                             &
        tab  60 "---------------"                               &
skip 2

heading at doc-dept  skip page

footing at doc-nbr                                                    &
skip 1  tab  1  doc-nbr fill "0"                                      &
        tab  10 x-doc-name                                            &
        tab  41 x-required sub pic "^^^,^^^.^^ " trail sign "-"       &
        tab  65 x-target   sub pic "^^^,^^^.^^ " trail sign "-"       &
skip 2


build $pb_obj/check94
  
