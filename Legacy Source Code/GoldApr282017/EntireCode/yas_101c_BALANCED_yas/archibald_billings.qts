;doc     : archibald_billings.qts
;purpose : create a detail claims report for Dr. Archibald billings for a selected PED range
;who     : Dr. Archibald

;
; *************************************************************

;Date		Who		Description
;2016/Aug/08    MC          	original


cancel clear
set default
set process nolimit
set lock record update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr                   

choose  key-clm-type 'B' ,  key-clm-batch-nbr "33319@",                  &
 key-clm-claim-nbr, key-clm-serv-code '00000', key-clm-adj-nbr '0'

select f002-claims-mstr if   clmhdr-batch-type = 'C'    	  &
		        and  clmhdr-date-period-end >=  20150701  &
       			and  clmhdr-date-period-end <=  20160630  

subfile archibald_hdr     keep include                    &
key-clm-batch-nbr             of f002-claims-mstr,        &
key-clm-claim-nbr             of f002-claims-mstr

request two                             &
        on edit        errors report    &
        on calculation errors report

access *archibald_hdr                                                    &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                         &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr                &
          of f002-claims-mstr

select f002-claims-mstr if                   &
            clmdtl-oma-cd      <> "0000"     & ; details only
       and  clmdtl-oma-cd      <> "ZZZZ"     & ;   
       and  clmdtl-oma-cd      <> "PAID"     & ; no payments
       and  clmdtl-oma-cd      <> "MICM"     & ; no miscellaneous billings
       and  clmdtl-oma-cd      <> "MISJ"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICV"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISP"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MOHR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICB"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MIBR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MINH"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MHSC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "NHSC"     &
       and  clmdtl-oma-cd      <> "DHSC"     &
       and  clmdtl-oma-cd      <> "AGEP"    

def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne " 00"  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

def x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2  zoned*2 unsigned =                                   &
                        nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr3  zoned*2 unsigned =                                   &
                        nconvert(ascii(clmdtl-consec-dates,9)[4:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr4  zoned*2 unsigned =                                   & 
                        nconvert(ascii(clmdtl-consec-dates,9)[7:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 			    &
                                   + x-sv-nbr2 			    &
                                   + x-sv-nbr3 			    &
                                   + x-sv-nbr4
;-----------------

def x-fee zoned*7 numeric = clmdtl-fee-ohip / x-nbr-svcs if clmdtl-adj-nbr = 0 &
		       else clmdtl-fee-ohip

; Note: adjustment record will contain zero nbr of svc
def x-clmdtl-fee-ohip-1 zoned*7 = x-sv-nbr1 * x-fee      if clmdtl-adj-nbr = 0 &
        		     else x-fee

;-----------------
def x-clmdtl-fee-ohip-2 zoned*7 = x-sv-nbr2 * x-fee  
def x-clmdtl-fee-ohip-3 zoned*7 = x-sv-nbr3 * x-fee 
def x-clmdtl-fee-ohip-4 zoned*7 = x-sv-nbr4 * x-fee 

def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2] &
                        if consec-flag = "Y"                              &
                        else " "


subfile archibald_dtl  keep include       &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-1                      ,&
x-sv-date-1                              ,&
clmdtl-adj-nbr        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr

subfile archibald_dtl alias archibald_svc2 append if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-2                      ,&
x-sv-date-2                              ,&
clmdtl-adj-nbr        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr

subfile archibald_dtl alias archibald_svc3 append if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-3                      ,&
x-sv-date-3                              ,&
clmdtl-adj-nbr        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr

subfile archibald_dtl alias archibald_svc4 append if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-4                      ,&
x-sv-date-4                              ,&
clmdtl-adj-nbr        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr

request three                           &
        on edit        errors report    &
        on calculation errors report


access *archibald_dtl                   &
	link key-clm-batch-nbr[3:3]	&
	 to doc-nbr of f020-doctor-mstr opt

temp tot-claim-amt zoned*9 numeric 
item tot-claim-amt = tot-claim-amt + x-clmdtl-fee-ohip-1 if clmdtl-adj-nbr = 0

temp tot-adjust-amt zoned*9 numeric 
item tot-adjust-amt = tot-adjust-amt + x-clmdtl-fee-ohip-1 if clmdtl-adj-nbr = 1

def comma 	cha*1 = "~"
def x-num-cr 	integer unsigned size 2 = 13
def x-cr        char*2 = char(x-num-cr)

subfile archibald_billings portable keep at final  include            &
doc-nbr						    ,&
comma                                               ,&
doc-name                                            ,&
comma                                               ,&
doc-inits                                           ,&
comma                                               ,&
tot-claim-amt                                       ,&
comma                                               ,&
tot-adjust-amt                                      ,&
x-cr

build $obj/archibald_billings
