;doc     : emergency_patient_count_dtl.qts          
;purpose : report patient count and amount by doctor for dept 44  with agent 0,2,4,6,9                                        
;	   for the period from July 1 - Dec 31, 2012 for clinic 22, 23 & 98 only
;who     : Ross/John Crossley/Rob Kerr
;NOTE:     Technically we do not need to go to the detail records to get the bill amount,
;	   but Yasemin has indicated that for clinic 98/ agent 6, when payment received, user would adjust off the original claim,
;	   then enter miscellaneous payment under clinic 22 in order to pay the doctors.
;	   For this situation, the bill amount would be zero at the header records; hence  now that we have to go to
;	   detail records to get the original amount
; *************************************************************

;Date           Who            Description
;2013/02/07     MC             original  
;2013/03/07     yas            ucc_patient_count_dtl (this is for dept 44)
;                              run it for all emergency dept 41, 42 separetaely and then 41 and 42 by them self
;                              comment out the choose and select on departments (all departments then one dept at a time)
;                              no clinic 43  for these dates  probably can choose on 22, 23, 88 and 98

cancel clear
set default
set process nolimit
set lock record update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr                   &
  link (clmhdr-claim-id[3:3])             &
    to doc-nbr of f020-doctor-mstr opt

choose  key-clm-type 'B', key-clm-batch-nbr, key-clm-claim-nbr, key-clm-serv-code '00000', key-clm-adj-nbr '0'

select f002-claims-mstr if         clmhdr-batch-type = 'C'             &
                           and (   clmhdr-doc-dept = 41                &
                                or clmhdr-doc-dept = 42                &
                                or clmhdr-doc-dept = 43               &
                               )                                       &
                           and (      clmhdr-serv-date   >= 20150701   &
                                 and  clmhdr-serv-date   <= 20160630   &
			       )				       &
			   and (      clmhdr-agent-cd = 0	       &
				  or  clmhdr-agent-cd = 2              &
				  or  clmhdr-agent-cd = 4              &
				  or  clmhdr-agent-cd = 6              &
				  or  clmhdr-agent-cd = 9              &
			       )				       


subfile emerg_select_4142 keep include		 &
key-clm-batch-nbr  		        ,&
key-clm-claim-nbr			,&
doc-dept				,&
doc-nbr					,&
doc-name				,&
clmhdr-agent-cd 			,&
clmhdr-tot-claim-ar-ohip


request two				&
        on edit        errors report    &
        on calculation errors report

access *emerg_select_4142							 &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                         &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr                &
          of f002-claims-mstr

select f002-claims-mstr if                           & 
            clmdtl-oma-cd      <> "0000"             &; ignore claim header recs
       and  clmdtl-oma-cd      <> "ZZZZ"             &; ignore description recs
       and  clmdtl-oma-cd      <> "PAID"             &; no payments
       and  clmdtl-adj-nbr     = 0     

sort on doc-nbr on key-clm-batch-nbr on key-clm-claim-nbr

temp agent-0-pat-count num*5                    
item agent-0-pat-count = agent-0-pat-count + 1			&
		if clmhdr-agent-cd = 0  or clmhdr-agent-cd = 4  &
		at key-clm-claim-nbr      			&
		reset at doc-nbr

temp agent-0-amount num*7            
item agent-0-amount = agent-0-amount + clmdtl-fee-ohip 		&
		if clmhdr-agent-cd = 0  or clmhdr-agent-cd = 4  &
		reset at doc-nbr

temp agent-2-pat-count  num*5 
item agent-2-pat-count = agent-2-pat-count + 1			&
		if clmhdr-agent-cd = 2                          &
		at key-clm-claim-nbr      			&
		reset at doc-nbr

temp agent-2-amount num*7 
item agent-2-amount = agent-2-amount + clmdtl-fee-ohip 		&
		if clmhdr-agent-cd = 2                          &
		reset at doc-nbr

temp agent-6-pat-count  num*5 
item agent-6-pat-count = agent-6-pat-count + 1			&
		if clmhdr-agent-cd = 6                          &
		at key-clm-claim-nbr      			&
		reset at doc-nbr

temp agent-6-amount num*7 
item agent-6-amount = agent-6-amount + clmdtl-fee-ohip 		&
		if clmhdr-agent-cd = 6                          &
		reset at doc-nbr

temp agent-9-pat-count  num*5 
item agent-9-pat-count = agent-9-pat-count + 1			&
		if clmhdr-agent-cd = 9                          &
		at key-clm-claim-nbr      			&
		reset at doc-nbr

temp agent-9-amount num*7 
item agent-9-amount = agent-9-amount + clmdtl-fee-ohip 		&
		if clmhdr-agent-cd = 9                          &
		reset at doc-nbr

temp agent-tot-pat-count  num*5 
item agent-tot-pat-count = agent-tot-pat-count + 1		&
		at key-clm-claim-nbr      			&
		reset at doc-nbr

temp agent-tot-amount num*7 
item agent-tot-amount = agent-tot-amount + clmdtl-fee-ohip 	&
		reset at doc-nbr

temp agent-tot-amount-hdr num*7 
item agent-tot-amount-hdr = agent-tot-amount-hdr + clmhdr-tot-claim-ar-ohip &
		at key-clm-claim-nbr      			&
		reset at doc-nbr

def comma cha*1 = "~"
def x-num-cr integer unsigned size 2 = 13
def x-cr        char*2 = char(x-num-cr)

	
subfile emerg_dtl_4142 portable   keep at doc-nbr  include      &
doc-dept                                            ,&
comma                                               ,&
doc-nbr                                             ,&
comma                                               ,&
doc-name                                            ,&
comma                                               ,&
agent-0-pat-count				    ,&
comma                                               ,&
agent-0-amount   				    ,&
comma                                               ,&
agent-2-pat-count				    ,&
comma                                               ,&
agent-2-amount   				    ,&
comma                                               ,&
agent-6-pat-count				    ,&
comma                                               ,&
agent-6-amount   				    ,&
comma                                               ,&
agent-9-pat-count				    ,&
comma                                               ,&
agent-9-amount   				    ,&
comma                                               ,&
agent-tot-pat-count				    ,&
comma                                               ,&
agent-tot-amount   				    ,&
comma                                               ,&
agent-tot-amount-hdr   				    ,&
x-cr

build $pb_obj/emergency_patient_count_dtl
