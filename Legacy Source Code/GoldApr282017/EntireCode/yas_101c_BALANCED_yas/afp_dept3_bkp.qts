;doc     : afp-dept3.qts
;purpose : create report for AFP dept 3 doctors by service date         
;	   by doctor by month by agent (0and4), 2, 6 and 9
;                               include "B" adjustments
;who     : Clare Mitchell             
;
; *************************************************************

;Date		Who		Description
;2003/03/01	Yasemin		Original


cancel clear
set default
set process nolimit
set lock file update

request one                             &
        on edit        errors report    &
        on calculation errors report


access f002-claims-mstr    

choose  key-clm-type 'B' ,  key-clm-batch-nbr, key-clm-claim-nbr,  &
                    key-clm-serv-code '00000', key-clm-adj-nbr '0'

;select f002-claims-mstr if clmhdr-doc-dept = 3                                &
;                  and     clmhdr-agent-cd <> 1                                &
;                  and(   (clmhdr-batch-type  = 'C' or clmhdr-batch-type = 'P')&
;                      or (clmhdr-batch-type  = 'A' and clmhdr-adj-cd = 'B')) 

select f002-claims-mstr if clmhdr-doc-dept = 3                                &
            and      clmhdr-agent-cd <> 1                                     &
            and (   (clmhdr-batch-type  = 'C')                                &
                 or (clmhdr-batch-type = 'P'and clmhdr-adj-cd-sub-type = 'M') &
                 or (clmhdr-batch-type  = 'A' and clmhdr-adj-cd = 'B')        &
                )


subfile afp-dept3              keep include                   &
key-clm-batch-nbr            of f002-claims-mstr,&
key-clm-claim-nbr            of f002-claims-mstr,&
clmhdr-loc                   of f002-claims-mstr,&
clmhdr-agent-cd

request two                             &
        on edit        errors report    &
        on calculation errors report

Access *afp-dept3                                                        &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                         &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr                &
          of f002-claims-mstr


select f002-claims-mstr if                   &
            clmdtl-sv-date     >= '20030101' & 
       and  clmdtl-sv-date     <= '20031231' &
       and  clmdtl-oma-cd      <> "0000"     & ; ignore claim header recs
       and  clmdtl-oma-cd      <> "ZZZZ"     & ; ignore description recs
       and  clmdtl-oma-cd      <> "PAID"     & ; no payments
       and  clmdtl-adj-nbr     = 0             ; ignore adjustments

def x-doc-nbr cha*3 = clmdtl-id[4:3]

def x-yyyymm cha*6 = clmdtl-sv-date[1:6]        


def x-code cha*4 = "Y" if clmdtl-oma-cd = "MICM"    &
                       or clmdtl-oma-cd = "MISJ"    &
                       or clmdtl-oma-cd = "MISC"    &
                       or clmdtl-oma-cd = "MICV"    &
                       or clmdtl-oma-cd = "MISP"    &
                       or clmdtl-oma-cd = "MICB"    &
                       or clmdtl-oma-cd = "MIBR"    &
                       or clmdtl-oma-cd = "MINH"    &
                       or clmdtl-oma-cd = "MHSC"    &
                       or clmdtl-oma-cd = "NHSC"    &
                       or clmdtl-oma-cd = "MOHR"    &
     else "N"

sort on x-doc-nbr    

temp x-jan-2003-0  num*7
item x-jan-2003-0   = x-jan-2003-0  + clmdtl-fee-ohip     &
                    if (          x-yyyymm   = "200301"   &
                         and      x-code   = "N"          &
                         and (   clmhdr-agent-cd = 0      &
                              or clmhdr-agent-cd = 4)     &
                       )                                  &
                    reset at x-doc-nbr 

temp x-jan-2003-2  num*7
item x-jan-2003-2   = x-jan-2003-2  + clmdtl-fee-ohip     &
                    if (          x-yyyymm   = "200301"   &
                         and      x-code   = "N"          &
                         and clmhdr-agent-cd = 2          &
                       )                                  &
                    reset at x-doc-nbr 

temp x-jan-2003-6  num*7
item x-jan-2003-6   = x-jan-2003-6  + clmdtl-fee-ohip     &
                    if (          x-yyyymm   = "200301"   &
                         and      x-code   = "N"          &
                         and clmhdr-agent-cd = 6          &
                       )                                  &
                    reset at x-doc-nbr 

temp x-jan-2003-9  num*7
item x-jan-2003-9   = x-jan-2003-9  + clmdtl-fee-ohip     &
                    if (          x-yyyymm   = "200301"   &
                         and      x-code   = "N"          &
                         and clmhdr-agent-cd = 9          &
                       )                                  &
                    reset at x-doc-nbr 

temp x-jan-2003-p  num*7
item x-jan-2003-p   = x-jan-2003-p  + clmdtl-fee-ohip     &
                    if (          x-yyyymm   = "200301"   &
                         and      x-code   = "Y"          &
                       )                                  &
                    reset at x-doc-nbr 


subfile afp-dept3a keep at x-doc-nbr include          &
x-doc-nbr                                           ,&
x-jan-2003-0                                        ,&
x-jan-2003-6                                        ,&
x-jan-2003-9                                        ,&
x-jan-2003-2                                        ,&
x-jan-2003-p                                        

build $obj/afp-dept3           
