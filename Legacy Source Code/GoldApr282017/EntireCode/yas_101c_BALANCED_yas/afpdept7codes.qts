;doc     : afppedser.qts
;purpose : rep dept 7 and 4 dept 3 docs 317, 340, 385 adn 626
;          from april 1/2001 to march 31st 2002 agent0, 4 cl 22 82 95
;          Pass 1 - select appropriate claims and "explode" repetitive services
;who     : Leena and fotheringham


;Date		Who		Description
;2003/05/14	Yasemin		Original

cancel clear
set default
set process nolimit
set lock file update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr                   &
  link clmhdr-pat-ohip-id-or-chart        &
    to key-pat-mstr of f010-pat-mstr opt  &
  link (nconvert(clmhdr-claim-id[4:3]))   &
    to doc-nbr of f020-doctor-mstr opt

 
choose  key-clm-type 'B' ,  key-clm-batch-nbr 220000000 to 229999999,	&
					      820000000 to 829999999,	&
					      950000000 to 959999999,	&
        key-clm-claim-nbr,  key-clm-serv-code '00000', key-clm-adj-nbr '0'

def x-doc cha*3 =clmhdr-claim-id[4:3]

def x-payment cha*3 = "HCP" if pat-prov-cd = "ON"    &
                 else "RMB"


select                  if ( (         clmhdr-doc-dept = 7                    &
                              or  (    clmhdr-doc-dept = 3                    &
                                   and (    x-doc = "317"                     & 
                                         or x-doc = "340"                     &
                                         or x-doc = "385"                     &
                                         or x-doc = "626" )                   &
                                  )                                           &
                             )                                                &
                        and  (   clmhdr-agent-cd = 0  or clmhdr-agent-cd = 4  &
                              or clmhdr-agent-cd = 1)                         &
                        and  (clmhdr-batch-type = 'C'                    )    &
                        and  (x-payment = "HCP"                          )    &
                           )

subfile afppedser keep include                   &
key-clm-batch-nbr            of f002-claims-mstr,& 
key-clm-claim-nbr            of f002-claims-mstr,& 
x-doc                                           

request two                             &
        on edit        errors report    &
        on calculation errors report

access *afppedser                                                        &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                         &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr                &
          of f002-claims-mstr 

def x-code cha*5 = clmdtl-id[12:5]

select f002-claims-mstr if                    &
            clmdtl-sv-date     >= '20010401'  & 
       and  clmdtl-sv-date     <= '20030331' &
       and  clmdtl-oma-cd      <> "0000"     & ; ignore claim header recs
       and  clmdtl-oma-cd      <> "ZZZZ"     & ; ignore description recs
       and  clmdtl-oma-cd      <> "PAID"     & ; no payments
       and  clmdtl-oma-cd      <> "MICM"     & ; no miscellaneous billings
       and  clmdtl-oma-cd      <> "MISJ"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICV"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISP"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MOHR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICB"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MIBR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MINH"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MHSC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "NHSC"     & ; "  "             "
       and  clmdtl-adj-nbr     = 0             ; ignore adjustments


def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne " 00"  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 unsigned =                                    &
                        nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr3 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[4:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-sv-nbr4 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[7:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

def x-fee = clmdtl-fee-ohip / x-nbr-svcs
def x-clmdtl-fee-ohip-1 zoned*7 = x-sv-nbr1 * x-fee
def x-clmdtl-fee-ohip-2 zoned*7 = x-sv-nbr2 * x-fee
def x-clmdtl-fee-ohip-3 zoned*7 = x-sv-nbr3 * x-fee
def x-clmdtl-fee-ohip-4 zoned*7 = x-sv-nbr4 * x-fee

def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2] &
                        if consec-flag = "Y"                              &
                        else " "

                                         
subfile afppedser1 keep include              &
x-doc     of afppedser                   ,&
x-sv-date-1                              ,&
x-sv-nbr1	                         ,& 
x-code	                         ,& 
x-clmdtl-fee-ohip-1                      

subfile afppedser1 alias afppedser_svc2 append keep if  x-sv-nbr2 <> 0 include &
x-doc      of afppedser                  ,&
x-sv-date-2                              ,&
x-sv-nbr2                                ,&
x-code                               ,&
x-clmdtl-fee-ohip-2                     

subfile afppedser1 alias afppedser_svc3 append keep if  x-sv-nbr3 <> 0 include &
x-doc                 of afppedser       ,&
x-sv-date-3                              ,&
x-sv-nbr3                                ,&
x-code                               ,&
x-clmdtl-fee-ohip-3                      

subfile afppedser1 alias afppedser_svc4 append keep if  x-sv-nbr4 <> 0 include &
x-doc               of afppedser         ,&
x-sv-date-4                              ,&
x-sv-nbr4                                ,&
x-code                               ,&
x-clmdtl-fee-ohip-4                      


build $obj/afppedser
