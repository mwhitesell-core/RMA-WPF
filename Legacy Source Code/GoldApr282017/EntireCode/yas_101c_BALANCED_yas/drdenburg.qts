;doc     : drdenburg.qts
;purpose : select diag code 710 and 695 for Lupus                        
;	   report pat last/first name,phone#,health#,mumc id, service date
;          if patient has more then one service date report one date earliest
;          service date, age is calculated at the time of service date       
;          if patient has more then one service date report one date earliest
;who     : send to Sari Herman as per Dr. Denburgs request

;
; *************************************************************

;Date		Who		Description
;2006/08/09	Yasemin	        original

cancel clear
set default
set process nolimit
set lock file update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr                   

choose  key-clm-type 'B' ,  key-clm-batch-nbr "22493@",                  &
                                              "34493@",                  &
                                              "98493@",                  &
 key-clm-claim-nbr, key-clm-serv-code '00000', key-clm-adj-nbr '0'


select f002-claims-mstr if  clmhdr-batch-type = 'C' ;and clmhdr-loc = "M200"

subfile drdenburg     keep include                    &
clmhdr-claim-id               of f002-claims-mstr,&
key-clm-batch-nbr             of f002-claims-mstr,&
key-clm-claim-nbr             of f002-claims-mstr,&
clmhdr-loc                    of f002-claims-mstr,&
clmhdr-hosp                   of f002-claims-mstr,&
clmhdr-pat-ohip-id-or-chart

request two                             &
        on edit        errors report    &
        on calculation errors report

access *drdenburg                                                             &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                         &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr                &
          of f002-claims-mstr

 select f002-claims-mstr if                  &
            clmdtl-sv-date     >= '20040101' &
       and  clmdtl-oma-cd      <> "0000"     & ; details only
       and  clmdtl-oma-cd      <> "ZZZZ"     & ;   
       and  clmdtl-oma-cd      <> "PAID"     & ; no payments
       and  clmdtl-oma-cd      <> "MICM"     & ; no miscellaneous billings
       and  clmdtl-oma-cd      <> "MISJ"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICV"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISP"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MOHR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICB"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MIBR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MINH"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MHSC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "NHSC"     

def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne " 00"  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 unsigned =                                    &
                        nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr3 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[4:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-sv-nbr4 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[7:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2] &
                        if consec-flag = "Y"                              &
                        else " "

;sort on clmdtl-sv-date

subfile drdenburg1     keep include           &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-sv-date-1                              ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmhdr-loc            of drdenburg           ,&
clmhdr-hosp           of drdenburg           ,&
clmhdr-pat-ohip-id-or-chart of drdenburg      

subfile drdenburg1 alias drdenburg_svc2 append keep if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-sv-date-2                              ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmhdr-loc            of drdenburg           ,&
clmhdr-hosp           of drdenburg           ,&
clmhdr-pat-ohip-id-or-chart of drdenburg    

subfile drdenburg1 alias drdenburg_svc3 append keep if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-sv-date-3                              ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmhdr-loc            of drdenburg           ,&
clmhdr-hosp           of drdenburg           ,&
clmhdr-pat-ohip-id-or-chart of drdenburg   

subfile drdenburg1 alias drdenburg_svc4 append keep if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-sv-date-4                              ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmhdr-loc            of drdenburg           ,&
clmhdr-hosp           of drdenburg           ,&
clmhdr-pat-ohip-id-or-chart of drdenburg   

request three                           &
        on edit        errors report    &
        on calculation errors report


access *drdenburg1                                                          &
  link clmhdr-pat-ohip-id-or-chart to key-pat-mstr of f010-pat-mstr opt

def clmdtl-sv-yy zoned*4 unsigned = nconvert(x-sv-date-1[1:4])
def clmdtl-sv-mm zoned*4 unsigned = nconvert(x-sv-date-1[5:2])
def clmdtl-sv-dd zoned*4 unsigned = nconvert(x-sv-date-1[7:2])

def x-age zoned*4 unsigned  =  clmdtl-sv-yy - pat-birth-date-yy     	&
        if   ( clmdtl-sv-mm >  pat-birth-date-mm)                   	&
          or (    clmdtl-sv-mm =  pat-birth-date-mm 		    	&
	      and clmdtl-sv-dd >= pat-birth-date-dd			&
	     )                  					&
        else clmdtl-sv-yy - pat-birth-date-yy - 1

;select if   ( x-age >= 6 and x-age <= 11 )        &
;                and   clmdtl-diag-cd = 477

select if    clmdtl-diag-cd = 710   &
          or clmdtl-diag-cd = 695    
    
sort on clmhdr-pat-ohip-id-or-chart on x-sv-date-1 d 

def comma cha*1 = "~"
def x-num-cr integer unsigned size 2 = 13
def x-cr        char*2 = char(x-num-cr)

subfile drdenburg2 portable keep at clmhdr-pat-ohip-id-or-chart include    &
clmdtl-id                                           ,&
comma                                               ,&
pat-given-name                                      ,&
comma                                               ,&
pat-surname                                         ,&
comma                                               ,&
pat-birth-date                                      ,&
comma                                               ,&
x-age                                               ,&
comma                                               ,&
pat-health-nbr                                      ,&
comma                                               ,&
pat-chart-nbr                                       ,&
comma                                               ,&
pat-phone-nbr                                       ,&
comma                                               ,&
x-sv-date-1                                         ,&
comma                                               ,&
clmdtl-diag-cd                                      ,&
x-cr

build $obj/drdenburg
