;doc     : dept4_average_by_pkey.qts
;purpose : create report by doctor and report doc# name and average amount date-entered vs service-date
;          for department 4  select service date from July 1, 2010 to June 30, 2011
;who     : Leena and Annette Rosati

;
; *************************************************************

;Date           Who             Description
;2011/06/07     Yasemin       

cancel clear
set default
set process nolimit
set lock record update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr                   &
  link (clmhdr-claim-id[3:3])             &
    to doc-nbr of f020-doctor-mstr opt

choose  key-p-clm-type 'P'  

select f002-claims-mstr if     clmhdr-batch-type = 'C'             &
                          and  clmhdr-doc-dept = 4                 &
		          and  clmhdr-serv-date   >= 20100701  	   &
          		  and  clmhdr-serv-date   <= 20110630

def x-claim cha*10 = clmhdr-claim-id[1:10]
def x-days zoned*4 unsigned = days(nconvert(clmhdr-date-sys)) - days(clmhdr-serv-date)

subfile average_p     keep include    &
clmhdr-claim-id                      ,&
key-clm-batch-nbr                    ,&
key-clm-claim-nbr                    ,&
x-claim                              ,&
clmhdr-doc-dept                      ,&
clmhdr-serv-date                     ,&
clmhdr-date-sys                      ,&
x-days				     ,&
doc-nbr                              ,&
doc-name                             ,&
doc-inits

request two                             &
        on edit        errors report    &
        on calculation errors report

access *average_p

sort on doc-nbr  on clmhdr-serv-date 

temp x-tot-days   zoned*6 unsigned
item x-tot-days = x-tot-days + x-days 	&
		reset at doc-nbr

temp claim-count  zoned*6 unsigned
item claim-count  = claim-count + 1   &
                  reset at doc-nbr         

subfile average1_p keep at doc-nbr include    	     &
clmhdr-doc-dept                                     ,&
doc-nbr                                             ,&
doc-name                                            ,&
doc-inits                                           ,&
x-tot-days                                          ,&
claim-count                                         ,&
clmhdr-serv-date                                   

request three                           &
        on edit        errors report    &
        on calculation errors report

access *average1_p

temp x-average    zoned*6 unsigned 
item x-average  = x-tot-days / claim-count * 100             

def comma cha*1 = "~"
def x-num-cr integer unsigned size 2 = 13
def x-cr        char*2 = char(x-num-cr)

subfile average2_p portable keep include             &
clmhdr-doc-dept                                     ,&
comma                                               ,&
doc-nbr                                             ,&
comma                                               ,&
doc-name                                            ,&
comma                                               ,&
doc-inits                                           ,&
comma                                               ,&
claim-count                                         ,&
comma                                               ,&
x-average                                           ,&
comma                                               ,&
clmhdr-serv-date                                    ,&
x-cr

build dept4_average_by_pkey
