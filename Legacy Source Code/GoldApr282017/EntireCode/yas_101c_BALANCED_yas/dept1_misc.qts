;doc     : dept1_misc.qts
;purpose : ohip billings from July 1,2001 to June 30 2002              
;          total billing amount then total detail misc amount by doctor
;who     : Leena      
;
; *************************************************************

;Date		Who		Description
;2002/10/10	Yasemin		Original


cancel clear
set default
set process nolimit
set lock file update

request one

access f002-claims-mstr     

choose  key-clm-type 'B' ,  key-clm-batch-nbr ,     &
        key-clm-claim-nbr,  key-clm-serv-code '00000', key-clm-adj-nbr '0'

select f002-claims-mstr if                                                   &
            (      (clmhdr-doc-dept = 1 or clmhdr-doc-dept = 17)             &  
               and (clmhdr-batch-type = 'P' and clmhdr-adj-cd = 'M')         &
            )

subfile dept1misc   keep include                 &
key-clm-batch-nbr            of f002-claims-mstr,&
key-clm-claim-nbr            of f002-claims-mstr,&
clmhdr-reference             of f002-claims-mstr,&
clmhdr-loc                   of f002-claims-mstr

request two                             &
        on edit        errors report    &
        on calculation errors report

access *dept1misc                                                        &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                         &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr                &
          of f002-claims-mstr


select f002-claims-mstr if                   &
         clmdtl-date-period-end >= '20010701' & 
    and  clmdtl-date-period-end <= '20020630' &
;    and  clmdtl-oma-cd         <> "0000"     & ; ignore claim header recs
;    and  clmdtl-oma-cd         <> "ZZZZ"     & ; ignore description recs
;    and  clmdtl-oma-cd         <> "PAID"     & ; no payments
     and ( clmdtl-oma-cd          = "MICM"     & ; no miscellaneous billings
     or  clmdtl-oma-cd          = "MISJ"     & ; "  "             "
     or  clmdtl-oma-cd          = "MISC"     & ; "  "             "
     or  clmdtl-oma-cd          = "MICV"     & ; "  "             "
     or  clmdtl-oma-cd          = "MISP"     & ; "  "             "
     or  clmdtl-oma-cd          = "MOHR"     & ; "  "             "
     or  clmdtl-oma-cd          = "MICB"     & ; "  "             "
     or  clmdtl-oma-cd          = "MIBR"     & ; "  "             "
     or  clmdtl-oma-cd          = "MINH"     & ; "  "             "
     or  clmdtl-oma-cd          = "MHSC"     & ; "  "             "
     or  clmdtl-oma-cd          = "NHSC")     
;   and  clmdtl-adj-nbr         = 0            ; ignore adjustments


def x-doc-nbr cha*3 = clmdtl-id[4:3]
def x-oma-code cha*5 = clmdtl-id[12:5]

sort on x-doc-nbr    

def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne " 00"  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 unsigned =                                    &
                        nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr3 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[4:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-sv-nbr4 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[7:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

def x-fee = clmdtl-fee-ohip / x-nbr-svcs
def x-clmdtl-fee-ohip-1 zoned*7 = x-sv-nbr1 * x-fee
def x-clmdtl-fee-ohip-2 zoned*7 = x-sv-nbr2 * x-fee
def x-clmdtl-fee-ohip-3 zoned*7 = x-sv-nbr3 * x-fee
def x-clmdtl-fee-ohip-4 zoned*7 = x-sv-nbr4 * x-fee

def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2] &
                        if consec-flag = "Y"                              &
                        else " "


subfile dept1misc2 keep include           &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-1                              ,&
x-sv-nbr1                                ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code                               ,& 
clmhdr-loc            of dept1misc       ,& 
clmhdr-reference      of dept1misc       ,& 
x-clmdtl-fee-ohip-1                      

subfile dept1misc2 alias dept1misc_svc2 append keep if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-2                              ,&
x-sv-nbr2                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code                               ,&
clmhdr-loc            of dept1misc       ,&
clmhdr-reference      of dept1misc       ,&
x-clmdtl-fee-ohip-2                     

subfile dept1misc2 alias dept1misc_svc3 append keep if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-3                              ,&
x-sv-nbr3                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code                               ,&
clmhdr-loc            of dept1misc       ,&
clmhdr-reference      of dept1misc       ,&
x-clmdtl-fee-ohip-3                      

subfile dept1misc2 alias dept1misc_svc4 append keep if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-4                              ,&
x-sv-nbr4                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code                               ,&
clmhdr-loc            of dept1misc       ,&
clmhdr-reference      of dept1misc       ,&
x-clmdtl-fee-ohip-4                      


build $obj/dept1misc
