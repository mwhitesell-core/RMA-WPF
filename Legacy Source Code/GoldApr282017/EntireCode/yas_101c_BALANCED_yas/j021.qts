;doc     : j021.qts
;purpose : select if claim have ome code Z440 and not j021 in the detail  
;	   and location code, service date admit date, patient name/health#
;          diag cd and dr name and number  
;who     : send to selected doctor's secretaries to data entry to web
;          Jane for data entery of Dr. Stanton's billings
;
; *************************************************************

;Date		Who		Description
;2004/11/05	Yasemin	        original
;2004/11/08     M.C.		made the neccessary changes

cancel clear
set default
set process nolimit
set lock file update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr                   

choose  key-clm-type 'B' ,  key-clm-batch-nbr "22158@",                  &
                                              "22660@",                  &
                                              "34660@",                  &
                                              "22115@",                  &
                                              "34115@",                  &
                                              "22891@",                  &
                                              "22976@",                  &
                                              "34976@",                  &
                                              "22980@",                  &
                                              "34980@",                  &
                                              "22593@",                  &
                                              "34593@",                  &
                                              "22907@",                  &
                                              "34907@",                  &
                                              "22329@",                  &
                                              "34329@",                  &
                                              "22441@",                  &
                                              "34441@",                  &
        key-clm-claim-nbr, key-clm-serv-code '00000', key-clm-adj-nbr '0'

select f002-claims-mstr if       clmhdr-batch-type = 'C'    

subfile j021      keep include                    &
clmhdr-claim-id               of f002-claims-mstr,&
key-clm-batch-nbr             of f002-claims-mstr,&
key-clm-claim-nbr             of f002-claims-mstr,&
clmhdr-loc                    of f002-claims-mstr,&
clmhdr-refer-doc-nbr          of f002-claims-mstr,&
clmhdr-pat-ohip-id-or-chart

request two                             &
        on edit        errors report    &
        on calculation errors report

access *j021                                                          &
  link clmhdr-pat-ohip-id-or-chart to key-pat-mstr of f010-pat-mstr opt  &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                         &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr                &
          of f002-claims-mstr

select f002-claims-mstr if                   &
            clmdtl-sv-date     >= '20040102' &
       and (clmdtl-oma-cd      =  "Z440"     & ; details only
        or  clmdtl-oma-cd      =  "J021")    & ;   
       and  clmdtl-adj-nbr     =  0

def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne " 00"  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 unsigned =                                    &
                        nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr3 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[4:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-sv-nbr4 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[7:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2] &
                        if consec-flag = "Y"                              &
                        else " "


def x-clinic cha*2 = clmdtl-id[1:2]

subfile j021a       keep include           &
clmdtl-id              of f002-claims-mstr,&
key-clm-batch-nbr      of f002-claims-mstr,&
key-clm-claim-nbr      of f002-claims-mstr,&
x-sv-date-1                               ,&
clmdtl-diag-cd         of f002-claims-mstr,&
clmdtl-oma-cd          of f002-claims-mstr,&
clmhdr-loc             of j021            ,&
clmhdr-refer-doc-nbr   of j021            ,&
clmhdr-date-admit      of f002-claims-mstr,&
x-clinic                                            ,&
pat-surname of f010-pat-mstr                        ,&
pat-given-name of f010-pat-mstr                     ,&
pat-health-nbr of f010-pat-mstr                                     

subfile j021a alias j021_svc2 append keep if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-sv-date-2                              ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of j021            ,&
clmhdr-refer-doc-nbr  of j021            ,&
clmhdr-date-admit      of f002-claims-mstr,&
x-clinic                                            ,&
pat-surname of f010-pat-mstr                        ,&
pat-given-name of f010-pat-mstr                     ,&
pat-health-nbr of f010-pat-mstr                                     

subfile j021a alias j021_svc3 append keep if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-sv-date-3                              ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of j021           ,&
clmhdr-refer-doc-nbr  of j021            ,&
clmhdr-date-admit      of f002-claims-mstr,&
x-clinic                                            ,&
pat-surname of f010-pat-mstr                        ,&
pat-given-name of f010-pat-mstr                     ,&
pat-health-nbr of f010-pat-mstr                                     

subfile j021a alias j021_svc4 append keep if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-sv-date-4                              ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of j021           ,&
clmhdr-refer-doc-nbr  of j021            ,&
clmhdr-date-admit      of f002-claims-mstr,&
x-clinic                                            ,&
pat-surname of f010-pat-mstr                        ,&
pat-given-name of f010-pat-mstr                     ,&
pat-health-nbr of f010-pat-mstr                   

request three                           &
        on edit        errors report    &
        on calculation errors report


access *j021a                                                          
       
sort on key-clm-batch-nbr on key-clm-claim-nbr on clmdtl-oma-cd

temp xcount 
item xcount = xcount + 1 reset at key-clm-claim-nbr

subfile j021b keep include		&
	j021a,				&
	xcount

 

request four                            &
        on edit        errors report    &
        on calculation errors report


access *j021b                                         &
  link clmhdr-loc to loc-nbr of f030-locations-mstr   & 
  link clmdtl-id[3:3] to doc-nbr of f020-doctor-mstr
       
sel if clmdtl-oma-cd = 'Z440' and xcount = 1


def comma cha*1 = "~"
def x-num-cr integer unsigned size 2 = 13
def x-cr        char*2 = char(x-num-cr)

subfile j021c portable keep  include    &
clmdtl-id                                           ,&
comma                                               ,&
x-clinic                                            ,&
comma                                               ,&
doc-nbr                                             ,&
comma                                               ,&
doc-name                                            ,&
comma                                               ,&
pat-surname 					    ,&
comma                                               ,&
pat-given-name                                      ,&
comma                                               ,&
pat-health-nbr                                      ,&
comma                                               ,&
x-sv-date-1                                         ,&
comma                                               ,&
clmhdr-refer-doc-nbr                                ,&
comma                                               ,&
clmdtl-diag-cd                                      ,&
comma                                               ,&
clmhdr-loc                                          ,&
comma                                               ,&
loc-name                                            ,&
comma                                               ,&
clmhdr-date-admit                                   ,&
x-cr

build $obj/j021
