;doc     : nephrcodes.qts
;purpose : report acute and new chronic dialysis codes by doctor 
;          july 1 to feb 28 2001/ july 1 to feb 28 2002/ july 1 to feb 28 2003
;          there is a second pass nephrcodes.qzs

;who     : Dr. Russell


;Date		Who		Description
;2003/04/28   	Yasemin		Original
;2003/05/27     Yasemin         run it for July 1st to april 30th
;2004/01/12     Yasemin         add new year 2004 add doc 832
;2004/10/27     Yasemin         add new year 2005              
;2005/10/08     Yasemin         add new year 2006, add doc C16, C18 and D80

can clear
set verify errors
set default
set process nolimit
set lock file update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr                           

choose  key-clm-type 'B' ,  key-clm-batch-nbr "22839@",   &
                                              "34839@",   &
                                              "98839@",   &
                                              "22481@",   &
                                              "34481@",   &
                                              "98481@",   &
                                              "22078@",   &
                                              "34078@",   &
                                              "98078@",   &
                                              "22188@",   &
                                              "34188@",   &
                                              "98188@",   &
                                              "34D69@",   &
                                              "22D69@",   &
                                              "98D69@",   &
                                              "22403@",   &
                                              "34403@",   &
                                              "98403@",   &
                                              "22279@",   &
                                              "34279@",   &
                                              "98279@",   &
                                              "22792@",   &
                                              "34792@",   &
                                              "98792@",   &
                                              "22879@",   &
                                              "34879@",   &
                                              "98879@",   &
                                              "22455@",   &
                                              "34455@",   &
                                              "98455@",   &
                                              "22D80@",   &
                                              "34D80@",   &
                                              "98D80@",   &
                                              "22928@",   &
                                              "34928@",   &
                                              "98928@",   &
                                              "22055@",   &
                                              "34055@",   &
                                              "98055@",   &
                                              "22662@",   &
                                              "34662@",   &
                                              "98662@",   &
                                              "22459@",   &
                                              "34459@",   &
                                              "98459@",   &
                                              "22425@",   &
                                              "34425@",   &
                                              "98425@",   &
                                              "22967@",   &
                                              "34967@",   &
                                              "98967@",   &
                                              "22448@",   &
                                              "34448@",   &
                                              "98448@",   &
                                              "22524@",   &
                                              "34524@",   &
                                              "98524@",   &
                                              "22832@",   &
                                              "34832@",   &
                                              "98832@",   &
                                              "22C16@",   &
                                              "34C16@",   &
                                              "98C16@",   &
                                              "22C18@",   &
                                              "34C18@",   &
                                              "98C18@",   &
                                              "22J26@",   &
                                              "34J26@",   &
                                              "98J26@",   &
key-clm-claim-nbr,  key-clm-serv-code '00000', key-clm-adj-nbr '0'


select if clmhdr-batch-type = 'C'

subfile nephrcodes_1 keep include          &
key-clm-batch-nbr      of f002-claims-mstr,&
key-clm-claim-nbr      of f002-claims-mstr

request two                             &
        on edit        errors report    &
        on calculation errors report

access *nephrcodes_1			&
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr 	&	
	of f002-claims-mstr  

def x-code char*4 = clmdtl-id[11:4]

select f002-claims-mstr if                              &
                   (    x-code = "R849"                 &           
                     or x-code = "G325"                 &           
                     or x-code = "G323"                 &           
                     or x-code = "G330"                 &           
                     or x-code = "G331"                 &           
                     or x-code = "G860"                 &           
                     or x-code = "G862"                 &           
                     or x-code = "G863"                 &           
                     or x-code = "G864"                 &           
                     or x-code = "G865"                 &           
                     or x-code = "G866")                &           
     and (     clmdtl-sv-date >= "20040701"             &  
           and clmdtl-sv-date <= "20070630")
;    and    clmdtl-adj-nbr     = 0                      & ; ignore adjustments


def x-doc-nbr cha*3 =(clmdtl-id[3:3])

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 =                         &
        nconvert(clmdtl-consec-dates-r[1:1])    &
        if (clmdtl-consec-dates-r[2:2] <> 'MR'  &
        and clmdtl-consec-dates-r[2:2] <> 'OP') &
        and clmdtl-consec-dates-r[1:1] <> '0'

def x-sv-nbr3 zoned*2 =                         &
        nconvert(clmdtl-consec-dates-r[4:1])    &
        if (clmdtl-consec-dates-r[5:2] <> 'MR'  &
        and clmdtl-consec-dates-r[5:2] <> 'OP') &
        and clmdtl-consec-dates-r[4:1] <> '0'

def x-sv-nbr4 zoned*2=                          &
        nconvert(clmdtl-consec-dates-r[7:1])    &
        if (clmdtl-consec-dates-r[8:2] <> 'MR'  &
        and clmdtl-consec-dates-r[8:2] <> 'OP') &
        and clmdtl-consec-dates-r[7:1] <> '0'

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4



subfile nephrcodes_2 keep include          &
clmdtl-id              of f002-claims-mstr,&
key-clm-batch-nbr      of f002-claims-mstr,&
key-clm-claim-nbr      of f002-claims-mstr,&
x-doc-nbr                                 ,&
x-nbr-svcs 	                          ,& 
x-code	                                  ,& 
clmdtl-sv-date                            ,& 
clmdtl-fee-ohip                           


request three				&
        on edit        errors report    &
        on calculation errors report


access *nephrcodes_2 				                &
   link x-doc-nbr to doc-nbr of f020-doctor-mstr opt            &
   link x-code to fee-oma-cd  of f040-oma-fee-mstr opt  

def x-yyyymm cha*6 = clmdtl-sv-date[1:6]

def oma-cd-desc char*55                        &
    = pack(fee-oma-cd + " - " + fee-desc)      


subfile nephrcodes_2005a keep if x-yyyymm >= '200407'     &
                         and     x-yyyymm <= '200502'     &

include nephrcodes_2,                                   &
        doc-name of f020-doctor-mstr,                   &
        doc-inits of f020-doctor-mstr,                  &
        oma-cd-desc

subfile nephrcodes_2006a keep if x-yyyymm >= '200507'     &
                         and     x-yyyymm <= '200602'     &

include nephrcodes_2,                                   &
        doc-name of f020-doctor-mstr,                   &
        doc-inits of f020-doctor-mstr,                  &
        oma-cd-desc
  
subfile nephrcodes_2007a keep if x-yyyymm >= '200607'   &
                         and     x-yyyymm <= '200702'   &

include nephrcodes_2,                                   &
        doc-name of f020-doctor-mstr,                   &
        doc-inits of f020-doctor-mstr,                  &
        oma-cd-desc


build $obj/nephrcodes

