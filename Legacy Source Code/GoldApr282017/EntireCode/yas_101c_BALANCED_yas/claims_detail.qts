;doc     : claims_detail.qts
;purpose : report claims detail for Lori to do "B" adjustments from       
;who     : Lori Danby

;
; *************************************************************

;Date		Who		Description
;2005/04/25	Yasemin	        original

cancel clear
set default
set process nolimit
set lock file update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr                   

choose  key-clm-type 'B' ,  key-clm-batch-nbr "62212488",                &
 key-clm-claim-nbr, key-clm-serv-code '00000', key-clm-adj-nbr '0'

select f002-claims-mstr if       clmhdr-batch-type = 'C'    

subfile detail     keep include                    &
clmhdr-claim-id               of f002-claims-mstr,&
key-clm-batch-nbr             of f002-claims-mstr,&
key-clm-claim-nbr             of f002-claims-mstr,&
clmhdr-pat-ohip-id-or-chart

request two                             &
        on edit        errors report    &
        on calculation errors report

access *detail                                                          &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                         &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr                &
          of f002-claims-mstr

select f002-claims-mstr if                   &
            clmdtl-oma-cd      <> "0000"     & ; details only
       and  clmdtl-oma-cd      <> "ZZZZ"     & ;   
       and  clmdtl-oma-cd      <> "PAID"     & ; no payments
       and  clmdtl-oma-cd      <> "MICM"     & ; no miscellaneous billings
       and  clmdtl-oma-cd      <> "MISJ"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICV"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISP"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MOHR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICB"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MIBR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MINH"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MHSC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "NHSC"     

def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne " 00"  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 unsigned =                                    &
                        nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr3 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[4:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-sv-nbr4 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[7:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

def x-clinic cha*2  = clmdtl-id[1:2]

def x-fee = clmdtl-fee-ohip / x-nbr-svcs                  
def x-clmdtl-fee-ohip-1 zoned*7 = x-sv-nbr1 * x-fee     
def x-clmdtl-fee-ohip-2 zoned*7 = x-sv-nbr2 * x-fee
def x-clmdtl-fee-ohip-3 zoned*7 = x-sv-nbr3 * x-fee     
def x-clmdtl-fee-ohip-4 zoned*7 = x-sv-nbr4 * x-fee     

def x-fee-t = (clmdtl-fee-ohip - clmdtl-amt-tech-billed) / x-nbr-svcs  &
                if     x-clinic <  "61"                                &
                   or  x-clinic >  "65"                                &
        else clmdtl-fee-ohip / x-nbr-svcs

def x-clmdtl-fee-tech-1 zoned*7 = x-sv-nbr1 * x-fee-t
def x-clmdtl-fee-tech-2 zoned*7 = x-sv-nbr2 * x-fee-t
def x-clmdtl-fee-tech-3 zoned*7 = x-sv-nbr3 * x-fee-t
def x-clmdtl-fee-tech-4 zoned*7 = x-sv-nbr4 * x-fee-t

subfile detail1     keep include          &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-1                      ,&
x-clmdtl-fee-tech-1    

subfile detail1 alias detail_svc2 append keep if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-2                      ,&
x-clmdtl-fee-tech-2

subfile detail1 alias detail_svc3 append keep if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-3                      ,&
x-clmdtl-fee-tech-3    

subfile detail1 alias detail_svc4 append keep if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-4                      ,&
x-clmdtl-fee-tech-4    

request three                           &
        on edit        errors report    &
        on calculation errors report


access *detail1          

def x-claim  cha*10 = clmdtl-id[1:10]
def x-code   cha*5 = clmdtl-id[11:5]

sort on x-claim

def comma cha*1 = "~"
def x-num-cr integer unsigned size 2 = 13
def x-cr        char*2 = char(x-num-cr)

subfile detail2 portable keep  include            &
x-claim                                          ,&
comma                                            ,&
x-code                                           ,&
comma                                            ,&
x-clmdtl-fee-ohip-1   of detail1                 ,&
comma                                            ,&
x-clmdtl-fee-tech-1   of detail1                 ,&
x-cr

build $obj/claims_detail
