;doc     : drpaes.qts
;purpose : report number of services and $amount for H267(attandance at Maternal
;          Delivery for department 7 (Neonatologist only)
;          Pass 1 - select appropriate claims and "explode" repetitive services
;who     : Dr. Paes


;Date		Who		Description
;2002/05/09	Yasemin		Original

cancel clear
set default
set process nolimit
set lock file update

request one

access f002-claims-mstr                           

choose  key-clm-type 'B' ,  key-clm-batch-nbr 220097000 to 220097999,   &
                                              220121000 to 220121999,   &
                                              220705000 to 220705999,   &
                                              220710000 to 220710999,   &
                                              220732000 to 220732999,   &
                                              220751000 to 220751999,   &
                                              220765000 to 220765999,   &
                                              220846000 to 220846999,   &
        key-clm-claim-nbr,  key-clm-serv-code 'H267A' to 'H267Z',       &
        key-clm-adj-nbr '0'

select f002-claims-mstr if                   &
            clmdtl-sv-date     >= "20010401" & 
       and  clmdtl-sv-date     <= "20020331" & 
       and  clmdtl-adj-nbr     = 0             ; ignore adjustments

def x-doc-nbr cha*3 = clmdtl-id[4:3]

def x-oma-code cha*5 = clmdtl-id[12:5]

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 =                         &
        nconvert(clmdtl-consec-dates-r[1:1])    &
        if (clmdtl-consec-dates-r[2:2] <> 'MR'  &
        and clmdtl-consec-dates-r[2:2] <> 'OP') &
        and clmdtl-consec-dates-r[1:1] <> '0'

def x-sv-nbr3 zoned*2 =                         &
        nconvert(clmdtl-consec-dates-r[4:1])    &
        if (clmdtl-consec-dates-r[5:2] <> 'MR'  &
        and clmdtl-consec-dates-r[5:2] <> 'OP') &
        and clmdtl-consec-dates-r[4:1] <> '0'

def x-sv-nbr4 zoned*2=                          &
        nconvert(clmdtl-consec-dates-r[7:1])    &
        if (clmdtl-consec-dates-r[8:2] <> 'MR'  &
        and clmdtl-consec-dates-r[8:2] <> 'OP') &
        and clmdtl-consec-dates-r[7:1] <> '0'

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

def x-fee = clmdtl-fee-ohip / x-nbr-svcs
def x-clmdtl-fee-ohip-1 zoned*7 = x-sv-nbr1 * x-fee         
def x-clmdtl-fee-ohip-2 zoned*7 = x-sv-nbr2 * x-fee  
def x-clmdtl-fee-ohip-3 zoned*7 = x-sv-nbr3 * x-fee 
def x-clmdtl-fee-ohip-4 zoned*7 = x-sv-nbr4 * x-fee 

def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2]

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2]

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2]


subfile drpaes keep include              &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-1                              ,&
x-sv-nbr1	                         ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code	                         ,& 
x-clmdtl-fee-ohip-1                      

subfile drpaes alias drpaes_svc2 append keep if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-2                              ,&
x-sv-nbr2                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code                               ,&
x-clmdtl-fee-ohip-2                     

subfile drpaes alias drpaes_svc3 append keep if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-3                              ,&
x-sv-nbr3                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code                               ,&
x-clmdtl-fee-ohip-3                      

subfile drpaes alias drpaes_svc4 append keep if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-doc-nbr                                ,&
x-sv-date-4                              ,&
x-sv-nbr4                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-oma-code                               ,&
x-clmdtl-fee-ohip-4                      


build $obj/drpaes
