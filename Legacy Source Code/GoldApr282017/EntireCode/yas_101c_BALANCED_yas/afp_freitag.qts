;doc     : afpfreitag.qts
;purpose : Report Peds/PICU  clinic 22 and payroll "B" and 64
;          from january 2003 to june 30 2003
;          create report patients under 18 (service date minus birth date)
;          select appropriate claims and "explode" repetitive services
;	   eue the professional amount only for clinic 64 and 
;	   use clmdtl-fee-ohip for the other clinics
;who     : Dr. Freitag


;Date		Who		Description
;2003/08/22	Yasemin         Original

cancel clear
set default
set process nolimit
set lock file update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr							&
    link clmhdr-pat-ohip-id-or-chart					&
     to  key-pat-mstr of f010-pat-mstr opt	
 
choose  key-clm-type 'B' ,  key-clm-batch-nbr 220357000 to 220357999,      &
                                              640357000 to 640357999,      &
        key-clm-claim-nbr,  key-clm-serv-code '00000', key-clm-adj-nbr '0'

select f002-claims-mstr if       clmhdr-batch-type = 'C'               &
                         and(    clmhdr-agent-cd    = 0                &
                              or clmhdr-agent-cd    = 4)               

def x-payment cha*3 = "HCP" if pat-prov-cd = "ON"    &
                 else "RMB"
        
def x-pat-name char*42 = pack(pat-surname+", "+pat-given-name)

subfile afpfreitag keep include                     &
clmhdr-claim-id              of f002-claims-mstr,&
key-clm-batch-nbr            of f002-claims-mstr,& 
key-clm-claim-nbr            of f002-claims-mstr,& 
clmhdr-amt-tech-billed       of f002-claims-mstr,&
clmhdr-tot-claim-ar-oma      of f002-claims-mstr,&
clmhdr-tot-claim-ar-ohip     of f002-claims-mstr,&
clmhdr-payroll               of f002-claims-mstr,& 
clmhdr-serv-date             of f002-claims-mstr,& 
clmhdr-loc                   of f002-claims-mstr,& 
clmhdr-hosp                  of f002-claims-mstr,&
x-pat-name					,&
x-payment  				,&
pat-birth-date 

request two                             &
        on edit        errors report    &
        on calculation errors report

access *afpfreitag                                                          &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                         &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr                &
          of f002-claims-mstr 

select f002-claims-mstr if                   &
            clmdtl-sv-date     >= '20030101' & 
       and  clmdtl-sv-date     <= '20030630' &
       and  x-payment           = "HCP"      &                                 
       and  clmdtl-oma-cd      <> "0000"     & ; ignore claim header recs
       and  clmdtl-oma-cd      <> "ZZZZ"     & ; ignore description recs
       and  clmdtl-oma-cd      <> "PAID"     & ; no payments
       and  clmdtl-oma-cd      <> "MICM"     & ; no miscellaneous billings
       and  clmdtl-oma-cd      <> "MISJ"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICV"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISP"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MOHR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICB"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MIBR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MINH"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MHSC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "NHSC"  


def x-oma-code cha*5 = clmdtl-id[12:5]

def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne " 00"  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 unsigned =                                    &
                        nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr3 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[4:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-sv-nbr4 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[7:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

def x-clinic zoned*2 unsigned = key-clm-batch-nbr / 10000000

def x-fee = clmdtl-fee-ohip / x-nbr-svcs		&
		if x-clinic <> 64 			&
	else (clmdtl-fee-ohip - clmdtl-amt-tech-billed) / x-nbr-svcs

def x-clmdtl-fee-ohip-1 zoned*7 = x-sv-nbr1 * x-fee	

def x-clmdtl-fee-ohip-2 zoned*7 = x-sv-nbr2 * x-fee

def x-clmdtl-fee-ohip-3 zoned*7 = x-sv-nbr3 * x-fee	

def x-clmdtl-fee-ohip-4 zoned*7 = x-sv-nbr4 * x-fee	


def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-doc-nbr zoned*3 unsigned = nconvert(clmdtl-id[4:3])


subfile afpfreitag1 keep include              &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-clinic				 ,&
x-oma-code	                         ,& 
x-doc-nbr                                ,&
x-sv-date-1                              ,&
x-sv-nbr1	                         ,& 
x-clmdtl-fee-ohip-1                      ,&
clmhdr-loc            of afpfreitag         ,&
clmhdr-hosp           of afpfreitag         ,& 
clmhdr-payroll        of afpfreitag         ,& 
x-pat-name     	      of afpfreitag	 ,&
pat-birth-date        of afpfreitag	

subfile afpfreitag1 alias afpfreitag_svc2 append keep if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-clinic				 ,&
x-oma-code                               ,&
x-doc-nbr                                ,&
x-sv-date-2                              ,&
x-sv-nbr2                                ,&
x-clmdtl-fee-ohip-2                      ,&
clmhdr-loc            of afpfreitag         ,&
clmhdr-hosp           of afpfreitag         ,& 
clmhdr-payroll        of afpfreitag         ,& 
x-pat-name     	      of afpfreitag	 ,&
pat-birth-date        of afpfreitag	

subfile afpfreitag1 alias afpfreitag_svc3 append keep if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-clinic				 ,&
x-oma-code                               ,&
x-doc-nbr                                ,&
x-sv-date-3                              ,&
x-sv-nbr3                                ,&
x-clmdtl-fee-ohip-3                      ,&
clmhdr-loc            of afpfreitag         ,&
clmhdr-hosp           of afpfreitag         ,& 
clmhdr-payroll        of afpfreitag         ,& 
x-pat-name     	      of afpfreitag	 ,&
pat-birth-date        of afpfreitag	

subfile afpfreitag1 alias afpfreitag_svc4 append keep if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
clmdtl-diag-cd        of f002-claims-mstr,&
x-clinic				 ,&
x-oma-code                               ,&
x-doc-nbr                                ,&
x-sv-date-4                              ,&
x-sv-nbr4                                ,&
x-clmdtl-fee-ohip-4                      ,&
clmhdr-loc            of afpfreitag         ,&
clmhdr-hosp           of afpfreitag         ,& 
clmhdr-payroll        of afpfreitag         ,& 
x-pat-name     	      of afpfreitag	 ,&
pat-birth-date        of afpfreitag	

request three                           &
        on edit        errors report    &
        on calculation errors report

access *afpfreitag1				 		                 
def pat-birth-date-yy zoned*4 unsigned = nconvert(ascii(pat-birth-date,8)[1:4])
def pat-birth-date-mm zoned*2 unsigned = nconvert(ascii(pat-birth-date,8)[5:2])
def pat-birth-date-dd zoned*2 unsigned = nconvert(ascii(pat-birth-date,8)[7:2])

def clmdtl-sv-yy zoned*4 unsigned = nconvert(x-sv-date-1[1:4])
def clmdtl-sv-mm zoned*2 unsigned = nconvert(x-sv-date-1[5:2])
def clmdtl-sv-dd zoned*2 unsigned = nconvert(x-sv-date-1[7:2])

def x-age zoned*4 unsigned  =  clmdtl-sv-yy - pat-birth-date-yy     &
        if   ( clmdtl-sv-mm >  pat-birth-date-mm)                   &
          or ((clmdtl-sv-mm =  pat-birth-date-mm) and               &
              (clmdtl-sv-dd >= pat-birth-date-dd))                  &
        else clmdtl-sv-yy - pat-birth-date-yy - 1

def x-year zoned*1 unsigned = 1 if x-sv-date-1 >= '20030101' and	&
				   x-sv-date-1 <= '20030630'		&
			else  2

subfile afpfreitag2 keep include              &
x-year                                    ,&
x-age                                     ,&
x-clinic              of afpfreitag1   	  ,&
x-doc-nbr             of afpfreitag1         ,&
x-sv-date-1           of afpfreitag1         ,&
x-sv-nbr1	      of afpfreitag1         ,& 
clmdtl-diag-cd        of afpfreitag1         ,&
x-oma-code	      of afpfreitag1         ,& 
clmhdr-loc	      of afpfreitag1         ,& 
clmhdr-hosp           of afpfreitag1         ,& 
clmhdr-payroll        of afpfreitag1         ,& 
x-clmdtl-fee-ohip-1   of afpfreitag1  

request four                            &
        on edit        errors report    &
        on calculation errors report

;purpose : Report Peds/PICU/ICU

access *afpfreitag2

sort on x-year  

temp x-peds     num*7
item x-peds     = x-peds + x-clmdtl-fee-ohip-1     &
             		if x-age < 18 		   &
                and ( clmhdr-payroll <> "B")	   &
                  reset at x-year

temp x-picu     num*7
item x-picu     = x-picu + x-clmdtl-fee-ohip-1              &
             		if   x-age < 18 and          	    &
             		   ( clmhdr-payroll = "B") 	    &
                  reset at x-year 

temp x-tot      num*7
item x-tot      = x-tot  + x-clmdtl-fee-ohip-1              &
                        if   x-age < 18                     &
                  reset at x-year



subfile afpfreitag3  keep at x-year include       &
x-doc-nbr                                     ,&
x-year					      ,&
x-peds                                        ,&
x-picu                                        ,&
x-tot                                        

build $obj/afpfreitag
