;doc     : afp_peds_oma.qts 
;purpose : create report for AFP dept 3 and 7 pediatrics doctors
;	   rep doc detail claims and age of patient and oma amount from f040
;          clinic 22,82, 91 to 96 agent 0,4  B adj included
;          clinic 61-65 professional fees only don't print oma code if tech only
;          no RMB and no MISC                                                  
;          if service date is less then effective date select f040 fee previous
;          report patient age at the time of service 
 
;who     : Coleen Fotheringham
;
; *************************************************************

;Date		Who		Description
;2005/04/13	Yasemin		afp-peds.qts
;2005/04/28	Yasemin		modify for hollenbur and year

cancel clear
set default
set process nolimit
set lock record update

request one                             &
        on edit        errors report    &
        on calculation errors report


access f002-claims-mstr                   &
  link clmhdr-pat-ohip-id-or-chart        &
    to key-pat-mstr of f010-pat-mstr opt  &
  link (clmhdr-claim-id[3:3])             &
    to doc-nbr of f020-doctor-mstr opt

choose   key-clm-type 'B' ,  key-clm-batch-nbr "22385@",  & 
                                               "91385@",  &
                                               "94385@",  &
          key-clm-claim-nbr, key-clm-serv-code '00000', key-clm-adj-nbr '0'

def x-doc     cha*3 =clmhdr-claim-id[3:3]
def x-clinic  cha*2 =clmhdr-claim-id[1:2]

select if                                                                     &
  (          (clmhdr-batch-type  = 'C')                                       &
    and      (clmhdr-agent-cd = 0 or clmhdr-agent-cd = 4)                     &
    and      (clmhdr-payroll  <> "B")                                         &
    and      (pat-prov-cd = "ON")                                             &
  ) 

subfile hollenburg       keep include               &
key-clm-batch-nbr            of f002-claims-mstr,&
key-clm-claim-nbr            of f002-claims-mstr,&
clmhdr-doc-dept		     of f002-claims-mstr,&
clmhdr-agent-cd		     of f002-claims-mstr,&
clmhdr-doc-spec-cd           of f002-claims-mstr,&
clmhdr-i-o-pat-ind           of f002-claims-mstr,&
clmhdr-batch-type	     of f002-claims-mstr,&
clmhdr-payroll               of f002-claims-mstr,&
pat-birth-date               of f010-pat-mstr   ,&
doc-name                     of f020-doctor-mstr,&
doc-inits                    of f020-doctor-mstr,&
x-clinic                                        ,&
x-doc                                           

request two                             &
        on edit        errors report    &
        on calculation errors report

access *hollenburg                                                          &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                         &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr                &
          of f002-claims-mstr                                            

select f002-claims-mstr if                      &
            clmdtl-sv-date     >= '20040401'    &
       and  clmdtl-sv-date     <= '20050331'    &
       and  clmdtl-oma-cd      <> "0000"        &; ignore claim header recs
       and  clmdtl-oma-cd      <> "ZZZZ"        &; ignore description recs
       and  clmdtl-oma-cd      <> "PAID"        &; no payments
       and  clmdtl-oma-cd      <> "MICM"        & ; no miscellaneous billings
       and  clmdtl-oma-cd      <> "MISJ"        & ; "  "             "
       and  clmdtl-oma-cd      <> "MISC"        & ; "  "             "
       and  clmdtl-oma-cd      <> "MICV"        & ; "  "             "
       and  clmdtl-oma-cd      <> "MISP"        & ; "  "             "
       and  clmdtl-oma-cd      <> "MOHR"        & ; "  "             "
       and  clmdtl-oma-cd      <> "MICB"        & ; "  "             "
       and  clmdtl-oma-cd      <> "MIBR"        & ; "  "             "
       and  clmdtl-oma-cd      <> "MINH"        & ; "  "             "
       and  clmdtl-oma-cd      <> "MHSC"        & ; "  "             "
       and  clmdtl-oma-cd      <> "NHSC"        & ; "  "             "
       and  clmdtl-adj-nbr     = 0                ; ignore adjustments
    

def x-yyyymm cha*6 = clmdtl-sv-date[1:6]  

def x-oma-code cha*5 = clmdtl-id[11:5]

def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne " 00"  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 unsigned =                                    &
                        nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr3 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[4:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-sv-nbr4 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[7:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

def x-fee = clmdtl-fee-ohip / x-nbr-svcs                  

def x-clmdtl-fee-ohip-1 zoned*7 = x-sv-nbr1 * x-fee     

def x-clmdtl-fee-ohip-2 zoned*7 = x-sv-nbr2 * x-fee

def x-clmdtl-fee-ohip-3 zoned*7 = x-sv-nbr3 * x-fee     

def x-clmdtl-fee-ohip-4 zoned*7 = x-sv-nbr4 * x-fee     


def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2] &
                        if consec-flag = "Y"                              &
                        else " "


def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2] &
                        if consec-flag = "Y"                              &
                        else " "

subfile hollenburg1  keep include            &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-suff       of f002-claims-mstr,&
x-clinic              of hollenburg         ,&
clmhdr-doc-spec-cd    of hollenburg         ,& 
clmhdr-payroll        of hollenburg         ,& 
pat-birth-date        of hollenburg         ,&
clmhdr-i-o-pat-ind    of hollenburg         ,&
clmhdr-doc-dept       of hollenburg         ,&
x-doc                 of hollenburg         ,&
doc-name              of hollenburg         ,&
doc-inits             of hollenburg         ,&
x-oma-code                               ,& 
x-sv-date-1                              ,&
x-sv-nbr1                                ,& 
x-clmdtl-fee-ohip-1                      

subfile hollenburg1 alias hollenburg_svc2 append keep if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-suff       of f002-claims-mstr,&
x-clinic              of hollenburg         ,&
clmhdr-doc-spec-cd    of hollenburg         ,&
clmhdr-payroll        of hollenburg         ,&
pat-birth-date        of hollenburg         ,&
clmhdr-i-o-pat-ind    of hollenburg         ,&
clmhdr-doc-dept       of hollenburg         ,&
x-doc                 of hollenburg         ,&
doc-name              of hollenburg         ,&
doc-inits             of hollenburg         ,&
x-oma-code                               ,&
x-sv-date-2                              ,&
x-sv-nbr2                                ,&
x-clmdtl-fee-ohip-2

subfile hollenburg1 alias hollenburg_svc3 append keep if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-suff       of f002-claims-mstr,&
x-clinic              of hollenburg         ,&
clmhdr-doc-spec-cd    of hollenburg         ,&
clmhdr-payroll        of hollenburg         ,&
pat-birth-date        of hollenburg         ,&
clmhdr-i-o-pat-ind    of hollenburg         ,&
clmhdr-doc-dept       of hollenburg         ,&
x-doc                 of hollenburg         ,&
doc-name              of hollenburg         ,&
doc-inits             of hollenburg         ,&
x-oma-code                               ,&
x-sv-date-3                              ,&
x-sv-nbr3                                ,&
x-clmdtl-fee-ohip-3

subfile hollenburg1 alias hollenburg_svc4 append keep if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-suff       of f002-claims-mstr,&
x-clinic              of hollenburg         ,&
clmhdr-doc-spec-cd    of hollenburg         ,&
clmhdr-payroll        of hollenburg         ,&
pat-birth-date        of hollenburg         ,&
clmhdr-i-o-pat-ind    of hollenburg         ,&
clmhdr-doc-dept       of hollenburg         ,&
x-doc                 of hollenburg         ,&
doc-name              of hollenburg         ,&
doc-inits             of hollenburg         ,&
x-oma-code                               ,&
x-sv-date-4                              ,&
x-sv-nbr4                                ,&
x-clmdtl-fee-ohip-4

request three                           &
        on edit        errors report    &
        on calculation errors report

access *hollenburg1 link (x-oma-code[1:4])   &
                   to fee-oma-cd of f040-oma-fee-mstr opt 


def x-svc-date date = nconvert(x-sv-date-1)

 
def pat-birth-date-yy zoned*4 unsigned = nconvert(ascii(pat-birth-date,8)[1:4])
def pat-birth-date-mm zoned*2 unsigned = nconvert(ascii(pat-birth-date,8)[5:2])
def pat-birth-date-dd zoned*2 unsigned = nconvert(ascii(pat-birth-date,8)[7:2])

def clmdtl-sv-yy zoned*4 unsigned = nconvert(x-sv-date-1[1:4])
def clmdtl-sv-mm zoned*2 unsigned = nconvert(x-sv-date-1[5:2])
def clmdtl-sv-dd zoned*2 unsigned = nconvert(x-sv-date-1[7:2])

def x-age zoned*4 unsigned  =  clmdtl-sv-yy - pat-birth-date-yy     &
        if   ( clmdtl-sv-mm >  pat-birth-date-mm)                   &
          or ((clmdtl-sv-mm =  pat-birth-date-mm) and               &
              (clmdtl-sv-dd >= pat-birth-date-dd))                  &
        else clmdtl-sv-yy - pat-birth-date-yy - 1

def x-month zoned*2 unsigned = clmdtl-sv-mm - pat-birth-date-mm         &
        if    (    clmdtl-sv-mm >= pat-birth-date-mm                    &
               and clmdtl-sv-dd >= pat-birth-date-dd                    &
               and x-age = 0                                            &
              )                                                         &
        else clmdtl-sv-mm - pat-birth-date-mm - 1                       &       
        if    (    clmdtl-sv-mm >  pat-birth-date-mm                    &
               and clmdtl-sv-dd <  pat-birth-date-dd                    &
               and x-age = 0                                            &
              )                                                         &
        else clmdtl-sv-mm - pat-birth-date-mm + 11                      &       
        
        if    (    clmdtl-sv-mm <= pat-birth-date-mm                    &
               and clmdtl-sv-dd <  pat-birth-date-dd                    &
               and x-age = 0                                            &
              )                                                         &
        else clmdtl-sv-mm - pat-birth-date-mm + 12                      &       
        if    (    clmdtl-sv-mm <  pat-birth-date-mm                    &
               and clmdtl-sv-dd >= pat-birth-date-dd                    &
               and x-age = 0                                            &
              )

def comma cha*1 = "~"
def x-num-cr integer unsigned size 2 = 13
def x-cr        char*2 = char(x-num-cr)

subfile hollenburg2 portable keep include     &
clmdtl-id                                 ,&
comma                                     ,&
x-age                                     ,&
comma                                     ,&
x-month                                   ,&
comma                                     ,&
x-clinic              of hollenburg1         ,&
comma                                     ,&
clmhdr-doc-dept       of hollenburg1         ,&
comma                                     ,&
clmhdr-doc-spec-cd    of hollenburg1         ,&
comma                                     ,&
x-doc                 of hollenburg1         ,&
comma                                     ,&
doc-name              of hollenburg1         ,&
comma                                     ,&
doc-inits             of hollenburg1         ,&
comma                                     ,&
clmdtl-diag-cd        of hollenburg1         ,&
comma                                     ,&
x-oma-code            of hollenburg1         ,& 
comma                                     ,&
x-sv-date-1           of hollenburg1         ,&
comma                                     ,&
x-sv-nbr1             of hollenburg1         ,& 
comma                                     ,&
x-clmdtl-fee-ohip-1   of hollenburg1         ,&
comma                                     ,&
fee-curr-h-fee-1                          ,&
comma                                     ,&
fee-curr-h-fee-2                          ,&
comma                                     ,&
fee-curr-add-on-perc-or-flat-ind          ,&
x-cr

