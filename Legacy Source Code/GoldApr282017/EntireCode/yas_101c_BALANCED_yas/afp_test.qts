;doc     : afp_peds_200501.qts 
;purpose : create report for AFP dept 3 and 7 pediatrics doctors
;	   rep doc,$, 
;         column 1 = cl 22 agent 0, 1 and 4 and HCP B adj included
;         column 1a= cl 96 agent 0, 1 and 4 and HCP B adj included
;         column 2 = cl 91-95 and 82 agent 0,4 and HCP B adj included
;         column 3 = cl 22, 91-96 and 82 all other agents not 0 and 4 and 1
;         column 4 = cl 22, 91-96 and 82 RMB
;         column 5 = cl 22, 91-96 and 82 MISC
;who     : Coleen Fotheringham
;
; *************************************************************

;Date		Who		Description
;2005/01/19	Yasemin		afp-peds.qts


cancel clear
set default
set process nolimit
set lock record update

request one                             &
        on edit        errors report    &
        on calculation errors report


access f002-claims-mstr                   &
  link clmhdr-pat-ohip-id-or-chart        &
    to key-pat-mstr of f010-pat-mstr opt  &
  link (clmhdr-claim-id[3:3])             &
    to doc-nbr of f020-doctor-mstr opt

choose   key-clm-type 'B' ,  key-clm-batch-nbr "22@",  & 
                                               "82@",  &
                                               "91@",  &
                                               "92@",  &
                                               "93@",  &
                                               "94@",  &
                                               "95@",  &
                                               "96@",  &
          key-clm-claim-nbr, key-clm-serv-code '00000', key-clm-adj-nbr '0'

def x-doc cha*3 =clmhdr-claim-id[3:3]

select f002-claims-mstr if                                                    &
 ( (          clmhdr-doc-dept = 7                                             & 
     or (     clmhdr-doc-dept = 3                                             &
         and (   x-doc = "317" or x-doc = "340" or x-doc = "385"              &
              or x-doc = "626" or x-doc = "909" or x-doc = "501"              &
              or x-doc = "739" )                                              &
        )                                                                     &
   )                                                                          &
    and (   (clmhdr-batch-type  = 'C')                                        &
         or (clmhdr-batch-type  = 'P' and clmhdr-adj-cd = 'M') 	              &
        )                                                                     &
  ) 

def x-payment cha*3 = "HCP" if pat-prov-cd = "ON"    &
                 else "RMB"

def x-clinic num*2 = nconvert(clmhdr-claim-id[1:2])

subfile afp_peds      keep include               &
key-clm-batch-nbr            of f002-claims-mstr,&
key-clm-claim-nbr            of f002-claims-mstr,&
clmhdr-loc                   of f002-claims-mstr,&
clmhdr-doc-dept		     of f002-claims-mstr,&
clmhdr-agent-cd		     of f002-claims-mstr,&
clmhdr-batch-type	     of f002-claims-mstr,&
clmhdr-payroll               of f002-claims-mstr,&
doc-name                     of f020-doctor-mstr,&
x-clinic                                        ,&
x-payment                                        


request two                             &
        on edit        errors report    &
        on calculation errors report

access *afp_peds                                                         &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                         &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr                &
          of f002-claims-mstr                                            

select f002-claims-mstr if                                              &
            clmdtl-sv-date     >= '20040401'                            &
       and  clmdtl-sv-date     <= '20050331'                            &
       and  clmhdr-payroll     <> "B"                &; ignore clinic 85 ICU
       and  clmdtl-oma-cd      <> "0000"             &; ignore claim header recs
       and  clmdtl-oma-cd      <> "ZZZZ"             &; ignore description recs
       and  clmdtl-oma-cd      <> "PAID"              ; no payments
;      and  (  	    (	clmdtl-adj-nbr     = 0            	        &   
; 		    and clmhdr-batch-type of afp_peds     = 'C'         &
;                )						        &
;              or   (   clmdtl-adj-nbr     = 1			        &
;                and (clmhdr-batch-type of afp_peds     = 'P'  or	&
;                       clmhdr-batch-type of afp_peds     = 'A')	&
;                )							&
;           )
	    
def x-doc-nbr cha*3 = clmdtl-id[3:3]

def x-yyyymm cha*6 = clmdtl-sv-date[1:6]  

def x-code cha*4 = "Y" if clmdtl-oma-cd = "MICM"    &
                       or clmdtl-oma-cd = "MISJ"    &
                       or clmdtl-oma-cd = "MISC"    &
                       or clmdtl-oma-cd = "MICV"    &
                       or clmdtl-oma-cd = "MISP"    &
                       or clmdtl-oma-cd = "MICB"    &
                       or clmdtl-oma-cd = "MIBR"    &
                       or clmdtl-oma-cd = "MINH"    &
                       or clmdtl-oma-cd = "MHSC"    &
                       or clmdtl-oma-cd = "NHSC"    &
                       or clmdtl-oma-cd = "MOHR"    &
     else "N"

sort on x-doc-nbr    

temp x-col1 num*9
item x-col1  = x-col1 + clmdtl-fee-ohip                                      &
    if      ( x-clinic = 22 )                                                &
        and ( x-code    = "N"  )                                             &
        and (x-payment = "HCP" )                                             &
        and (clmhdr-agent-cd = 0 or clmhdr-agent-cd = 4 or clmhdr-agent-cd = 1)&
   reset at x-doc-nbr

temp x-col1a num*9                                                     
item x-col1a  = x-col1a + clmdtl-fee-ohip                                    &
    if      ( x-clinic = 96 )                                                &
        and ( x-code    = "N"  )                                             &
        and (x-payment = "HCP" )                                             &
        and (clmhdr-agent-cd = 0 or clmhdr-agent-cd = 4 or clmhdr-agent-cd = 1)&
   reset at x-doc-nbr

temp x-col2 num*9
item x-col2  = x-col2 + clmdtl-fee-ohip                                      &
    if      (    x-clinic = 82 or x-clinic = 91 or x-clinic = 92             &
              or x-clinic = 93 or x-clinic = 94 or x-clinic = 95             & 
            )                                                                &
        and ( x-code    = "N"  )                                             &
        and (x-payment = "HCP" )                                             &
        and (clmhdr-agent-cd = 0  or clmhdr-agent-cd = 4 )                   &
   reset at x-doc-nbr

temp x-col3 num*9
item x-col3  = x-col3 + clmdtl-fee-ohip                                      &
    if      (    x-clinic = 22 or x-clinic = 82 or x-clinic = 91             &
              or x-clinic = 92 or x-clinic = 93 or x-clinic = 94             & 
              or x-clinic = 95 or x-clinic = 96                              & 
            )                                                                &
        and ( x-code    = "N"  )                                             &
        and (x-payment = "HCP" )                                             &
        and (    clmhdr-agent-cd ne 0 and clmhdr-agent-cd ne 4               &
             and clmhdr-agent-cd ne 1)                                       &
   reset at x-doc-nbr

temp x-col4  num*9
item x-col4  = x-col4 + clmdtl-fee-ohip                                      &
    if      (    x-clinic = 22 or x-clinic = 82 or x-clinic = 91             &
              or x-clinic = 92 or x-clinic = 93 or x-clinic = 94             &
              or x-clinic = 95 or x-clinic = 96                              &
            )                                                                &
        and ( x-code    = "N"  )                                             &
        and (x-payment = "RMB" )                                             &
   reset at x-doc-nbr

temp x-col5  num*9
item x-col5  = x-col5 + clmdtl-fee-ohip                                      &
    if      (    x-clinic = 22 or x-clinic = 82 or x-clinic = 91             &
              or x-clinic = 92 or x-clinic = 93 or x-clinic = 94             &
              or x-clinic = 95 or x-clinic = 96                              &
            )                                                                &
        and ( x-code    = "Y"  )                                             &
   reset at x-doc-nbr

def comma cha*1 = "~"
def x-num-cr integer unsigned size 2 = 13
def x-cr        char*2 = char(x-num-cr)

subfile afp-pedsa  keep at x-doc-nbr include         &
x-doc-nbr                                           ,&
comma                                               ,&
clmhdr-doc-dept  of afp_peds                        ,&
comma                                               ,&
doc-name                                            ,&
comma                                               ,&
x-col1                                              ,&
comma                                               ,&
x-col1a                                             ,&
comma                                               ,&
x-col2                                              ,&
comma                                               ,&
x-col3                                              ,&
comma                                               ,&
x-col4                                              ,&
comma                                               ,&
x-col5                                              ,&
x-cr

