;# PROGRAM-ID.      f002_ins.qks
;
;              ((C)) DYAD INFOSYS LTD 
;
;	PROGRAM PURPOSE : patient's claims that are possible paid by the insurance company
;			  this program is called from d003.cbl under option 'I'
;
;	MODIFICATION HISTORY
;    DATE          WHO           DESCRIPTION
;    2013/jun/04   M.C.     	 original


screen $pb_obj/f002_ins receiving w-batch-nbr, w-claim-nbr, w-pat-ikey

temp w-batch-nbr   char*8
temp w-claim-nbr   zoned*2 unsigned
temp w-pat-ikey    char*15

file f002-ins      occurs  6
  access via clmhdr-batch-nbr, clmhdr-claim-nbr, key-pat-mstr 	&
	 using w-batch-nbr, w-claim-nbr, w-pat-ikey
  item clmhdr-batch-nbr  initial  w-batch-nbr
  item clmhdr-claim-nbr  initial  w-claim-nbr
  item key-pat-mstr      initial  w-pat-ikey
;  item ins-ikey	 	 initial  ins-ikey of f010-ins

temp w-ins-acronym char*10 occurs with f002-ins
temp w-ins-name    char*30 occurs with f002-ins
temp w-ins-ikey    zoned*5 unsigned occurs with f002-ins
temp w-priority-seq zoned*1 unsigned occurs with f002-ins
temp w-percentage-to-pay zoned*3 unsigned occurs with f002-ins

file f076-insurance-mstr designer occurs with f002-ins
  access via ins-acronym using w-ins-acronym

file f010-ins designer  occurs with f002-ins
  access via key-pat-mstr, ins-ikey using w-pat-ikey, ins-ikey of f076-insurance-mstr

file f076-insurance-mstr alias f076-find designer occurs with f002-ins open 1
  access via ins-ikey using ins-ikey of f002-ins

title "Patient's Claim Insurance Company"	at 1,30

skip 1
align (,4,15) (,35,43) (,,51)
field w-pat-ikey    label "PATIENT:" display
field w-batch-nbr   label "Claim:  " display
field w-claim-nbr   fill '0'         display

title "Ins Acronym  Priority Seq  % to pay  Insurance Cmpany" at 5,4

align (1,,4) (,,20)  (,,33) (,,41)
cluster occurs with f002-ins
field w-ins-acronym required nochange upshift help 'Enter"." only to go to lookup screen for patient insurance'
field priority-seq   predisplay
field percentage-to-pay predisplay
field w-ins-name  display
skip 1

procedure input  w-ins-acronym
begin
   if fieldtext  = "."
   then begin
          let w-ins-acronym = ' '
          let w-ins-name    = ' '
	  let w-ins-ikey    = 0
	  let w-priority-seq = 0
	  let w-percentage-to-pay = 0
          run screen $pb_obj/f002_ins_f mode f passing w-ins-acronym, w-ins-name, w-ins-ikey, 	&
				w-priority-seq, w-percentage-to-pay, w-pat-ikey
          let fieldtext = w-ins-acronym
        end
end

procedure edit w-ins-acronym
begin
     get f076-insurance-mstr opt
     if not accessok
     then error 'Invalid Insurance Acronym'
     get f010-ins opt
     if not accessok
     then error 'This insurance company has not assigned to the patient'
     get f002-ins opt via clmhdr-batch-nbr, clmhdr-claim-nbr, key-pat-mstr, ins-ikey	&
		using w-batch-nbr, w-claim-nbr, w-pat-ikey, ins-ikey of f076-insurance-mstr
     if accessok
     then error 'Insurance Company has already assigned to this claim'
end

procedure process w-ins-acronym
begin
     let ins-ikey of f002-ins = w-ins-ikey
     let priority-seq of f002-ins = w-priority-seq
     let percentage-to-pay of f002-ins = w-percentage-to-pay
     display w-ins-name
end



procedure append
  begin
;   get f010-ins opt
;   get f076-insurance-mstr opt
;   let w-ins-acronym = ins-acronym of f076-insurance-mstr
;   let w-ins-name = ins-full-name of f076-insurance-mstr
    accept  w-ins-acronym
    display w-ins-name
    accept priority-seq of f002-ins
    accept percentage-to-pay of f002-ins
    end



procedure entry
  begin
    display w-pat-ikey
    display w-batch-nbr
    display w-claim-nbr
    for f002-ins
      begin
        perform append
        end
    end

procedure find
begin
     for missing f002-ins
     begin
       get f002-ins via clmhdr-batch-nbr, clmhdr-claim-nbr, key-pat-mstr 	&
	using w-batch-nbr, w-claim-nbr, w-pat-ikey
       get f076-find opt
       let w-ins-acronym = ins-acronym of f076-find
       let w-ins-name    = ins-full-name of f076-find
       display w-ins-acronym
       display w-ins-name
       end
end

build detail list
