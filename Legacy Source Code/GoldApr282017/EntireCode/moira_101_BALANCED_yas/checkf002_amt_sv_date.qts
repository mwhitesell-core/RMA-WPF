; 2010/Nov/03 M.C.	- check  clmdtl-amt-tech-billed from all details including
;			  adjust detail to see if match with clmhdr-amt-tech-billed.
;			- check if clmhdr-serv-date = 0

cancel clear
set lock record update
set process nolimit


request extract_claim_clmdtl            	&
	       	on calculation errors report	&
		on edit errors report

access *extf002hdr link to f002-claims-mstr

sel if clmdtl-oma-cd <> '0000' and clmdtl-oma-cd <> 'ZZZZ' and clmdtl-oma-cd <> 'PAID'

sorted on key-clm-batch-nbr on key-clm-claim-nbr

temp x-amt-tech-billed zoned*6 signed
item x-amt-tech-billed = x-amt-tech-billed + clmdtl-amt-tech-billed	&
	reset at key-clm-claim-nbr

temp x-amt-ohip zoned*7 signed
item x-amt-ohip  = x-amt-ohip + clmdtl-fee-ohip		&
	reset at key-clm-claim-nbr

temp x-amt-oma  zoned*7 signed
item x-amt-oma   = x-amt-oma  + clmdtl-fee-oma 		&
	reset at key-clm-claim-nbr

def x-date zoned*8 unsigned = nconvert(clmdtl-sv-date)

temp x-sv-date zoned*8 unsigned
item x-sv-date minimum x-date reset at key-clm-claim-nbr

temp xcount zoned*7
item xcount = xcount + 1

subfile f002dtl_tech_serv_date keep at key-clm-claim-nbr include	&
	extf002hdr, x-amt-tech-billed, x-sv-date, xcount, x-amt-ohip, x-amt-oma


request sel_diff_tech_serv_date			&
	       	on calculation errors report	&
		on edit errors report

access *f002dtl_tech_serv_date

subfile sel_tech_amt_diff keep if x-amt-tech-billed <> clmhdr-amt-tech-billed &
			or    x-amt-ohip <> clmhdr-tot-claim-ar-ohip      &
			or    x-amt-oma  <> clmhdr-tot-claim-ar-oma       &
include f002dtl_tech_serv_date               

subfile sel_sv_date_diff   keep if clmhdr-serv-date = 0 	&
			       or clmhdr-serv-date <> x-sv-date &
include f002dtl_tech_serv_date               


