;#> program-id.     m027.qks
;    program purpose : entry of a doctor's contacts including their own address/phone etc
;
;    modification historY
;        date   who     description
; 2005/jan/19   b.e.	- original
; 2005/mar/17   M.C.    - include web user name
; 2006/apr/04   M.C.	- if user update contacts-surname for doctor type in f027, also update doc-name in f020
;			- include f020-doctor-mstr 
; 2006/aug/08   M.C.    - define & pass x-doc-ohip-nbr when calling to m028
; 2006/oct/19   M.C.	- extend the size of x-contacts-name from 40 to 60   
; 2006/oct/23   M.C.	- Upshift x-contacts-name 
; 2006/oct/26   M.C.	- passing f027-contacts-mstr when calling m028 
; 2006/oct/31   M.C.	- add new designer procedure 'copy' to the other doctor records that
;                         share the same doc ohip nbr

can clear
; 2006/06/06 - MC
;screen $pb_obj/m027 receiving x-doc-nbr, x-doc-name  
screen $pb_obj/m027 receiving f020-doctor-mstr, x-doc-nbr, x-doc-name  
; 2006/06/06 - end

; 2006/08/08 - MC
temp x-doc-ohip-nbr zoned*6 unsigned
; 2006/08/08 - end

temp x-doc-nbr char*3
temp x-contacts-type char*1
temp x-doc-name char*30
;temp x-contacts-name char*40
temp x-contacts-name char*60


; 2006/06/06 - MC
file f020-doctor-mstr master
; 2006/06/06 - end

file f027-contacts-mstr occurs 4
  access viaindex  contacts-key  using filler , x-doc-nbr
item filler initial " "
item doc-nbr initial x-doc-nbr


; 2006/10/31 - MC
file f028-contacts-info-mstr designer
   access viaindex contacts-info-key using ' ', doc-nbr of f027-contacts-mstr,	&
                x-contacts-type

file f020-doctor-mstr alias f020-designer designer open 1
   access viaindex doc-ohip-nbr using x-doc-ohip-nbr

file f027-contacts-mstr alias f027-copy     designer  open 1
   access viaindex contacts-key via filler, doc-nbr, contacts-type      &
        using ' ', doc-nbr of f020-designer, x-contacts-type

file f028-contacts-info-mstr alias f028-copy  designer open 1
   access viaindex contacts-info-key using ' ', doc-nbr of f020-designer, &
                x-contacts-type, contacts-location of f028-contacts-info-mstr

; 2006/10/31 - end


title "Contacts for: "              at 1,30
align (,,50)
field x-doc-name  display  predisplay

title "Type"			at 3,3
skip 
align (1,,5) (,9,22)
cluster occurs with f027-contacts-mstr
field contacts-type of f027-contacts-mstr 	required nochange	&
      lookup noton f027-contacts-mstr 				 	&
	    via filler, 						&
		doc-nbr,  						&
		contacts-type                     			&
	  using filler of f027-contacts-mstr, 				&
		doc-nbr of f027-contacts-mstr,				&
		contacts-type  of f027-contacts-mstr			
field contacts-surname     	label "Surname    :"
skip 
align        (,9,22) (,54,61) (,69,74)
field contacts-given-names 	label "Given Names:"
field contacts-inits 	   	label "Inits:" 
field contacts-sex	   	label "Sex:"
skip
align        (,9,22) (,54,74)
field contacts-title	 	label "Title      :"
field billing-entry-flag 	label "Billing data entry?"
; 2005/03/17 - -MC
field logon-username 		label "Web username:"
; 2005/03/17 - end
skip 1

procedure designer more
begin
  let x-doc-nbr = doc-nbr of f027-contacts-mstr
; 2006/08/08 - MC
  let x-doc-ohip-nbr = doc-ohip-nbr of f020-doctor-mstr
; 2006/08/08 - end
  let x-contacts-type = contacts-type of f027-contacts-mstr
   let x-contacts-name =  upshift( truncate(lj(contacts-surname of f027-contacts-mstr))                &
                         + ","                                           &
                         + truncate(lj(contacts-given-names of f027-contacts-mstr)))
  run screen $pb_obj/m028 passing x-doc-nbr,            &
; 2006/08/08 - MC - add in the passing parameter
				  x-doc-ohip-nbr,	&
; 2006/08/08 - end
                                  x-contacts-type,      &
; 2006/10/26 - MC
;                                  x-contacts-name       mode same
                                  x-contacts-name,      &
                           f027-contacts-mstr           mode same
; 2006/10/26 - end
end

; 2006/10/31 - MC
procedure internal copy_contact
begin
  get f027-copy optional
; if f027 contacts mstr exists then update f027;otherwise, create f027
  if accessok
  then begin
      let contacts-surname of f027-copy  = contacts-surname of f027-contacts-mstr
      let contacts-given-names of f027-copy = contacts-given-names of f027-contacts-mstr
      let contacts-inits of f027-copy = contacts-inits of f027-contacts-mstr
      let contacts-title of f027-copy = contacts-title of f027-contacts-mstr
      let contacts-sex of f027-copy = contacts-sex of f027-contacts-mstr
      let billing-entry-flag of f027-copy = billing-entry-flag of f027-contacts-mstr
      let logon-username of f027-copy = logon-username of f027-contacts-mstr
      put f027-copy       
      end
  else begin
      let doc-nbr of f027-copy   = doc-nbr of f020-designer    
      let contacts-type of f027-copy = x-contacts-type
      let contacts-surname of f027-copy  = contacts-surname of f027-contacts-mstr
      let contacts-given-names of f027-copy = contacts-given-names of f027-contacts-mstr
      let contacts-inits of f027-copy = contacts-inits of f027-contacts-mstr
      let contacts-title of f027-copy = contacts-title of f027-contacts-mstr
      let contacts-sex of f027-copy = contacts-sex of f027-contacts-mstr
      let billing-entry-flag of f027-copy = billing-entry-flag of f027-contacts-mstr
      let logon-username of f027-copy = logon-username of f027-contacts-mstr
      put f027-copy
      end

;  check f028 contacts info mstr, if exists, update;otherwise, create f028
   while retrieving f028-contacts-info-mstr
   begin      
     get f028-copy optional
     if accessok
     then begin
          let contacts-addr-1     of f028-copy = contacts-addr-1     of f028-contacts-info-mstr
          let contacts-addr-2     of f028-copy = contacts-addr-2     of f028-contacts-info-mstr
          let contacts-addr-3     of f028-copy = contacts-addr-3     of f028-contacts-info-mstr
          let postal-code         of f028-copy = postal-code         of f028-contacts-info-mstr
          let contacts-phone-nbr  of f028-copy = contacts-phone-nbr  of f028-contacts-info-mstr
          let contacts-phone-ext  of f028-copy = contacts-phone-ext  of f028-contacts-info-mstr
          let contacts-pager-nbr  of f028-copy = contacts-pager-nbr  of f028-contacts-info-mstr
          let contacts-cell-nbr   of f028-copy = contacts-cell-nbr   of f028-contacts-info-mstr
          let contacts-fax-nbr    of f028-copy = contacts-fax-nbr    of f028-contacts-info-mstr
          let contacts-email-addr of f028-copy = contacts-email-addr of f028-contacts-info-mstr
          put f028-copy
          end
    else begin      
           let doc-nbr of f028-copy = doc-nbr of f020-designer    
           let contacts-type of f028-copy = x-contacts-type
           let contacts-location of f028-copy = contacts-location of f028-contacts-info-mstr
           let contacts-addr-1     of f028-copy = contacts-addr-1     of f028-contacts-info-mstr
           let contacts-addr-2     of f028-copy = contacts-addr-2     of f028-contacts-info-mstr
           let contacts-addr-3     of f028-copy = contacts-addr-3     of f028-contacts-info-mstr
           let postal-code         of f028-copy = postal-code         of f028-contacts-info-mstr
           let contacts-phone-nbr  of f028-copy = contacts-phone-nbr  of f028-contacts-info-mstr
           let contacts-phone-ext  of f028-copy = contacts-phone-ext  of f028-contacts-info-mstr
           let contacts-pager-nbr  of f028-copy = contacts-pager-nbr  of f028-contacts-info-mstr
           let contacts-cell-nbr   of f028-copy = contacts-cell-nbr   of f028-contacts-info-mstr
           let contacts-fax-nbr    of f028-copy = contacts-fax-nbr    of f028-contacts-info-mstr
           let contacts-email-addr of f028-copy = contacts-email-addr of f028-contacts-info-mstr
           put f028-copy
        end
     end
end



procedure designer copy help 'Copy contact to other doctor records'
begin
  let x-doc-nbr = doc-nbr of f027-contacts-mstr
  let x-doc-ohip-nbr = doc-ohip-nbr of f020-doctor-mstr
  let x-contacts-type = contacts-type of f027-contacts-mstr
  while retrieving f020-designer    
  begin
    if doc-nbr of f020-designer <> x-doc-nbr and doc-date-fac-term of f020-designer = 0
    then begin
        do copy_contact    
        end
   end
end
; 2006/10/31 - end

; 2006/04/04 - MC
procedure preupdate  
begin
  if alteredrecord
    then begin
      for f027-contacts-mstr
       begin
	let x-contacts-name = contacts-surname of f027-contacts-mstr + ',' +	&
  			      contacts-inits of f027-contacts-mstr
    	if     x-doc-name <> x-contacts-name		&
      	   and contacts-type of f027-contacts-mstr = 'D'
    	then  begin 
           let doc-name of f020-doctor-mstr = contacts-surname of f027-contacts-mstr
           let doc-inits of f020-doctor-mstr = contacts-inits  of f027-contacts-mstr 
           let doc-name-soundex of f020-doctor-mstr = soundex(doc-name of f020-doctor-mstr)
	   end
	end
     end
end
;2006/04/04 - end

build detail list

