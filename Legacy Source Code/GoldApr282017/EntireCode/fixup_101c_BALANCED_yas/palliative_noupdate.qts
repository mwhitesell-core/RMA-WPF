;doc     : palliative.qts
;purpose : create a detail claims report for palliative care to send to MOH on CD
;who     : MOH             

;
; *************************************************************

;Date		Who		Description
;2009/09/10    yas               copied from palliative.qts and take out update to f002
;                                
cancel clear
set default
set process nolimit
set lock record update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr                   

choose  key-clm-type 'B' ,  key-clm-batch-nbr "89@",                   &
        key-clm-claim-nbr, key-clm-serv-code '00000', key-clm-adj-nbr '0'

;def x-clm-date-entered date = parm prompt 'Enter Claim date(yyyymmdd): '

select f002-claims-mstr if       clmhdr-batch-type = 'C'          &
                            and  clmhdr-submit-date  = 20090624
 
subfile palliative_a     keep include                    &
clmhdr-claim-id               of f002-claims-mstr,&
key-clm-batch-nbr             of f002-claims-mstr,&
key-clm-claim-nbr             of f002-claims-mstr,&
clmhdr-doc-nbr-ohip           of f002-claims-mstr,&
clmhdr-loc                    of f002-claims-mstr,&
clmhdr-hosp                   of f002-claims-mstr,&
clmhdr-serv-date	      of f002-claims-mstr,&
CLMHDR-DATE-ADMIT      	      of f002-claims-mstr,&
clmhdr-pat-ohip-id-or-chart   of f002-claims-mstr

request two                             &
        on edit        errors report    &
        on calculation errors report

access *palliative_a                                                    &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                         &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr                &
          of f002-claims-mstr

select f002-claims-mstr if                   &
            clmdtl-oma-cd      <> "0000"     & ; details only
       and  clmdtl-oma-cd      <> "ZZZZ"     & ;   
       and  clmdtl-oma-cd      <> "PAID"     & ; no payments
       and  clmdtl-oma-cd      <> "MICM"     & ; no miscellaneous billings
       and  clmdtl-oma-cd      <> "MISJ"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICV"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISP"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MOHR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICB"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MIBR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MINH"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MHSC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "NHSC"     &
       and  clmdtl-oma-cd      <> "DHSC"     &
       and  clmdtl-oma-cd      <> "AGEP"     &
       and  clmdtl-oma-cd      <> "MICA"     &
       and  clmdtl-oma-cd      <> "MICC"     &
       and  clmdtl-oma-cd      <> "MICD"     &
       and  clmdtl-oma-cd      <> "MICE"     &
       and  clmdtl-oma-cd      <> "MICF"     &
       and  clmdtl-oma-cd      <> "MICG"     &
       and  clmdtl-oma-cd      <> "MICH"     &
       and  clmdtl-oma-cd      <> "MICJ"     &
       and  clmdtl-oma-cd      <> "MICK"     &
       and  clmdtl-oma-cd      <> "MICL"     &
       and  clmdtl-adj-nbr     =  0            ; exclude adjustments

def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne " 00"  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 unsigned =                                    &
                        nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr3 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[4:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-sv-nbr4 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[7:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

def x-fee = clmdtl-fee-ohip / x-nbr-svcs
def x-clmdtl-fee-ohip-1 zoned*7 = x-sv-nbr1 * x-fee    
def x-clmdtl-fee-ohip-2 zoned*7 = x-sv-nbr2 * x-fee  
def x-clmdtl-fee-ohip-3 zoned*7 = x-sv-nbr3 * x-fee 
def x-clmdtl-fee-ohip-4 zoned*7 = x-sv-nbr4 * x-fee 

def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2] &
                        if consec-flag = "Y"                              &
                        else " "


subfile palliative_1     keep include      &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
clmhdr-doc-nbr-ohip   of palliative_a      ,&
x-clmdtl-fee-ohip-1                      ,&
x-sv-date-1                              ,&
x-sv-nbr1                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of palliative_a      ,&
clmhdr-hosp           of palliative_a      ,&
clmhdr-pat-ohip-id-or-chart of palliative_a      

subfile palliative_1 alias palliative_svc2 append keep if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
clmhdr-doc-nbr-ohip   of f002-claims-mstr,&
x-clmdtl-fee-ohip-2                      ,&
x-sv-date-2                              ,&
x-sv-nbr2                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of palliative_a           ,&
clmhdr-hosp           of palliative_a           ,&
clmhdr-pat-ohip-id-or-chart of palliative_a    

subfile palliative_1 alias palliative_svc3 append keep if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
clmhdr-doc-nbr-ohip   of palliative_a      ,&
x-clmdtl-fee-ohip-3                      ,&
x-sv-date-3                              ,&
x-sv-nbr3                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of palliative_a           ,&
clmhdr-hosp           of palliative_a           ,&
clmhdr-pat-ohip-id-or-chart of palliative_a   

subfile palliative_1 alias palliative_svc4 append keep if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
clmhdr-doc-nbr-ohip   of palliative_a      ,&
x-clmdtl-fee-ohip-4                      ,&
x-sv-date-4                              ,&
x-sv-nbr4                                ,&
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of palliative_a           ,&
clmhdr-hosp           of palliative_a           ,&
clmhdr-pat-ohip-id-or-chart of palliative_a   

request three                           &
        on edit        errors report    &
        on calculation errors report

access *palliative_1                                                         &
  link clmhdr-pat-ohip-id-or-chart to key-pat-mstr of f010-pat-mstr opt     &
  link clmdtl-id[3:3] to doc-nbr of f020-doctor-mstr opt                    

def x-claim-nbr cha*8 = clmdtl-id[3:8]
def x-group cha*4 = "C022"

def comma cha*1 = "~"
def x-num-cr integer unsigned size 2 = 13
def x-cr        char*2 = char(x-num-cr)

subfile palliative_dtl portable keep  include           &
x-claim-nbr                                         ,&
comma                                               ,&
x-group                                             ,&
comma                                               ,&
clmhdr-doc-nbr-ohip                                 ,&
comma                                               ,&
pat-health-nbr                                      ,&
comma                                               ,&
pat-version-cd                                      ,&
comma                                               ,&
clmdtl-oma-cd                                       ,&
comma                                               ,&
x-sv-nbr1                                           ,&
comma                                               ,&
x-clmdtl-fee-ohip-1                                 ,&
comma                                               ,&
x-sv-date-1                                         ,&
x-cr


request four                            &
        on edit        errors report    &
        on calculation errors report

access *palliative_a                                                     &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                         &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr                &
          of f002-claims-mstr opt

select f002-claims-mstr if                   &
            clmdtl-oma-cd      =  "ZZZZ"  ;   & ; description  

subfile palliative_b    keep include      &
clmhdr-claim-id       of palliative_a    ,&
key-clm-batch-nbr     of palliative_a    ,&
key-clm-claim-nbr     of palliative_a    ,&
clmhdr-doc-nbr-ohip   of palliative_a      ,&
CLMHDR-DATE-ADMIT     of palliative_a      ,&
clmdtl-desc-rec       of f002-claims-mstr,&
clmhdr-loc            of palliative_a      ,&
clmhdr-hosp           of palliative_a      ,&
clmhdr-serv-date      of palliative_a      ,&
clmhdr-pat-ohip-id-or-chart of palliative_a      

request five                            &
        on edit        errors report    &
        on calculation errors report

access *palliative_b    	&
  link clmhdr-pat-ohip-id-or-chart to key-pat-mstr of f010-pat-mstr opt     &
  link clmhdr-claim-id[3:3] to doc-nbr of f020-doctor-mstr opt                    

sorted on key-clm-batch-nbr on key-clm-claim-nbr

temp clmdtl-desc char*40
item clmdtl-desc = truncate(pack(clmdtl-desc)) + truncate(pack(clmdtl-desc-rec)) reset at key-clm-claim-nbr

def x-claim-nbr cha*8 = clmhdr-claim-id[3:8]
def x-group cha*4 = "C022"

def x-pat-name char*42 = pack(pat-surname+", "+pat-given-name)

def clmdtl-sv-yy zoned*4 unsigned = nconvert(ascii(clmhdr-serv-date,8)[1:4])
def clmdtl-sv-mm zoned*4 unsigned = nconvert(ascii(clmhdr-serv-date,8)[5:2])
def clmdtl-sv-dd zoned*4 unsigned = nconvert(ascii(clmhdr-serv-date,8)[7:2])

def x-age zoned*4 unsigned  =  clmdtl-sv-yy - pat-birth-date-yy     &
        if   ( clmdtl-sv-mm >  pat-birth-date-mm)                   &
          or ((clmdtl-sv-mm =  pat-birth-date-mm) and               &
              (clmdtl-sv-dd >= pat-birth-date-dd))                  &
        else clmdtl-sv-yy - pat-birth-date-yy - 1

def comma cha*1 = "~"
def x-num-cr integer unsigned size 2 = 13
def x-cr        char*2 = char(x-num-cr)

;subfile palliative_desc  portable keep  include           &
subfile palliative_desc  portable keep at key-clm-claim-nbr  include           &
x-claim-nbr                                         ,&
comma                                               ,&
PAT-SURNAME                                         ,&
comma                                               ,&
x-age                                               ,&
comma                                               ,&
PAT-SEX                                             ,&
comma                                               ,&
pat-health-nbr                                      ,&
comma                                               ,&
pat-version-cd                                      ,&
comma                                               ,&
clmhdr-doc-nbr-ohip                                 ,&
comma                                               ,&
CLMHDR-DATE-ADMIT                                   ,&
comma                                               ,&
CLMDTL-DESC                                         ,&
x-cr

build $obj/palliative_noupdate
