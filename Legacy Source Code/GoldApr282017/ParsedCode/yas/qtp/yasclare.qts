;doc     : clare_peds_ortha.qts 
;Purpose : report peds ortho surgery billings for Dr. Peterson, Dr. Ayeni                         
;          for period July 1, 2009 to June 30, 2010 for under age 16 patients                     
;          for period July 1, 2009 to June 30, 2010 for under age 18 patients                     
;who     : Clare Mitchell (manager surgery)       
;
; *************************************************************

;Date           Who             Description
;2011/01/18     Yasemin         
;2011/04/01     Yasemin        20100701 to current 
;2012/03/28     Yasemin        20110401 to 20110830 levis strumas bain     
;2012/03/28     Yasemin        20110901 to current  levis strumas bain choi    

cancel clear
set default
set process nolimit
set lock record update

request one                             &
        on edit        errors report    &
        on calculation errors report

access f002-claims-mstr                   

;peds plastic surgery
choose  key-clm-type 'B' ,  key-clm-batch-nbr "22G19@",                  &
                                              "33G19@",                  &
                                              "98G19@",                  &
                                              "22K41@",                  &
                                              "33K41@",                  &
                                              "98K41@",                  &
                                              "22W38@",                  &
                                              "33W38@",                  &
                                              "98W38@",                  &
                                              "22116@",                  &
                                              "33116@",                  &
                                              "98116@",                  &
                                              "2265E@",                  &
                                              "3365E@",                  &
                                              "9865E@",                  &
                                              "22449@",                  &
                                              "33449@",                  &
                                              "98449@",                  &
                                              "22942@",                  &
                                              "33942@",                  &
                                              "98942@",                  &
                                              "22910@",                  &
                                              "33910@",                  &
                                              "98910@",                  &
                                              "22219@",                  &
                                              "33219@",                  &
                                              "98219@",                  &
;peds ortho surgery
                                              "2209P@",                  &
                                              "3309P@",                  &
                                              "9809P@",                  &
                                              "22098@",                  &
                                              "33098@",                  &
                                              "98098@",                  &
                                              "22W37@",                  &
                                              "33W37@",                  &
                                              "98W37@",                  &
                                              "22P31@",                  &
                                              "33P31@",                  &
                                              "98P31@",                  &
                                              "22567@",                  &
                                              "33567@",                  &
                                              "98567@",                  &
;peds opthalmologist
                                              "22Y37@",                  &
                                              "33Y37@",                  &
                                              "98Y37@",                  &
                                              "22V13@",                  &
                                              "33V13@",                  &
                                              "98V13@",                  &
 key-clm-claim-nbr, key-clm-serv-code '00000', key-clm-adj-nbr '0'

select f002-claims-mstr if       clmhdr-batch-type = 'C'    &
                      and	 clmhdr-agent-cd <> 6

subfile clare     keep include                    &
clmhdr-claim-id               of f002-claims-mstr,&
key-clm-batch-nbr             of f002-claims-mstr,&
key-clm-claim-nbr             of f002-claims-mstr,&
clmhdr-loc                    of f002-claims-mstr,&
clmhdr-hosp                   of f002-claims-mstr,&
clmhdr-agent-cd               of f002-claims-mstr,&
clmhdr-pat-ohip-id-or-chart

request two                             &
        on edit        errors report    &
        on calculation errors report

access *clare                                                          &
  link 'B', key-clm-batch-nbr, key-clm-claim-nbr                         &
   to  key-clm-type, key-clm-batch-nbr, key-clm-claim-nbr                &
          of f002-claims-mstr

select f002-claims-mstr if                   &
            clmdtl-sv-date     >= '20160401' &
       and  clmdtl-sv-date     <= '20170331' &
;            CLMDTL-DATE-PERIOD-END   >= '20120501' &
;       and  CLMDTL-DATE-PERIOD-END   <= '20120530' &
       and  clmdtl-oma-cd      <> "0000"     & ; details only
       and  clmdtl-oma-cd      <> "ZZZZ"     & ;   
       and  clmdtl-oma-cd      <> "PAID"     & ; no payments
       and  clmdtl-oma-cd      <> "MICM"     & ; no miscellaneous billings
       and  clmdtl-oma-cd      <> "MISJ"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICV"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MISP"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MOHR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MICB"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MIBR"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MINH"     & ; "  "             "
       and  clmdtl-oma-cd      <> "MHSC"     & ; "  "             "
       and  clmdtl-oma-cd      <> "NHSC"     &
       and  clmdtl-oma-cd      <> "DHSC"     &
       and  clmdtl-oma-cd      <> "AGEP"     &
       and  clmdtl-oma-cd      <> "MICA"     &
       and  clmdtl-oma-cd      <> "MICC"     &
       and  clmdtl-oma-cd      <> "MICD"     &
       and  clmdtl-oma-cd      <> "MICE"     &
       and  clmdtl-oma-cd      <> "MICF"     &
       and  clmdtl-oma-cd      <> "MICG"     &
       and  clmdtl-oma-cd      <> "MICH"     &
       and  clmdtl-oma-cd      <> "MICJ"     &
       and  clmdtl-oma-cd      <> "MICK"     &
       and  clmdtl-oma-cd      <> "MICL"     &
       and  clmdtl-oma-cd      <> "T995"      ;clinic 60 proff fees
;       and  clmdtl-adj-nbr     =  0            ; regular claims

def consec-flag  char*1 = "Y" if clmdtl-consec-dates-r[1:3] ne "0OP"  &
                             and clmdtl-consec-dates-r[1:3] ne "0MR"  &
                             and clmdtl-consec-dates-r[1:3] ne "0BI"  &
                             and clmdtl-consec-dates-r[1:3] ne "0  "  &
                             and clmdtl-consec-dates-r[1:3] ne " 00"  &
                             and clmdtl-consec-dates-r[1:3] ne "000"  &
                             and clmdtl-consec-dates-r[1:3] ne "00 "  &
                             and clmdtl-consec-dates-r[1:3] ne "   "

define x-sv-nbr1  zoned*2 unsigned = clmdtl-nbr-serv

def x-sv-nbr2 zoned*2 unsigned =                                    &
                        nconvert(ascii(clmdtl-consec-dates,9)[1:1]) &
                        if consec-flag = "Y"                        &
                        else 0

def x-sv-nbr3 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[4:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-sv-nbr4 zoned*2 unsigned =                                     &
                        nconvert(ascii(clmdtl-consec-dates,9)[7:1])  &
                        if consec-flag = "Y"                         &
                        else 0

def x-nbr-svcs zoned*2 unsigned =    x-sv-nbr1 &
                                   + x-sv-nbr2 &
                                   + x-sv-nbr3 &
                                   + x-sv-nbr4

def x-fee zoned*7 numeric = clmdtl-fee-ohip / x-nbr-svcs if clmdtl-adj-nbr = 0 &
        else clmdtl-fee-ohip

; Note: adjustment record will contain zero nbr of svc
def x-clmdtl-fee-ohip-1 zoned*7 = x-sv-nbr1 * x-fee    if clmdtl-adj-nbr = 0  &
        else x-fee
def x-clmdtl-fee-ohip-2 zoned*7 = x-sv-nbr2 * x-fee  
def x-clmdtl-fee-ohip-3 zoned*7 = x-sv-nbr3 * x-fee 
def x-clmdtl-fee-ohip-4 zoned*7 = x-sv-nbr4 * x-fee 

def x-sv-date-1 char*8 = clmdtl-sv-date

def x-sv-date-2 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[2:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-3 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[5:2] &
                        if consec-flag = "Y"                              &
                        else " "

def x-sv-date-4 char*8 = clmdtl-sv-date[1:6] + clmdtl-consec-dates-r[8:2] &
                        if consec-flag = "Y"                              &
                        else " "


subfile clare1     keep include           &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-1                      ,&
x-sv-date-1                              ,&
x-sv-nbr1                                ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of clare           ,&
clmhdr-hosp           of clare           ,&
clmhdr-agent-cd       of clare           ,&
clmhdr-pat-ohip-id-or-chart of clare      

subfile clare1 alias clare_svc2 append keep if  x-sv-nbr2 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-2                      ,&
x-sv-date-2                              ,&
x-sv-nbr2                                ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of clare           ,&
clmhdr-hosp           of clare           ,&
clmhdr-agent-cd       of clare           ,&
clmhdr-pat-ohip-id-or-chart of clare    

subfile clare1 alias clare_svc3 append keep if  x-sv-nbr3 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-3                      ,&
x-sv-date-3                              ,&
x-sv-nbr3                                ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of clare           ,&
clmhdr-hosp           of clare           ,&
clmhdr-agent-cd       of clare           ,&
clmhdr-pat-ohip-id-or-chart of clare   

subfile clare1 alias clare_svc4 append keep if  x-sv-nbr4 <> 0 include &
clmdtl-id             of f002-claims-mstr,&
key-clm-batch-nbr     of f002-claims-mstr,&
key-clm-claim-nbr     of f002-claims-mstr,&
x-clmdtl-fee-ohip-4                      ,&
x-sv-date-4                              ,&
x-sv-nbr4                                ,& 
clmdtl-diag-cd        of f002-claims-mstr,&
clmdtl-oma-cd         of f002-claims-mstr,&
clmhdr-loc            of clare           ,&
clmhdr-hosp           of clare           ,&
clmhdr-agent-cd       of clare           ,&
clmhdr-pat-ohip-id-or-chart of clare   

request three                           &
        on edit        errors report    &
        on calculation errors report


access *clare1                                                              &
  link clmhdr-pat-ohip-id-or-chart to key-pat-mstr of f010-pat-mstr opt     &
  link clmdtl-id[3:3] to doc-nbr of f020-doctor-mstr opt                    

def pat-birth-date-yy zoned*4 unsigned = nconvert(ascii(pat-birth-date,8)[1:4])
def pat-birth-date-mm zoned*2 unsigned = nconvert(ascii(pat-birth-date,8)[5:2])
def pat-birth-date-dd zoned*2 unsigned = nconvert(ascii(pat-birth-date,8)[7:2])

def clmdtl-sv-yy zoned*4 unsigned = nconvert(x-sv-date-1[1:4])
def clmdtl-sv-mm zoned*2 unsigned = nconvert(x-sv-date-1[5:2])
def clmdtl-sv-dd zoned*2 unsigned = nconvert(x-sv-date-1[7:2])

def x-age zoned*4 unsigned  =  clmdtl-sv-yy - pat-birth-date-yy     &
        if   ( clmdtl-sv-mm >  pat-birth-date-mm)                   &
          or ((clmdtl-sv-mm =  pat-birth-date-mm) and               &
              (clmdtl-sv-dd >= pat-birth-date-dd))                  &
        else clmdtl-sv-yy - pat-birth-date-yy - 1

;sel if pat-prov-cd = 'ON' and x-age < 18 

sort on doc-nbr

temp x-18   num*7
item x-18   = x-18  + x-clmdtl-fee-ohip-1             &
              if x-age <18                            &                   
              reset at doc-nbr

temp x-18-plus     num*7
item x-18-plus     = x-18-plus    + x-clmdtl-fee-ohip-1      &
                   if x-age >= 18                            &
                   reset at doc-nbr

temp x-all         num*7
item x-all         = x-all        + x-clmdtl-fee-ohip-1      &
                   reset at doc-nbr

def comma cha*1 = "~"
def x-num-cr integer unsigned size 2 = 13
def x-cr        char*2 = char(x-num-cr)

subfile clare2 portable keep at doc-nbr include                &
doc-dept                                            ,&
comma                                               ,&
doc-nbr                                             ,&
comma                                               ,&
doc-name                                            ,&
comma                                               ,&
doc-inits                                           ,&
comma                                               ,&
x-18                                                ,&
comma                                               ,&
x-18-plus                                           ,&
comma                                               ,&
x-all                                               ,&
x-cr

build $obj/yasclare
