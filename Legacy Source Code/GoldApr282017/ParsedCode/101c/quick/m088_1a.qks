;Program: m088_1a.qks
; Purpose: display and maintain rat rejection records in f088-hdr/dtl
;          This program is called from d003.cbl
; Mod Hist
; 2002/nov/13 M.C.	- original clone from m088.qks
; 2003/dec/10 A.A.      - alpha doctor nbr	

screen $pb_obj/m088_1a activities find, change

temp x-clmhdr-batch-nbr   char*8 reset at startup

temp x-ped 				zoned unsigned size 8
temp x-balance-due			zoned   signed size 7
temp x-clmhdr-manual-and-tape-payments	zoned   signed size 7

file parameter-file designer

file f088-rat-rejected-claims-hist-hdr alias hdr primary
        access backwards via rat-rejected-claim	&
        using      clmhdr-pat-key-data[1:11] 

  item last-mod-date            final sysdate
  item last-mod-time            final systime/10000
  item last-mod-user-id         final signonuser


file f002-claims-mstr reference
       access via  key-clm-type,		&
      		   key-clm-batch-nbr,		&
      		   key-clm-claim-nbr,		&
      		   key-clm-serv-code,		&
		   key-clm-adj-nbr		&
        using ('B'), 				&
	      clmhdr-batch-nbr of hdr,		& 
	      clmhdr-claim-nbr of hdr,		&
	      "00000",				&
	      "0"

file f088-rat-rejected-claims-hist-dtl alias dtl detail occurs 9
	access via clmhdr-batch-nbr, clmhdr-claim-nbr,  &
		   clmhdr-oma-suff-adj, ped 	&
	using      clmhdr-batch-nbr of hdr, clmhdr-claim-nbr of hdr, &
		   '@', ped of hdr

  item last-mod-date            final sysdate
  item last-mod-time            final systime/10000
  item last-mod-user-id         final signonuser

title "RAT Errors Costing Maintenance Screen" at 1,40 

align (1,4,21) (,33,34)
field x-clmhdr-batch-nbr      	nochange pic "^^-^^^-^^^"	&
					 label "Batch/Claim Nbr:"  auto
field clmhdr-claim-nbr of hdr	nochange label ":" significance 2
align (1,4,21) 
field clmhdr-doc-nbr of hdr	nochange label "Doctor Nbr     :"
Field ped of hdr		nochange label "RAT's PED      :"
align (1,4,21) (37,40,55)
field ohip-err-code of hdr	nochange label "Error code     :"
field charge-status of hdr		 label "Chargeable?    :"

skip 1

title"ORIGINAL CLAIM'S:" at ,3
skip 
title"             Outstanding      Billed      Paid       PED    cycle" at ,3

align (,5,19) (,,29) (,,41) (,,51) (,,64)
field x-balance-due pic "^^^^^.^^ " label "CURRENT" display
field clmhdr-tot-claim-ar-ohip 			    display
field x-clmhdr-manual-and-tape-payments significance 5 pic "^^^^^.^^ "	&
						    display
field clmhdr-date-period-end of f002-claims-mstr    display
field clmhdr-cycle-nbr 				    display
skip  
align (,5,31) (,,41) 
field part-hdr-amt-bill		nochange label "AT TIME OF RAT"
field part-hdr-amt-paid		nochange

skip 1
title 'PAYMENT DETAILS'    at ,1

skip 
title  &
'      OMA       OHIP                               Doc         ' at ,1
skip 
title &
'    Svc date    Code  Error     Billed      Paid   Nbr      PED' at ,1
;01 yyyy/mm/dd xxxx x    XX    9,999,99- 9,999,99-  xxx  yyyy/mm/dd
;123456789 123456789 123456789 123456789 123456789 123456789
;         1         2         3         4         5         6

skip 

align (1,,4) (,,15) (,,20) (,,25) (,,31) (,,41) (,,52) (,,59)
cluster occurs with dtl
field clmdtl-sv-date      of dtl pic "^^^^/^^/^^"
field clmhdr-adj-oma-cd   of dtl 
field clmhdr-adj-oma-suff of dtl 
field ohip-err-code       of dtl 
field part-dtl-amt-bill   of dtl
field part-dtl-amt-paid   of dtl

field clmhdr-doc-nbr      of dtl  pic "^^^"
field ped 	          of dtl

procedure find
begin
  get parameter-file sequential
  get hdr via rat-rejected-claim backwards	&
;!	using clmhdr-pat-key-data of parameter-file [1:11]
	using clmhdr-pat-key-data of parameter-file [1:10]
end

procedure detail find
begin
   for missing dtl
       begin
         get dtl optional
         end
     end

procedure postfind
begin
  let x-clmhdr-batch-nbr   =   clmhdr-pat-key-data of parameter-file[1:2] &
			     + clmhdr-pat-key-data of parameter-file[4:6]
; (reverse sign of payments to show payment as a positive amount)
  let x-clmhdr-manual-and-tape-payments = 0 - clmhdr-manual-and-tape-payments
  let x-balance-due = clmhdr-tot-claim-ar-ohip + clmhdr-manual-and-tape-payments
end


build detail list
