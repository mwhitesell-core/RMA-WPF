set verify errors
;#> PROGRAM-ID.     H113.QKS
;
;       ((C)) Dyad Technologies
;
;    PURPOSE: Query DEFAULT Physician Earning Compensation
;	      transcations
;    MODIFICATION HISTORY
;        DATE    SAF #  WHO      DESCRIPTION
;     95/JUN/29  ____   B.M.L    - original COPIED FROM D113.QKS
;   1999/Apr/21		S.B. 	 - Fixed sizes of dates and alignment of
;                                  fields for Y2K.  Shortened the description
;                                  field to make space.
;   1999/Jun/07         S.B.     - Altered the call to scrtitle.use and
;                                  stdhilite.use to be called from $use
;                                  instead of src.
;                                - Removed the call to secfile.use because
;                                  it was not doing anything.
; 2004/sep/02 b.e.      - made all fields display so that data can't be
;                         accidently changed
;                       - added new designer procedcure FIX to allow change
;                         of each field only after a 'secret' password has
;                         been entered
;

screen $pb_obj/h113.qkc							&
	on line 6 for 18					&
	activities find						&
	message on line 24					&
	receiving 	w-doc-nbr,				&
			w-ep-yr,      				&
			constants-mstr-rec-6,			&
			f020-doctor-mstr

temp w-doc-nbr char*3
temp w-ep-yr
temp x-srch-code char*6
temp w-password date
temp w-sysdate date

description of screen                                                 &
"                                                                   " &
"       This screen allows a User to query ANY DEFAULT              " &
"       Compensation transactions.                                  "

file  f113-default-comp-history  occurs 12
	access viaindex default-comp-history-key		&
		via doc-nbr, ep-yr                              &
	using w-doc-nbr, w-ep-yr


file f020-doctor-mstr master

file f190-comp-codes      reference
     access viaindex comp-code using comp-code of f113-default-comp-history
file constants-mstr-rec-6 designer

define x-screen-name char*55 = "DEFAULT COMPENSATION HISTORY"
use $use/scrtitle.use  nol nod
use $use/stdhilite.use nol nod

skip to 2
title &
"   --E/P--                                                                ENTRY" &
	at ,1
skip
title &
"   FROM   TO     COMPENSATION      FACTOR    UNITS       GROSS         NET  E/P" &
	at ,1
skip

cluster occurs with f113-default-comp-history
;         1         2         3         4         5         6         7         8
;....v....0....v....0....v....0....v....0....v....0....v....0....v....0....v....0
;01 YYYYMM YYYYMM TAX___-_______________99.9999*99.999- 999,999.99- 999,999.99- YYMM
align (1,,4) (,,11) (,,18)(,24,25) (,,36) (,,44) (,,48) (,,52) (,,64)(,,75)
field ep-nbr-from	of f113-default-comp-history 		
field ep-nbr-to		of f113-default-comp-history
field comp-code         of f113-default-comp-history
field desc-short	of f190-comp-codes label "-"	&
                        pic "^^^^^^^^^^^"
field factor		of f113-default-comp-history
field comp-units	of f113-default-comp-history if units-dollars-flag of f190-comp-codes = "U" pic "^^^^^^"
field amt-gross		of f113-default-comp-history ; ;;;;IF UNITS-DOLLARS-FLAG OF F190-COMP-CODES = "D"
field amt-net		of f113-default-comp-history
field ep-nbr-entry	of f113-default-comp-history

cluster
field w-password noid noecho

procedure designer fix help "Use this 'secret' function to make corrections to data"
begin
  let w-sysdate = sysdate
  accept w-password
  if w-password = w-sysdate
  then begin
    accept ep-nbr-from	
    accept ep-nbr-to		
    accept comp-code         
    accept desc-short	
    accept factor		
    accept comp-units	
    accept amt-gross		
    accept amt-net		
    accept ep-nbr-entry	
  end
end

build
