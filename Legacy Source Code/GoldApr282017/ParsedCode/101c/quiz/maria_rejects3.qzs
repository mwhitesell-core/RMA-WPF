; Program: maria_rejects3.qzs
; Purpose: ohip submission reject report
;
; Mod Hist
; 2000/may/06 Y.B.
; 2003/jul/31 Y.B.   if physician has 10 claims same patient same error code
;                    counts as 1. It will never match costing because doctor
;                    might have the same patient in different months, costings
;                    is just by doctor not month
; 2003/nov/27 yas   access f087-submitted instead of f087-manual
; 2003/nov/28 b.e.  - support access to f087_hdr/dtl 
; 2004/may/18 M.C.  - alpha doc nbr
; 2006/oct/12 yas.  - modify for new year
; 2008/oct/20 yas.  - modify for new year
; 2009/Aug/31 yas.  - modify for new year
; 2011/Mar/15 yas.  - modify for new year

cancel clear
set rep nolimit
set page length 0
set page width 254
set rep nohead
set rep device disc name maria_rejects3

access  *maria_rejects2						&
;!  link (nconvert(doc-nbr))					&	
  link doc-nbr							&	
   to  doc-nbr of f020-doctor-mstr opt				&
  link edt-err-h-cd-1						&
    to ohip-err-code of f093-ohip-error-msg-mstr opt		& 
  link ('B', 							&
	clmhdr-batch-nbr, 					&
	clmhdr-claim-nbr, 					&
	'00000', 						&
	'0')                					& 
    to  key-clm-type, 						&
	key-clm-batch-nbr, 					&
	key-clm-claim-nbr, 					&
	key-clm-serv-code, 					&
	key-clm-adj-nbr  of f002-claims-mstr  opt

select if    ohip-err-cat-code = "E"    &
         and ped > 20140630 		&
	 and ped < 20150701

and sel                                                                 &
     if    (   doc-date-fac-start  = 0000000                            &
            or doc-date-fac-start < 20150701                            &
           )                                                            &
       and (  doc-date-fac-term   = 00000000                            &
            or doc-date-fac-term >  20140630                            &
           )

def x-ped   cha*8 = ascii(ped,8)

def x-mm    cha*2 = x-ped[5:2]

def x-dd    cha*2 = x-ped[7:2]

def x-yyyy   = nconvert(x-ped[1:4])

def x-mmm   cha*9 = "January"   if x-mm  = "01"      &
               else "February"  if x-mm  = "02"      &
               else "March"     if x-mm  = "03"      &
               else "April"     if x-mm  = "04"      &
               else "May"       if x-mm  = "05"      &
               else "June"      if x-mm  = "06"      &
               else "July"      if x-mm  = "07"      &
               else "August"    if x-mm  = "08"      &
               else "September" if x-mm  = "09"      &
               else "October"   if x-mm  = "10"      &
               else "November"  if x-mm  = "11"      &
               else "December"  if x-mm  = "12"

def x-sort-key cha*1 =       "a" if x-mm = "07"  &
                       else  "b" if x-mm = "08"  &
                       else  "c" if x-mm = "09"  &
                       else  "d" if x-mm = "10"  &
                       else  "e" if x-mm = "11"  &
                       else  "f" if x-mm = "12"  &
                       else  "g" if x-mm = "01"  &
                       else  "h" if x-mm = "02"  &
                       else  "i" if x-mm = "03"  &
                       else  "j" if x-mm = "04"  &
                       else  "k" if x-mm = "05"  &
                       else  "l" if x-mm = "06"  

sort on x-sort-key		        &
     on doc-nbr				&
     on clmhdr-pat-ohip-id-or-chart	&
     on edt-err-h-cd-1

footing at x-sort-key                                  &
tab 1   x-mmm                         		       &
tab 15  'Totals: '				       &
tab 25  count at edt-err-h-cd-1      pic "^^^,^^^"    &
skip 2

final footing						&
skip 3							&
tab 1 'Grand Totals:'					&
tab 25 count at edt-err-h-cd-1       pic "^^^,^^^"            


build $obj/maria_rejects3
