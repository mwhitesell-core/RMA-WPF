;#> PROGRAM-ID.     R712.QZS
;
;
;
;    PROGRAM PURPOSE : TO REPORT SUSPENDED DUPLICATE CLAIMS WITH
;                      STATUS "Y"
;
;    MODIFICATION HISTORY
;	DATE	SMS #	WHO	DESCRIPTION
;     91.03.13	000	Y.B.	COPIED FROM R711
;   1999/jan/28         B.E.    - y2k
;   1999/May/27 	S.B.    - Added the use file
;                        	  def_clmhdr_status.def to 
;                        	  prevent hardcoding of clmhdr-status.
;

can clear
set report nolimit
set page width 132 length 63
set report device disc name r712
set formfee

access f002-suspend-hdr		&
	link clmhdr-doc-ohip-nbr, clmhdr-accounting-nbr	&
	to   clmdtl-doc-ohip-nbr, clmdtl-accounting-nbr of f002-suspend-dtl &
	link clmhdr-doc-ohip-nbr, clmhdr-accounting-nbr	&
	to   add-doc-ohip-nbr, add-accounting-nbr of f002-suspend-address

;S.B.
use $use/def_clmhdr_status.def
use $use/f002_consecutive_dates.def

;select f002-suspend-hdr if clmhdr-status = "Y"
select f002-suspend-hdr if clmhdr-status = clmhdr-status-cancel

sorted on clmhdr-doc-ohip-nbr, clmhdr-accounting-nbr

page heading	&
  tab   1 "RUN DATE:"		&
  tab  12 sysdate		&
  tab  53 "REGIONAL MEDICAL ASSOCIATES"	&
  tab 120 "PAGE"		&
  tab 126 syspage		&
skip 2	&
  tab  54 "LIST OF DUPLICATE CLAIMS"

heading at clmhdr-accounting-nbr	&
skip 2	&
  tab   1 "DOCTOR OHIP NBR"		&
  tab  17 clmhdr-doc-ohip-nbr pic "^^^^^^" significance 6	&
  tab  25 "ACCOUNTING NBR"		&
  tab  40 clmhdr-accounting-nbr		&
  tab  50 "AGENT"			&
  tab  56 clmhdr-agent-cd		&
  tab  59 "SUBSCRIBER NAME"		&
  tab  75 add-surname			&
; TAB  85 "INITIALS"			&
; TAB  98 ADD-INIT			&
skip 1	&
  tab   1 "PAT ID"			&
  tab   9 clmhdr-pat-key-data		&
  tab  27 "FIRST"			&
  tab  33 add-first-name		&
  tab  42 "ACRONYM"			&
  tab  50 clmhdr-pat-acronym		&
skip 1	&
  tab   1 "LOC"				&
  tab   5 clmhdr-loc			&
  tab  12 "HOSP"			&
  tab  18 clmhdr-hosp			&
  tab  24 "ADMIT DATE"			&
;y2k
  tab  37 clmhdr-date-admit pic "^^^^/^^/^^"	&
  tab  49 "IN/OUT"			&
  tab  56 clmhdr-i-o-pat-ind		&
  tab  60 "DIAG CODE"			&
  tab  70 clmhdr-diag-cd-alpha          &
  tab  75 "TAPE SUBMIT"			&
  tab  87 clmhdr-tape-submit-ind	&
skip 1	&
  tab   1 "DOCTOR OHIP NBR"		&
  tab  17 clmhdr-doc-ohip-nbr pic "^^^^^^"	&
  tab  26 "SPEC"			&
  tab  31 clmhdr-doc-spec-cd		&
  tab  36 "REFFERING PHY"		&
  tab  50 clmhdr-refer-doc-nbr pic "^^^^^^"	&
skip 2	&
  tab   1 "OMA SF"			&
  tab  14 "PED"				&
  tab  24 "SERVICE"			&
  tab  41 "AMT  SVC   DD   #   DD   #   DD   #"


report	&
  tab   1 clmdtl-oma-cd			&
  tab   5 clmdtl-oma-suff		&
  tab  10 clmdtl-sv-date pic "^^^^/^^/^^"	&
  tab  23 clmdtl-sv-date pic "^^^^/^^/^^"	&
  tab  34 clmdtl-fee-oma		&
  tab  47 clmdtl-nbr-serv		&
;  tab  52 clmdtl-consecutive-sv-date-alpha(1) pic "^^ / ^"   &
;  tab  61 clmdtl-consecutive-sv-date-alpha(2) pic "^^ / ^"   &
;  tab  70 clmdtl-consecutive-sv-date-alpha(3) pic "^^ / ^"
  tab  52 w-clmdtl-sv-day-1           &
  tab  55 "/"                         &
  tab  57 clmdtl-sv-nbr(1) bwz        &

  tab  61 clmdtl-sv-day(2) bwz        &
  tab  64 "/"                         &
  tab  66 clmdtl-sv-nbr(2) bwz        &

  tab  70 clmdtl-sv-day(3) bwz        &
  tab  73 "/"                         &
  tab  75 clmdtl-sv-nbr(3) bwz

build $pb_obj/r712
