; 2008/11/24	M.C.	- modify to have the same headings and format as r140_b.txt
; 2008/12/02	MC1	- modify page length and  add nohead to the same as for r140_b.txt
;			  so that when Yas upload in word, it displays properly

cancel clear
set rep nolimit
set report dev disc name r140_a2s
; 2008/12/02 - MC1
;set page width 132 length 60	
set page width 132 length 50	
set report nohead
; 2008/12/02 - end


access afp-a2s-file						&
        link doc-afp-paym-group of afp-a2s-file                 &
        to   doc-afp-paym-group of f074-afp-group-mstr opt   


sort	on doc-afp-paym-group 	&
	on afp-solo-name 

def x-doc-count = 1

def x-pay-percentage char*5 = '10/60' if afp-payment-percentage = 0     &
                else ' ' + ascii(afp-payment-percentage,2)

def x-conversion-amt zoned*11 numeric		&
        = afp-conversion-amt                    &
                if afp-conversion-sign = " "    &
    else  0 - afp-conversion-amt

def x-submission-amt zoned*11 numeric		&
        = afp-submission-amt                    &
                if afp-submission-sign = " "    &
    else  0 - afp-submission-amt

use $pb_src/printer_codes.def nol 

page heading 					&
    tab  1 x-ff					&
    tab  2 "r140_a2s"				&
    tab 20 "Governance Detail Report for "      & 
    tab 49 afp-reporting-mth pic "^^^^/^^"	&
    tab 90 "Page: "				&
    tab 96 syspage				&
  skip 1					&
    tab 01 "Governance Group: HAMILTON ACADEMIC HEALTH SCIENCES ORGANIZATION" &
  skip 2

heading at doc-afp-paym-group 		&
skip page				&
    tab  1 "Group:"			&
    tab  8 doc-afp-paym-group 		&
    tab 12 "-"				&
    tab 13 afp-group-name 		&
  skip 2				&
    tab  11 "Physician"			&
    tab  70 "Billing"			&
    tab  80 "Conversion"		&
    tab  95 "Conversion"		&
    tab 111 "Submission" 		&
  skip 1				&
    tab  11 "Name"			&
    tab  71 "Number"			&
    tab  82 "Rate (%)"			&
    tab  95 "Amount ($)"		&
    tab 111 "Amount ($)"		&
  skip 2

rep								&
    tab  11 afp-solo-name	          			&
    tab  71 doc-afp-paym-solo 					&
    tab  84 x-pay-percentage                                    &
    tab  90 x-conversion-amt   		pic " ^^^,^^^,^^^.^^ "	&
    tab 106 x-submission-amt 		pic " ^^^,^^^,^^^.^^ "                       

footing at doc-afp-paym-group           &
    skip 1                              &
    tab  90                                          "   ============ "	&
    tab 106                                          "   ============ "	&
  skip 1								&
    tab   1 "Total for Group:"          &
    tab  19 doc-afp-paym-group          &
    tab  90 x-conversion-amt   subt reset at doc-afp-paym-group  pic " ^^^,^^^,^^^.^^ " &
    tab 106 x-submission-amt   subt reset at doc-afp-paym-group  pic " ^^^,^^^,^^^.^^ "  &
  skip 2								&
    tab  11 "Total Nbr Doctors in Dept:"                                &
    tab  38  x-doc-count subt pic "^^^"

final footing				&
  skip 2				&
    tab  90                                          "   ============ "	&
    tab 106                                          "   ============ "	&
  skip 1							 	&
    tab  49 "Group Conversion Payment/Submission: "		 	&
    tab  90 x-conversion-amt    subt  pic " ^^^,^^^,^^^.^^ " 		&
    tab 106 x-submission-amt	subt  pic " ^^^,^^^,^^^.^^ "                       

build $obj/r140_a2s

