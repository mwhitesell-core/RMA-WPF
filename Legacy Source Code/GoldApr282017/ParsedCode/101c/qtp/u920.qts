;
;  91/11/14	M.C.	- ORIGINAL
;			- THIS PROGRAM SHOULD BE RUN AFTER CLAIMS
;			  PURGE (U072.CB), THIS PROGRAM WILL UPDATE
;			  THE OUTSTANDING CLAIMS TO PATIENT MSTR
;  1999/May/20 S.B.	- Y2K checked.
;  2000/Jul/28 M.C.     - change access statement
;  2013/Apr/08 MC1      - add 'set lock record update'
;  2013/Apr/22 MC2	- ran 4 times and received '*W* qtpscr.dat', assuming the record complex is big and
;			  it may complain about the file size being too big for the intermediated scratch file
;			- modify to split into 2 requests where first request extract the necessary item into 
;			  the subfile, second request update f010 based on subfile, to see if it will eliminate
;			  the problem.  Ran the program twice and it seemed work okay.
;  2015/Feb/19 MC3	- save the f010 subfile before updating f010 file

can clear
set process nolimit

set lock record update

request access_clmhdr

access f002-claims-mstr 
choose key-p-clm-type 'P'
sorted on key-p-clm-data

temp x-claim-count
   item x-claim-count = x-claim-count + 1 reset at key-p-clm-data

subfile saveclmhdr_count keep at key-p-clm-data	&
  include key-p-clm-data, x-claim-count

request update_pat_clm_nbr


access *saveclmhdr_count link ('I' + key-p-clm-data)                   &
	to key-pat-mstr of f010-pat-mstr

sorted on key-pat-mstr

; MC3 - save in subfile before update 
subfile savef010 keep at key-pat-mstr		&
   include key-pat-mstr, 			&
	   pat-nbr-outstanding-claims, 		&
	   pat-total-nbr-claims

output f010-pat-mstr update at key-pat-mstr
   item pat-nbr-outstanding-claims final x-claim-count
   item pat-total-nbr-claims final x-claim-count

build $pb_obj/u920
  
